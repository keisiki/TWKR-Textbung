;-------------------------------------------------
; -KR- KR판 전용 옵션 설정 함수
; 본래 OPTION에 있던 KR판 전용 요소를 격리시킨 함수
;-------------------------------------------------
@OPTION_KR
#DIM L_COUNT
#DIM L_FEED
#DIM TEMP
VARSET LOCAL
L_FEED = LINECOUNT
TOOLTIP_SETDELAY 100
TEMP = CURRENTREDRAW()
REDRAW 0


DRAWLINE
FOR L_COUNT, 0, 99
	CALL OPTION_SETNAME_KR(L_COUNT)
	SIF !RESULT
		CONTINUE
	LOCAL:L_COUNT = 1
	IF RESULTS:2 == "" 
		PRINTFORML [{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%
	ELSE
		HTML_PRINT @"<button value='{L_COUNT}' title='%RESULTS:2%'>[{L_COUNT, 2}] - %RESULTS:0, 30, RIGHT%：%RESULTS:1%</button>"
	ENDIF
NEXT
DRAWLINE

PRINTFORML [999] - 설정 완료
INPUT

SELECTCASE RESULT
CASE 0 TO 99
	SIF LOCAL:RESULT
	CALL OPTION_SETTING_KR(RESULT)
	CLEARLINE LINECOUNT - L_FEED
CASEELSE
	TOOLTIP_SETDELAY 500
	REDRAW TEMP
	RETURN
ENDSELECT
RESTART


@OPTION_SETNAME_KR(OPTION_NUM)
#DIM OPTION_NUM
VARSET RESULTS 
RESULT = 1
SELECTCASE OPTION_NUM
CASE 1 ; -KR- 오리지널 요소
	RESULTS:0 = 일상 호감 신뢰 상승 배율 설정
	IF FLAG:호감신뢰상승률
		RESULTS:1 = (현재 {1+FLAG:호감신뢰상승률}배)
	ELSE
		RESULTS:1 = 디폴트
	ENDIF
CASE 2 ; -KR- 오리지널 요소
	RESULTS:0 = 의뢰 호감 신뢰 상승 배율 설정
	IF FLAG:의뢰호감신뢰상승률
		RESULTS:1 = (현재 {1+FLAG:의뢰호감신뢰상승률}배)
	ELSE
		RESULTS:1 = 디폴트
	ENDIF
CASE 3 ; -KR- 오리지널 요소
	RESULTS:0 = 정력 상승 상한 설정
	IF MAXIMUM_VIGOR == 0
		RESULTS:1 = 상한 없음
	ELSE
		RESULTS:1 = 현재 상한 : {MAXIMUM_VIGOR}
	ENDIF
CASE 4 ; -KR- 오리지널 요소
	RESULTS:0 = 미번역 구상 필터 설정
	IF KR_KOJO_FILTER == 0
		RESULTS:1 = 미번역 구상도 표시
	ELSE
		RESULTS:1 = 미번역 구상 미표시
	ENDIF
CASEELSE
	RESULT = 0
ENDSELECT
RETURN RESULT


@OPTION_SETTING_KR(ARG)
CALL OPTION_SETNAME_KR(ARG)
SELECTCASE RESULTS:0
CASE "일상 호감 신뢰 상승 배율 설정"
	PRINTFORML 변경할 일반 호감도/신뢰 추가 획득 배율 입력 『현재 : {1 + FLAG:호감신뢰상승률}x, 최대 20』
	PRINTFORML 0을 입력하면 디폴트로 설정됩니다.
	INPUT
	FLAG:호감신뢰상승률 = LIMIT(RESULT, 0, 20)
CASE "의뢰 호감 신뢰 상승 배율 설정"
	PRINTFORML 변경할 의뢰 호감도/신뢰 추가 획득 배율 입력 『현재 : {1 + FLAG:의뢰호감신뢰상승률}x, 최대 10』
	PRINTFORML 0을 입력하면 디폴트로 설정됩니다.
	INPUT
	FLAG:의뢰호감신뢰상승률 = LIMIT(RESULT, 0, 10)
CASE "정력 상승 상한 설정"
	PRINTFORML 사정으로 상승하는 정력의 최대치 설정 『현재 상한 : {MAXIMUM_VIGOR}』『디폴트값 : 3000』
	PRINTFORML 0으로 설정하면 상한이 사라집니다
	PRINTFORML *경고* 정력량이 과도하게 높을 경우, 몇몇 기능이 의도한 대로 작동하지 않을 수 있습니다.
	INPUT
	IF RESULT == 0
		MAXIMUM_VIGOR = 0
	ELSE
		MAXIMUM_VIGOR = LIMIT(RESULT, 1000, __INT_MAX__)
	ENDIF
CASE "미번역 구상 필터 설정"
	PRINTFORML 미번역 구상의 필터를 재설정합니다.
	PRINTFORML *경고* 별인판 구상의 경우, 현재 의도한 대로 작동하지 않을 수 있습니다.
	PRINTFORML [0] - 필터를 적용하지 않는다
	PRINTFORML [1] - 필터를 적용한다
	INPUT
	IF RESULT == 0
		KR_KOJO_FILTER = 0
		PRINTFORMW 모든 구상을 표시합니다.
	ELSE
		KR_KOJO_FILTER = 1
		PRINTFORMW 일부분도 번역되지 않은 구상은 표시되지 않습니다.
	ENDIF
ENDSELECT