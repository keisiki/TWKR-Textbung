;----------------------------------------------------------
;@COLOR(ARGS)
;----------------------------------------------------------
@COLOR, ARGS
#FUNCTION
SELECTCASE TOUPPER(ARGS)
	CASE "RED", "빨강", "赤"
		RETURNF 0xFF0000
	CASE "DARKRED"
		RETURNF 0x990000
	CASE "BLUE"
		RETURNF 0x0000FF
	CASE "P-RED", "PASTEL-RED", "パ赤"
		RETURNF 0xC07070
	CASE "P_BLUE", "PASTEL-BLUE", "パ青"
		RETURNF 0x7070C0
	CASE "P_GREEN", "PASTEL-GREEN", "パ緑"
		RETURNF 0x70C070
	CASE "MEDSPRINGGREEN"
		RETURNF 0x00FA9A
	CASE "GREEN","초록", "緑"
		RETURNF 0x00FF00
	CASE "P_PURPLE", "PASTEL-PURPLE", "パ紫"
		RETURNF 0xC070C0
	CASE "P-YELLOW", "PASTEL-YELLOW", "パ黄"
		RETURNF 0x505020
	CASE "P-BGREEN", "P_BLUEGREEN", "PASTEL-BLUEGREEN", "パ青緑"
		RETURNF 0x205050
	CASE "WHITE", "흰색", "白"
		RETURNF 0xC0C0C0
	CASE "BLACK", "흑", "黒"
		RETURNF 0x000000
	CASE "AQUA", "물색", "水色"
		RETURNF 0x66FFFF
	CASE "GRAY", "회색", "灰色"
		RETURNF 0x404040
	CASE "LIGHT-GRAY", "명회색", "明灰色"
		RETURNF 0x777777
	CASE "PINK", "복숭아", "桃"
		RETURNF 0xFFCCFF
	CASE "DARK-PINK", "흑도", "黒桃"
		RETURNF 0x990099
	CASE "YELLOW", "노랑", "黄"
		RETURNF 0xFFFF00
	CASE "ORANGE","등","橙"
		RETURNF 0xFFA500
	CASE "CREAM"
		RETURNF 0xFFFFCC
ENDSELECT

;----------------------------------------------------------
;着色プリント関数
;直前まで使用していた色とフォントを保存し、使用後はそちらに復帰するように
;表示形式           0=通常,1=PRINTL,2=PRINTW,3=PRINTPLAIN
;フォントスタイル   0=通常,1=太字,2=斜体,4=打ち消し線,8=下線
;(ビットで管理されているため例えば3なら太字＋斜体)
;----------------------------------------------------------
@COLORMESSAGE(ARGS, 색, 표시형식, 폰트스타일)
#DIM 현재지정의색
#DIM 현재지정의폰트스타일
#DIM 색
#DIM 폰트스타일
#DIM 표시형식

현재지정의색 = GETCOLOR()
현재지정의폰트스타일 = GETSTYLE()

SIF 폰트스타일
	FONTSTYLE 폰트스타일
SETCOLOR 색
SELECTCASE 표시형식
	CASE 0
		PRINTFORM %ARGS%
	CASE 1
		PRINTFORML %ARGS%
	CASE 2
		PRINTFORMW %ARGS%
	CASE 3
		PRINTPLAINFORM %ARGS%
ENDSELECT
SETCOLOR 현재지정의색
FONTSTYLE 현재지정의폰트스타일

;조건을 채우고 있었을 경우에 황색을 붙인다
@COND_COLORMESSAGE(COND,MESSAGE)
#DIM COND
#DIMS MESSAGE
SIF COND
	SETCOLOR C_YELLOW
PRINTFORML %MESSAGE%
RESETCOLOR

;-------------------------------------------------------------------------------
;    BAR의 색 달리하고 판
;-------------------------------------------------------------------------------
;    예  PRINTFORM %PRINT_COLORBAR(40,100,8, %UNICODE(0x2584)%, %UNICODE(0x2584)%, , 0x404040)% 
;        [===     ]
;    인수는 BAR와 거의 함께 + ARGS로 묘사 문자의 변경이 가능(지정하지 않는 경우는 BAR와 같은 사양)
;    ARG:3은 바의 색(0 xFFFFFF 형식에서 지정), ARG:4는 바의 배경 색(0 xFFFFFF 형식에서 지정)
;===============================================================================
@PRINT_COLORBAR(ARG, ARG:1, ARG:2, ARGS = "*", ARGS:1 = ".", ARG:3, ARG:4)
#LOCALSIZE 5
SIF ARG:1 <= 0
	ARG:1 = 1
LOCAL:1 = ARG * ARG:2 / ARG:1
LOCAL:2 = GETCOLOR()
FOR LOCAL, 1, ARG:2 + 1
	IF LOCAL:1 >= LOCAL
		IF !LOCAL:3
			SETCOLOR ARG:3
			LOCAL:3 = 1
		ENDIF
		PRINTFORM %ARGS%
	ELSE
		IF !LOCAL:4
			SETCOLOR ARG:4
			LOCAL:4 = 1
		ENDIF
		PRINTFORM %ARGS:1%
	ENDIF
NEXT
SETCOLOR LOCAL:2
VARSET LOCAL

;-------------------------------------------------
;커스텀 바용색세트 함수 @BARCOLORSET
;PRINT_COLORBAR의 ARG:3에 이용해, ARG;4에 RESULT를 사용한다
;-------------------------------------------------
@BARCOLORSET, ARGS
#FUNCTION
 ;LOCAL에 색을 대입
SELECTCASE ARGS
	CASE "빨강", "赤"
		RESULT:1 = 0x502020
		RETURNF 0xC07070
	CASE "엷은 빨간색", "薄赤"
		RESULT:1 = 0x803040
		RETURNF 0xF07090
	CASE "파랑", "青"
		RESULT:1 = 0x202050
		RETURNF 0x7070C0
	CASE "초록", "緑"
		RESULT:1 = 0x205020
		RETURNF 0x70C070
	CASE "유카리", "紫"
		RESULT:1 = 0x502050
		RETURNF 0xC070C0
	CASE "황색", "黄色"
		RESULT:1 = 0x505020
		RETURNF 0xC0C070
	CASE "청록색", "青緑"
		RESULT:1 = 0x205050
		RETURNF 0x70C0C0
	CASE "흰색", "白"
		RESULT:1 = 0x202020
		RETURNF 0xC0C0C0
	CASE "오렌지", "オレンジ"
		RESULT:1 = 0x504010
		RETURNF 0xFF8000
	CASE "물색", "水色"
		RESULT:1 = 0x203030
		RETURNF 0x5090FF
	CASE "EXTRA"
		RESULT:1 = 0x505020
		RETURNF 0xFFFFDD
	CASE "EXTRA2"
		RESULT:1 = 0x505020
		RETURNF 0xFFFFDD
	 ;흰색
	 CASEELSE
		RESULT:1 = 0x202020
		RETURNF 0xCCFFCC
ENDSELECT

;-------------------------------------------------
;하트 마크 함수 @HEART
;	인수 0：하트의 수(생략 하면(자) 1개)
;	인수 1：사용하는 폰트명(생략 하면(자) Verdana)
;솎아내기의 하트 마크를 표시합니다
;-------------------------------------------------
@HEART(ARG = 1, ARGS)
LOCALS = %GETFONT()%
SETFONT "Times New Roman"
PRINTFORM %UNICODE(0x2665)*ARG%
;PRINTFORM %UNICODE(0x2661)*ARG%
SETFONT LOCALS


@HEARTMARK(ARG, ARGS)
GETFONT
LOCAL = GETCOLOR()
SIF ARGS != "PLAIN"
	SETCOLOR C_H_PINK
SETFONT "Times New Roman"
;PRINTFORM %UNICODE(0x2665) * ARG%
PRINTFORM %UNICODE(0x2764) * ARG%

SETFONT RESULTS
SETCOLOR LOCAL

;하트 문자를 돌려준다
;폰트는 고려하고 있지 않다
@GET_HEART()
#FUNCTIONS
RETURNF UNICODE(0x2665)


;하트 문자로 사이에 둔다
@CLIP_HEART(캐릭터라인)
#FUNCTIONS
#DIMS 캐릭터라인
캐릭터라인 = %GET_HEART() + 캐릭터라인 + GET_HEART()%
RETURNF 캐릭터라인


;穣子口上からパクリ
;### ハートのみ別の色でPRINTするための関数 ##################
;文字列中に含まれる"HPH"をホットピンクカラーのハートとしてPRINTし、それ以外の文字列は通常の色で普通にPRINTする
;ARGS "W"=PRINTSWで表示。"L"=PRINTSLで表示。"W"でも"L"でもなければPRINTSで表示
@HPH_PRINT(対象文字列,ARGS)
#DIMS 対象文字列
#DIM 出力文字数
#DIM HPHインデクス値
#DIM BPBインデクス値
#DIM NEXTインデクス値

DO
	HPHインデクス値 = STRFIND(対象文字列, "HPH")
	BPBインデクス値 = STRFIND(対象文字列, "BPB")
	IF HPHインデクス値 >= 0 & BPBインデクス値 < 0
		NEXTインデクス値 = HPHインデクス値
	ELSEIF HPHインデクス値 < 0 & BPBインデクス値 >= 0
		NEXTインデクス値 = BPBインデクス値
	ELSE
		NEXTインデクス値 = MIN(HPHインデクス値, BPBインデクス値)
	ENDIF
	
	;対象文字列がHPHまたBPBで始まらないが後ろにHPHまたBPBがある場合
	IF NEXTインデクス値 >= 1
		;HPHの直前まで文字表示
		LOCALS = %SUBSTRING(対象文字列, 0, NEXTインデクス値)%
		IF GROUPMATCH(ARGS, "DW", "DL", "dw", "dl")
			PRINTSD @"%LOCALS%"
		ELSE
		PRINTS @"%LOCALS%"
		ENDIF
		出力文字数 = STRLENS(LOCALS)
	;対象文字列がHPHで始まる場合
	ELSEIF HPHインデクス値 == 0
		LOCALS:1 = %GETFONT()%
		SETFONT "Times New Roman"
		LOCAL = GETCOLOR()
		SETCOLOR C_H_PINK
		PRINTFORM %UNICODE(0x2764)%
		SETCOLOR LOCAL
		SETFONT LOCALS:1
		出力文字数 = 3
	;対象文字列がBPBで始まる場合
	ELSEIF BPBインデクス値 == 0
		LOCALS:1 = %GETFONT()%
		SETFONT "Times New Roman"
		LOCAL = GETCOLOR()
		SETCOLOR C_H_PINK
		PRINTFORM ///
		SETCOLOR LOCAL
		SETFONT LOCALS:1
		出力文字数 = 3
	;HPHとBPBがもうない場合
	ELSE
		;ここでARGSによる分岐
		SELECTCASE ARGS
		CASE "DW", "dw"
			PRINTSDW @"%対象文字列%"
		CASE "DL", "dl"
			PRINTSDL @"%対象文字列%"
		;----------
		CASE "W", "w"
			PRINTSW @"%対象文字列%"
		CASE "L", "l"
			PRINTSL @"%対象文字列%"
		CASEELSE
			PRINTS @"%対象文字列%"
		ENDSELECT
		BREAK
	ENDIF

	;表示済みの文字分を削除して対象文字列を更新する
	対象文字列 = %SUBSTRING(対象文字列, 出力文字数, -1)%
	
LOOP 1
