@PRINT_STATE(선택중캐릭터ID, 선택중페이지번호, 조교중플래그)
#DIM 선택중캐릭터ID
#DIM 조교중플래그
#DIM REF 선택중페이지번호
#DIM CONST 이전캐릭터를표시 = 4
#DIM CONST 다음캐릭터를표시 = 6
#DIM CONST 돌아가요 = 5

$PRINT_LOOP
	DRAWLINEFORM ━
	PRINT ■
	PRINTS NAME:선택중캐릭터ID
	PRINT (호감도:
	CALL PRINT_RANK(CFLAG:선택중캐릭터ID:2, "MAX_50000")
	PRINTFORM  {CFLAG:선택중캐릭터ID:2} 신뢰도:
	CALL PRINT_RANK(CFLAG:선택중캐릭터ID:신뢰도, "MAX_20000")
	PRINTFORM  {CFLAG:선택중캐릭터ID:신뢰도}) 
	;MASTER라면 소지금을 기재
	SIF 선택중캐릭터ID == MASTER
		PRINTFORM (%"소지금:\\" + TOSTR(MONEY,"#,###")%)
	IF NOTICE_CYCLE(선택중캐릭터ID)
		IF 조교중플래그
			CALL CYCLE_PRINT(선택중캐릭터ID)
		ELSE
			CALL CYCLE_PRINT다음날(선택중캐릭터ID)
		ENDIF
		SETCOLOR C_AQUA
	ENDIF
	IF CFLAG:선택중캐릭터ID:생해금 == 2
		PRINT 위험일 생H해금
	ELSEIF CFLAG:선택중캐릭터ID:생해금 == 1
		PRINT 생H해금
	ELSEIF GETBIT(CFLAG:선택중캐릭터ID:기정사실, 1)
		PRINT 합의 취득
	ENDIF
	RESETCOLOR
	PRINTL 　
;*******************************************
;1페이지눈
;*******************************************
IF 선택중페이지번호 == 0
	PRINT 체력 
				CALL PRINT_COLORBAR, BASE:선택중캐릭터ID:0, MAXBASE:선택중캐릭터ID:0, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("빨강"), RESULT:1
				PRINTFORML  ({BASE:선택중캐릭터ID:체력, 5}/{MAXBASE:선택중캐릭터ID:체력, 5}) 
	PRINT 기력 
				CALL PRINT_COLORBAR, BASE:선택중캐릭터ID:1, MAXBASE:선택중캐릭터ID:1, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("초록"), RESULT:1
				PRINTFORML  ({BASE:선택중캐릭터ID:기력, 5}/{MAXBASE:선택중캐릭터ID:기력, 5}) 
	IF BASE:선택중캐릭터ID:15
	PRINT 술기운 
				CALL PRINT_COLORBAR, BASE:선택중캐릭터ID:15, MAXBASE:선택중캐릭터ID:15, 28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("파랑"), RESULT:1
				PRINTFORML  ({BASE:선택중캐릭터ID:술기운, 5}/{MAXBASE:선택중캐릭터ID:술기운, 5}) 
	ENDIF
	SELECTCASE CFLAG:선택중캐릭터ID:욕구불만도
		CASE 301 TO 500
			SETCOLOR C_YELLOW
		CASE IS >= 501
			SETCOLOR C_RED
	ENDSELECT
	SIF NOTICE_LUST(선택중캐릭터ID)
		PRINTFORML 욕구 불만도：{CFLAG:선택중캐릭터ID:욕구불만도 / 10}\%
	RESETCOLOR
	CALL SHOW_SIZE(선택중캐릭터ID)
	IF !FLAG:그림표시 ;KR판 구잔재
		PRINTL [99] AA표시
	ELSE
		PRINTL [99] 그림표시
	ENDIF
	IF FLAG:AA표시 && !FLAG:그림표시
		CALL PRINT_AA(선택중캐릭터ID)
	ELSEIF FLAG:AA표시 && FLAG:그림표시
		CALL PRINT_STATE_IMAGE(선택중캐릭터ID)
	ENDIF

	CALL PRINT_STATE_CLOTH(선택중캐릭터ID)
	;도구라고 할까 아이템이지만 외가 2 문자이고
	IF 선택중캐릭터ID == MASTER
		PRINT □ 
		CALL PRINTBUTTON_EX("도구", 167, TFLAG:167)
		PRINTL  □---------------------------------------------------------------------------
		SIF !TFLAG:도구표시
			CALL LIST_ITEM("모두")
	ENDIF
	CALL PRINT_STATE_ABL(선택중캐릭터ID)
	PRINTL
	CALL PRINT_STATE_TALENT(선택중캐릭터ID)
	PRINTL
	IF 선택중캐릭터ID != MASTER
		PRINTL
		PRINT □ 
		CALL PRINTBUTTON_EX("각인", 166, TFLAG:166)
		PRINTL  □---------------------------------------------------------------------------
		;PRINT_MARK 선택중캐릭터ID
		CALL PRINT_각인(선택중캐릭터ID)
	ENDIF
;*******************************************
;2페이지눈
;*******************************************
ELSEIF 선택중페이지번호 == 1
	PRINTL
	CALL PRINT_STATE_EXP(선택중캐릭터ID)
	PRINTL
	SIF 선택중캐릭터ID != MASTER && ( TALENT:선택중캐릭터ID:임신 || CFLAG:선택중캐릭터ID:아이인원수 > 0)
		CALL 임신관련(선택중캐릭터ID)
	SIF 선택중캐릭터ID != MASTER && !조교중플래그
		CALL PRINT_STATE_JUEL(선택중캐릭터ID)
	SIF 조교중플래그
		CALL PRINT_STATE_EX(선택중캐릭터ID)
	PRINTL
	SIF 선택중캐릭터ID
		CALL 팬티수집상황(선택중캐릭터ID)
;*******************************************
;3페이지눈
;*******************************************
ELSEIF 선택중페이지번호 == 2
	CALL PRINT_STATE_PERSONAL(선택중캐릭터ID)
	CALL 육체정보(선택중캐릭터ID)
	CALL SHOW_RELATION(선택중캐릭터ID)
;*******************************************
;4페이지눈
;*******************************************
ELSEIF 선택중페이지번호 == 3
	IF !선택중캐릭터ID
		CALL 예정
	ELSE
		CALL 함락조건표시(선택중캐릭터ID)
		CALL 바람기파악상황(선택중캐릭터ID)
	ENDIF
ENDIF
IF 조교중플래그
	DRAWLINE
	IF 선택중페이지번호 > 0
		PRINTC [0] 이전페이지
	ELSE
		PRINTC 　
	ENDIF
	PRINTC 　
	IF 선택중페이지번호 < 상세능력표시최대페이지번호
		PRINTC [1] 다음페이지
	ELSE
		PRINTC 　
	ENDIF
	PRINTL
		PRINTC  
		PRINTFORMC [{돌아가요}] 돌아간다
	INPUT
	SELECTCASE RESULT
		CASE 0
		SIF 선택중페이지번호 > 0
			선택중페이지번호 --
			GOTO PRINT_LOOP
		CASE 1
			SIF 선택중페이지번호 < 상세능력표시최대페이지번호
				선택중페이지번호 ++
				GOTO PRINT_LOOP
		CASE 160 TO 167
			TFLAG:RESULT = !TFLAG:RESULT
			GOTO PRINT_LOOP
		;이 근처 지워 버려서도 좋은가
		CASE 이전캐릭터를표시
			IF !조교중플래그
				선택중캐릭터ID--
			ENDIF
			IF 선택중캐릭터ID < 0
				선택중캐릭터ID = CHARANUM - 1
			ENDIF
			GOTO PRINT_LOOP
		CASE 다음캐릭터를표시
			IF !조교중플래그
				선택중캐릭터ID++
			ENDIF
			IF 선택중캐릭터ID >= CHARANUM
				선택중캐릭터ID = 0
			ENDIF
			GOTO PRINT_LOOP
		CASE 돌아가요
			RETURN 0
		CASE AA표시
			FLAG:AA표시 = !FLAG:AA표시
			GOTO PRINT_LOOP
		CASEELSE
			RETURN 0
	ENDSELECT
ENDIF



@PRINT_STATE_CLOTH(선택중캐릭터ID)
#DIM 선택중캐릭터ID
#DIM 장비부위
PRINTL
PRINT □ 
CALL PRINTBUTTON_EX("복장", 161, TFLAG:161)
PRINTL  □---------------------------------------------------------------------------
IF TFLAG:복장표시
	PRINTFORML 
	RETURN
ENDIF
IF !EQUIP:선택중캐릭터ID:0 && !TEQUIP:선택중캐릭터ID:하반신착의상황 && !TEQUIP:선택중캐릭터ID:상반신착의상황
	PRINTFORML ＜전라＞
	SIF INROOM(CFLAG:선택중캐릭터ID:현재위치) == 1
		RETURN
ENDIF
SIF CFLAG:선택중캐릭터ID:장난 > 1
	PRINTFORML ＜벌 무너뜨려＞
CALL SHOW_CLOTHES(선택중캐릭터ID, "현재의 의상")




@PRINT_STATE_ABL(선택중캐릭터ID)
#DIM 선택중캐릭터ID
#DIM 표시항목수
PRINT □ 
CALL PRINTBUTTON_EX("능력", 162, TFLAG:162)
PRINTL  □---------------------------------------------------------------------------
IF !TFLAG:능력표시
	표시항목수 = 0
	FOR LOCAL, 0, 50
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF LOCAL > 50 && 선택중캐릭터ID != MASTER
			CONTINUE
		;동성애 관계
		SIF GROUPMATCH(ABLNAME:LOCAL, "레즈끼", "레즈중독") && !GETBIT(TALENT:선택중캐릭터ID:2, 0)
			CONTINUE
		SIF GROUPMATCH(ABLNAME:LOCAL, "호모끼", "게이중독") && GETBIT(TALENT:선택중캐릭터ID:2, 0)
			CONTINUE
		PRINTFORM %ABLNAME:LOCAL,12%:
		IF LOCAL < 40
			CALL PRINT_RANK(ABL:선택중캐릭터ID:LOCAL, "MAX_99")
		ELSE
			CALL PRINT_RANK(ABL:선택중캐릭터ID:LOCAL, "MAX_6")
		ENDIF
		PRINTFORM  {ABL:선택중캐릭터ID:LOCAL, 5, LEFT} 
		표시항목수 ++
		SIF 표시항목수 % 4 == 0
			PRINTL 
	NEXT
ENDIF
PRINTL
PRINTL
PRINT □ 
CALL PRINTBUTTON_EX("성 기술", 164, TFLAG:164)
PRINTL  □---------------------------------------------------------------------------
표시항목수 = 0
IF !TFLAG:164
	FOR LOCAL, 50, 60
		SIF !STRLENS(ABLNAME:LOCAL)
			CONTINUE
		SIF !(TALENT:선택중캐릭터ID:2 & 1) && (LOCAL == 52 || LOCAL == 54)
			CONTINUE
		PRINTFORM %ABLNAME:LOCAL,12%:
		CALL PRINT_RANK(ABL:선택중캐릭터ID:LOCAL, "MAX_6")
		PRINTFORM  {ABL:선택중캐릭터ID:LOCAL, 5, LEFT} 
		표시항목수 ++
		SIF 표시항목수 % 4 == 0
			PRINTL 
	NEXT
ENDIF



@PRINT_STATE_EXP(선택중캐릭터ID)
#DIM 선택중캐릭터ID
#DIM 표시항목수
PRINT □ 
CALL PRINTBUTTON_EX("경험", 163, TFLAG:163)
PRINTL  □---------------------------------------------------------------------------
표시항목수 = 0
IF !TFLAG:경험표시
	FOR LOCAL, 0, 100
		;EXP.CSV에 등록되지 않으면 CONTINUE
		;SIF !STRLENS(EXPNAME:LOCAL)
		;	CONTINUE
		;미경험을 표시시키지 않다
		SIF !EXP:선택중캐릭터ID:LOCAL
			CONTINUE
		;동성애 관계
		IF !(TALENT:선택중캐릭터ID:2 & 1) && LOCAL == 51
			CONTINUE
		ELSEIF TALENT:선택중캐릭터ID:2 & 1 && LOCAL == 52
			CONTINUE
		ENDIF
		PRINTFORM %EXPNAME:LOCAL,14%:{EXP:선택중캐릭터ID:LOCAL, 5, LEFT} 
		표시항목수 += 1
		SIF 표시항목수 % 4 == 0
			PRINTL 
	NEXT
ENDIF
PRINTL



@PRINT_STATE_TALENT(선택중캐릭터ID)
#DIM 선택중캐릭터ID
#DIM TLNT_CNT, 6
#DIM TLNT_NO, 6, 100

VARSET TLNT_CNT
VARSET TLNT_NO

PRINT □ 
CALL PRINTBUTTON_EX("소질", 165, TFLAG:165)
PRINTL  □---------------------------------------------------------------------------
IF !TFLAG:165
	;TALENT:200(범용 캐릭터)는 표시시키지 않다
	FOR LOCAL, 0, 300
		SIF !STRLENS(GET_TALENTNAME(LOCAL, TALENT:선택중캐릭터ID:LOCAL))
			CONTINUE
		LOCALS = {LOCAL}/
		IF GROUPMATCH(LOCAL, 90,91,132,164,165,166,189,190,191,192,193,194,195,196,197)
			;종족
			TLNT_NO:0:(TLNT_CNT:0) = LOCAL
			TLNT_CNT:0 ++
		ELSEIF GROUPMATCH(LOCAL, 0,1,3,4,5,6,7,8,9,60,70,72,73,74,75,76,77,78,82,83,122,142,152,183,184,185,250,256,257,258,259,260,261,262,263,264)		
			;성적 특징
			TLNT_NO:1:(TLNT_CNT:1) = LOCAL
			TLNT_CNT:1 ++
		ELSEIF GROUPMATCH(LOCAL, 2,16,17,40,41,54,56,57,92,100,101,102,103,104,105,106,121,130,133,134,137,138,143,146,149)
			;몸 목표 특징
			TLNT_NO:2:(TLNT_CNT:2) = LOCAL
			TLNT_CNT:2 ++
		ELSEIF GROUPMATCH(LOCAL, 9,10,11,12,13,14,18,19,20,21,22,23,24,25,26,30,31,32,33,61,62,71,80,81,84,85,86,94,151,155,311,313,315,316,320)
			;정신적 특징
			TLNT_NO:3:(TLNT_CNT:3) = LOCAL
			TLNT_CNT:3 ++
		ELSEIF GROUPMATCH(LOCAL, 47,48,49,50,51,52,53,55,93,135,136,144,147,161,162,178,179)
			;기술적 특징
			TLNT_NO:4:(TLNT_CNT:4) = LOCAL
			TLNT_CNT:4 ++
		ELSE
			;그 외
			TLNT_NO:5:(TLNT_CNT:5) = LOCAL
			TLNT_CNT:5 ++
		ENDIF
	NEXT

	FOR LOCAL:2, 0, 6
		SELECTCASE LOCAL:2
			CASE 0
				PRINT 　 종 족 　：
			CASE 1
				PRINT 　성적 특징：
			CASE 2
				PRINT 신체적 특징：
			CASE 3
				PRINT 정신적 특징：
			CASE 4
				PRINT 기술적 특징：
			CASE 5
				PRINT 　그 외　：
		ENDSELECT
		LOCALS = ""
		SIF LOCAL:2 == 0 && TLNT_CNT:0 == 0
			CALL PRINT_종족추출(선택중캐릭터ID)
		FOR LOCAL:1, 0, TLNT_CNT:(LOCAL:2)
			LOCAL = TLNT_NO:(LOCAL:2):(LOCAL:1)
			;동정
			SIF LOCAL == 1 && !(TALENT:선택중캐릭터ID:성별 & 2)
				CONTINUE
			;남자 절벽
			IF LOCAL == 105 && (TALENT:선택중캐릭터ID:성별 == 2)
				SIF (TALENT:선택중캐릭터ID:105 == -2) && (TALENT:선택중캐릭터ID:성별 == 2)
					CONTINUE
				SIF (TALENT:선택중캐릭터ID:105 == 0) && (TALENT:선택중캐릭터ID:성별 == 2)
					PRINTFORM [보유]
			ENDIF
			;청소부는 살고 있는 때만 표시
			SIF LOCAL == GETNUM(TALENT, "청소부") && !CFLAG:선택중캐릭터ID:신사거주
				CONTINUE
			LOCALS += GET_TALENTNAME(LOCAL, TALENT:선택중캐릭터ID:LOCAL)
			IF STRLENS(LOCALS) > 60
				LOCALS = " "
				PRINTL
				PRINT 　　　　　　
			ENDIF
			;소질에 따라서는 색 바꾸고
			IF GROUPMATCH(LOCAL, 0,3,4,6,7,73,74,75,76,77,78,122,142)
				SETCOLOR 255, 120, 200
				FONTBOLD
			ELSEIF LOCAL == 8
				SETCOLOR C_PINK
			ENDIF
			PRINTFORM [%GET_TALENTNAME(LOCAL, TALENT:선택중캐릭터ID:LOCAL)%]
			RESETCOLOR
			FONTREGULAR
		NEXT
		SIF LOCALS != " "
			PRINTL 
	NEXT
ENDIF

@PRINT_종족추출(선택중캐릭터ID)
#DIM 선택중캐릭터ID
#DIM TMP_RESULT
#DIMS STR_LIST, 10

VARSET STR_LIST
TMP_RESULT:0 = RESULT
LOCALS =  %CSVCSTR(선택중캐릭터ID, 10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "종족：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "종족：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			PRINTFORM [%LOCALS:1%]
			BREAK
		ENDIF
	NEXT
ELSEIF 선택중캐릭터ID == [[소악마]]
	PRINTFORM [악마]
;ELSEIF TALENT:선택중캐릭터ID:스침 とどっちがいいんでしょうね？
ELSEIF 선택중캐릭터ID == RANDOM_CHARANUM
	PRINTFORM [%CSTR:선택중캐릭터ID:모브코종족%]
ENDIF
RESULT = TMP_RESULT:0


@PRINT_각인(선택중캐릭터ID)
#DIM 선택중캐릭터ID
IF !TFLAG:166
	FOR LOCAL, 0, 6
		SIF !STRLENS(MARKNAME:LOCAL)
			CONTINUE
		PRINTFORM 　%MARKNAME:LOCAL,12%:
		IF LOCAL == 3
			SETCOLOR C_RED
			PRINTFORML %"×" * (MARK:선택중캐릭터ID:LOCAL)%
		ELSE
			SETCOLOR 255, 255, 0
			PRINTFORML %"★" * (MARK:선택중캐릭터ID:LOCAL)%
		ENDIF
		RESETCOLOR
	NEXT
ELSE
	PRINTL
ENDIF



@PRINT_알파벳평가(선택중캐릭터ID, ARG, 표시신분번호)
;ARG(1. 호감도 2. 신뢰도 3. 성 기술 및 기능 4. 그 외 ABL)
;「표시신분번호」는 ARG가  3또는 4 때에만 사용
#DIM 선택중캐릭터ID
#DIM 표시신분번호
IF ARG == 1
	IF CFLAG:선택중캐릭터ID:2 >= 100000
		SETCOLOR C_P_PURPLE
		PRINT Ph
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 50000
		SETCOLOR C_BLUE
		PRINT Ex
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 30000
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 10000
		SETCOLOR C_WHITE
		PRINT  S
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 5000
		SETCOLOR C_PINK
		PRINT  A
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 3000
		SETCOLOR C_RED
		PRINT  B
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 1500
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 1000
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 500
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF CFLAG:선택중캐릭터ID:2 >= 100
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
ELSEIF ARG == 2
	IF CFLAG:선택중캐릭터ID:신뢰도 >= 30000
		SETCOLOR C_P_PURPLE
		PRINT Ph
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 20000
		SETCOLOR C_BLUE
		PRINT Ex
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 10000
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 3000
		SETCOLOR C_WHITE
		PRINT  S
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 1000
		SETCOLOR C_PINK
		PRINT  A
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 500
		SETCOLOR C_RED
		PRINT  B
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 150
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 100
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 50
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF CFLAG:선택중캐릭터ID:신뢰도 >= 10
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
ELSEIF ARG == 3
	IF ABL:선택중캐릭터ID:표시신분번호 == 7
		SETCOLOR C_P_PURPLE
		PRINT Ph
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 6
		SETCOLOR C_BLUE
		PRINT Ex
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 5
		SETCOLOR C_WHITE
		PRINT  S
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 4
		SETCOLOR C_PINK
		PRINT  A
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 3
		SETCOLOR C_RED
		PRINT  B
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 2
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 == 1
		SETCOLOR C_YELLOW
		PRINT  D
	ELSE
		SETCOLOR C_GRAY
		PRINT  E
	ENDIF
	RESETCOLOR
	RETURN 0
ELSEIF ARG == 4
	IF ABL:선택중캐릭터ID:표시신분번호 == 99
		SETCOLOR C_P_PURPLE
		PRINT Ph
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 80
		SETCOLOR C_WHITE
		PRINT Ex
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 60
		SETCOLOR C_WHITE
		PRINT SS
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 40
		SETCOLOR C_WHITE
		PRINT  S
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 20
		SETCOLOR C_PINK
		PRINT  A
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 10
		SETCOLOR C_RED
		PRINT  B
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 5
		SETCOLOR C_ORANGE
		PRINT  C
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 3
		SETCOLOR C_YELLOW
		PRINT  D
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 2
		SETCOLOR C_GREEN
		PRINT  E
	ELSEIF ABL:선택중캐릭터ID:표시신분번호 >= 1
		SETCOLOR C_AQUA
		PRINT  F
	ELSE
		SETCOLOR C_GRAY
		PRINT  G
	ENDIF
	RESETCOLOR
	RETURN 0
ELSE
ENDIF
