
;SKIPSTART와 맨 밑의 SKIPEND는 지우고 CTRL+H로 82는 해당 캐릭터의 번호로 교체해 주십시오
@고유사고패턴82
#DIM CONST 시전자 = 82;이 부분에 사고패턴 당사자의 캐릭터No.를 입력해 주세요
#DIM CONST 대사유 = 0 ; 행동시에 대사를 출력하려면 1로 고치십시오
#DIM DYNAMIC 내체력
#DIM DYNAMIC 상대체력
#DIM DYNAMIC 상대방
#DIM DYNAMIC 항복플래그 ; 1일때 항복합니다
#DIM DYNAMIC 공격플래그 ; 1일때 기본공격합니다
#DIM DYNAMIC 회피플래그 ; 1일때 회피합니다
#DIM DYNAMIC 스킬플래그 ; 특정 스킬의 No.를 넣으면 그 스킬을 시전합니다 
#DIM DYNAMIC 도주플래그 ; 1일때 도망갑니다
#DIM DYNAMIC 이동플래그 ; -1 일때 좌, 1일때 우, 0일때 턴 넘김
;DYNAMIC으로 시전된 변수들은 이 함수가 불러와 질 때 마다 초기화 되니 따로 초기화 하려면 PHASE 1에서 해 주십시오
;
;===========================
;PHASE 0, 턴 넘겨받는중
;===========================
;딱히 제어할 수 있는건 없습니다, 대사가 나온다 해도 그닥...
;===========================
;PHASE 1, 초기화
;===========================
사고패턴당사자 = 시전자 ;이 값은 시스템에 지금 시전중인 대상을 알려주는 값이기 때문에 건드리지 않는걸 추천합니다
상대방 = 사용자반대(시전자)
CALL 사용가능스킬목록생성
;===========================
;PHASE 2, 현재 상황 인식
;===========================
내체력 = BASEPERCENT(시전자,"체력")
상대체력 = BASEPERCENT(상대방,"체력")

;===========================
;PHASE 3, 분석
;===========================

;이하는 예시 코드입니다
IF 행동기회여부(시전자,T_통상행동)
    IF 내체력 < 5&&상대체력 >= 30
        ;내 체력이 5% 미만이면서 상대체력이 30% 이상일때 실행되는 구절입니다
        IF 항복불가
            ;항복을 거절당했다면 도망갑니다
            도주플래그 = 1
        ELSE
            ;아니라면 항복합니다
            항복플래그 = 1
        ENDIF
    ENDIF
    VARSET LOCAL
    LOCAL:0 = 33 ; 공격확률
    LOCAL:1 = 67 ; 회피확률
    LOCAL:2 = RAND:100 + 1
    IF LOCAL:2 <= LOCAL:0
        공격플래그 = 1
    ELSEIF LOCAL:2 <= LOCAL:0 + LOCAL:1
        회피플래그 = 1
    ELSE
        회피플래그 = 1
    ENDIF
    IF IS_COOLDOWN(326)&&!RAND:20&&고로시
        스킬플래그 = 326
        공격플래그 = 0
        회피플래그 = 0
    ENDIF
ELSEIF 행동기회여부(시전자,T_이동행동)
    ;TODO : 수정필요
    SELECTCASE RAND:3
        CASE 0
            이동플래그 = -1
        CASE 1
            이동플래그 = 1
        CASE 2
            이동플래그 = 0
    ENDSELECT
ELSE
    CALL T_COM턴넘김
ENDIF
;===========================
;PHASE 4, 실행
;===========================
IF 항복플래그 == 1
    ;이 조건문에 해당하면 항복하게됩니다
    IF 대사유
        ;행위시에 대사를 출력하고 싶다면 이쪽에 입력하십시오
        
    ENDIF
    CALL T_항복(시전자)
ELSEIF 도주플래그 == 1
    CALL T_COM도망간다(시전자)
ELSEIF 공격플래그 == 1
    ;이 조건문에 해당되면 공격하게 됩니다
    IF 대사유
        ;행위시에 대사를 출력하고 싶다면 이쪽에 입력하십시오
        
    ENDIF
    CALL T_COM공격한다
ELSEIF 회피플래그 == 1
    ;이 조건문에 해당되면 회피하게 됩니다
    IF 대사유
        ;행위시에 대사를 출력하고 싶다면 이쪽에 입력하십시오
               
    ENDIF
    CALL T_COM회피한다(시전자)
ELSEIF 스킬플래그 != 0
    ;이 조건문에서는 스킬을 시전합니다
    IF 대사유
        ;행위시에 대사를 출력하고 싶다면 이쪽에 입력하십시오
        SELECTCASE 스킬플래그
            CASE 1 ; 시전하는 스킬 번호에 따라 다른 대사를 출력하고 싶다면 이용하십시오
                
            CASEELSE ; 아니라면 위에 있는 모든 CASE를 지우고 이 부분에만 공용 대사를 넣으십시오
                
        ENDSELECT
    ENDIF
    CALL DO_T_SKILL(시전자,스킬플래그)
ELSEIF 이동플래그 != 0
    CALL T_MOVE(시전자,이동플래그)
ELSE
    CALL T_COM턴넘김
ENDIF
;상기한 모든 조건에서 실행 실패했을경우 발동합니다


;===========================
;PHASE 5, 턴 종료 페이즈
;===========================
IF 대사유&&항복플래그 == 0
    ;행위시에 대사를 출력하고 싶다면 이쪽에 입력하십시오
        
ENDIF
RETURN 1

@고유사고패턴82_EXIST
;사고패턴의 존재 유무를 판정하는 함수, 이 함수가 있다면 위에 있는 함수가 실행되게 됩니다
RETURN 1
