
;========================================================================
;각 함수별 역할 정리
;T_SKILL101 : 해당 스킬의 기본 정수 스텟 처리
;T_SKILL101_STR : 해당 스킬의 기본 문자열 스텟 처리
;T_SKILL101_COLOR : 상황에 따른 출력문 색깔 처리
;ONGOING_T_SKILL101 : 스킬 사용 이후 처리 (쿨타임 감소, 지속효과 등)
;AFTER_T_SKILL101 : 스킬 사용 직후 처리
;
;시전자 = T_턴주인, 상대방 = T_턴상대
;========================================================================
@T_SKILL101(ARGS)
#FUNCTION
;ARGS = 인자

#DIM 쿨타임
#DIM 지속시간
#DIM 명중률
#DIM 피해량
#DIM 타입
#DIM HP코스트
#DIM TSP코스트
;-------------------
;성능 입력
;쿨타임 0 = 한 턴에 몇 번이라도, 쿨타임 1 = 한 턴에 한번, 쿨타임 2= 두 턴에 한번
;-------------------
쿨타임 = 0;스킬의 쿨타임을 넣으세요
지속시간 = 0 ;스킬의 효과가 지속되는 시간을 넣으세요
명중률 = 10 ; 몇% 확률로 명중하는지 쓰세요
피해량 = 0 ;얼마만큼의 데미지를 주는지 쓰세요, 스텟을 참고해도 좋습니다
타입 = 자동형 ;공격형/방어형/회복형/상태이상형/버프형/해주형 ;ERH 변수입니다.
;여러 타입을 한꺼번에 입력하고자 하는 경우:
;타입 = 공격형 + 버프형
;과 같이 + 를 포함해 입력하시면 됩니다.
;-------------------
SELECTCASE ARGS
CASE "쿨타임"
    ;쿨타임을 반환, 건들일 없다
    RESULT = 쿨타임
CASE "명중률"
    ;명중률을 반환함
    RESULT = 명중률
CASE "공격력"
    ;피해량을 반환, 공격력을 참조하려면 사용
    RESULT = 피해량
CASE "지속시간"
    RESULT = 지속시간
CASE "타입"
    RESULT = 타입
CASE "HP코스트"
    RESULT = HP코스트
CASE "TSP코스트"
    RESULT = TSP코스트
ENDSELECT
RETURNF RESULT

;문자열부
@T_SKILL101_STR(ARGS)
#FUNCTIONS
#DIMS 스킬명
#DIMS 속성
;-------------------
;성능 입력
;-------------------
스킬명 = 어둠을 조종하는 정도의 능력
속성 = 물리
;-------------------
SELECTCASE ARGS
CASE "스킬명"
    RESULTS '= 스킬명
CASE "속성"
    ;피해속성을 반환
    RESULTS '= 속성
CASEELSE
    THROW 지정되지 않은 인자 %ARGS%
ENDSELECT
RETURNF RESULTS

;표시할 때 다른 색깔을 쓰고 싶은 경우 변경
@T_SKILL101_COLOR(ARGS)
;ARGS = 인자
SELECTCASE ARGS
CASE "색깔_시전가능"
    ;시전가능, 흰색으로 빛남
    RETURN 0xFFFFFF
CASE "색깔_시전불가"
    ;시전불가, 회색임
    RETURN 0x585858
ENDSELECT

@ONGOING_T_SKILL101(사용자, ARGS)
#DIM 사용자
#DIM 남은지속시간, 200 ;캐릭터별 지속시간 저장 배열
#DIM 남은쿨타임, 200 ;캐릭터별 지속시간 저장 배열
#DIM 피해량
#DIM 명중률
#DIMS 속성
피해량 = T_SKILL101("공격력")
속성 '= T_SKILL101_STR("속성")
IF TB_SUM_REMAIN(사용자,TCV_실명)&&RAND:2 == 0
    명중률 = T_SKILL101("명중률") / 2
ELSE
    명중률 = T_SKILL101("명중률")
ENDIF
SELECTCASE ARGS
;스킬 발동시 남은 지속시간/쿨타임 처리 부분
CASE "쿨타임설정"
    남은쿨타임:사용자 = T_SKILL101("쿨타임")
CASE "지속시간설정"
    남은지속시간:사용자 = T_SKILL101("지속시간")
CASE "턴엔드"
    ;매 턴이 지날때마다 지속시간과 쿨타임처리를 하는 부분
    IF 남은지속시간:사용자 > 0
        남은지속시간:사용자--
        CALL ONGOING_T_SKILL101(사용자, "지속효과발동")
    ENDIF
    SIF 남은쿨타임:사용자 > 0
        남은쿨타임:사용자 --
CASE "남은지속시간"
    ;남은 지속시간 표시, 건들일 없다
    RETURN 남은지속시간:사용자
CASE "남은쿨타임"
    ;남은쿨타임을 반환, 건들일 없다
    RETURN 남은쿨타임:사용자
CASE "초기화"
    VARSET 남은쿨타임
VARSET 남은지속시간
CASE "시스템체크"
    ;시스템 메시지에 따른 검사, 패시브 스킬등에 이용, 액티브는 딱히 쓸 일이 있나?
    IF T_시스템 == 1||T_시스템 == 2
        IF T_턴반대 == T_시전자
            IF 명중판정(10)
                CALL 통합버프(T_시전자반대, TCV_실명,101,1, 1)
                LOCAL = RESULT
                IF LOCAL == 1
                    CALLFORMF T_SKILL101_STR("스킬명")
                    IF 사용자 == MASTER
                        SETCOLOR 0xFFFFFF
                    ELSE
                        SETCOLOR 0xFF4444
                    ENDIF
                    PRINTFORML %RESULTS%
                    RESETCOLOR
                    PRINTFORML 돌연, 짙은 어둠이 %CALLNAME:T_시전자반대%의 주위에 깔린다!
                ENDIF
            ENDIF
        ENDIF
    ENDIF  
   
CASE "지속효과발동"
    ;지속적으로 효과가 발동하는 스킬의 경우, 여기에 효과를 입력
    IF 남은지속시간 > 0

    ENDIF
ENDSELECT

@AFTER_T_SKILL101(사용자)
#DIM 사용자
#DIM 명중률
#DIM 피해량
#DIMS 속성

;쿨타임은 미리 설정함
CALL ONGOING_T_SKILL101(사용자, "쿨타임설정")

;명중 대응, 특정상황에서 명중률을 변동하고 싶으면 사용
IF TB_SUM_REMAIN(사용자,TCV_실명)&&RAND:2 == 0
    명중률 = T_SKILL101("명중률") / 2
ELSE
    명중률 = T_SKILL101("명중률")
ENDIF

IF 명중률 >= RAND:100 + 1
    ;명중!
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------

    피해량 = T_SKILL101("공격력")
    속성 '= T_SKILL101_STR("속성")
    CALL T_데미지(T_턴반대, 피해량, 속성)

    CALL ONGOING_T_SKILL101(사용자, "지속시간설정")
    RETURN 1
ELSE
    ;!감나빗!
    SETCOLOR 0xFF4444
    PRINTFORML 빗나감!
    RESETCOLOR
    ;----------------
    ;대사는 이쪽
    ;----------------
    PRINTFORML 
    ;----------------
    ;처리는 이쪽, 무언가 효과를 넣으려면 여기에 작성
    ;----------------
    RETURN 0
ENDIF

@HELP_T_SKILL101
;스킬의 설명을 출력하는 함수
;적당히 8줄 이하의 설명을 써두자
PRINTFORML 루미아의 능력
PRINTFORML 
PRINTFORML 상대방의 턴이 시작할때, 낮은 확률로 그 턴동안 실명시킨다
PRINTFORML
PRINTFORML 
PRINTFORML 그녀는 빛을 싫어했고 그렇기에 스스로 어둠을 만들어냈다
PRINTFORML 그렇지만 스스로도 그 너머를 볼 수 있는 능력은 없었다고 한다
PRINTFORML 

