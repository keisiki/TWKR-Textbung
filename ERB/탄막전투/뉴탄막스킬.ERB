
@T_시스템처리(ARG,ARG:1 = 0,ARG:2 = 0,ARG:3 = 0,ARG:4 = 0,ARG:5 = 0,ARG:6 = 0,ARG:7 = 0,ARG:8 = 0,ARG:9 = 0)
#DIM DYNAMIC 대피소,2
#DIM DYNAMIC LOOPCOUNT
;기본적으로 홀수 = 적 짝수 = 플레이어

;1 = 적 턴 시작
;2 = 플레이어 턴 시작
;10 = 게임 시작
;100 = 플레이어공격 명중
;101 = 적 공격 명중
;102 = 플레이어 공격 빗나감
;103 = 적 공격 빗나감
;104 = 플레이어가 적에게 데미지를 줌,최종데미지,원래데미지,피격자
;105 = 적이 플레이어에게 데미지를 줌,최종데미지,원래데미지,피격자
;106 = 플레이어가 적에게 데미지를 받기직전,최종데미지,원래데미지,피격자
;107 = 적이 플레이어에게 데미지를 받기직전,최종데미지,원래데미지,피격자
;110 = 플레이어 스킬표 생성
;120 = 플레이어 회피시도
;121 = 적 회피 시도
;122 = 플레이어 공격 적중 후
;123 = 적 공격 적중 후
;200 = 플레이어 체력 0
;201 = 적 체력 0
;202 = 플레이어 도주시도
;203 = 적 도주시도
;220 = 플레이어 운 데미지 관통
;221 = 적 운 데미지 관통
;250 = 플레이어 스킬 빗나감, 스킬번호,스킬타입
;251 = 적 스킬 빗나감, 스킬번호, 스킬타입
;1000 = 플레이어 항복 시도
;1001 = 적 항복 시도
IF T_SYS처리중
    T_SYSBUFF:T_SYS처리중:0 = ARG
    T_SYSBUFF:T_SYS처리중:1 = ARG:1
    T_SYSBUFF:T_SYS처리중:2 = ARG:2
    T_SYSBUFF:T_SYS처리중:3 = ARG:3
    T_SYSBUFF:T_SYS처리중:4 = ARG:4
    T_SYSBUFF:T_SYS처리중:5 = ARG:5
    T_SYSBUFF:T_SYS처리중:6 = ARG:6
    T_SYSBUFF:T_SYS처리중:7 = ARG:7
    T_SYSBUFF:T_SYS처리중:8 = ARG:8
    T_SYSBUFF:T_SYS처리중:9 = ARG:9
    T_SYS처리중++
    RETURN
ENDIF
T_SYS처리중++
T_시스템:0 = ARG
T_시스템:1 = ARG:1
T_시스템:2 = ARG:2
T_시스템:3 = ARG:3
T_시스템:4 = ARG:4
T_시스템:5 = ARG:5
T_시스템:6 = ARG:6
T_시스템:7 = ARG:7
T_시스템:8 = ARG:8
T_시스템:9 = ARG:9
$LOOP
대피소:0 = T_시전자
대피소:1 = T_시전자반대
FOR LOCAL,0,1000
    T_시전자 = T_턴주인
    T_시전자반대 = T_턴반대
    IF HAVE_T_SKILL(T_턴주인,LOCAL)
        IF T_턴주인 != MASTER||(IS_MEMORIZED(LOCAL)&&T_턴주인 == MASTER)
            TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴주인,"시스템체크")
        ENDIF
    ENDIF
    T_시전자 = T_턴반대
    T_시전자반대 = T_턴주인
    IF HAVE_T_SKILL(T_턴반대,LOCAL)
        IF T_턴반대 != MASTER||(IS_MEMORIZED(LOCAL)&&T_턴반대 == MASTER)
            TRYCALLFORM ONGOING_T_SKILL{LOCAL}(T_턴반대,"시스템체크")
        ENDIF
    ENDIF
NEXT
T_시전자 = 대피소:0
T_시전자반대 = 대피소:1
T_SYS처리중--
VARSET T_시스템
IF T_SYS처리중
    LOOPCOUNT++
    IF T_SYSBUFF:LOOPCOUNT:0 == 0
        GOTO ENDPHASE
    ENDIF
    T_시스템:0 = T_SYSBUFF:LOOPCOUNT:0
    T_시스템:1 = T_SYSBUFF:LOOPCOUNT:1
    T_시스템:2 = T_SYSBUFF:LOOPCOUNT:2
    T_시스템:3 = T_SYSBUFF:LOOPCOUNT:3
    T_시스템:4 = T_SYSBUFF:LOOPCOUNT:4
    T_시스템:5 = T_SYSBUFF:LOOPCOUNT:5
    T_시스템:6 = T_SYSBUFF:LOOPCOUNT:6
    T_시스템:7 = T_SYSBUFF:LOOPCOUNT:7
    T_시스템:8 = T_SYSBUFF:LOOPCOUNT:8
    T_시스템:9 = T_SYSBUFF:LOOPCOUNT:9
    GOTO LOOP
ENDIF

$ENDPHASE
VARSET T_SYSBUFF
VARSET T_SYS처리중
VARSET LOOPCOUNT
CALL 데미지스택호출

@T_COM턴넘김(ARG)
T_행동기회:ARG:0 = 0
T_행동기회:ARG:1 = 0

@T_COM공격한다(ARGS = "")
#DIM DYNAMIC 보너스
IF ARGS == ""
    ARGS = 물리
ENDIF
IF TB_SUM_REMAIN(T_턴주인,TCV_무장해제)
    PRINTFORML %T_턴주인은% 공격할 수 없다!
    RETURN 0
ENDIF
PRINTFORML %T_턴주인의% 공격이 %T_턴반대에게% 날아갔다!
CALL 행동기회소모(T_턴주인,T_통상행동)
IF TB_SUM_REMAIN(T_턴주인,TCV_실명)
    IF 평타명중판정(ARG능력치(T_턴주인,"적중률") / 2)
        IF T_턴주인 == MASTER
            CALL T_시스템처리(100)
        ELSE
            CALL T_시스템처리(101)
        ENDIF
        보너스 = RAND:100
        CALL T_데미지(T_턴반대,TCVAR:T_턴주인:T_공격력+보너스,ARGS)
        IF T_턴주인 == MASTER
            CALL T_시스템처리(122)
        ELSE
            CALL T_시스템처리(123)
        ENDIF
        RETURN 1
    ELSE
        IF T_턴주인 == MASTER
            CALL T_시스템처리(102)
        ELSE
            CALL T_시스템처리(103)
        ENDIF
        PRINTFORML 그러나 앞이 제대로 보이지 않아 빗맞추고 말았다...
        RETURN 0
    ENDIF
ELSE
    IF 평타명중판정(ARG능력치(T_턴주인,"적중률"))
        IF T_턴주인 == MASTER
            CALL T_시스템처리(100)
        ELSE
            CALL T_시스템처리(101)
        ENDIF
        보너스 = RAND:100 * 2
        CALL T_데미지(T_턴반대,TCVAR:T_턴주인:T_공격력+보너스,ARGS)
        IF T_턴주인 == MASTER
            CALL T_시스템처리(122)
        ELSE
            CALL T_시스템처리(123)
        ENDIF
        RETURN 1
    ELSE
        IF T_턴주인 == MASTER
            CALL T_시스템처리(102)
        ELSE
            CALL T_시스템처리(103)
        ENDIF
        PRINTFORML 그러나 빗나가고 말았다...
        RETURN 0
    ENDIF
ENDIF

@평타명중판정(ARG)
#FUNCTION
#DIM DYNAMIC 보정

SELECTCASE TB_거리()
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 - (3 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 -= 5
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 - (2 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 += 0
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 - (1 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 += 5
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리
        보정 += 10
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 + (1 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 += 5
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 + (2 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 += 0
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 + (3 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 -= 10
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 + (4 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 -= 20
    CASE IS < TA_평타사거리:T_턴주인:TA_최적사거리 + (5 * TA_평타사거리:T_턴주인:TA_사거리범위)
        보정 -= 35
    CASE IS > TA_평타사거리:T_턴주인:TA_최대사거리
        보정 -= 95
ENDSELECT

RETURNF 명중판정(MAX(ARG + 보정, 0))


@스킬명중판정(명중률,스킬번호,추가인수 = 0,지점 = 0,대상 = 0,디코이 = 0)
#FUNCTION
#DIM DYNAMIC 명중률
#DIM DYNAMIC 스킬번호
#DIM DYNAMIC 추가인수
#DIM DYNAMIC 보정
#DIM DYNAMIC 최적사거리
#DIM DYNAMIC 최대사거리
#DIM DYNAMIC 사거리범위
#DIM DYNAMIC 속성
#DIM DYNAMIC 유도력
#DIM DYNAMIC 판정
#DIM DYNAMIC 상대속도
#DIM DYNAMIC 지점
#DIM DYNAMIC 거리
#DIM DYNAMIC 대상
#DIM DYNAMIC 대상반대
#DIM DYNAMIC 디코이

IF 대상 == 0
    대상 = T_시전자반대 ;즉, 맞는 쪽
ENDIF
대상반대 = 사용자반대(대상) ; 때리는 쪽

상대속도 = ARG능력치(대상반대,"속도") - ARG능력치(대상,"속도")
IF TB_SUM_REMAIN(대상,TCV_속박)
    상대속도 += ARG능력치(대상,"속도")/2
ENDIF
IF TB_SUM_REMAIN(대상반대,TCV_속박)
    상대속도 -= ARG능력치(대상반대,"속도")/2
ENDIF
IF TB_SUM_REMAIN(대상,TCV_봉쇄)
    상대속도 += ARG능력치(대상,"속도")/2
ENDIF
IF TB_SUM_REMAIN(대상반대,TCV_봉쇄)
    상대속도 -= ARG능력치(대상반대,"속도")/2
ENDIF

IF 지점 == 0
    거리 = TB_거리_은신(대상)
ELSE
    거리 = TB_거리(대상,지점)
    IF 디코이
        거리 = TB_거리_은신(대상,지점)
    ENDIF
ENDIF

IF 추가인수 == 0
    CALLFORMF T_SKILL{스킬번호}_RANGE("최적사거리")
    최적사거리 = RESULT
    CALLFORMF T_SKILL{스킬번호}_RANGE("최대사거리")
    최대사거리 = RESULT
    CALLFORMF T_SKILL{스킬번호}_RANGE("범위변동단위")
    사거리범위 = RESULT
    CALLFORMF T_SKILL{스킬번호}_RANGE("조준속성")
    속성 = RESULT
    CALLFORMF T_SKILL{스킬번호}_RANGE("유도력")
    유도력 = RESULT
ELSE
    CALLFORMF T_SKILL{스킬번호}_{추가인수}_RANGE("최적사거리")
    최적사거리 = RESULT
    CALLFORMF T_SKILL{스킬번호}_{추가인수}_RANGE("최대사거리")
    최대사거리 = RESULT
    CALLFORMF T_SKILL{스킬번호}_{추가인수}_RANGE("범위변동단위")
    사거리범위 = RESULT
    CALLFORMF T_SKILL{스킬번호}_{추가인수}_RANGE("조준속성")
    속성 = RESULT
    CALLFORMF T_SKILL{스킬번호}_{추가인수}_RANGE("유도력")
    유도력 = RESULT
ENDIF
SELECTCASE 속성
    CASE T_근거리
        보정 += LIMIT(상대속도 / 20, -8 , 8)
    CASE T_고정탄
        SELECTCASE 거리
            CASE IS < 최적사거리 - (5 * 사거리범위)
                보정 -= 25
            CASE IS < 최적사거리 - (4 * 사거리범위)
                보정 -= 10
            CASE IS < 최적사거리 - (3 * 사거리범위)
                보정 -= 5
            CASE IS < 최적사거리 - (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 - (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리
                보정 += 10
            CASE IS < 최적사거리 + (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리 + (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 + (3 * 사거리범위)
                보정 -= 5
            CASE IS < 최적사거리 + (4 * 사거리범위)
                보정 -= 10
            CASE IS < 최적사거리 + (5 * 사거리범위)
                보정 -= 25
            CASE IS > 최대사거리 ;불가능하지만 뭐
                보정 -= 80
        ENDSELECT
        보정 += LIMIT(상대속도 / 20, -8 , 8)
    CASE T_조준탄
        SELECTCASE 거리
            CASE IS < 최적사거리 - (5 * 사거리범위)
                보정 -= 25
            CASE IS < 최적사거리 - (4 * 사거리범위)
                보정 -= 10
            CASE IS < 최적사거리 - (3 * 사거리범위)
                보정 -= 5
            CASE IS < 최적사거리 - (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 - (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리
                보정 += 10
            CASE IS < 최적사거리 + (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리 + (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 + (3 * 사거리범위)
                보정 -= 5
            CASE IS < 최적사거리 + (4 * 사거리범위)
                보정 -= 10
            CASE IS < 최적사거리 + (5 * 사거리범위)
                보정 -= 25
            CASE IS > 최대사거리 ;불가능하지만 뭐
                보정 -= 80
        ENDSELECT
        보정 += LIMIT(상대속도 / 10 , -15 , 15)
    CASE T_유도탄
        SELECTCASE 거리
            CASE IS < 최적사거리 - (5 * 사거리범위)
                보정 -= 20
            CASE IS < 최적사거리 - (4 * 사거리범위)
                보정 -= 10
            CASE IS < 최적사거리 - (3 * 사거리범위)
                보정 -= 5
            CASE IS < 최적사거리 - (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 - (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리
                보정 += 10
            CASE IS < 최적사거리 + (1 * 사거리범위)
                보정 += 5
            CASE IS < 최적사거리 + (2 * 사거리범위)
                보정 += 0
            CASE IS < 최적사거리 + (3 * 사거리범위)
                보정 -= 5 - (유도력/5) ;유도력 25부터 보너스
            CASE IS < 최적사거리 + (4 * 사거리범위)
                보정 -= 10 - (유도력/5) ;유도력 50부터 보너스
            CASE IS < 최적사거리 + (5 * 사거리범위)
                보정 -= 25 - (유도력/5) ;유도력 125부터 보너스
            CASE IS > 최대사거리 ;불가능하지만 뭐
                보정 -= 80
        ENDSELECT
        보정 += LIMIT((상대속도 + 유도력) / 20, -10 , 10)
    CASE T_즉발탄
        보정 += LIMIT(상대속도 / 20, -5 , 5)
    CASE T_버프기
        ;처리없어-
ENDSELECT
IF 거리 > 최대사거리 && 속성 != T_버프기
    RETURNF 0
ENDIF
판정 = 명중판정(MAX(명중률 + 보정, 0))
IF 명중률인터셉트
    판정 = 명중판정(MAX(명중률인터셉트,0))
ENDIF
RETURNF 판정


@T_COM회피한다(ARG)
#DIM DYNAMIC 회피량
;ARG = 회피하는 대상
IF ARG == MASTER
    PRINTFORML 공격을 피하기 위해 신경을 집중했다!  
    CALL T_시스템처리(120)
    ;========
    ;회피 판정
    ;========
    ;플레이어는 미니게임(화살표 방향 누르기)를 해야 버프를 받게해? 나중에 고려해본다
    ;
    
ELSEIF ARG == TARGET
    
    PRINTFORML %타겟은% 공격을 회피하려 하였다!
    CALL T_시스템처리(121)
ENDIF

회피량 = 100
회피량 += MIN(ARG능력치(ARG,"속도")/5, 50) + MIN(ARG능력치(ARG,"운")/5, 50)
IF TALENT:ARG:그레이즈능숙함
    회피량 *= 2
ENDIF
CALL 통합버프(ARG, TCV_회피,-1,회피량, 1)
CALL 행동기회소모(T_턴주인,T_통상행동)
RETURN 0


;리턴값 0-실패 1-성공
@T_COM도망간다(ARG)
#DIM DYNAMIC 도주판정값
#DIM DYNAMIC 도주저지값

도주판정값 = RAND:(MAX(ARG능력치(ARG,"속도") + ARG능력치(ARG,"운") + 1, 1)) + RAND:10 + 5 + TB_거리()
도주저지값 = RAND:(MAX(ARG반대능력치(ARG,"속도") + ARG반대능력치(ARG,"운") + 1, 1)) + ARG반대능력치(ARG,"이동력")
CALL 행동기회소모(T_턴주인,T_통상행동)
IF 도주판정값 >= 2* 도주저지값
    PRINTFORML %ARG는% 냅다 도망가버렸다!
    PRINTFORML %조사처리(CALLNAME:사용자반대(ARG),"는")% 그저 바라볼 수 밖에 없었다...
    IF ARG == T_대상1
        도망감 = 1
    ELSE
        도망감 = 2
    ENDIF
    IF ARG != MASTER
        CFLAG:ARG:현재위치 = SUKIMA()
    ENDIF
    RETURN 1
ELSEIF 도주판정값 >= 도주저지값
    PRINTFORML %ARG는% 필사적으로 도망가기 시작했다!
    IF 명중판정(50+MIN(ARG능력치(ARG,"속도")/10,25))
        PRINTFORML 저 멀리 사라지는 %ARG를% %조사처리(CALLNAME:사용자반대(ARG),"는")% 그저 바라볼 수 밖에 없었다...
        IF ARG == T_대상1
            도망감 = 1
        ELSE
            도망감 = 2
        ENDIF
        IF ARG != MASTER
            CFLAG:ARG:현재위치 = SUKIMA()
        ENDIF
        RETURN 1
    ELSE
        IF ARG == MASTER
            PRINTFORML 그러나 금새 뒤쫓아온 %CALLNAME:사용자반대(ARG)%에게 붙잡히고 말았다!
            RETURN 0
        ELSE
            PRINTFORML 뒤쫓을까?
            PRINTFORML [1] 예
            PRINTFORML [2] 아니오
            INPUT
            IF RESULT == 1
                PRINTFORML 필사적으로 뒤쫓아간 %마스터는% 도망가던 %ARG를% 따라잡는데 성공했다!
                RETURN 0
            ELSE
                PRINTFORML 저 멀리 사라지는 %ARG를% %조사처리(CALLNAME:사용자반대(ARG),"는")% 그저 바라만 보고 있었다...
                IF ARG == T_대상1
                    도망감 = 1
                ELSE
                    도망감 = 2
                ENDIF
                IF ARG != MASTER
                    CFLAG:ARG:현재위치 = SUKIMA()
                ENDIF
                RETURN 1
            ENDIF
        ENDIF
    ENDIF
ELSE
    PRINTFORML %ARG는% 필사적으로 도망가기 시작했다!
    PRINTFORML 그러나 %조사처리(CALLNAME:사용자반대(ARG),"의")% 속도가 너무 빨라 금새 따라잡히고 말았다!
    RETURN 0
ENDIF

@T_COM이동한다(ARG)
IF ARG == -2
    CALL T_MOVE(T_대상1,-1)
ELSEIF ARG == -1
    CALL T_MOVE(T_대상1,-1,0,1)
ELSEIF ARG == 1
    CALL T_MOVE(T_대상1,1,0,1)
ELSEIF ARG == 2
    CALL T_MOVE(T_대상1,1)
ENDIF
RETURN 1

@T_COM스킬사용
#DIM 스킬번호
#DIM DYNAMIC 시전가부
#DIM DYNAMIC 쿨타임

CALL T_시스템처리(110)

T_시전자 = T_턴주인
T_시전자반대 = T_턴반대
$스킬표시
PRINTL 무엇을 쓸까?
DRAWLINE
PRINTL ┌[번호]─[스킬명]────────────────────┬[쿨타임]┬[ＨＰ소모량]┬[마  력]┬[사거리]┬[조준속성]┬[기본적중률]┬[행동]┐
FOR 스킬번호,1,T_스킬한계값
    ;IF HAVE_T_SKILL(T_턴주인, 스킬번호)
    IF IS_MEMORIZED(스킬번호)
        ;스킬 소유, 스킬창에 이름 표시
        CALLFORMF T_SKILL{스킬번호}("타입")
        IF RESULT == 자동형
            CONTINUE
        ENDIF
        PRINT │
        CALL CAN_T_SKILL(T_턴주인, 스킬번호)
        시전가부 = RESULT
        IF RESULT == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
        PRINTFORM [{스킬번호, 4, RIGHT}]　%RESULTS, 46, LEFT%  
        RESETCOLOR
        PRINT │
        IF 시전가부 == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORM ONGOING_T_SKILL{스킬번호}(MASTER,"남은쿨타임")
        쿨타임 = RESULT
        IF RESULT > 0
            쿨타임 *= -1
        ELSE
            ;쿨타임 0, 시전가능
            CALLFORMF T_SKILL{스킬번호}("쿨타임")
            쿨타임 = RESULT
        ENDIF
        PRINTFORM {쿨타임,8}
        RESETCOLOR
        PRINT │
        CALLFORMF T_SKILL{스킬번호}("HP코스트")
        IF RESULT > 0
            SETCOLOR C_RED
            PRINTFORM {RESULT, 12, LEFT}
            RESETCOLOR

        ELSE
            PRINTFORM 　　　　　　
        ENDIF
        PRINT │
        CALLFORMF T_SKILL{스킬번호}("마력코스트")
        IF RESULT > 0
            SETCOLOR 0x1111FF
            PRINTFORM {RESULT, 8, LEFT}
            RESETCOLOR
        ELSE
            PRINTFORM 　　　　
        ENDIF
        PRINT │
        IF 시전가부 == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_RANGE("최대사거리")
        PRINTFORM {RESULT,6,RIGHT}ｍ
        RESETCOLOR
        PRINT │
        PRINT 　
        IF 시전가부 == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}_RANGE("조준속성")
        SELECTCASE RESULT
            CASE T_근거리
                PRINT 근거리
            CASE T_고정탄
                PRINT 고정탄
            CASE T_조준탄
                PRINT 조준탄
            CASE T_유도탄
                PRINT 유도탄
            CASE T_즉발탄
                PRINT 즉　발
            CASE T_버프기
                PRINT 버프기
        ENDSELECT
        PRINT 　
        RESETCOLOR
        PRINT │
        IF 시전가부 == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        CALLFORMF T_SKILL{스킬번호}("명중률")
        PRINTFORM {RESULT,10,RIGHT}％
        RESETCOLOR
        PRINT │
        IF 시전가부 == 1
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전가능")
            SETCOLOR RESULT
        ELSE
            CALLFORM T_SKILL{스킬번호}_COLOR("색깔_시전불가")
            SETCOLOR RESULT
        ENDIF
        RESULT = 0
        CALLFORMF T_SKILL{스킬번호}("행동코스트")
        PRINTFORM {RESULT+1,2,RIGHT}／
        RESULT = 0
        CALLFORMF T_SKILL{스킬번호}("이동코스트")
        PRINTFORM {RESULT,2,LEFT}
        RESETCOLOR
        PRINTL │
    ELSE
        ;스킬 소유하고 있지 않음
    ENDIF
NEXT
PRINTL └────────────────────────────┴────┴──────┴────┴────┴─────┴──────┴───┘
PRINTL
PRINTL [9998]설명
PRINTL [9999]돌아간다
WHILE 1 ;$스킬입력루프
    INPUT
    LOCAL = RESULT
    IF LOCAL == 9999
        RETURN 0
    ELSEIF LOCAL == 9998
        CALL T_HELP
        GOTO 스킬표시
    ;ELSEIF HAVE_T_SKILL(T_턴주인, LOCAL)
    ELSEIF IS_MEMORIZED(LOCAL)
        DRAWLINE
        CALL CAST_SPELL(T_턴주인, LOCAL)
        ;시전할수 없을때엔 턴 넘김 없어야 합니다
        ;RESULT == 1일때만 턴 넘어감
        RETURN RESULT
    ELSE
        ;시전 불가시는 DO_T_SKILL에서 처리
        PRINTL
        PRINTW 없는 번호입니다.
        CLEARLINE 1
    ENDIF
WEND

@T_HELP
#DIM 스킬번호
#DIM DYNAMIC IS_BATTLE
#DIM DYNAMIC PAGE
#DIM DYNAMIC SKIPCOUNT
#DIM DYNAMIC PRINTCOUNT
#DIM DYNAMIC LASTPAGE
#DIM DYNAMIC 출력형태
#DIMS DYNAMIC 검색문구
IF BATTLE_NOW
    T_시전자 = T_턴주인
    T_시전자반대 = T_턴반대
    IS_BATTLE = 1
ELSE
    T_시전자 = MASTER
    T_시전자반대 = MASTER 
ENDIF
$LOOP
PRINTL
PRINT 어떤 스킬의 설명을 볼까? 
IF 출력형태 != 1
    PRINTBUTTON "[검색모드]" , 514514
ELSE
    PRINTBUTTON "[통상모드]" , 514514
ENDIF
PRINTL
DRAWLINE
PRINTCOUNT = 0
SKIPCOUNT = 0
FOR 스킬번호,1,T_스킬한계값
    IF IS_BATTLE
        IF IS_MEMORIZED(스킬번호)
            IF 출력형태 == 1 && STRFIND(TALENTNAME:(500+스킬번호), 검색문구) == -1
                CONTINUE
            ENDIF
        ELSE
            CONTINUE
        ENDIF
    ELSE
        IF HAVE_T_SKILL(MASTER, 스킬번호)
            IF 출력형태 == 1 && STRFIND(TALENTNAME:(500+스킬번호), 검색문구) == -1
                CONTINUE
            ENDIF
        ELSE
            CONTINUE
        ENDIF
    ENDIF
    IF PAGE * 25 > SKIPCOUNT
        SKIPCOUNT++
        CONTINUE
    ENDIF
    ;스킬 소유, 스킬창에 이름 표시
    CALLFORMF T_SKILL{스킬번호}_STR("스킬명")
    PRINTFORMLC [{스킬번호}] %RESULTS%
    RESETCOLOR
    PRINTL
    PRINTCOUNT++
    IF PRINTCOUNT >= 25
        BREAK
    ENDIF
NEXT
PRINTL
DRAWLINE
IF PRINTCOUNT < 25
    LASTPAGE = 1
ENDIF
PRINTFORML [11111] 이전페이지
PRINTFORML [22222] 다음페이지
PRINTL [9999]돌아간다
WHILE 1
    INPUT
    LOCAL = RESULT
    CLEARLINE 1
    IF LOCAL == 9999
        RETURN 0
    ELSEIF RESULT == 11111
        LASTPAGE = 0
        PAGE = MAX(PAGE - 1, 0)
        CLEARLINE 31
        GOTO LOOP
    ELSEIF RESULT == 22222
        IF LASTPAGE != 1
            PAGE++
            CLEARLINE 31
        ELSE
            CLEARLINE PRINTCOUNT
            CLEARLINE 6
        ENDIF
        GOTO LOOP
    ELSEIF RESULT == 514514
        IF 출력형태 == 0
            출력형태 = 1
            $검색어루프
            PRINT 검색어를 입력해 주세요
            INPUTS
            IF STRLENS(RESULTS) >= 20
                PRINTFORMW 너무 깁니다
                CLEARLINE 2
                GOTO 검색어루프
            ELSE
                검색문구 = %RESULTS%
                CLEARLINE 2
            ENDIF
        ELSE
            출력형태 = 0
        ENDIF
        GOTO LOOP
    ELSEIF (IS_MEMORIZED(LOCAL)&&IS_BATTLE)||(HAVE_T_SKILL(MASTER, LOCAL)&&!IS_BATTLE)
        DRAWLINE
        CALLFORMF T_SKILL{LOCAL}_STR("스킬명")
        PRINTFORML %RESULTS%
        PRINT 타입 :
        CALL 타입표시(LOCAL)
        CALLFORMF T_SKILL{LOCAL}_STR("속성")
        PRINTFORM 속성 : %RESULTS%  
        CALLFORMF T_SKILL{LOCAL}("명중률")
        PRINTFORML 명중률 : {RESULT}％
        CALLFORMF T_SKILL{LOCAL}("공격력")
        PRINTFORML 피해량 : {RESULT}
        PRINTL
        CALLFORMF T_SKILL{LOCAL}_STR("계수")
        IF RESULTS:0 != "없음" && RESULTS:0 != ""
            PRINTFORM 계수 : 
            FOR LOCAL:1,0,4
                IF RESULTS:(LOCAL:1) != "없음" && RESULTS:(LOCAL:1) != ""
                    IF LOCAL:1 != 0
                        PRINTFORM  + 
                    ENDIF
                    FONTSTYLE 1
                    PRINTFORM %RESULTS:(LOCAL:1)%
                    FONTSTYLE 0
                ENDIF
            NEXT
            PRINTFORM  / 타입 :
            CALLFORMF T_SKILL{LOCAL}_RANGE("조준속성")
            SELECTCASE RESULT
                CASE T_근거리
                    PRINT  근거리
                CASE T_고정탄
                    PRINT  고정탄
                CASE T_조준탄
                    PRINT  조준탄
                CASE T_유도탄
                    PRINT  유도탄
                CASE T_즉발탄
                    PRINT  즉발
                CASE T_버프기
                    PRINT  버프기
            ENDSELECT
        ENDIF
        PRINTL
        PRINTL
        CALLFORM HELP_T_SKILL{LOCAL}
        DRAWLINE
        PRINTW
        CLEARLINE 18
    ELSE
        ;시전 불가시는 DO_T_SKILL에서 처리
        PRINTL
        PRINTW 없는 번호입니다.
        CLEARLINE 1
    ENDIF
    RESULT = 0
WEND

@타입표시(ARG)
CALLFORMF T_SKILL{ARG}("타입")
IF RESULT & 공격형
    PRINT  공격형
ENDIF
IF RESULT & 방어형
    PRINT  방어형
ENDIF
IF RESULT & 회복형
    PRINT  회복형
ENDIF
IF RESULT & 상태이상형
    PRINT  상태이상형
ENDIF
IF RESULT & 버프형
    PRINT  버프형
ENDIF
IF RESULT & 해주형
    PRINT  해주형
ENDIF
IF RESULT & 자동형
    PRINT  자동형
ENDIF
PRINTL

;시전 대응
@CAST_SPELL(사용자, 스킬번호)
#DIM DYNAMIC 사용자
#DIM DYNAMIC 스킬번호
#DIM DYNAMIC 스킬타입
CALL DO_T_SKILL(사용자, 스킬번호)
IF RESULT
    ;CALL 행동기회소모(사용자,T_통상행동)
    CALLFORMF T_SKILL{스킬번호}("행동코스트")
    CALL 행동기회소모(사용자,T_통상행동,RESULT + 1)
    RESULT = 0
    CALLFORMF T_SKILL{스킬번호}("이동코스트")
    CALL 행동기회소모(사용자,T_이동행동,RESULT)
ENDIF

@DO_T_SKILL(사용자, 스킬번호)
#DIM DYNAMIC 사용자
#DIM DYNAMIC 스킬번호
#DIM DYNAMIC 스킬타입
;스킬의 시전시 처리, 대사만 넣으면 된다
CALL CAN_T_SKILL(사용자, 스킬번호)
IF RESULT == 0
    ;시전불가
    PRINTL 시전할수 없습니다
    RETURN 0
ELSE
    ;시전성공
    CALL PRINT_SPELLNAME(스킬번호)
    ;이 아래의 대사 작성
    CALLFORMF T_SKILL{스킬번호}("타입")
    스킬타입 = RESULT
    RESULT = 0
    CALLFORM AFTER_T_SKILL{스킬번호}(사용자)
    IF RESULT == 0
        IF 사용자 == T_대상1
            CALL T_시스템처리(250,스킬번호,스킬타입)
        ELSEIF 사용자 == T_대상2
            CALL T_시스템처리(251,스킬번호,스킬타입)
        ENDIF
    ENDIF
    RETURN 1
ENDIF

;그 스킬을 소유하고 있는지 여부
;보유 대응
@HAVE_T_SKILL(사용자, 스킬번호)
#FUNCTION
#DIM 사용자
#DIM 스킬번호
LOCAL = 스킬번호 + 500 ;TALENT.csv 내 스킬번호

SIF TALENTNAME:LOCAL == ""
    RETURNF 0

SIF TALENT:사용자:LOCAL == 1
    RETURNF 1
SIF 탄막디버그 == 1
    RETURNF 1

RETURNF 0

;지금 스킬을 시전할수 있는지
;실행가부 대응
@CAN_T_SKILL(사용자, 스킬번호)
#DIM 사용자
#DIM 스킬번호
;식중함수는 CALLFORM 사용 불가 이기 때문에 수정

IF HAVE_T_SKILL(사용자, 스킬번호)
    ;이 스킬 소유중
    IF 사용자 == MASTER
        IF !IS_MEMORIZED(스킬번호)
            RETURN 0
        ENDIF
    ENDIF
    TRYCALLFORM ONGOING_T_SKILL{스킬번호}(사용자, "남은쿨타임")
    IF RESULT == 0
        ;쿨타임 0 시전가능
        RESULT = 0
        TRYCALLFORM DENY_SILENCE{스킬번호}
        IF TB_MAX_VALUE(사용자,TCV_침묵) > RESULT - 1 && TB_MAX_VALUE(사용자,TCV_침묵)
            RETURN 0
        ENDIF
        CALLFORMF T_SKILL{스킬번호}("HP코스트")
        IF RESULT >= TCVAR:사용자:T_체력
            RETURN 0
        ENDIF
        RESULT = 0
        IF 사용자 == MASTER
            CALLFORMF T_SKILL{스킬번호}("마력코스트")
            IF RESULT > BASE:사용자:마력
                RETURN 0
            ENDIF
        ENDIF
        RESULT = 0

        CALLFORMF T_SKILL{스킬번호}_RANGE("조준속성")
        IF RESULT != T_버프기
            RESULT = 0
            CALLFORMF T_SKILL{스킬번호}_RANGE("최대사거리")
            IF RESULT < TB_거리_은신(사용자반대(사용자))
                RETURN 0
            ENDIF
        ENDIF
        RESULT = 0
        TRYCALLFORM SKILLTYPE_MOVE{스킬번호}
        IF RESULT
            IF !T_MOVEABLE(사용자) && RESULT == 2
                RETURN 0
            ENDIF
            IF !T_TPABLE(사용자) && RESULT == 3
                RETURN 0
            ENDIF
        ENDIF
        RESULT = 0
        CALLFORMF T_SKILL{스킬번호}("행동코스트")
        IF RESULT + 1 > T_행동기회:사용자:T_통상행동
            RETURN 0
        ENDIF
        RESULT = 0
        CALLFORMF T_SKILL{스킬번호}("이동코스트")
        IF RESULT > T_행동기회:사용자:T_이동행동
            RETURN 0
        ENDIF
        RESULT = 0
        RETURN 1
    ELSE
        ;아직 쿨타임이라 시전불가능
        RETURN 0
    ENDIF
ELSE
    ;이스킬 없음, 시전불가
    RETURN 0
ENDIF

;해당 문자열을 변수명으로 하는 TCVAR 내 값을 반환함.
@ARG능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치
#DIM 결과값
IF ARG >= CHARANUM
    THROW ARG가 너무 큽니다, 현재 CHARANUM:{CHARANUM}, ARG:{ARG}, RANDOM_CHARANUM:{RANDOM_CHARANUM}, 마지막번호의캐릭터:%CALLNAME:(CHARANUM - 1)%
ENDIF
SELECTCASE 능력치
    CASE "힘"
        결과값 = TCVAR:ARG:T_힘
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:힘 + T_능력치보너스:ARG:0
    CASE "지성"
        결과값 = TCVAR:ARG:T_지성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:지성 + T_능력치보너스:ARG:1
    CASE "속도"
        결과값 = TCVAR:ARG:T_속도
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:속도 + T_능력치보너스:ARG:2
    CASE "인내"
        결과값 = TCVAR:ARG:T_인내
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:인내 + T_능력치보너스:ARG:3
    CASE "운"
        결과값 = TCVAR:ARG:T_운
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:운 + T_능력치보너스:ARG:4
    CASE "체력"
        결과값 = TCVAR:ARG:T_체력
        SIF !BATTLE_NOW
            결과값 = BASE:ARG:체력
    CASE "최대체력"
        결과값 = TCVAR:ARG:T_최대체력
        SIF !BATTLE_NOW
            결과값 = MAXBASE:ARG:체력
    CASE "공격력"
        결과값 = TCVAR:ARG:T_공격력
    CASE "방어력"
        결과값 = TCVAR:ARG:T_방어력
    CASE "물리내성"
        결과값 = TCVAR:ARG:T_물리내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:물리내성
    CASE "화염내성"
        결과값 = TCVAR:ARG:T_화염내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:화염내성
    CASE "빙결내성"
        결과값 = TCVAR:ARG:T_빙결내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:빙결내성
    CASE "전격내성"
        결과값 = TCVAR:ARG:T_전격내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:전격내성
    CASE "질풍내성"
        결과값 = TCVAR:ARG:T_질풍내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:질풍내성
    CASE "마력내성"
        결과값 = TCVAR:ARG:T_마력내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:마력내성
    CASE "수격내성"
        결과값 = TCVAR:ARG:T_수격내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:수격내성
    CASE "정신내성"
        결과값 = TCVAR:ARG:T_정신내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:정신내성
    CASE "물리증폭"
        결과값 = TBTCVAR:ARG:T_물리증폭
    CASE "화염증폭"
        결과값 = TBTCVAR:ARG:T_화염증폭
    CASE "빙결증폭"
        결과값 = TBTCVAR:ARG:T_빙결증폭
    CASE "전격증폭"
        결과값 = TBTCVAR:ARG:T_전격증폭
    CASE "질풍증폭"
        결과값 = TBTCVAR:ARG:T_질풍증폭
    CASE "마력증폭"
        결과값 = TBTCVAR:ARG:T_마력증폭
    CASE "수격증폭"
        결과값 = TBTCVAR:ARG:T_수격증폭
    CASE "정신증폭"
        결과값 = TBTCVAR:ARG:T_정신증폭
    CASE "관통내성"
        결과값 = TCVAR:ARG:T_관통내성
        SIF !BATTLE_NOW
            결과값 = CFLAG:ARG:관통내성
    CASE "적중률"
        결과값 = TCVAR:ARG:T_적중률
    CASE "이동력"
        결과값 = TCVAR:ARG:T_이동력
    CASE "레벨"
        결과값 = ABL:ARG:레벨
    CASEELSE
        THROW 상정하지 않은 능력치 %능력치%
ENDSELECT

RETURNF 결과값

@ARG반대능력치(ARG,능력치)
#FUNCTION
#DIMS 능력치

IF ARG == T_턴주인
    RETURNF ARG능력치(T_턴반대, 능력치)
ELSEIF ARG == T_턴반대
    RETURNF ARG능력치(T_턴주인, 능력치)
ENDIF

@HP_PERCENT(ARG,ARG:1)
#FUNCTION
RETURNF (ARG능력치(ARG,"최대체력") * ARG:1)/100

@TSP_PERCENT(ARG,ARG:1 = 0)
#FUNCTION
#DIM DYNAMIC 소모량
#DIM DYNAMIC 기준지성
;기준지성 = 300
기준지성 = MAX(100,(ARG능력치(MASTER,"지성") / 100) * 100) ; 100 단위로 기준이 올라가도록
;소모량 = ((MAXBASE:MASTER:마력 * ARG * 10)+(MAXBASE:MASTER:마력 * ARG:1))/1000
소모량 = (((기준지성 * 100) * ARG * 10)+((기준지성 * 100) * ARG:1))/1000
;즉, 1% = 300, 0.1% = 30
IF 소모량 <= 0
    소모량 = 1
ENDIF
RETURNF 소모량