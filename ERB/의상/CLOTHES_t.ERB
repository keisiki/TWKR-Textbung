[SKIPSTART]
◆의상 설정의 사양
·기본적으로는 기법을 바꾸어 가독성을 올렸을 뿐
·GET_INT/GET_STR를 사용하는 것으로 정보를 취득할 수가 있다
·1개의 의상에 대해 2함수를 사용한다(나 1개는 존부 함수)
· 각 의상은 의상 카테고리(≒EQUIPNAME와 닮아 있지만 조금 바꾼) 마다 관리한다
·같은 의상 카테고리 속에서 동일 명칭의 장비품은 만들 수 없다
　반대로 말하면, 다른 의상 카테고리이다면 명칭은 중복 해도 상관없다
　예) 「머리장비」의 리본과 「그외의상」의 리본
　다만, 「외투」의 백의(입는 있고)와 「상반신윗도리_탈의불가」의 백의(입는 네, 먹을 수 있다)의 경우는
　「동일 표기로 지시하는 것이 완전히 다르다」위해(때문에) 상반신윗도리1의 옆을 변경했다(헷갈려)
·의상 카테고리와 장비부위는 기본적으로 일대일의 대응 관계에 있다
　예를 들어 「머리장비」는 EQUIP:모자에게만 장비 가능
　다만, 예외로서 「그외의상」은 EQUIP:그외1, EQUIP:그외2, EQUIP:그외3의 모두에 장비 할 수 있다
　이 사양은 바꾸려고 생각하면 바꿀 수 있지만@CLOTHES_ID나 전의상 ID의 재설정이 필요하게 될 것(즉 귀찮아)
·의상의 참조 시에는 이와 같이 한다
　GET_INT(등록 번호등 , 의상 카테고리, 의상의 카테고리내 ID, 참조하고 싶은 정보의 종류)
·특정의 이름의 의상의 ID를 취득하고 싶을 때는 이와 같이 한다
　CLOTHES_ID(장비부위명, 장비명)
·장비부위로부터 해당하는 의상 카테고리를 취득할 때는 이와 같이 한다
　CLOTHES_PARTS_TO_CATEGORY(장비부위명)
·이상의 함수로부터, 장비부위와 장비명만으로(번호를 사용하지 않고 ) 장비를 참조할 수가 있다
　GET_INT(등록 번호등 , CLOTHES_PARTS_TO_CATEGORY(장비부위명), CLOTHES_ID(장비부위명, 장비명), 참조하고 싶은 정보의 종류)
　―, 현재 장비중의 장비의 참조라면 CLOTHES_ID는 불필요해서
　GET_INT(등록 번호등 , CLOTHES_PARTS_TO_CATEGORY(장비부위명), EQUIP:등록 번호:장비부위명, 참조하고 싶은 정보의 종류)
　이러한

·자신용의 각서
　(하반신속옷계는 또 구별)
　아마 이런 생각으로 좋다고 생각한다
　상반신윗도리1(탈의 불가)이 바깥쪽, 상반신윗도리2(탈의 가능)는 안쪽에 착용하는 의상


·의상 나토리 이득 함수 @CLOTHNAME
각 장비의 설정으로부터 이끌어 올 수 있게 되었으므로 큰폭으로 간략화

·복장 나토리 이득 함수(@SHOW_BOTTOM등 )
단지 가장 위의 장비의 이름을 돌려준다 게변경(간략화)하고 있습니다
이름과는 별도로 하고 싶으면 STR용으로"묘사"든지를 전의상으로 설정하면 간단하게 참조할 수 있습니다

·갈아입히기 관련에 대해서도 유사한 사양으로 실장했다
·의상세트함수군
　이 함수군은 갈아입히기 세트의 내용을 기술한다
·캐릭터데이터함수군
　이 함수군은 각 캐릭터의 개별의 데이터를 일원 관리시킨다
　평상복의 설정은 각 캐릭터로 독립하고 있기 위해서(때문에) 여기에 기술했다
　또, 앨리스의 의상에 존재하고 있던 섣달그믐 의복 처리의 실현을 위해서(때문에)
　평상복의 전환 제어의 기능도 추가했다
　요일에 의해 바꾼다든가도 하고 싶다면 할 수 있다
　노려라 칠색의 마법사
·의상 설정에 대해
　갈아입히기에 의해 「이 복장을 매일 입도록(듯이)」라고 말하는 지정이 가능
　0　　　Default인 채
　1~99　COORDINATE1~99를 지정
　100　　수동으로 설정한다(CFLAG:600~630)
　101~특정 캐릭터의 흉내(특정 캐릭터의 평상복을 참조하는, 101이라면 레이무)
·파자마 설정에 대해
　파자마는 그렇게 구애되어도 방법이 없는데라고 생각했으므로
　COORDINATE 함수 속으로부터 어떤 것을 입게 하는지를 선택하는 형식으로 했다
　·파자마(주반)
　·파자마(네글리제)
　·파자마(베이비돌)
　·파자마(원피스)
　·파자마(바지상하)
　·파자마(치마상하)
　·파자마(바지＋후드)
　·파자마(치마＋후드)
　다만, 예외로서"판이치"로 팬티 일인분,"전라"에서는이다 관로 자게 할 수가 있다
　(그러한 처리가 실장되고 있었던 딸(아가씨)가 있었으므로)



◆개개의 장비품 주위의 변경
·명칭 변경
　네글리제→롱 네글리제
　화상 검색의 결과가 너무 과격하므로 명칭 변경
　베이비돌와의 차별화를 위해서(때문에) 어른 제한 표지인 의상을 상정한다
·카테고리 변경
　「상반신윗도리_탈의불가」의 「백의(입는 네, 먹을 수 있다)」의 명칭을 「흰 소매」로 변경
　의상세트계에서는 「신관 세트」만이 사용하고 있어 「양호실의 여교사 세트」에서도 「백의의 천사 세트」에서도 사용하지 않았다
　또, 평상복 사용 캐릭터도 없었기 때문에 변경
　
　게다가 이 「흰 소매」는 안에 껴입는 의상인 것을 생각되기 (위해)때문에,
　의상 카테고리를 「상반신윗도리_탈의가능」으로 이동했다
·추가 의상
　머리장비「후드」, 파자마용으로 신설
　스레에서의 논의로 나온 아이디어를 채용시켜 주었습니다
·추가 의상
　완부의복「암 커버」, 파르시용으로 신설
·추가 의상
　상반신의상_탈의 불가 「관복」, 후토용으로 신설
　무녀에서도 사용하는 곳(중) 있는 것 같구나
·추가 의상
　상반신의상_탈의 불가 「신관복」
　화 같을 방향을 확충하고 싶었다
　흰색이라면 매우 신관 같아요
·추가 의상
　신발「답」
　신관이든지 귀족적인 의복이라고 하는 것으로 「관복」 「신관복」이라고세트를 상정해 추가
·추가 의상
　그외「브로치」
　카게로의 목 언저리의 매우 인상적인 브로치가 없었기 때문에 증설했다
　레티씨에게도 장비



◆의상 카테고리의 이름에 대해
사양이 까다로워져 버리는 것은 감안하고 가
왜 일부의 의상 카테고리의 이름을 EQUIPNAME와 바꾸어 있는가 하면
그것은 이하의 이유로부터입니다
　이유 1：함수명으로@하반신속옷1  2혹은@하반신속옷1_2이라든지 의 것이 너무나 기색 너무 나쁘다
　이유 2：직감적이 아닌 EQUIPNAME가 있다
　　　　　·리본이나 캬츄샤(헤어밴드)는 「모자」
　　　　　·하반신속옷1으로 하반신속옷2의 구분의 차이가 이해하기 어렵다(이 번호도 magic number-)
　　　　　·우선 「탈의 가능·탈의 불가」 「비켜 놓기 가능·비켜 놓기 불가」의 이름으로 했다
　　　　　·아무래도 이 분류도 불명확인 것 같지만 번호보다는 좋은가?
　　　　　·우선 잠정의 사양입니다
　이유 3：EQUIPNAME측을 만지작거리려고 하면(자) 수정 개소가 방대
　　　　　추가 부분만큼 바꾸는 편이 편안(간편)

이하, EQUIPNAME와 의상 카테고리의 대응

액세서리
→액세서리
　그 외는 다른,  어떠한 효과를 가지는 일이 있다
　우산은 날씨 의존으로 행동시의 BASE 소비 경감에 사용되는 일이 있다

모자
→머리장비
　모자 뿐만이 아니라 화이트브림이나 리본등이라고 하는 머리 부분 장식품을 가리킨다

화
→화
　실내에서는 벗는다

양말
→양말
　일부의 캐릭터는 콘피그에 의해 평상복의 의상이 니소가 된다

하반신속옷1
→하반신속옷_비켜놓기불가

하반신속옷2
→하반신속옷_비켜놓기가능
　하반신속옷(비켜 놓기 가능)
　이른바 팬티(＋비키니의 하반신만)
　독특한 처리가 많기 때문에, 다른 의상과는 별취급으로 관리한다

상반신속옷1
→상반신속옷_탈의불가

상반신속옷2
→상반신속옷_탈의가능

레오타드
→레오타드

보디슈트
→보디슈트

바지
→바지
　비켜 놓기 불가

하반신윗도리
→해당 장비가 너무 적으므로 폐지, 부활시키고 싶은 경우는 그 외든지로 추가해 주세요

치마
→치마
　넘길 수 있는 하반신장비

상반신윗도리1
→상반신윗도리_탈의불가

상반신윗도리2
→상반신윗도리_탈의가능

원피스
→원피스

기모노
→기모노

외투
→외투
　의복과는 별도로 위로부터 겉옷 비쳐보이는 의상

그외1, 그외2, 그외3
→1~3을 전부 정리해 「그외의상」에 통합

완부의복
→완부의복
　주인의 장갑계



◆팬티
특수한 처리가 많은 것 같고 별개여서 처리한다
·CFLAG:오늘의팬티
　하루의 최초로 오늘팬티를 설정해 CFLAG로 보유
·CFLAG:파자마팬티
　하루의 최초로 파자마용의 팬티를 설정해 CFLAG로 보유
　파자마로 갈아입었을 때에 노팬티 라면 파자마팬티를 착용한다
　파자마팬티를 착용한 시점에서 파자마팬티에 0이 대입되기 (위해)때문에
　무한 파자마팬티는 불가능합니다



◆프롭퍼티에 대해

장비품계 클래스의 프롭퍼티
CASE "이름"
	이름, 그 의상의 명칭(STR)
	의상 카테고리내에서 중복 시킬 수 없다
	CALLF MAKE_STR(V_NAME, "안경")
CASE "장비부위"
	그 의상을 장비 하는 EQUIP(STR)
	CALLF MAKE_STR(V_NAME, "「액세서리」")
CASE "갈아입히기 가능"
	팬티 한정, 갈아입히기 커맨드로 설정할 수 있을지 어떨지
	비키니 보텀, 마이크로 보텀 한정
	CALLF MAKE_INT(V_NAME, 1)
	만들었지만 수동 갈아입히기 기능이 되어 있지 않기 때문에사실 미실장이다
CASE "입다"
	오늘의팬티에서의 판정용(INT, 라고 할까 진위치1/0)
	이 항목을 만들지 않는 팬티는 캐릭터가 자발적으로 신을 것은 없다



의상세트클래스의 프롭퍼티
CASE "이름"
	이름, 그 의상세트의 명칭(STR)
	의상세트클래스내에서 중복 시킬 수 없다
CASE "각종 장비부위"
	그 장비부위에 덮어 씌우는 의상의 내용(INT)
	항목을 만들지 않았던 부위는 0으로 간주해지고 아무것도 착용하지 않는다
CASE "기본 의상세트"
CASE "파자마 세트"
CASE "코스프레 의상세트"
CASE "캐릭터 특수 의상세트"
	이것들은 모두 의상세트의 종류를 나타낸다(INT)
	―, 현상으로 그것 같게 했을 뿐입니다
	1으로 하면(자) 그 속성의 의상세트라고 보인다
	리스트 표시용의 설정입니다



캐릭터데이터클래스의 프롭퍼티
CASE "통칭"
	필수, STR
	통칭, 그 캐릭터의 명칭
	캐릭터데이터클래스내에서 중복 시킬 수 없다
CASE "오늘의 파자마"
	필수, STR
	착용하는 파자마의 종류를 기술한다
	파자마의 종류의 판정으로서는
	1. 우선, 예외를 처리한다(코이시를 참조)
	"판이치"를 지정했을 경우, 파자마는 착용하지 않고 속옷만을 착용한다
	"전라"를 지정했을 경우, 옷은 굳이 입지 않는
	2. 다음에, STR를 기초로 의상세트를 참조한다
	일치하는 의상세트가 존재하는 경우는 그 의상세트를 착용한다
	와이셔츠만이라든지도 가능(팬티의 유무는 별개로 지정)
	3. 마지막으로, STR를 기초로 캐릭터데이터를 참조한다
	STR == "평상복"이라면 그 캐릭터의 평상복이 참조된다

CASE "평상복：각종 장비부위"
	필수, INT
	평상복의 의상 설정
	그 장비부위에 덮어 씌우는 의상의 내용
	항목을 만들지 않았던 부위는 0으로 간주해지고 아무것도 착용하지 않는다
	「평상복」이외의의상의 캐릭터별 지정도 가능
	예)"섣달그믐：각종 장비부위"에서 섣달그믐 용의 의상세트를 작성할 수 있다
	다만, 이 경우는"오늘의 의상", "내일의 의상"을 설정할 필요가 있다
CASE "오늘의 의상", "내일의 의상"
	생략가능, STR
	평상복 이외의의상을 설정하지 않는 경우에는 필요없다
	특정 조건에 맞추어 의상을 변경시키고 싶을 때에 사용한다
	SIF (DAY:2 == 1 && DAY:3 == 1) || (DAY:2 == 4 && DAY:3 == 31)
		CALLF MAKE_STR(V_NAME, "섣달그믐")
	이와 같이 기술하면(자) 조건에 합치할 때에 「섣달그믐」의 의상을 착용한다(앨리스의 처리를 참조)
	
	"오늘의 의상", "내일의 의상"의 무엇이 다른가 하면(자)
	의상의 갱신 처리는 MASTER가 기상했을 때에 실시하므로
	그 시점에서 수면중(=파자마 모습)이라면 내일의 의상을 설정해
	그 시점에서 활동중이라면 현재 착용중의 평상복을 설정하게 된다
	
	주) 기능 추가
	STR로 의상세트에 존재하는 이름을 돌려주었을 때에는 그 의상을 세트 하는 기능을 추가


	의상 지정용의 캐릭터라인으로서"추천", "고정"은 사용 불가능(다른 기능으로 사용중)
	
CASE "추천：각종 장비부위", "고정：각종 장비부위"
	보고 실상
	하려고 생각하면 할 수 있는, 만들까 헤매고 있다
	
	생략가능, INT
	캐릭터마다의 「이것은 제외하면 안 돼이겠지」라고 말하는 장비용의 처리
	지정한 부위의 장비를 추천·강요한다
	Emuera의 config에 있어서의 default/fix같은 것
	이 정보는 갈아입히기 관련 처리의 마지막에 적용된다
	례) 카센
	·올림머리망는 「추천」
	·붕대와 구속도구는 「고정」
	추천을 지정했을 경우, 그 부위에 장비 하고 있는 것이 아무것도 없으면 장비를 실시한다
	고정을 지정했을 경우, 그 부위의 장비를 덧쓰기한다
	알몸이 될 때는
	EQUIP:0 = 0한 다음 고정 장비만 착용한다
[SKIPEND]


;EQUIP:X:ARG에 장비 했다
;ARG:1의 의상의 이름을 돌려준다
@CLOTHNAME(ARG, ARG:1)
#FUNCTIONS
RETURNF GET_STR(, CLOTHES_PARTS_TO_CATEGORY(EQUIPNAME:(ARG:0)), ARG:1, "이름")



;장비부위로부터 해당하는 의상 카테고리를 돌려준다
@CLOTHES_PARTS_TO_CATEGORY(ARGS)
#FUNCTIONS
SELECTCASE ARGS
[SKIPSTART]
;의상의 통합화
;도중까지 생각했지만 째응 물러나는이라고 그만두었다
CASE "하반신속옷2"
	RETURNF "팬티"
CASE "기모노", "레오타드", "보디슈트", "원피스"
	RETURNF "전신 의상"
CASE "모자", "외투", "완부의복", "상반신윗도리1", "상반신윗도리2", "상반신속옷1", "상반신속옷2"
	RETURNF "상반신의상"
CASE "치마", "바지", "신발", "양말", "하반신속옷1"
	RETURNF "하반신의상"
CASE "액세서리", "그외1", "그외2", "그외3"
	RETURNF "그외의상"
[SKIPEND]

CASE "액세서리", "신발", "양말", "레오타드", "보디슈트", "바지", "치마", "원피스", "기모노", "외투", "완부의복"
	RETURNF ARGS
CASE "모자"
	RETURNF "머리장비"
CASE "하반신속옷1"
	RETURNF "하반신속옷_비켜놓기불가"
CASE "하반신속옷2"
	RETURNF "하반신속옷_비켜놓기가능"
CASE "상반신속옷1"
	RETURNF "상반신속옷_탈의불가"
CASE "상반신속옷2"
	RETURNF "상반신속옷_탈의가능"
CASE "상반신윗도리1"
	RETURNF "상반신윗도리_탈의불가"
CASE "상반신윗도리2"
	RETURNF "상반신윗도리_탈의가능"
CASE "그외1", "그외2", "그외3"
	RETURNF "그외의상"
ENDSELECT
THROW 불명한 부위 %ARGS%


;장비부위와 의상명으로부터 각 의상의 카테고리내 ID를 돌려준다
@CLOTHES_ID(PARTS, C_NAME)
#FUNCTION
#DIMS PARTS
#DIMS C_NAME
#DIM  C_ID
C_ID = OBJNAME_TO_ID(, "GET", CLOTHES_PARTS_TO_CATEGORY(PARTS), C_NAME)
SIF C_ID == -1
	THROW %C_NAME%는 %CLOTHES_PARTS_TO_CATEGORY(PARTS)%에는 존재하지 않습니다
RETURNF C_ID



;-------------------------------------------------
;함수명 :SHOW_BOTTOM
;개요:하반신복장 나토리 이득 함수
;반환값:복장명(캐릭터라인)
;인수:0 참조하는 캐릭터 번호
;인수:1치마, 2비켜놓기 가능, 3비켜놓기 불가, 4 하반신윗도리비켜놓기 불가의 종별(생략 했을 경우는 가장 노출을 방해하고 있는 것)
;비고:문중 함수
;TEQUIP:0에 대응해 맨 위에 입고 있는 의상의 종류를 돌려준다
;
;인수:1에 복수의 비트를 지정하면 그 중에서 우선되는 것을 표시시킬 수가 있다
;인수:1가 9라면 치마라고 하반신윗도리비켜놓기 불가의 어느 쪽인지 입고 있는 것을 표시, 등
;COUNTER_MESSAGE.ERB내의@DATUI_MESSAGE가 사용예가 되고 있기 때문에 뾰족하게(팍하고) 오지 않는 사람은 봐야만 해
;-------------------------------------------------
@SHOW_BOTTOM(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 5 = "", "치마", "비켜 놓기 가능", "비켜 놓기 불가", "하반신윗도리비켜놓기 불가"
#DIMS PARTS
#DIM EQUIP_TARGET

;인수 1으로 TEQUIP:0을 대조해 표시하는 것을 선택한다
IF ARG:1
	EQUIP_TARGET = ARG:1 & TEQUIP:ARG:0
ELSE
	EQUIP_TARGET = TEQUIP:ARG:0
ENDIF

IF EQUIP_TARGET & 8
	LOCAL = 4
	;4하반신복장 묘사_하반신윗도리비켜놓기 불가
ELSEIF EQUIP_TARGET & 4
	;3하반신복장 묘사_비켜 놓기 불가
	LOCAL = 3
ELSEIF EQUIP_TARGET & 2
	;2하반신복장 묘사_비켜 놓기 가능
	LOCAL = 2
ELSEIF EQUIP_TARGET & 1
	;1하반신복장 묘사_치마
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"하반신복장 묘사 _%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "이름")



;-------------------------------------------------
;함수명:SHOW_하반신속옷
;개요:하반신속옷나토리 이득 함수
;반환값:속옷명(캐릭터라인)
;인수:0 참조하는 캐릭터 번호
;인수:1치마, 2비켜놓기 가능, 3비켜놓기 불가, 4 하반신윗도리비켜놓기 불가의 종별(생략 했을 경우는 가장 노출을 방해하고 있는 것)
;비고:문중 함수
;TEQUIP:0에 대응해 맨 위에 입고 있는 의상의 종류를 돌려준다
;
;인수:1에 복수의 비트를 지정하면 그 중에서 우선되는 것을 표시시킬 수가 있다
;인수:1가 9라면 치마라고 하반신윗도리비켜놓기 불가의 어느 쪽인지 입고 있는 것을 표시, 등
;COUNTER_MESSAGE.ERB내의@DATUI_MESSAGE가 사용예가 되고 있기 때문에 뾰족하게(팍하고) 오지 않는 사람은 봐야만 해
;-------------------------------------------------
@SHOW_하반신속옷(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 5 = "", "치마", "비켜 놓기 가능", "비켜 놓기 불가", "하반신윗도리비켜놓기 불가"
#DIMS PARTS
#DIM EQUIP_TARGET

;인수 1으로 TEQUIP:0을 대조해 표시하는 것을 선택한다
IF ARG:1
	EQUIP_TARGET = ARG:1 & TEQUIP:ARG:0
ELSE
	EQUIP_TARGET = TEQUIP:ARG:0
ENDIF

IF EQUIP_TARGET & 8
	LOCAL = 4
	;4하반신복장 묘사_하반신윗도리비켜놓기 불가
ELSEIF EQUIP_TARGET & 4
	;3하반신복장 묘사_비켜 놓기 불가
	LOCAL = 3
ELSEIF EQUIP_TARGET & 2
	;2하반신복장 묘사_비켜 놓기 가능
	LOCAL = 2
ELSEIF EQUIP_TARGET & 1
	;1하반신복장 묘사_치마
	LOCAL = 1
ELSE
	LOCAL = 0
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"하반신속옷묘사 _%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "이름")



;-------------------------------------------------
;함수명 :SHOW_TOP
;개요:상반신복장 나토리 이득 함수
;반환값:복장명(캐릭터라인)
;인수:0 참조하는 캐릭터 번호
;인수:1탈의 가능, 2탈의 불가(생략 했을 경우는 가장 노출을 방해하고 있는 것)
;비고:문중 함수
;TEQUIP:1에 대응해 맨 위에 입고 있는 의상의 종류를 돌려준다
;-------------------------------------------------
@SHOW_TOP(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 3 = "", "탈의 가능", "탈의 불가"
#DIMS PARTS
IF ARG:1
	LOCAL = ARG:1
ELSEIF TEQUIP:ARG:1 & 2
	LOCAL = 2
ELSEIF TEQUIP:ARG:1 & 1
	LOCAL = 1
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"상반신복장 묘사 _%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "이름")



;-------------------------------------------------
;함수명:SHOW_상반신속옷
;개요:상반신속옷나토리 이득 함수
;반환값:상반신속옷명(캐릭터라인)
;인수:0 참조하는 캐릭터 번호
;인수:1탈의 가능, 2탈의 불가(생략 했을 경우는 가장 노출을 방해하고 있는 것)
;비고:문중 함수
;TEQUIP:1에 대응해 맨 위에 입고 있는 의상의 종류를 돌려준다
;-------------------------------------------------
@SHOW_상반신속옷(ARG, ARG:1)
#FUNCTIONS
#DIMS CONST TYPE, 3 = "", "탈의 가능", "탈의 불가"
#DIMS PARTS
LOCAL = 0
IF ARG:1
	LOCAL = ARG:1
ELSEIF TEQUIP:ARG:1 & 2
	LOCAL = 2
ELSEIF TEQUIP:ARG:1 & 1
	LOCAL = 1
ENDIF
PARTS '= COVER_CLOTHES(ARG, @"상반신속옷묘사 _%TYPE:LOCAL%")
SIF STRLENS(PARTS)
	RETURNF GET_STR(ARG, CLOTHES_PARTS_TO_CATEGORY(PARTS), EQUIP:ARG:PARTS, "이름")




;가장 노출을 방해하고 있는 의상의 장비부위를 돌려준다
;@SHOW_장비계의 판정이 번거로왔기 때문에 간략화용으로
@COVER_CLOTHES(ARG, TYPE)
#FUNCTIONS
#DIMS TYPE
SELECTCASE TYPE
CASE "상반신복장 묘사_탈의 불가"
	SIF EQUIP:ARG:상반신윗도리1
		RETURNF "상반신윗도리1"
	SIF EQUIP:ARG:원피스
		RETURNF "원피스"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	SIF EQUIP:ARG:상반신속옷1
		RETURNF "상반신속옷1"
	RETURNF
CASE "상반신복장 묘사_탈의 가능"
	SIF EQUIP:ARG:상반신윗도리2
		RETURNF "상반신윗도리2"
	SIF EQUIP:ARG:기모노
		RETURNF "기모노"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	SIF EQUIP:ARG:상반신속옷2
		RETURNF "상반신속옷2"
	RETURNF


CASE "상반신속옷묘사_탈의 불가"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	SIF EQUIP:ARG:상반신속옷1
		RETURNF "상반신속옷1"
	RETURNF
CASE "상반신속옷묘사_탈의 가능"
	SIF EQUIP:ARG:상반신속옷2
		RETURNF "상반신속옷2"
	RETURNF


CASE "하반신속옷묘사_하반신윗도리비켜놓기 불가"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
CASE "하반신속옷묘사_비켜 놓기 불가"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	SIF EQUIP:ARG:하반신속옷1
		RETURNF "하반신속옷1"
CASE "하반신속옷묘사_비켜 놓기 가능"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:하반신속옷2
		RETURNF "하반신속옷2"
CASE "하반신속옷묘사_치마"
	SIF EQUIP:ARG:치마
		RETURNF "치마"
	SIF EQUIP:ARG:기모노
		RETURNF "기모노"
	RETURNF


CASE "하반신복장 묘사_하반신윗도리비켜놓기 불가"
	SIF EQUIP:ARG:바지
		RETURNF "바지"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	RETURNF
CASE "하반신복장 묘사_비켜 놓기 불가"
	SIF EQUIP:ARG:바지
		RETURNF "바지"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:보디슈트
		RETURNF "보디슈트"
	SIF EQUIP:ARG:하반신속옷1
		RETURNF "하반신속옷1"
	SIF EQUIP:ARG:하반신속옷2
		RETURNF "하반신속옷2"
	RETURNF
CASE "하반신복장 묘사_비켜 놓기 가능"
	SIF EQUIP:ARG:레오타드
		RETURNF "레오타드"
	SIF EQUIP:ARG:하반신윗도리
		RETURNF "하반신윗도리"
	SIF EQUIP:ARG:하반신속옷2
		RETURNF "하반신속옷2"
	RETURNF
CASE "하반신복장 묘사_치마"
	SIF EQUIP:ARG:치마
		RETURNF "치마"
	SIF EQUIP:ARG:기모노
		RETURNF "기모노"
	SIF EQUIP:ARG:원피스
		RETURNF "원피스"
	RETURNF
ENDSELECT



;전캐릭터의 의상을 Default에 되돌린다
@RESET_ALL_CLOTHES
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:7 = 0
	CALL SELECT_CLOTHES(LOCAL, "오늘의 의상")
	CALL CTRL_CLOTHES_SET(LOCAL, "현재 의상의 변경_평상복")
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT



;EQUIP로부터 TEQUIP에 변환
;이 타이밍에 화탈와 전라 체크
@CLOTHES_SETTING_TRAIN(ARG)
TEQUIP:ARG:0 = 0
TEQUIP:ARG:1 = 0

;솔직히 여기의 처리는 적당
IF INROOM(CFLAG:ARG:현재위치)
	SIF EQUIP:ARG:액세서리 == 6
		EQUIP:ARG:액세서리 = 0
	SIF INROOM(CFLAG:ARG:현재위치) == 1
		EQUIP:ARG:신발= 0
ELSE
	IF CFLAG:ARG:203
		EQUIP:ARG:신발= CFLAG:ARG:203
	ELSEIF CFLAG:ARG:603
		EQUIP:ARG:신발= CFLAG:ARG:603
	ENDIF
	IF !EQUIP:ARG:액세서리 && CFLAG:ARG:201 == 6 && !FLAG:시간정지
		EQUIP:ARG:액세서리 = CFLAG:ARG:201
	ELSEIF !EQUIP:ARG:액세서리 && CFLAG:ARG:601 == 6 && !FLAG:시간정지
		EQUIP:ARG:액세서리 = CFLAG:ARG:601
	ENDIF
ENDIF

IF CHECK_CHARA(ARG, "전라")
	EQUIP:ARG:0 = 0
ELSE
	EQUIP:ARG:0 = 1
ENDIF

;갈아입히기가 유효하지 않다
SIF !EQUIP:ARG:0
	RETURN 0
;하반신
;TEQUIP:0 하반신착의상황(비트0=치마1=비켜 놓기 가능 2=비켜 놓기 불가 3=비켜 놓을 수 없는 하반신윗도리)
;	(비트를 사용하고 싶지 않은 인용 0=이루어 1=노팬티 치마2=팬티 마셔 3=치마+팬티 
;	4=스패츠(속옷) 혹은 노팬티 바지5=치마+스패츠 6=바지+팬티 7=풀 장비(이 바리안트에서는 무리))
;EQUIP:5, 6 하반신속옷
SIF EQUIP:ARG:하반신속옷1
	TEQUIP:ARG:0 |= 4
SIF EQUIP:ARG:하반신속옷2
	TEQUIP:ARG:0 |= 2
;EQUIP:7~9 하반신윗도리
IF EQUIP:ARG:바지
	TEQUIP:ARG:0 |= 4
	TEQUIP:ARG:0 |= 8
ENDIF
SIF EQUIP:ARG:하반신윗도리
	TEQUIP:ARG:0 |= 2
SIF EQUIP:ARG:치마
	TEQUIP:ARG:0 |= 1
;EQUIP:14~16 전신 윗도리
IF EQUIP:ARG:보디슈트
	TEQUIP:ARG:0 |= 4
	TEQUIP:ARG:0 |= 8
	TEQUIP:ARG:0 |= 16	
ENDIF
SIF EQUIP:ARG:원피스
	TEQUIP:ARG:0 |= 1
SIF EQUIP:ARG:기모노
	TEQUIP:ARG:0 |= 1
IF EQUIP:ARG:레오타드
	TEQUIP:ARG:0 |= 2
	TEQUIP:ARG:0 |= 16
ENDIF
;상반신
;TEQUIP:1 상반신착의상황(0=없음 1=탈의 가능 2=탈의 불가 3=공격 불가)
;	(비트를 사용하고 싶지 않은 인용 0=없음 1=노브라 일본식 옷 2=브라만 3=일본식 옷+브라)
;EQUIP:10, 11 상반신속옷
IF EQUIP:ARG:상반신속옷1
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:상반신속옷2
	TEQUIP:ARG:1 |= 1
;EQUIP:12, 13 상반신윗도리
IF EQUIP:ARG:상반신윗도리1
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:상반신윗도리2
	TEQUIP:ARG:1 |= 1
;EQUIP:14~16 전신 윗도리
IF EQUIP:ARG:보디슈트
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF
SIF EQUIP:ARG:원피스
	TEQUIP:ARG:1 |= 2
SIF EQUIP:ARG:기모노
	TEQUIP:ARG:1 |= 1
IF EQUIP:ARG:레오타드
	TEQUIP:ARG:1 |= 2
	TEQUIP:ARG:1 |= 4
ENDIF

;비켜 놓기 처리
SIF TEQUIP:ARG:상반신탈의상태
	CLEARBIT TEQUIP:ARG:1, 2
SIF TEQUIP:ARG:4
	CLEARBIT TEQUIP:ARG:1, 1



;특수 의상에의 일시 갈아입히기
;우선 의상세트에 있는 녀석·혹은 타캐릭터의 평상복만
;갈아입으면(자) RESULT==1
@EX_COSTUME(C_ID, TYPE)
#DIM  C_ID
#DIM  EX_COS
#DIMS TYPE
EX_COS = GET_INT(C_ID, "캐릭터데이터", NO:C_ID, @"%TYPE%의상")
;#; PRINTFORML {C_ID}특수 의상 변경 %TYPE% {EX_COS}
SIF !EX_COS
	RETURN
CALL OKIGAE(C_ID, EX_COS)
;#; CALL SHOW_CLOTHES(C_ID, "현재의 의상")
RETURN 1


;한때 갈아 입을 옷 함수
@OKIGAE(C_ID, COS_ID, COS_TYPE)
#DIM C_ID      ;갈아입히기를 실시하는 캐릭터
#DIM COS_ID    ;의상세트의 ID, 101이상이라면 캐릭터 의상이라고 보인다(캐릭터 의상을 지정하고 싶을 때는 NO + 100으로 하는 것)
#DIMS COS_TYPE ;참조 의상 캐릭터라인, 캐릭터데이터에 기재가 있는 것이 전제이므로, 미실장이라면 완전 알몸이 된다
TCVAR:C_ID:본래의복장 = CFLAG:C_ID:기본복장세트
CFLAG:C_ID:기본복장세트 = COS_ID

IF STRLENS(COS_TYPE)
;캐릭터라인 지정으로 평상복 이외도 지정할 수 있도록(듯이)
	;#; PRINTFORML {COS_ID}특수 의상 변경 %COS_TYPE%
	CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_캐릭터 의상", COS_ID, COS_TYPE)
	CALL CLOTHES_SETTING_TRAIN(C_ID)
	RETURN
ENDIF

SELECTCASE COS_ID
CASE 100
	CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_걸러서 필요해")
CASE 1 TO 99
	CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_의상세트", COS_ID)
CASE 101 TO 100 + 캐릭터수상한
	CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_캐릭터 의상", COS_ID - 100, "평상복")
CASEELSE
	THROW 부정한 의상 ID {COS_ID}
ENDSELECT
CALL CHANGE_CLOTHES(C_ID, "의상 리셋")
CALL CLOTHES_SETTING_TRAIN(C_ID)




@SET_UNDERWEAR(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
CALL SET_PANTS(C_ID, TIME_TYPE)
CALL SET_BRA(C_ID, TIME_TYPE)

;팬티 세팅 함수
;CFLAG로 설정된 의상을 덧쓰기한다
;팬티 지정이 있는 경우에는 하반신속옷1을 해제해
;하반신속옷1이 지정되고 있는 경우에는 팬티보다 그것을 우선한다
@SET_PANTS(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  P_ID
IF TIME_TYPE == "오늘의 파자마"
	IF CFLAG:C_ID:지정팬티 == 100
		P_ID =  0
		CFLAG:C_ID:245 = 0
	ELSEIF CFLAG:C_ID:지정팬티
		P_ID = CFLAG:C_ID:지정팬티
		CFLAG:C_ID:245 = 0
	ELSEIF CFLAG:C_ID:245
		CFLAG:C_ID:246 = 0
		RETURN
	ELSEIF CFLAG:C_ID:246
		CFLAG:C_ID:245 = 0
		CFLAG:C_ID:파자마팬티 = CFLAG:C_ID:246
		RETURN
	ELSEIF !GETBIT(TALENT:C_ID:2, 0)
		P_ID = CLOTHES_ID("하반신속옷2", "트렁크스")
	ELSEIF !CFLAG:C_ID:245
		P_ID = 오늘의팬티(C_ID)
	ENDIF
	;PRINTFORMW %CALLNAME:C_ID%방해 팬티：%GET_STR(C_ID, "하반신속옷_비켜놓기가능", P_ID, "이름")%
	CFLAG:C_ID:파자마팬티 = P_ID
	CFLAG:C_ID:246 = P_ID
	RETURN
ENDIF

IF CFLAG:C_ID:지정팬티 == 100
;노팬티 지정
	P_ID =  0
	CFLAG:C_ID:205 = 0
ELSEIF CFLAG:C_ID:지정팬티
;지정팬티가 있다면 하반신속옷1을 벗긴다
	CFLAG:C_ID:205 = 0
	P_ID = CFLAG:C_ID:지정팬티
ELSEIF CFLAG:C_ID:205
;하반신속옷1이 지정이라면 팬티는 없음
	CFLAG:C_ID:206 = 0
	RETURN
ELSEIF CFLAG:C_ID:206
;하반신속옷2가 지정이라면 팬티 추첨은 없음
	CFLAG:C_ID:205 = 0
	CFLAG:C_ID:오늘팬티 = CFLAG:C_ID:206
	RETURN
ELSEIF !GETBIT(TALENT:C_ID:2, 0)
;남자라면 우선 트렁크스로
	P_ID = CLOTHES_ID("하반신속옷2", "트렁크스")
ELSEIF !CFLAG:C_ID:205
;상기 이외로 팬티 추첨
	P_ID = 오늘의팬티(C_ID)
ENDIF
CFLAG:C_ID:오늘팬티 = P_ID
CFLAG:C_ID:206 = P_ID


@SET_BRA(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  DYNAMIC B_ID
IF TIME_TYPE == "오늘의 파자마"
	IF CFLAG:C_ID:247
		CFLAG:C_ID:248 = 0
		B_ID = CFLAG:C_ID:247
	ELSEIF CFLAG:C_ID:248
	ELSEIF !GETBIT(TALENT:C_ID:2, 0)
		B_ID = CLOTHES_ID("상반신속옷1", "T셔츠")
	ELSE
		B_ID = 오늘의브래지어(C_ID, CFLAG:C_ID:파자마팬티)
	ENDIF
	CFLAG:C_ID:파자마브라 = B_ID
	CFLAG:C_ID:247 = B_ID
	RETURN
ENDIF

IF CFLAG:C_ID:207
	CFLAG:C_ID:208 = 0
	B_ID = CFLAG:C_ID:207
ELSEIF CFLAG:C_ID:208
ELSEIF !GETBIT(TALENT:C_ID:2, 0)
	B_ID = CLOTHES_ID("상반신속옷1", "T셔츠")
ELSE
	B_ID = 오늘의브래지어(C_ID, CFLAG:C_ID:오늘팬티)
ENDIF
CFLAG:C_ID:오늘브라 = B_ID
CFLAG:C_ID:207 = B_ID


;오늘의 의상 결정 함수
;반드시 의상→파자마로 세트로 사용하는 것
@SELECT_CLOTHES(C_ID, TIME_TYPE)
#DIM  C_ID
#DIMS TIME_TYPE
#DIM  의상C_ID
#DIMS 의상TYPE

VARSET 의상C_ID
VARSET 의상TYPE
;먼저 파자마의 설정
IF TIME_TYPE == "오늘의 파자마"
	SELECTCASE CFLAG:C_ID:파자마지정
		CASE 300
		;개별 설정
			CALL CTRL_CLOTHES_SET(C_ID, "파자마의 변경_거름필요한의상")
			CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
			RETURN
		CASE 301
		;판이치
			의상TYPE '= "판이치"
		CASE 302
		;전라
			의상TYPE '= "전라"
		CASE 1 TO 99
		;의상세트의 지정
			의상TYPE '= GET_STR(C_ID, "의상세트", CFLAG:C_ID:파자마지정, "이름")
		CASE 0
		;Default 의상에 의한 설정
			IF !(C_ID == MASTER)
				의상C_ID = NO:C_ID
				의상TYPE '= GET_STR(C_ID, "캐릭터데이터", 의상C_ID, TIME_TYPE)
			ELSEIF FLAG:역극
				의상C_ID = FLAG:역극
				의상TYPE '= GET_STR(C_ID, "캐릭터데이터", 의상C_ID, TIME_TYPE)
			ENDIF
	ENDSELECT
	;미설정이라면 파자마 바지
	SIF !STRLENS(의상TYPE)
		의상TYPE '= "파자마(바지) 세트"
	CALL CHANGE_CLOTHES(C_ID, TIME_TYPE, 의상C_ID, 의상TYPE)
	SIF 의상TYPE == "판이치"
		JUMP SET_PANTS(C_ID, TIME_TYPE)
	SIF !(의상TYPE == "전라")
		CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
	RETURN
ENDIF
;오늘의 의상, 내일의 의상
SELECTCASE CFLAG:C_ID:기본복장세트
CASE 100
;개별 설정
	CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_거름필요한의상")
	CALL SET_UNDERWEAR(C_ID, TIME_TYPE)
CASE 1 TO 99
;의상세트의 지정
	의상TYPE '= GET_STR(C_ID, "의상세트", CFLAG:C_ID:기본복장세트, "이름")
	;만약을 위해
	SIF !STRLENS(의상TYPE)
		GOTO DEFAULT
CASE 101 TO 100 + 캐릭터수상한
;특정 캐릭터의 의상을 참조할 때
	의상C_ID = CFLAG:C_ID:기본복장세트 - 100
	의상TYPE '= "평상복"
	;만약을 위해
	SIF !EXISTCSV(의상C_ID)
		GOTO DEFAULT
CASE 0
;Default 의상에 의한 설정
	$DEFAULT
	IF !(C_ID == MASTER)
		의상C_ID = NO:C_ID
		의상TYPE '= GET_STR(C_ID, "캐릭터데이터", 의상C_ID, TIME_TYPE)
	ELSEIF FLAG:역극
		의상C_ID = FLAG:역극
		의상TYPE '= GET_STR(C_ID, "캐릭터데이터", 의상C_ID, TIME_TYPE)
	ELSEIF GETBIT(TALENT:C_ID:2, 0)
	;그대로응이 있을 때는 여장을 기본으로 한다
		의상TYPE '= "여장 세트"
	ELSE
	;♂(은)는 남장을 기본으로 한다
		의상TYPE '= "남장 세트"
	ENDIF
	;미설정이라면 평상복
	SIF !STRLENS(의상TYPE)
		의상TYPE '= "평상복"
ENDSELECT
CALL CHANGE_CLOTHES(C_ID, TIME_TYPE, 의상C_ID, 의상TYPE)
CALL SET_UNDERWEAR(C_ID, TIME_TYPE)



;의상 갱신 함수
@CHANGE_CLOTHES(C_ID, TYPE, 의상C_ID, 의상TYPE)
#DIM  C_ID
#DIMS TYPE
#DIM  의상C_ID
#DIMS 의상TYPE
#DIM  의상SET_ID
의상SET_ID = 0
SIF STRLENS(의상TYPE)
	의상SET_ID = OBJNAME_TO_ID(C_ID, "GET", "의상세트", 의상TYPE)
SELECTCASE TYPE
	CASE "오늘의 파자마"
		SIF 의상TYPE == "판이치"
			JUMP CTRL_CLOTHES_SET(C_ID, "파자마의 변경_판이치", 의상SET_ID)
		SIF 의상TYPE == "전라"
			JUMP CTRL_CLOTHES_SET(C_ID, "파자마의 변경_전라", 의상SET_ID)	
		IF 의상SET_ID > 0
			CALL CTRL_CLOTHES_SET(C_ID, "파자마의 변경_의상세트", 의상SET_ID)
		ELSE
			CALL CTRL_CLOTHES_SET(C_ID, "파자마의 변경_캐릭터 의상", 의상C_ID, 의상TYPE)
		ENDIF
	CASE "오늘의 의상", "내일의 의상"
		IF 의상SET_ID > 0
			CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_의상세트", 의상SET_ID)
		ELSE
			CALL CTRL_CLOTHES_SET(C_ID, "평상복의 변경_캐릭터 의상", 의상C_ID, 의상TYPE)
		ENDIF
	CASE "의상 리셋"
		;#;SIF C_ID == TARGET
		;#;	PRINTFORML TARGET 의상 리셋
		IF 수면시간(C_ID)
			CALL CTRL_CLOTHES_SET(C_ID, "현재 의상의 변경_파자마")
			CFLAG:C_ID:파자마 = 1
		ELSE
			SELECTCASE CFLAG:C_ID:의상일시변경
			CASE 0
				CALL CTRL_CLOTHES_SET(C_ID, "현재 의상의 변경_평상복")
			CASE 100
				CALL CTRL_CLOTHES_SET(C_ID, "현재 의상의 변경_걸러서 필요해")
			CASE 1 TO 99
				CALL CTRL_CLOTHES_SET(C_ID, "현재 의상의 변경_의상세트", CFLAG:C_ID:의상일시변경)
			CASE 101 TO 100 + 캐릭터수상한
				CALL CTRL_CLOTHES_SET(C_ID, "현재 의상의 변경_캐릭터 의상", CFLAG:C_ID:의상일시변경 - 100, "평상복")
			ENDSELECT
			CFLAG:C_ID:파자마 = 0
		ENDIF
ENDSELECT



[SKIPSTART]
의상세트의 컨트롤 함수
CFLAG와 EQUIP의 교환을 실시한다
EQUIP:0의 취급으로서는
·@CLOTHES_SETTING_TRAIN의 모두에 갱신 처리를 가르쳤다
·SAVE/LOAD의 범위를 1~22까지 한다(EQUIP:0은 보존되지 않는다)
이것으로 대체로 좋다고 생각한다
[SKIPEND]
@CTRL_CLOTHES_SET(C_ID, TYPE, ARG, ARGS)
#DIM  C_ID
#DIMS TYPE
SELECTCASE TYPE
;리셋용으로 기록한다
CASE "현재 의상의 보존_평상복"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = EQUIP:C_ID:LOCAL
	NEXT
CASE "현재 의상의 보존_파자마"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = EQUIP:C_ID:LOCAL
	NEXT
CASE "현재 의상의 보존_걸러서 필요해"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = EQUIP:C_ID:LOCAL
	NEXT
	CFLAG:ARG:거름필요한의상 = 1

;리셋
CASE "거름필요한의상의 리셋"
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(600 + LOCAL) = 0
	NEXT
	SIF CFLAG:C_ID:기본복장세트 == 100
		CFLAG:C_ID:기본복장세트 = 0
	SIF CFLAG:C_ID:파자마지정 == 300
		CFLAG:C_ID:파자마지정 = 0
	CFLAG:C_ID:거름필요한의상 = 0

;세트 한 의상을 로드
;이 근처에 아앙 갈퀴 로드 처리이며 파자마 플래그는 만지지 않기 때문에 주의군요
CASE "현재 의상의 변경_평상복"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(200 + LOCAL)
	NEXT
	CFLAG:C_ID:의상일시변경 = 0
	CALL CTRL_CLOTHES_SET(C_ID, "속옷 일괄 설정")


CASE "현재 의상의 변경_파자마"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(240 + LOCAL)
	NEXT
	IF CFLAG:C_ID:파자마팬티 == 100
		EQUIP:C_ID:하반신속옷1 = 0
		EQUIP:C_ID:하반신속옷2 = 0
		CFLAG:C_ID:245 = 0
		CFLAG:C_ID:246 = 0
		CFLAG:C_ID:파자마팬티 = 0
		CFLAG:C_ID:노팬티 = 1
	ELSEIF CFLAG:C_ID:노팬티 && CFLAG:C_ID:파자마팬티
		EQUIP:C_ID:하반신속옷1 = 0
		EQUIP:C_ID:하반신속옷2 = CFLAG:C_ID:파자마팬티
		SIF !CFLAG:C_ID:245
			CFLAG:C_ID:246 = CFLAG:C_ID:파자마팬티
		CFLAG:C_ID:파자마팬티 = 0
		CFLAG:C_ID:노팬티 = 0
	ELSEIF CFLAG:C_ID:노팬티 && (EQUIP:C_ID:하반신속옷1 || EQUIP:C_ID:하반신속옷2)
		EQUIP:C_ID:하반신속옷1 = 0
		EQUIP:C_ID:하반신속옷2 = 0
		CFLAG:C_ID:245 = 0
		CFLAG:C_ID:246 = 0
	ENDIF
	CFLAG:C_ID:의상일시변경 = 0

CASE "현재 의상의 변경_걸러서 필요해"
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = CFLAG:C_ID:(600 + LOCAL)
	NEXT
	IF CFLAG:C_ID:노팬티
		EQUIP:C_ID:하반신속옷1 = 0
		EQUIP:C_ID:하반신속옷2 = 0
	ELSEIF EQUIP:C_ID:하반신속옷1
		EQUIP:C_ID:하반신속옷2 = 0
	ELSEIF EQUIP:C_ID:하반신속옷2
		EQUIP:C_ID:하반신속옷1 = 0
	ELSEIF CFLAG:C_ID:파자마
		EQUIP:C_ID:하반신속옷2 = CFLAG:C_ID:246
	ELSE
		EQUIP:C_ID:하반신속옷2 = CFLAG:C_ID:206
	ENDIF
	CFLAG:C_ID:의상일시변경 = 100

CASE "현재 의상의 변경_캐릭터 의상"
;ARG로 지정된 캐릭터의 의상을 참조한다
	CFLAG:C_ID:의상일시변경 = 100 + ARG
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = GET_INT(C_ID, "캐릭터데이터", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
	CALL CTRL_CLOTHES_SET(C_ID, "속옷 일괄 설정")

CASE "현재 의상의 변경_의상세트"
;ARG로 지정된 ID의 의상세트를 참조한다
	CFLAG:C_ID:의상일시변경 = ARG
	FOR LOCAL, 1, 23
		EQUIP:C_ID:LOCAL = GET_INT(C_ID, "의상세트", ARG, EQUIPNAME:LOCAL)
	NEXT
	CALL CTRL_CLOTHES_SET(C_ID, "속옷 일괄 설정")
CASE "속옷 일괄 설정"
	CALL CTRL_CLOTHES_SET(C_ID, "팬티 일괄 설정")
	CALL CTRL_CLOTHES_SET(C_ID, "브라 일괄 설정")
CASE "팬티 일괄 설정"
	IF !CFLAG:C_ID:파자마
		IF CFLAG:C_ID:노팬티
			EQUIP:C_ID:하반신속옷1 = 0
			EQUIP:C_ID:하반신속옷2 = 0
		ELSEIF EQUIP:C_ID:하반신속옷1
			EQUIP:C_ID:하반신속옷2 = 0
		ELSE
			EQUIP:C_ID:하반신속옷2 = CFLAG:C_ID:오늘팬티
		ENDIF
	ELSE
		IF CFLAG:C_ID:노팬티
			EQUIP:C_ID:하반신속옷1 = 0
			EQUIP:C_ID:하반신속옷2 = 0
		ELSEIF EQUIP:C_ID:하반신속옷1
			EQUIP:C_ID:하반신속옷2 = 0
		ELSE
			EQUIP:C_ID:하반신속옷2 = CFLAG:C_ID:246
		ENDIF
	ENDIF
CASE "브라 일괄 설정"
	IF !CFLAG:C_ID:파자마
		EQUIP:C_ID:상반신속옷1 = CFLAG:C_ID:오늘브라
	ELSE
		EQUIP:C_ID:상반신속옷1 = CFLAG:C_ID:247
	ENDIF

;탈의처리
CASE "현재 의상의 변경_전라"
	FOR LOCAL, 1, 23
		SIF LOCAL == 3 && FLAG:시간정지
			CONTINUE
		SIF LOCAL == 4 && EQUIP:C_ID:신발
			CONTINUE
		EQUIP:C_ID:LOCAL = 0
	NEXT

CASE "현재 의상의 변경_속옷 모습"
;이 코드는 기분 나쁘지만 우선 이대로
	FOR LOCAL, 18, 11, -1
		EQUIP:C_ID:LOCAL = 0
	NEXT
	EQUIP:C_ID:모자 = 0
	TEQUIP:C_ID:상반신착의상황 = 2
	TEQUIP:C_ID:윗도리탈의가끝난상태 = 1

CASE "현재 의상의 변경_반라"
	EQUIP:C_ID:상반신속옷1 = 0
	TEQUIP:C_ID:상반신탈의상태 = 1
	SIF EQUIP:C_ID:바지
		EQUIP:C_ID:바지= 0



CASE "평상복의 변경_캐릭터 의상"
;ARG로 지정된 캐릭터의 ARGS로 지정된 의상을 참조한다
	CFLAG:C_ID:기본복장세트 = 100 + ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "캐릭터데이터", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT

CASE "평상복의 변경_의상세트"
;ARG로 지정된 ID의 의상세트를 참조한다
	CFLAG:C_ID:기본복장세트 = ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = GET_INT(C_ID, "의상세트", ARG, EQUIPNAME:LOCAL)
	NEXT
CASE "평상복의 변경_거름필요한의상"
	CFLAG:C_ID:기본복장세트 = 100
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(200 + LOCAL) = CFLAG:C_ID:(600 + LOCAL)
	NEXT

CASE "파자마의 변경_캐릭터 의상"
;ARG로 지정된 캐릭터의 ARGS로 지정된 의상을 참조한다
	CFLAG:C_ID:파자마지정 = 100 + ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = GET_INT(C_ID, "캐릭터데이터", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
CASE "파자마의 변경_의상세트"
	CFLAG:C_ID:파자마지정 = ARG
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = GET_INT(C_ID, "의상세트", ARG, EQUIPNAME:LOCAL)
	NEXT
CASE "파자마의 변경_거름필요한의상"
	CFLAG:C_ID:파자마지정 = 300
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(240 + LOCAL) = CFLAG:C_ID:(600 + LOCAL)
	NEXT
CASE "파자마의 변경_판이치"
	CFLAG:C_ID:파자마지정 = 301
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(240 + LOCAL) = 0
	NEXT
	CFLAG:C_ID:246 = CFLAG:C_ID:파자마팬티
	SIF CFLAG:C_ID:246 == 100
		CFLAG:C_ID:246 = 0
	
CASE "파자마의 변경_전라"
	CFLAG:C_ID:파자마지정 = 302
	FOR LOCAL, 0, 23
		CFLAG:C_ID:(240 + LOCAL) = 0
	NEXT
	CFLAG:C_ID:파자마팬티 = 100

CASE "거름필요한의상의 변경_캐릭터 의상"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = GET_INT(C_ID, "캐릭터데이터", ARG, @"%ARGS%：%EQUIPNAME:LOCAL%")
	NEXT
	CFLAG:C_ID:거름필요한의상 = 1

CASE "거름필요한의상의 변경_의상세트"
	FOR LOCAL, 1, 23
		CFLAG:C_ID:(600 + LOCAL) = GET_INT(C_ID, "의상세트", ARG, EQUIPNAME:LOCAL)
	NEXT
	CFLAG:C_ID:거름필요한의상 = 1
ENDSELECT











;특정 캐릭터의 의상 설정의 내용을 표시한다
@SHOW_CHARA_CLOTHES(C_ID, PAGE = 0)
#DIM  C_ID
#DIMS CONST 표시의상, 4 = "현재의 의상", "오늘의 의상", "오늘의 파자마", "거름필요한의상"
#DIM  PAGE
PRINTFORML %CALLNAME:C_ID%의%표시의상 :PAGE%
SELECTCASE CFLAG:C_ID:지정팬티
CASE 0
	PRINTFORML 　현재의 팬티 설정：지정 없음
CASE 100
	PRINTFORML 　현재의 팬티 설정：노팬티 지정
CASEELSE
	PRINTFORML 　현재의 팬티 설정：%GET_STR(C_ID, "하반신속옷_비켜놓기가능", CFLAG:C_ID:지정팬티, "이름")%
ENDSELECT
PRINTFORML 
CALL SHOW_CLOTHES(C_ID, 표시의상 :PAGE)
PRINTFORML 
PRINTBUTTON @"[1] - %표시의상:((PAGE + 4 - 1) % 4), 14%", 1
PRINTFORM 　　　　　　　　　　
PRINTBUTTON @"[3] - %표시의상:((PAGE + 4 + 1) % 4), 14%", 3
PRINTFORML 
PRINTBUTTON "[0] - 돌아오는", 0
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		PAGE = (PAGE + 4 - 1) % 4
	CASE 3
		PAGE = (PAGE + 4 + 1) % 4
	CASEELSE
		REUSELASTLINE 무효인 입력입니다
		CONTINUE
	ENDSELECT
	RESTART
LOOP 1


;의상계의 표시 함수
@SHOW_CLOTHES(C_ID, TYPE, ID)
#DIM  C_ID
#DIMS TYPE
#DIM  ID
#DIM  장비부위
#DIM  CLOTHES
#DIMS 표시내용
{
#DIMS CONST 표시부정도, 23 = "", 
	"모자", "액세서리", "완부의복", 
	"외투", 
	"상반신윗도리1", "상반신윗도리2", 
	"기모노", "원피스", 
	"치마", "하반신윗도리", "바지", 
	"레오타드", "보디슈트", 
	"상반신속옷1", "상반신속옷2", 
	"하반신속옷1", "하반신속옷2", 
	"그외1", "그외2", "그외3", 
	"양말", "신발"
}
{
#DIMS CONST 표시라벨, 23 = "", 
	"　장비:머리　　", "　장비:장식　　", "　장비:팔　　　", 
	"　장비:외투　　", 
	"　장비:웃도리　", "　장비:웃도리　", 
	"　장비:기모노　", "　장비:전신옷　", 
	"　장비:하의　　", "　장비:하의　　",  "　장비:하의　　", 
	"　장비:전신속옷", "　장비:전신속옷", 
	"　장비:속옷(상)", "　장비:속옷(상)", 
	"　장비:속옷(하)", "　장비:속옷(하)", 
	"　장비:부속　　", "　장비:부속　　", "　장비:부속　　", 
	"　장비:양말　　", "　장비:신발　　"
}

SELECTCASE TYPE
CASE "현재의 의상"
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		
		;노브라, 노팬티 용무의 예외 처리
		;여기에서
		IF 표시부정도:LOCAL == "상반신속옷1" && TALENT:C_ID:성별 != 2 && EQUIP:C_ID:상반신속옷2 == 0 && EQUIP:C_ID:상반신속옷1 == 0 && EQUIP:C_ID:보디슈트 == 0 && EQUIP:C_ID:레오타드 == 0 && EQUIP:C_ID:0
			IF 상반신속옷표시(C_ID) == 1
				PRINTFORML 　장비:속옷(상)[입지 않았다]
			ELSEIF 하반신속옷표시(C_ID) == 1
				PRINTFORML   장비:속옷(상)[？？？？？ ]
			ENDIF
		ENDIF
		IF 표시부정도:LOCAL == "하반신속옷1" && EQUIP:C_ID:하반신속옷2 == 0 && EQUIP:C_ID:하반신속옷1 == 0 && EQUIP:C_ID:보디슈트 == 0 && EQUIP:C_ID:레오타드 == 0 && EQUIP:C_ID:0
			IF !상반신속옷표시(C_ID) && !하반신속옷표시(C_ID)
				PRINTFORML   장비:속옷　　[？？？？？ ]
				CONTINUE
			ELSEIF !하반신속옷표시(C_ID)
				PRINTFORML   장비:속옷(하)[？？？？？ ]
			ELSE
				PRINTFORML 　장비:속옷(하)[입지 않았다]
			ENDIF
		ENDIF
		
		SIF !EQUIP:C_ID:장비부위 
			CONTINUE
		IF STRLENS(CSTR:C_ID:(50 + 장비부위)) && !CFLAG:C_ID:파자마
			표시내용 '= CSTR:C_ID:(50 + 장비부위)
		;팬티는 전용 처리가 필요
		ELSEIF 표시부정도:LOCAL == "상반신속옷1" && !(C_ID == MASTER) && !상반신속옷표시(C_ID)
			SIF !하반신속옷표시(C_ID)
			 	CONTINUE
			표시내용 '= "？？？？？"
		ELSEIF 표시부정도:LOCAL == "하반신속옷2" && !(C_ID == MASTER) && !하반신속옷표시(C_ID)
			IF !상반신속옷표시(C_ID)
				PRINTFORML   장비:속옷　　[？？？？？ ]
				CONTINUE
			ELSE
				표시내용 '= "？？？？？"
			ENDIF
		ELSE
			IF 표시부정도:LOCAL == "상반신속옷1"
				표시내용 '= BRANAME(EQUIP:C_ID:상반신속옷1, C_ID)
			ELSEIF 표시부정도:LOCAL == "하반신속옷2"
				표시내용 '= PANTSNAME(EQUIP:C_ID:하반신속옷2, C_ID)
			ELSE
				표시내용 '= CLOTHNAME(장비부위, EQUIP:C_ID:장비부위)
			ENDIF
		ENDIF

		PRINTFORML %표시라벨 :LOCAL%[%표시내용%]
	NEXT


CASE "오늘의 의상", "평상복"
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		CLOTHES = CFLAG:C_ID:(200 + 장비부위)
		IF !CLOTHES
			CONTINUE
		ELSE
			표시내용 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(표시부정도:LOCAL), CLOTHES, "이름")
		ENDIF
		PRINTFORML %표시라벨 :LOCAL%：%표시내용%
	NEXT


CASE "오늘의 파자마"
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		CLOTHES = CFLAG:C_ID:(240 + 장비부위)
		SIF 표시부정도:LOCAL == "하반신속옷2" && !(C_ID == MASTER)
			CONTINUE
		IF !CLOTHES
			CONTINUE
		ELSE
			표시내용 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(표시부정도:LOCAL), CLOTHES, "이름")
		ENDIF
		PRINTFORML %표시라벨 :LOCAL%：%표시내용%
	NEXT


CASE "거름필요한의상"
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		CLOTHES = CFLAG:C_ID:(600 + 장비부위)
		SIF 표시부정도:LOCAL == "하반신속옷2" && !(C_ID == MASTER)
			CONTINUE
		IF !CLOTHES
			CONTINUE
		ELSE
			표시내용 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(표시부정도:LOCAL), CLOTHES, "이름")
		ENDIF
		PRINTFORML %표시라벨 :LOCAL%：%표시내용%
	NEXT


CASE "캐릭터 평상복"
;캐릭터 번호 ID의 평상복의 내용을 표시한다
	IF !EXISTCSV(ID)
		PRINTFORML 존재하지 않는 캐릭터를 참조하고 있습니다
		RETURN
	ENDIF
	PRINTFORML %CALLNAME:ID%의 평상복
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		CLOTHES = GET_INT(C_ID, "캐릭터데이터", ID, @"평상복：%표시부정도:(LOCAL)%")
		SIF 표시부정도:LOCAL == "하반신속옷2" && !(C_ID == MASTER)
			CONTINUE
		IF !CLOTHES
			CONTINUE
		ELSE
			표시내용 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(표시부정도:LOCAL), CLOTHES, "이름")
		ENDIF
		PRINTFORML %표시라벨 :LOCAL%：%표시내용%
	NEXT


CASE "의상세트"
;의상세트ID의 내용을 표시한다
	IF GET_STR(C_ID, "의상세트" , ID, "이름") == ""
		PRINTFORML 존재하지 않는 의상을 참조하고 있습니다
		RETURN
	ENDIF
	PRINTFORML %GET_STR(C_ID, "의상세트" , ID, "이름")%
	FOR LOCAL, 1, 23
		;장비부위의 ID취득
		장비부위 = FINDELEMENT(EQUIPNAME, 표시부정도:LOCAL)
		SIF 장비부위 < 0
			THROW 존재하지 않는 장비부위%표시부정도:LOCAL%
		CLOTHES = GET_INT(C_ID, "의상세트", ID, 표시부정도:LOCAL)
		SIF 표시부정도:LOCAL == "하반신속옷2" && !(C_ID == MASTER)
			CONTINUE
		IF !CLOTHES
			CONTINUE
		ELSE
			표시내용 '= GET_STR(C_ID, CLOTHES_PARTS_TO_CATEGORY(표시부정도:LOCAL), CLOTHES, "이름")
		ENDIF
		PRINTFORML %표시라벨 :LOCAL%：%표시내용%
	NEXT
ENDSELECT

@SPECIAL_OUTFIT(CHARA)
#FUNCTION
#DIM CHARA
IF FLAG:패션
	RETURNF FLAG:패션
ENDIF
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "특수 의상 착용")


@UNIQUE_UNDERWARE_TOP(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "캐릭터데이터", CHARA, "고유 상반신속옷명")

@UNIQUE_UNDERWARE_BOTTOM(CHARA)
#FUNCTIONS
#DIM CHARA
SIF GET_STR(0, "캐릭터데이터", CHARA, "고유 하반신속옷명") == ""
	RETURNF "신품의 팬티"
RETURNF GET_STR(0, "캐릭터데이터", CHARA, "고유 하반신속옷명")

@UNIQUE_UNDERWARE_ADJ(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "캐릭터데이터", CHARA, "고유 속옷 형용사")

@UNIQUE_UNDERWARE_ABLE(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "고유 속옷 착용 조건")

@UNIQUE_UNDERWARE_TRANSPARENCY_FRONT(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "고유 속옷 비쳐보이는(앞)")

@UNIQUE_UNDERWARE_TRANSPARENCY_BACK(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "고유 속옷 비쳐보이는(뒤)")

@UNIQUE_UNDERWARE_TOP_THICK(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "고유 속옷 옷감 두께")

@상반신속옷표시(C_ID)
#FUNCTION
#DIM C_ID

SIF !CFLAG:C_ID:수면 && CFLAG:C_ID:상반신속옷확인
	RETURNF 1
SIF CFLAG:C_ID:수면 && CFLAG:C_ID:파자마상반신속옷확인
	RETURNF 1
RETURNF 0

@하반신속옷표시(C_ID)
#FUNCTION
#DIM C_ID

SIF !CFLAG:C_ID:수면 && CFLAG:C_ID:하반신속옷확인
	RETURNF 1
SIF CFLAG:C_ID:수면 && CFLAG:C_ID:파자마하반신속옷확인
	RETURNF 1
RETURNF 0

@상반신속옷체크(C_ID)
#FUNCTION
#DIM C_ID

IF !CFLAG:C_ID:수면
	CFLAG:C_ID:상반신속옷확인 = 1
ELSE
	CFLAG:C_ID:파자마상반신속옷확인 = 1
ENDIF

@하반신속옷체크(C_ID)
#FUNCTION
#DIM C_ID

IF !CFLAG:C_ID:수면
	CFLAG:C_ID:하반신속옷확인 = 1
ELSE
	CFLAG:C_ID:파자마하반신속옷확인 = 1
ENDIF

@속옷확인리셋(C_ID)
#DIM C_ID

CFLAG:C_ID:상반신속옷확인 = 0
CFLAG:C_ID:하반신속옷확인 = 0
CFLAG:C_ID:파자마상반신속옷확인 = 0
CFLAG:C_ID:파자마하반신속옷확인 = 0

RETURN
