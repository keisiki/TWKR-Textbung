[SKIPSTART]
소작인용 각종 설정 함수

돈은 주지만 구속 시간에 나누면 대단히 박봉, 즉 풍미입니다(에서도 받을 수 있으면(자) 기쁘네요)
표준의 보수 계수를 400으로서 격차 있어, 히가시카타 자세하게 없기 때문에 위화감 있으면(자) 바꾸어 창고 차이
450은 장사 하고 있는 것 같은 사람들

착상을 있는 것 전부 쌓았다
자주(잘) 몰랐던 사람들은 실장하고 있지 않다
추가·정정 구한다

치운 소재
·카센 저택 뒷산에 과일마누(밤, 감, 매화 등)
그냥 이미지대로 있을 듯
여러모로 너무 애매해서 일단 보류
·도레미 씨가 설탕을 생산
여하튼 스위트한 것을 이라고 생각했다
사탕수수나 사탕무이지만 어쩐지 슈르(초현실)이므로 보류
양봉 농가에서 벌꿀을 만드는  방법도 있을까
후에 사탕무는 치르노의작물이 되었다
·모코우 or 카구야로 죽림에서 죽순 농가
죽순은 얻을 것 같지만 이 2명이 작물을 징수하러 오는 이미지가 하지 않는다
지주라고 하는 형태로 타인과 서로 관계가 될까? (이)라고 생각해 그만두고
·렌코의 연근밭, 세이란의 사과원
메리와 세이란의 작물을 생각해내지 못했다
만 지주 설정하고 있다고(면) 위화감
·세이자로 미라클 후르츠 밭
쓴 맛＆산미를 달면 느끼게 한다=확 뒤집었다
대량생산 하는 것 같은 것도 아닌 것 같은이라고 생각해 그만두고
·순호 씨의 생강밭
대단한 형상으로 외치면서 설충분하는 거야? 너무 일발 재료 지나므로 각하
·미마님의 목장
하지만(ry


과일나무에서는 감이나 꿀 감등이 있으면 일본 느낌이 늘어나지만 맡길 수 있는 사람이 생각해내지 못했다
이 밖에 섬유계로 면화, 삼, 염료계로 천 등을 들어 본다


2018/05/19
·각종 거점 맵의 실장에 수반해 버그번호를 수정
·농민계의 데이터의 기술은 각지 주인의 캐릭터데이터함수 곁에 이동시켰습니다
　소작인 패치의 구상 단계에서 캐릭터데이터함수적인 기술은 생각했었습니다만
　함수군이 꽤에 거대해서 소작인 패치만의 위해(때문에) 실장하는 것은 이라고 생각해 그만두고 있었습니다
　만들어 버렸기 때문에 캐릭터데이터함수 곁에 이동
·@STR_NOUMIN()의 사양 변경
　1. STR_NOUMIN/INT_NOUMIN의 2종류로 나누어 TOINT의 필요를 없게 했다
　2. 캐릭터데이터함수로 관리하게 되었으므로 제일 인수로 건네주는 내용을 CALLNAME로부터 등록 번호로 변경
　3. STR/INT_NOUMIN의 제2 인수는 종래대로, STR_NOUMIN(1, "설명") 등으로 움직입니다
　4. 캐릭터데이터함수측의 기술로 사용하고 있는"농민："의 부분은 STR/INT_NOUMIN로 마음대로 부여합니다

[SKIPEND]
@INT_NOUMIN(C_ID, TYPE)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;지주의 등록 번호
#DIMS TYPE;취득 정보의 종류
RETURNF GET_INT(, "캐릭터데이터", C_ID, "농민：" + TYPE)

@STR_NOUMIN(C_ID, TYPE)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM  C_ID;지주의 등록 번호
#DIMS TYPE;취득 정보의 종류
RETURNF GET_STR(, "캐릭터데이터", C_ID, "농민：" + TYPE)



;농민 초기설정 함수
@SET_NOUMIN
#LOCALSIZE 1000
#LOCALSSIZE 1
#DIM LCOUNT
#DIM C_ID
#DIM LIST_ID
#DIM CHARA_COUNT
#DIM LIST_COUNT
#DIM PAGE
#DIMS NAME_지주
PRINTFORML 당신은 농민입니까?
PRINTFORML [1] - 네
PRINTFORML [0] - 아니오
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 무효인 입력입니다
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

PAGE = 0
CHARA_COUNT = 0
;지주 일람의 취득
FOR C_ID, 1, CHARANUM
	SIF C_ID == FLAG:역극
		CONTINUE
	SIF !STRLENS(STR_NOUMIN(C_ID, "설명"))
		CONTINUE
	LOCAL:CHARA_COUNT = C_ID
	CHARA_COUNT ++
NEXT

FLAG:지주 = 0
FLAG:밭일성과 = 0
FLAG:변제연체회수 = 0
FLAG:수확납입회수 = 0
FLAG:누적수확평가 = 0
FLAG:전회수확평가 = 0


DO
	LIST_COUNT = 0
	PRINTFORML 당신을 고용하고 있는 지주를 선택해주세요
	PRINTFORML 
	FOR LIST_ID, (PAGE * 20), CHARA_COUNT
		C_ID = LOCAL:LIST_ID
		PRINTFORM [{C_ID , 3}] - 지주：%CALLNAME:C_ID, 10%　　
		PRINTFORM %STR_NOUMIN(C_ID, "설명"), 14%　　
		PRINTFORML %STR_NOUMIN(C_ID, "밭의 종류")%의 위치는 %STR_NOUMIN(C_ID, "밭의 맵")%의 부근
		LIST_COUNT ++
		SIF LIST_COUNT > 19
			BREAK
	NEXT
	PRINTFORML 
	PRINTFORML [901] - 이전페이지
	PRINTFORML [902] - 다음페이지
	PRINTFORML [  0] - 역시 그만둔다
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 901
			SIF PAGE > 0
				PAGE --
		CASE 902
			SIF (PAGE + 1) * 20 < CHARA_COUNT
				PAGE ++
		CASE 1 TO CHARANUM - 1
			SIF !STRLENS(STR_NOUMIN(RESULT, "설명"))
				GOTO RETRY
			FLAG:지주 = RESULT
		CASEELSE
			$RETRY
			REUSELASTLINE 무효인 입력입니다
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP !(FLAG:지주)

NAME_지주 = %CALLNAME:(FLAG:지주)%
PRINTFORML 
PRINTFORML 당신은 %NAME_지주% 밑에서 %STR_NOUMIN(FLAG:지주, "작물")%를 만드는 소작인이 되었습니다
PRINTFORML 당신이 빌린 %STR_NOUMIN(FLAG:지주, "밭의 종류")%은 %STR_NOUMIN(FLAG:지주, "밭의 맵")% 부근에 있습니다
PRINTFORMW 매일 농땡이 피우지 말고 손봅시다

CALL MODE_NOUMIN
CALL LOAN_NOUMIN

[IF_DEBUG]
;거동, 레이 만난다라는 확인용
;MONEY = 100000
;DO
;	SIF FLAG:농민난이도
;		CALL COST_NOUMIN
;	CALL REPAY_NOUMIN
;	WAIT
;LOOP 1
[ENDIF]

@MODE_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_지주
NAME_지주 = %CALLNAME:(FLAG:지주)%
PRINTFORML 농민 생활의 난이도를 설정해주세요
PRINTFORML (선택지에 따라서는 캐릭터의 이미지가 붕괴할 우려가 있습니다)
PRINTFORML 
PRINTFORML [1] - 노멀 농민　　　%조사처리(NAME_지주,"는")% 무척 친절한 지주입니다
PRINTFORML [2] - 하드코어 농민　%조사처리(NAME_지주,"는")% 평범하게 욕심 많은 지주입니다
PRINTFORML [3] - 익스트림 농민　%조사처리(NAME_지주,"는")% 압정과 횡포를 부리는 지주입니다
DO
	INPUT
	IF INRANGE(RESULT, 1, 3)
		FLAG:농민난이도 = RESULT
		BREAK
	ELSE
		REUSELASTLINE 무효인 입력입니다
		CONTINUE
	ENDIF
LOOP 1
SELECTCASE FLAG:농민난이도
CASE 1
	PRINTFORML 당신의 지주는 매우 친절하고 착한 사람입니다
	FLAG:밭확장 = 1
CASE 2
	PRINTFORML 당신은 지주에게 시달리는 농민이 되었습니다
	FLAG:밭확장 = 2
CASE 3
	PRINTFORML 당신은 지주에게 철저하게 착취당하는 농민이 되었습니다
	FLAG:밭확장 = 3
ENDSELECT
PRINTFORMW (농민 난이도는 컨피그에서 변경 가능합니다)
SETBIT FLAG:1000, 2



;%TOSTR(LOCAL, "#,###")%
;보수 400 * 1000 = 40만엔(표준 금액)
@LOAN_NOUMIN
#DIMS NAME_지주
NAME_지주 = %CALLNAME:(FLAG:지주)%
LOCAL = INT_NOUMIN(FLAG:지주, "보수") * 100000
PRINTFORML 농업을 시작한 %CALLNAME:MASTER%에게 초기 투자 비용으로 %금액표시(LOCAL)%의 차입금이 발생했다
FLAG:지주대출금 = LOCAL
FLAG:연시대출금 = LOCAL
SELECTCASE FLAG:농민난이도
CASE 1
	PRINTFORML %조사처리(NAME_지주,"는")% 무이자로 빌려줄 테니 느긋하게 갚아도 된다고 말해주었다
	IF !ITEM:원예용품일식
		PRINTFORML 더해서 %조사처리(NAME_지주,"는")% 먼저 인간 마을의 꽃집에서 원예용품을 장만두라고 조언해주었다
		PRINTFORML 농기구 구입비용까지는 부담해주지 않을 것 같다
	ENDIF
CASE 2, 3
	PRINTFORML 이 차입금에는 {INT_NOUMIN(FLAG:지주, "금리")}\%의 금리가 발생하는 모양이다
	PRINTFORML 더해서 %조사처리(NAME_지주,"는")% 농기구를 대출하겠다고 제안했다
	PRINTFORML ……아무래도 거절할 권리는 없을 것 같다
	;아마 이 그 밖에 현지에 쓸데없게 다양한 도구가 산만큼 있다
	SIF !ITEM:원예용품일식
		ITEM:원예용품일식 ++
ENDSELECT
WAIT

@RESET_NOUMIN
FLAG:지주 = 0
FLAG:밭일성과 = 0
FLAG:변제연체회수 = 0
FLAG:수확납입회수 = 0
FLAG:누적수확평가 = 0
FLAG:전회수확평가 = 0
FLAG:농민난이도 = 0
FLAG:밭확장 = 0
FLAG:지주대출금 = 0
FLAG:연시대출금 = 0

@SET_APPLY_NOUMIN
SIF FLAG:농민난이도 >= 2 && !ITEM:원예용품일식
	ITEM:원예용품일식 ++


;농민 정보 표시 함수
@SHOW_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS NAME_지주
#DIM HATAKE
NAME_지주 = %CALLNAME:(FLAG:지주)%
HATAKE = INT_NOUMIN(FLAG:지주, "장소 ID")
SIF GET_MAPID(HATAKE) != MAIN_MAP
	HATAKE = 6000 + (GET_MAP_REPLACEMENT(HATAKE) / 10)

PRINTFORML %NAME_지주%의 소작인(%STR_NOUMIN(FLAG:지주, "작물")%를 만들고 있는 %STR_NOUMIN(FLAG:지주, "설명")%)
PRINTFORML %STR_NOUMIN(FLAG:지주, "밭의 종류")%은 %STR_NOUMIN(FLAG:지주, "밭의 맵")%의 %NAME_FROM_PLACE(HATAKE)% 부근에 있다
;착한 사람이라도 작물은 전부 받아갑니다
SELECTCASE FLAG:농민난이도
CASE 1
	PRINTFORML 지주는 무척 친절하고 착한 사람이다
	SIF FLAG:지주대출금
		PRINTFORML %금액표시(FLAG:지주대출금)%을 무이자로 빌렸다
CASE 2
	PRINTFORML 지주에게 시달리고 있다
	SIF FLAG:지주대출금
		PRINTFORML %금액표시(FLAG:지주대출금)%을 빌렸으며 금리는 {INT_NOUMIN(FLAG:지주, "금리")}\%(연리)다
CASE 3
	PRINTFORML 지주에게 철저하게 착취당하고 있다
	SIF FLAG:지주대출금
		PRINTFORML %금액표시(FLAG:지주대출금)%을 빌렸으며 금리는 {INT_NOUMIN(FLAG:지주, "금리")}\%(월리)다
ENDSELECT
PRINTFORML 


;징수 함수
;사실은 연공이든 뭐든 없지만 알기 쉬움 우선
;말대응이라든지 포상 엣찌라든지는 사양을 몰라서 실장할 수 없다
@NENGU_NOUMIN
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM 연공의평가
PRINTFORML %CALLNAME:(FLAG:지주)%가 이번 달 분 성과를 징수하러 왔다
PRINTFORMW (이번 달 수확량：{FLAG:밭일성과})
연공의평가 = FLAG:밭일성과 / 30
LOCAL = (연공의평가 - 3) * 5
SIF LOCAL < 0
	LOCAL = LOCAL - SQRT(MAX(1, CFLAG:(FLAG:지주):신뢰도))
;(天候パッチ)寒気による作物被害が20を超えたら20毎に冷害判定
SIF AMOUNT_COLD_CROP_DAMAGE >= 20
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 40
	IS_COLD_CROP_DAMAGE ++
SIF AMOUNT_COLD_CROP_DAMAGE >= 60
	IS_COLD_CROP_DAMAGE ++
SELECTCASE 연공의평가
;(天候パッチ)冷害・風害発生時は態度が軟化
	CASE IS <= 0
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 거의 제로다
			PRINTFORML 악천우로 인한 심각한 피해에 %조사처리(CALLNAME:(FLAG:지주),"는")% 망연자실해 있다
		ELSE
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 거의 제로다
			PRINTFORML %CALLNAME:(FLAG:지주)%는 이쪽을 험악한 표정으로 노려보고 있다
		ENDIF
	CASE 1
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 꽤 적다
			PRINTFORML 악천우로 인한 피해에 %조사처리(CALLNAME:(FLAG:지주),"는")% 어깨를 늘어뜨리고 있다
		ELSE
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 꽤 적다
			PRINTFORML %CALLNAME:(FLAG:지주)%는 수확량이 너무나 적어서 명명백백 실망한 눈치다
		ENDIF
	CASE 2
		IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 적은 편이다
			PRINTFORML 악천우로 인한 피해에 %조사처리(CALLNAME:(FLAG:지주),"는")% 아쉬운 듯 하다
		ELSE
			PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 적은 편이다
			PRINTFORML %CALLNAME:(FLAG:지주)%는 수확량이 적어 불만스런 기색이다
		ENDIF
	CASE 3
		PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 약간 적은 편이다
		PRINTFORML %CALLNAME:(FLAG:지주)%는 수확량에 다소 부족하다 느끼는 모양이다
	CASE 4
		PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 평범한 축이다
		PRINTFORML %CALLNAME:(FLAG:지주)%는 수확량에 만족해준 모양이다
	CASE 5
		PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 약간 많은 축이다
		PRINTFORML %CALLNAME:(FLAG:지주)%는 수확량이 많아 흡족한 기색이다
	CASE 6
		PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 꽤 많다
		PRINTFORML %CALLNAME:(FLAG:지주)%는 예상 이상의 수확량에 웃음을 띠고 있다
	CASEELSE
		PRINTFORML 이번 달 분 %STR_NOUMIN(FLAG:지주, "작물")%의 수확량은 어마어마한 양이다!
		PRINTFORML %CALLNAME:(FLAG:지주)%는 상상을 초월한 수확량에 압도당하고 있다
ENDSELECT
WAIT

;(天候パッチ)冷害・風害発生時は怒らない
IF LOCAL < 0 && IS_COLD_CROP_DAMAGE <= 0 && IS_STORM_CROP_DAMAGE <= 0 && IS_FLOOD_CROP_DAMAGE <= 0
	PRINTFORMW %CALLNAME:(FLAG:지주)%는 당신에게 불신감을 품고 있다
	CFLAG:(FLAG:지주):신뢰도 = MAX(0, CFLAG:(FLAG:지주):신뢰도 + LOCAL)
	IF 연공의평가 <= 0
		PRINTFORMW ……%CALLNAME:(FLAG:지주)%를 화나게 한 것 같다
		CFLAG:(FLAG:지주):언짢음 = 1
		CFLAG:(FLAG:지주):빡침 = 1
		SIF MARK:(FLAG:지주):반발각인 < 3
			MARK:(FLAG:지주):반발각인 ++
	ENDIF
	PRINTFORMW ...돌연 %CALLNAME:(FLAG:지주)%가 채찍을 들고왔다!
	PRINTFORMW %마스터가% 엎드려서 용서를 빌었지만 그런다고 용서받는 일은 없었다
	PRINTFORMW ...
	PRINTFORMW 도합 {RAND:10+3}대의 채찍질 끝에도 그녀의 분노가 풀리진 않았다...
	BASE:MASTER:체력 = -999999
	PRINTFORMW 다음달에는 더 성실하게 일하라고 신신당부한 뒤에야 겨우 풀려났다
ELSEIF LOCAL > 0
	;(天候パッチ)冷害・風害発生時はより評価される
	IF IS_COLD_CROP_DAMAGE > 0 || IS_STORM_CROP_DAMAGE > 0 || IS_FLOOD_CROP_DAMAGE > 0
		PRINTFORM 악천후임에도 불구하고 풍년이 들었으므로 
		PRINTFORMW %CALLNAME:(FLAG:지주)%로부터의 신뢰도가 매우 올랐다
		LOCAL += ((IS_COLD_CROP_DAMAGE + IS_STORM_CROP_DAMAGE + (IS_FLOOD_CROP_DAMAGE * 4)) * 2)
	ELSE
		PRINTFORMW %CALLNAME:(FLAG:지주)%의 신뢰도가 올랐다
	ENDIF
	CFLAG:(FLAG:지주):신뢰도 += LOCAL
	IF !MARK:(FLAG:지주):반발각인
		LOCAL = 연공의평가 * INT_NOUMIN(FLAG:지주, "보수")
		PRINTFORMW %CALLNAME:(FLAG:지주)%는 %금액표시(LOCAL)%의 용돈을 주었다
		MONEY += LOCAL
	ENDIF
ENDIF
FLAG:밭일성과 = 0
FLAG:연공 = 0
FLAG:수확납입회수 ++
FLAG:누적수확평가 += 연공의평가
FLAG:전회수확평가 = 연공의평가
SIF FLAG:야채낭데일리 == 2
	FLAG:야채낭데일리 = 1
CALL REPAY_NOUMIN
SIF FLAG:농민난이도 > 1
	CALL COST_NOUMIN

;(天候パッチ)冷害発生回数と風害発生回数をリセット
IS_COLD_CROP_DAMAGE = 0
IS_STORM_CROP_DAMAGE = 0
IS_FLOOD_CROP_DAMAGE = 0
AMOUNT_COLD_CROP_DAMAGE = 0


@REPAY_NOUMIN
#DIM HENSAI
#DIM MIN_HENSAI
#DIMS NAME_지주
NAME_지주 = %CALLNAME:(FLAG:지주)%
SIF !FLAG:지주대출금
	GOTO KANRYOU
SELECTCASE FLAG:농민난이도
CASE 1
	MIN_HENSAI = 100
CASE 2
	MIN_HENSAI = INT_NOUMIN(FLAG:지주, "보수") * 5
CASE 3
	MIN_HENSAI = INT_NOUMIN(FLAG:지주, "보수") * (10 + FLAG:변제연체회수)
ENDSELECT
MIN_HENSAI = MIN(MIN_HENSAI, FLAG:지주대출금)

PRINTFORML %조사처리(NAME_지주,"는")% 빚의 변제를 요구하고 있다
PRINTFORM 얼마나 내겠습니까? (변제금: 
PRINTBUTTON 금액표시(FLAG:지주대출금), FLAG:지주대출금
PRINTFORM , 소지금: 
PRINTBUTTON 금액표시(MONEY), MONEY
PRINTFORM , 최저 금액: 
PRINTBUTTON 금액표시(MIN_HENSAI), MIN_HENSAI
PRINTFORML 미만이면 연체 판정)
DO
	INPUT
	HENSAI = MIN(RESULT, FLAG:지주대출금)
	SELECTCASE HENSAI
	CASE IS < MIN_HENSAI
		PRINTFORML %마스터는% 제발 변제를 미뤄달라고 %NAME_지주%에게 애원했다
		IF FLAG:농민난이도 == 1
			IF MONEY < MIN_HENSAI
			;진심으로 돈이 없을 때
				PRINTDATAL
					DATAFORM %조사처리(NAME_지주,"는")% 이쪽의 생활 상황을 진심으로 걱정하고 있다
					DATAFORM %조사처리(NAME_지주,"는")% 이쪽의 생활 상황을 진심으로 걱정하고 있다
					DATAFORM %조사처리(NAME_지주,"는")% %마스터를% 격려하고 있다
					DATAFORM %조사처리(NAME_지주,"는")% %마스터를% 위로하고 있다
					DATAFORM %조사처리(NAME_지주,"는")% 건넬 말을 찾지 못하는 눈치다
					DATAFORM %NAME_지주%의 연민 어린 시선이 꽂힌다
					DATAFORM %NAME_지주%의 표정이 어두워졌다
					DATAFORM %조사처리(NAME_지주,"는")% 난처해하고 있다
				ENDDATA
			ELSE
			;조금 기다려!
				PRINTDATAL
					DATAFORM %조사처리(NAME_지주,"는")% 깊은 한숨을 쉬고 있다
					DATAFORM %조사처리(NAME_지주,"는")% 관자놀이를 누르고 있다
					DATAFORM %조사처리(NAME_지주,"는")% 뚱한 눈으로 이쪽을 바라보고 있다
					DATAFORM %조사처리(NAME_지주,"는")% 팔짱을 끼고 떨떠름한 표정을 짓고 있다
					DATAFORM %NAME_지주%의 연민 어린 시선이 꽃힌다
					DATAFORM %NAME_지주%의 얼굴이 실룩거리고 있다
					DATAFORM %NAME_지주%의 미간에 주름이 잡혔다
					DATAFORM %NAME_지주%의 표정이 어두워졌다
					DATAFORM %조사처리(NAME_지주,"는")% 난처해하고 있다
				ENDDATA
				PRINTFORML 
				PRINTFORML ……가까스로 납득해준 모양이다
			ENDIF
			FLAG:변제연체회수 ++
		ELSEIF MONEY >= MIN_HENSAI
			PRINTFORML %조사처리(NAME_지주,"는")% %CALLNAME:MASTER%의 말을 가로막고는 지갑에서 강제로 %금액표시(MIN_HENSAI)%을 강탈했다
			MONEY -= MIN_HENSAI
		ELSE
			PRINTFORML %조사처리(NAME_지주,"는")% 혀를 차고 이쪽을 싸늘한 눈으로 노려보고 있다
			FLAG:변제연체회수 ++
		ENDIF
	CASE IS > MONEY
		$RETRY
		REUSELASTLINE 무효인 입력입니다
		CONTINUE
	CASEELSE
		PRINTFORML %마스터는% 빚 가운데 %금액표시(HENSAI)%를 변제했다
		MONEY -= HENSAI
		FLAG:변제연체회수 = 0
		FLAG:지주대출금 -= HENSAI
		IF !FLAG:지주대출금
			IF FLAG:농민난이도 == 1
				PRINTFORML %마스터는% 마침내 빚을 완납했다
				PRINTFORML %NAME_지주%도 함께 기뻐해주고 있다
			ELSE
				PRINTFORML %마스터는% 빚을 완납했다
				PRINTFORML ……그래봤자 또 금방 뭔가 구실을 달아서 돈을 뜯기겠지만
			ENDIF
		ELSE
			PRINTFORML 남은 빚은 %금액표시(FLAG:지주대출금)%이다……
		ENDIF
		$KANRYOU
		PRINTFORML 
		IF FLAG:농민난이도 == 1
			PRINTDATAL
				DATAFORM %NAME_지주%과의 잡담으로 꽃을 피웠다
				DATAFORM %조사처리(NAME_지주,"는")% 밭일 보고를 흥미롭게 듣고 있다
				DATAFORM %조사처리(NAME_지주,"는")% 힘든 일이 없는지 신경 써주고 있다
				DATAFORM %NAME_지주%가 가져와 준 과자를 집어먹으며 환담을 나누었다
				DATAFORM %조사처리(NAME_지주,"는")% 농지 개선 요망에 진지하게 귀를 기울이고 있다
				DATAFORM %조사처리(NAME_지주,"는")% 온화한 시선으로 이쪽을 보고 있다
				DATAFORM %NAME_지주%의 웃는 얼굴이 눈부시다
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM %조사처리(NAME_지주,"는")% 틈만 나면 %CALLNAME:MASTER%에게 물건을 강매하려든다
				DATAFORM %조사처리(NAME_지주,"는")% 밭일 보고를 대충 듣고 넘기고 있다
				DATAFORM %조사처리(NAME_지주,"는")% 돈 문제를 겪는 사람은 없는지 떠보고 있다
				DATAFORM %조사처리(NAME_지주,"는")% 농지 개선 계획이라며 설비 투자를 제안해왔다…… 그런 돈은 없다
				DATAFORM %NAME_지주%의 시선에선 못된 꿍꿍이의 낌새가 엿보인다
				DATAFORM %NAME_지주%의 웃는 얼굴에는 뭔가 수작이 있다고밖에 안 보인다
				DATAFORM %NAME_지주%의 웃는 얼굴을 봐도 왠지 마음이 편치 않다
			ENDDATA
		ENDIF
	ENDSELECT
	BREAK
LOOP 1

IF FLAG:변제연체회수
	LOCAL = POWER(FLAG:변제연체회수, FLAG:농민난이도 + 1)
	LOCAL = MIN(LOCAL, CFLAG:(FLAG:지주):신뢰도)
	IF LOCAL
		PRINTFORMW %CALLNAME:(FLAG:지주)%는 당신에게 불신감을 품고 있다
		CFLAG:(FLAG:지주):신뢰도 -= LOCAL
	ENDIF
ENDIF



;이러니 저러니 이유 붙여 공제해진다
;표준 레이트 400으로 금리·반제 연체에 의한 패널티를 제외하면(자)
;하드코어로 1만, 익스트림으로 3만의 경비가 매월 발생한다
@COST_NOUMIN
#LOCALSIZE 9
#LOCALSSIZE 9
#DIMS NAME_지주
#DIMS PREV_지주
#DIM COST_RATE
#DIM LCOUNT
NAME_지주 = %CALLNAME:(FLAG:지주)%
COST_RATE = INT_NOUMIN(FLAG:지주, "보수")
LOCAL:0 = 0
LOCAL:8 = 0
PRINTFORML 이번 달 분의 기타 경비로 이하의 요금이 빚에 추가되었다


;금리 계산
;하드코어라면 연시 차입금 준거로 연리 계산
;익스트림이라면 연시 차입금·이번달 차입금의 나 큰 편으로 월리 계산
IF FLAG:지주대출금 < 10000000
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:1 = FLAG:연시대출금 * INT_NOUMIN(FLAG:지주, "금리") / 1200
	CASE 3
		LOCAL:1 = MAX(FLAG:지주대출금, FLAG:연시대출금) * INT_NOUMIN(FLAG:지주, "금리") / 100
	ENDSELECT
ENDIF

SELECTCASE FLAG:농민난이도
CASE 2
	LOCAL:2 = FLAG:변제연체회수 * COST_RATE
CASE 3
	LOCAL:2 = FLAG:변제연체회수 * COST_RATE * 10
ENDSELECT

;고정치가 대부분 어째서 매회 재계산할 필요는 그다지 없다
;지주의 교체(와 재기동)의 타이밍에 판정하도록(듯이)
IF NAME_지주 != PREV_지주
	LOCALS:0 = 경비 총액
	LOCALS:1 = 이자
	LOCALS:2 = 변제 연체비
	LOCALS:3 = 농구 이용비
	LOCALS:8 = 차입금 총액
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:3 = COST_RATE * 5
	CASE 3
		LOCAL:3 = COST_RATE * 10
	ENDSELECT

	LOCALS:4 = 비료 구입비
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:4 = COST_RATE * 5
	CASE 3
		LOCAL:4 = COST_RATE * 10
	ENDSELECT

	LOCALS:5 = 작물 출하비
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:5 = COST_RATE * 5
	CASE 3
		LOCAL:5 = COST_RATE * 15
	ENDSELECT

	LOCALS:6 = 수장 이용비
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:6 = COST_RATE * 5
	CASE 3
		LOCAL:6 = COST_RATE * 20
	ENDSELECT

	IF STR_NOUMIN(FLAG:지주, "작물") == "생사"
		LOCALS:7 = 차기란비
	ELSE
		LOCALS:7 = 차기 볍씨비
	ENDIF
	
	SELECTCASE FLAG:농민난이도
	CASE 2
		LOCAL:7 = COST_RATE * 5
	CASE 3
		LOCAL:7 = COST_RATE * 20
	ENDSELECT
	PREV_지주 = %NAME_지주%
ENDIF

LOCAL:0 = SUMARRAY(LOCAL)
LOCAL:8 = LIMIT(FLAG:지주대출금 + LOCAL:0,  0, 10000000)
FLAG:지주대출금 = LOCAL:8
PRINTFORML ――――――――――――――――――――――――――――――――
PRINTFORML 　기타 경비
PRINTFORML ――――――――――――――――――――――――――――――――
FOR LCOUNT, 1, 8
	SIF LOCAL:LCOUNT
		PRINTFORML 　　%LOCALS:LCOUNT, 12%｜　　　%금액표시(LOCAL:LCOUNT),20%
NEXT
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORML 　　%LOCALS:0, 12%｜　　　%금액표시(LOCAL:0),20%
PRINTFORML ――――――――｜―――――――――――――――――――――――
PRINTFORM 　　%LOCALS:8, 12%｜　　　%금액표시(LOCAL:8),20%
IF FLAG:지주대출금 == 10000000
	PRINTFORML 　!　상한　!
ELSE
	PRINTFORML 
ENDIF
PRINTFORMW ――――――――――――――――――――――――――――――――

;CONFIGからの農民再設定(試作）
@RESTART_NOUMIN
#DIM LOCAL_지주
#DIM LOCAL_밭일성과
#DIM LOCAL_변제연체회수
#DIM LOCAL_수확납입회수
#DIM LOCAL_누적수확평가
#DIM LOCAL_전회수확평가
#DIM LOCAL_농민난이도
#DIM LOCAL_밭확장
#DIM LOCAL_지주대출금
#DIM LOCAL_연시대출금
#DIMS LOCALS_원지주
#DIMS LOCALS_신지주
#DIMS LOCALS_원난이도
#DIMS LOCALS_신난이도

LOCAL_지주 = FLAG:지주
LOCAL_밭일성과 = FLAG:밭일성과
LOCAL_변제연체회수 = FLAG:변제연체회수
LOCAL_수확납입회수 = FLAG:수확납입회수
LOCAL_누적수확평가 = FLAG:누적수확평가
LOCAL_전회수확평가 = FLAG:전회수확평가
LOCAL_농민난이도 = FLAG:농민난이도
LOCAL_밭확장 = FLAG:밭확장
LOCAL_지주대출금 = FLAG:지주대출금
LOCAL_연시대출금 = FLAG:연시대출금
LOCALS_원지주 = %CALLNAME:(FLAG:지주)%

IF FLAG:지주
	PRINTFORML %마스터는% %LOCALS_원지주% 아래에서 일하는 농민입니다
	SIF FLAG:지주대출금 > 0
		PRINTFORML 남은 빚은 %금액표시(FLAG:지주대출금)% 입니다
	SELECTCASE FLAG:농민난이도
	CASE 1
		LOCALS_원난이도 = 노멀
	CASE 2
		LOCALS_원난이도 = 하드코어
	CASE 3
		LOCALS_원난이도 = 익스트림
	ENDSELECT
ELSE
	PRINTFORML %마스터는% 홀가분한 자유인입니다
ENDIF

PRINTFORML 농가의 설정을 변경하시겠습니까?
PRINTFORML [1] - 네
PRINTFORML [0] - 아니오
DO
	INPUT
	SELECTCASE RESULT
	CASE 0
		PRINTFORML 
		RETURN
	CASE 1
	CASEELSE
		REUSELASTLINE 유효하지 않은 입력입니다
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1

DO
	CALL RESET_NOUMIN
	CALL SET_NOUMIN
	PRINTFORML 

	LOCALS_신지주 = %CALLNAME:(FLAG:지주)%
	IF LOCAL_지주 && FLAG:지주 && LOCAL_지주 != FLAG:지주
		PRINTFORML %마스터는% %LOCALS_원지주%의 아래에서 벗어나, 앞으로는 %LOCALS_신지주%의 아래에서 일하게 됩니다
		IF LOCAL_지주대출금 > 0
			PRINTFORML %LOCALS_원지주%에게 진 빚 %금액표시(FLAG:지주대출금)%은 %LOCALS_신지주%가 대신해서 갚기 때문에
			FLAG:지주대출금 += LOCAL_지주대출금
		ENDIF
		PRINTFORML %LOCALS_신지주%에 대한 차입금은 총 %금액표시(FLAG:지주대출금)%입니다

	ELSEIF LOCAL_지주 && FLAG:지주 && LOCAL_지주 == FLAG:지주
		; 고용주의 변경이 없어서 원래대로 되돌리다
		SETCOLOR C_YELLOW
		PRINTFORML 고용주의 변경이 없어, 상기의 차입금은 발생하지 않습니다
		SIF LOCAL_지주대출금 > 0
			PRINTFORML %LOCALS_신지주%로의 차입금은 현재와 같이 %금액표시(LOCAL_지주대출금)%이 됩니다
		RESETCOLOR
		FLAG:밭일성과 = LOCAL_밭일성과
		FLAG:변제연체회수 = LOCAL_변제연체회수
		FLAG:수확납입회수 = LOCAL_수확납입회수
		FLAG:누적수확평가 = LOCAL_누적수확평가
		FLAG:전회수확평가 = LOCAL_전회수확평가
		FLAG:지주대출금 = LOCAL_지주대출금
		FLAG:연시대출금 = LOCAL_연시대출금

		; 난이도 변경되었는가
		IF LOCAL_농민난이도 != FLAG:농민난이도
			SELECTCASE FLAG:농민난이도
			CASE 1
				LOCALS_신난이도 = 노멀
			CASE 2
				LOCALS_신난이도 = 하드코어
			CASE 3
				LOCALS_신난이도 = 익스트림
			ENDSELECT
			PRINTFORML 난이도를 [%LOCALS_원난이도%]에서 [%LOCALS_신난이도%]로 변경합니다
		ELSE
			PRINTFORML 난이도의 설정은 변경되지 않습니다
		ENDIF

	ELSEIF !LOCAL_지주 && !FLAG:지주
		PRINTFORML 설정에는 변함이 없습니다
	ELSEIF LOCAL_지주 && !FLAG:지주
		IF LOCAL_지주대출금 > 0
			PRINTFORML 빚이 남아 있는 동안은 농민을 그만둘 수는 없다
			CONTINUE
		ENDIF
		PRINTFORML 농민을 그만두고 자유인이 됩니다
	ENDIF

	PRINTFORML 
	PRINTFORML 설정을 변경하시겠습니까?
	PRINTFORML [1] - 네
	PRINTFORML [2] - 다시 한번 결정한다
	PRINTFORML [0] - 변경하지 않는다
	DO
		INPUT
		SELECTCASE RESULT
		CASE 0
			FLAG:지주 = LOCAL_지주
			FLAG:밭일성과 = LOCAL_밭일성과
			FLAG:변제연체회수 = LOCAL_변제연체회수
			FLAG:수확납입회수 = LOCAL_수확납입회수
			FLAG:누적수확평가 = LOCAL_누적수확평가
			FLAG:전회수확평가 = LOCAL_전회수확평가
			FLAG:농민난이도 = LOCAL_농민난이도
			FLAG:밭확장 = LOCAL_밭확장
			FLAG:지주대출금 = LOCAL_지주대출금
			FLAG:연시대출금 = LOCAL_연시대출금
			PRINTFORML 설정변경을 중지했습니다
			PRINTFORML 
			RETURN
		CASE 1
			PRINTFORML 설정을 변경했습니다
			PRINTFORML 
			RETURN
		CASE 2
		CASEELSE
			REUSELASTLINE 유효하지 않은 입력입니다
			CONTINUE
		ENDSELECT
		BREAK
	LOOP 1
LOOP 1
