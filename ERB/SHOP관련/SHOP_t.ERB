;FileName_SHOP.ERB -------------------------------- Rev1. 00
;기상 전메뉴 화면 표시
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@SHOW_SHOP
#DIM 임시플래그1
;첫 날에 돈 설정, 턴 끝날때에 보정을 하지만 첫날에는 지갑이 텅텅 빈 상태이므로 해둔다
임시플래그1 = 0
FLAG:SHOP처리중 = 1
CALL 본가에서이사
FLAG:ISTEXTBUNG = 1
CALL 신캐릭터추가함수()
;기상 전화면 표시중 플래그
TFLAG:100 = 0
;---------------------
;モブ店員画像を一度だけ作成。スマートなやり方ではない。もっといい場所はないだろうか
;SIF !SPRITECREATED("顔絵_服_通常_MOB_0")
;	CALL モブ店員画像作成
;SPTALKで呼ばれたとき、画像がなければ作成にしました
;---------------------
;기상시간 자동 보정, 최저 수면 시간은 4시간
LOCAL:1 = CFLAG:MASTER:취침시간 - DAY * 1440
IF (TIME:3 >= LOCAL:1 && (TIME:3 - LOCAL:1) < 240) || (TIME:3 < 240 && (1440 + TIME:3 - LOCAL:1) < 240)
	PRINTL 수면 시간이 너무 짧습니다. 적어도 4시간 이상은 자야 합니다
	CALL 최저수면시간설정()
	PRINTFORMW 기상시간을 (%시각표시(TIME:3)%)로 설정했습니다
ENDIF
;---------------------
IF FLAG:물가 == 0
	FLAG:물가 = 100
ENDIF
IF FLAG:물가 <= 50
	FLAG:물가 = 50
ENDIF
CALL 패치후초기설정
CALL 전체스킬세팅()
CALL 암시장물가책정
FOR LOCAL, 1, CHARANUM
	;여기 껴들어서 처리
	CALL 창녀일정하기(LOCAL)
	IF CFLAG:LOCAL:시작소지금 == 0
		임시플래그1 = 1
	ENDIF
	SIF CFLAG:LOCAL:보지붕괴 > 0
		CFLAG:LOCAL:보지붕괴--
	SIF CFLAG:LOCAL:항문붕괴 > 0
		CFLAG:LOCAL:항문붕괴--
	;---------
	IF CFLAG:LOCAL:보지붕괴 == 0&&TALENT:LOCAL:헐렁보지
		PRINTFORML %CALLNAME:LOCAL%의 보지는 다시 전처럼 복원되었다...
		TALENT:LOCAL:헐렁보지 = 0
	ENDIF
	IF CFLAG:LOCAL:항문붕괴 == 0&&TALENT:LOCAL:헐렁항문
		PRINTFORML %CALLNAME:LOCAL%의 항문은 다시 전처럼 복원되었다...
		TALENT:LOCAL:헐렁항문 = 0
	ENDIF
NEXT
IF 임시플래그1 == 1
	CALL 시작소지금설정
	임시플래그1 = 0
ENDIF

CALL 주민증발급


DRAWLINE
;PRINTFORML {DAY}일째 (%GET_DAY()%) 소지금 :\\%TOSTR(MONEY,"#,###")%
PRINTFORM {DAY}일째(
CALL COLOR_YOUBI
PRINTFORML ) 소지금 :\\%TOSTR(MONEY,"#,###")%

PRINTL

CALL BASE_BAR(MASTER, "체력", 28, "초록")
PRINTFORM 　　
CALL BASE_BAR(MASTER, "기력", 28, "파랑", "「개행」")
CALL BASE_BAR(MASTER, "TSP", 28, "물색")
PRINTFORM 　　
SIF TALENT:MASTER:2 & 2
	CALL BASE_BAR(MASTER, "정력", 28, "노랑")
PRINTFORML 

;PRINT 체력
;CALL PRINT_COLORBAR, BASE:MASTER:0, MAXBASE:MASTER:0, 26, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("빨강"), RESULT:1
;PRINTFORML  ({BASE:MASTER:체력, 5}/{MAXBASE:MASTER:체력, 5})  
;PRINT 기력
;CALL PRINT_COLORBAR, BASE:MASTER:1, MAXBASE:MASTER:1, 26, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("초록"), RESULT:1
;PRINTFORML  ({BASE:MASTER:1, 5}/{MAXBASE:MASTER:1, 5})  
PRINTFORML 
PRINTBUTTON "자택위치", 110
PRINTFORM ：%GET_MAPNAME(MAIN_MAP)%
IF FLAG:기절중단
	PRINTFORML  (기절중) 	
ELSE
	PRINTFORML 
ENDIF
PRINTBUTTON "[기상시간]", 2400
PRINTFORML ：%시각표시(TIME:3)%
;CALL INFO_CLOCK_IMAGE(TIME:3, 500, "left")
PRINTFORML 
CALL TW_TIPS
PRINTFORML 

SETCOLOR 103,228,126
PRINTL 【메인 메뉴】
RESETCOLOR
PRINTLC [100] - 기상
SIF FLAG:기절중단
	SETCOLOR C_GRAY
PRINTLC [102] - 생활환경 설정
IF CFLAG:MASTER:현재위치 != OMANEKIBEYA() &&!FLAG:감방남은일수
	SIF FLAG:기절중단
		SETCOLOR C_GRAY
	PRINTLC [110] - 자택 이사
	RESETCOLOR
ENDIF

SIF MAIN_MAP == 14 ;KR판 저택용 설정
	PRINTLC [104] - 주거 설정
PRINTL
PRINTL

PRINTLC [101] - 능력 표시
SIF FLAG:기절중단
	SETCOLOR C_GRAY
IF !FLAG:감방남은일수
	PRINTLC [103] - 스키마 통신 판매
ENDIF

PRINTL
PRINTLC [105] - 실적 해제
IF FLAG:700
	SIF FLAG:기절중단
		SETCOLOR C_GRAY
		PRINTLC [106] - 콜렉션
	RESETCOLOR
ENDIF

PRINTLC [114] - 메모라이즈

PRINTL
;PRINTLC [109] - 잡은 물고기 리스트;TODO KR판 낚시 컬랙션 기능 이식시 주석 해제
SIF !FLAG:감방남은일수
	PRINTLC [111] - 다누키 은행
[IF_DEBUG]
	PRINTLC [112] - 지하 매음굴
[ENDIF]
	
IF FLAG:조교전&&!FLAG:감방남은일수
	PRINTLC [113] - 조교전
ENDIF
SIF CFLAG:MASTER:아이인원수
	PRINTLC [152] - 아이 일람

PRINTL
PRINTLC [310] - 손에 넣은 악보
PRINTLC [311] - 받은 선물
PRINTLC [312] - 잡은 물고기 리스트
PRINTLC [313] - 소지 아이템 목록
PRINTFORML 
PRINTLC [331] - 고유 요리 리스트
PRINTL 

PRINTL

SETCOLOR 103,228,126
PRINTL 【시스템 메뉴】
RESETCOLOR
SIF FLAG:기절중단
	SETCOLOR C_GRAY
PRINTLC [107] - 의뢰 리셋
RESETCOLOR
PRINTLC [700] - 의상 리셋

PRINTL
PRINTLC [400] - OPTION
PRINTLC [410] - 채집 리스트
PRINTLC [411] - 사냥한 조수 리스트
PRINTLC [412] - 잡은 곤충 리스트


SIF SAVESTR:본체버전 != eraTW_Version
	SETCOLOR C_RED
PRINTLC [500] - UPDATE
RESETCOLOR
PRINTLC [511] - 출현 장소 리스트
PRINTL 
PRINTLC [420] - TUTORIAL
RESETCOLOR

PRINTL
PRINTL

PRINTLC [200] - SAVE
PRINTLC [300] - LOAD
PRINTL

SIF MONEY:2
	PRINTFORMLC [600] - 카리스마 매각
FOR LOCAL,1,CHARANUM
	IF CFLAG:LOCAL:감금됨 || TALENT:LOCAL:당신의노예
		PRINTFORMLC [601] - 감금실/노예 설정
		BREAK
	ENDIF
NEXT
SIF FLAG:기절중단
	SETCOLOR C_GRAY
PRINTLC [602] - 농가 설정
SIF R_강습기회
	PRINTFORMLC [603] - 매음굴 강습
SIF FLAG:동거인
	PRINTFORMLC [604] - 동거 설정
SIF 스탯포인트
	PRINTFORMLC [605] - 스탯 분배
PRINTL
RESETCOLOR

PRINTL
CALL 외부SHOP파일목록


;record who have custon shop button
CALL createKojoShopButtonTargetList()
;show
CALL showKojoShopButton()
PRINTL
DRAWLINE
FLAG:SHOP처리중 = 0

;FileName_SHOP.ERB -------------------------------- Rev1. 00
;기상 전메뉴 RESULT 처리
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@USERSHOP
;기상
IF RESULT == 100
	시간흐름 = 0
	FOR LOCAL, 1, CHARANUM
		IF LOCAL <= CHARANUM
			CFLAG:LOCAL:오늘몸팜 = 0
		ENDIF
	NEXT
	BEGIN TRAIN
	RETURN 1
ELSEIF RESULT == 410
	CALL SHOW_GATHERING_LIST
;능력표시
ELSEIF RESULT == 101
	DEBUGCLEAR
	CALL SHOW_CHARA_LIST("존재하는 캐릭터리스트", "체력 기력", "「정보 표시」 「조건 변경」")
;	CALL SHOW_CHARADATA
;콜렉션
ELSEIF RESULT == 106 && FLAG:700 && !FLAG:기절중단
	CALL SHOW_COLLECTION
;통신 판매
ELSEIF RESULT == 103 && !FLAG:기절중단 &&!FLAG:감방남은일수
	CALL ITEM_BUY
;설정
ELSEIF RESULT == 102 && !FLAG:기절중단
	CALL SCHEDULE
;KR판 저택용
ELSEIF RESULT == 104 && !FLAG:기절중단 && MAIN_MAP==14
	CALL 주거설정함수
ELSEIF RESULT == 313
	CALL SHOW_ALLITEM
ELSEIF RESULT == 105 && !FLAG:기절중단
	CALL BONUS
ELSEIF RESULT == 107 && !FLAG:기절중단
	CALL IRAI_RESET
;108 - 악보, 109 - 낚시 기능 완전 이식시 주석해제
ELSEIF RESULT == 310
	CALL SHOW_COLLECTION_SCORE
ELSEIF RESULT == 312
	CALL SHOW_COLLECTION_FISH
ELSEIF RESULT == 110 && CFLAG:MASTER:현재위치 != OMANEKIBEYA()&& !FLAG:기절중단 &&!FLAG:감방남은일수
	CALL SET_MAINHOME
;너굴너굴
ELSEIF RESULT == 111 &&!FLAG:감방남은일수
	CALL 다누키은행
ELSEIF RESULT == 112
	[IF_DEBUG]
	CALL 지하매음굴
	[ENDIF]
ELSEIF RESULT == 113&&FLAG:조교전 &&!FLAG:감방남은일수
	CALL 조교전
ELSEIF RESULT == 114
	CALL 메모라이즈
;子供一覧
ELSEIF RESULT == 152 && CFLAG:MASTER:아이인원수
	CALL SHOW_ALLCHILDLIST
ELSEIF RESULT == 311
	CALL SHOW_COLLECTION_GIFT
;SAVE
ELSEIF RESULT == 200
	;SAVEGAME
	;KR판 추가수정, 저장용 커스텀 함수
	CALL SAVEGAME_KR
;LOAD
ELSEIF RESULT == 300
	;LOADGAME
	;KR판 추가수정, 로드용 커스텀 함수
	CALL LOADGAME_KR
ELSEIF RESULT == 331
	CALL SHOW_SP_DISH_LIST
;OPTION
ELSEIF RESULT == 400
	CALL OPTION
ELSEIF RESULT == 411
	CALL SHOW_COLLECTION_HUNTED
;잡은 곤충 리스트
ELSEIF RESULT == 412
	CALL SHOW_COLLECTION_MUSHI
ELSEIF RESULT == 420
	CALL TUTORIAL_MAIN
ELSEIF RESULT == 500
	CALL UPDATE
;출현 장소 리스트
ELSEIF RESULT == 511
	CALL SHOW_APPEAR_LIST
ELSEIF RESULT == 600
	CALL CM_BALANCE
ELSEIF RESULT == 601
	CALL 감금실설정
;농가 설정
ELSEIF RESULT == 602 && !FLAG:기절중단
	CALL RESTART_NOUMIN
ELSEIF RESULT == 603 && R_강습기회
	CALL 매음굴강습
ELSEIF RESULT == 604 && FLAG:동거인
	CALL 동거설정(FLAG:동거인)
ELSEIF RESULT == 605 && 스탯포인트
	CALL 레벨업(0)
ELSEIF RESULT == 700
	CALL RESET_ALL_CLOTHES
ELSEIF INRANGE(RESULT,1000,1100)
	CALL 외부SHOP파일호출(RESULT)
ELSEIF RESULT == 2400
	CALL 기상시간설정()
;custom shop button use
ELSEIF RESULT == 797
	CALL choiceButtonTarget()
ELSEIF RESULT == 798
	IF ShopButtonTargetId > 0
		ShopButtonTargetId --
		ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
	ENDIF
ELSEIF RESULT == 799
	IF ShopButtonTargetId + 1 < ShopButtonTargetNum
		ShopButtonTargetId ++
		ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
	ENDIF
ELSEIF INRANGE(RESULT, 800, 800 + MAX_CUSTOM_SHOP)
	CALL useKojoShopButton(RESULT - 800)
ENDIF


@TW_TIPS
SETCOLOR C_YELLOW
PRINTPLAIN TIPS:
PRINTDATA
	DATAFORM 특정 장소가 아니면 실행할 수 없는 명령어가 있다.
	DATAFORM 이름 옆의 【★】을 클릭하면, 【쫓는다】명령어로 그 캐릭터가 있는 곳으로 이동할 수 있다.
	DATAFORM 맵에 따라 텃밭이나 전투훈련 장소가 없다. 필요에 따라 이사하자.
	DATAFORM 모두에게 쉽게 사랑받는다는 꿈은 집어치워라, 그러기에 세상은 가혹하다
	DATAFORM 과도한 빚, 불행의 시작입니다
	DATAFORM 매일 밥을 먹지 않으면 건강에 해롭다
	DATAFORM 밥을 먹고 양치하지 않으면 이가 썩는다
	DATAFORM 시간정지 능력은 사용할수록 성장한다.
	DATAFORM 유실물을 찾고 싶으면 물건 찾는 게 특기인 캐릭터한테 부탁해라.
	DATAFORM 술은 술집에서 사준다.
	DATAFORM 운세 뽑기 및 점의 결과에 따라서는 일부 판정에 보정이 붙는다. 제비뽑기에도…
	DATAFORM 눈이 내리면 작물이 자라기 어려워진다.
	DATAFORM 카리스마는 홍마관의 카지노 등에서 쓸 수 있는 통화.
	DATAFORM 대개 현금보다 카리스마로 지불하는 게 싼 편.
	DATAFORM 일부 캐릭터는 고유 커맨드가 있다.
	DATAFORM 전투능력 및 교양이 낮으면 호감도가 올라가기 어려워질 때가 있다.
	DATAFORM 웬만한 건 신한테 비는 걸로 해결할 수 있다.
	DATAFORM 인간 마을 광장에서는 랜덤 캐릭터를 낚을 수 있다.
	DATAFORM 랜덤 캐릭터는 첫날만 능력이 많이 오른다.
	DATAFORM 랜덤 캐릭터를 망각했을 경우 두 번 다시 재등장하지 않지만, 임신 판정을 하는 것은 망각했을 경우만.
	DATAFORM 랜덤 캐릭터가 재등장시키는 경우, 능력 상승 보정을 잃어 임신하지 않게 된다.
	DATAFORM 랜덤 캐릭터를 두 명 이상 동시에 출현시킬 수 없다.
	DATAFORM 랜덤 캐릭터 정보는 dat 폴더에 저장된다.
	DATAFORM 만난 맵이 아니면 랜덤 캐릭터를 재등장시킬 수 없다.
	DATAFORM 금욕이 힘들어지면 순순히 싸거나 마음의 평안을 소원으로 빌어라.
	DATAFORM 주야역전 생활하는 캐릭터도 있다. 친해지고 싶으면 기상 시간을 늦추자.
	DATAFORM 커맨드가 표시되지 않는다? 필터가 걸렸는지 봐봐.
	DATAFORM 무능력자라면 다양한 커맨드를 사용할 수 없지만 덮쳐지기 쉬워진다.
	DATAFORM 캐릭터의 업무는 돕는 데에 조건이 있거나 애초에 돕지 못하는 것도 있다.
	DATAFORM 요리하려면 식재료가 필요.
	DATAFORM 술벌레 병에 물을 넣으면 술이 생긴다.
	DATAFORM 로터는 텃밭에서 만들 수 있다.
	DATAFORM 더러움 내성이 높으면 청소가 엉성하게 된다.
	DATAFORM 카지노에서 많이 놀면 회원 랭크가 오른다.
	DATAFORM 시간각인은 같은 레벨의 쾌락·괘씸각인에 준한 효과가 있다. 뻣뻣한 캐릭터라면 몸부터 친해지는 것도 한 방편.
	DATAFORM 콜렉션의 스키마 등은 현실 날짜가 바뀌면 보충된다. 소지수 상한이 있으니 적극적으로 사용하자.
	DATAFORM 당신의 능력 및 특정 인물과의 친교 등, 조건을 충족해야 들어갈 수 있는 장소가 있다.
	DATAFORM 공부나 훈련은 다른 사람과 함께 하는 편이 효율적.
	DATAFORM 애태우기 모드는 부위 쾌락이 들어가기 어려워지는 대신에 절정을 억제한다. 다중 절정에 다소 도움이 된다.
	DATAFORM 스즈나안에서 책을 빌리면 대응기능의 경험이 들어가기 쉬워진다.
	DATAFORM 미라다의 돌은 욕망을 올리기 쉽고 덮치기가 먹히기 쉬워지지만, 친밀과 신뢰는 올라가기 어려워진다.
	DATAFORM 1년 이상 플레이하면 옵션의 바이처 더스트로 일부 능력을 계승해 주회 가능하다.
	DATAFORM 동시 절정하면 호감도 상승치나 절정·피사정에 따른 소스 추가에 보너스가 들어간다.
	DATAFORM 3일 금욕하면 【농후정액】이 되어 사정이 강화되지만, 사정한 다음 날에는 【농후정액】을 상실한다.
	DATAFORM 계속 금욕하면 스트레스를 받거나 몽정한다.
	DATAFORM 요리평가가 80점 이상이라면 기분이 좋아지고, 35점 이하라면 나빠진다.
	DATAFORM 츠쿠모가미는 욕정이 올라가기 어렵고, 임신 확률이 낮은 편이지만 플레이어의 도구사용경험이 완화된다.
	DATAFORM 신뢰도가 너무 낮으면 호감도가 올라가기 어려워진다. 데이트 중이라면 이 제약은 사라진다.
	DATAFORM 육아중은 욕구 불만도에 상한이 걸린다. 아이가 자거나 통학중이라면…
	DATAFORM 조제 세트를 소지하고 있지 않아도, 조제 가능한 장소가 몇개인가 있다.
	DATAFORM 조제 성공율에는 조제 Lv 외에 교양도 영향을 준다.
	DATAFORM 특정 계절에만 채집할 수 있는 소재가 있다.
	DATAFORM 고급 재료를 채집하고 싶다면, 채집 레벨을 올려라.
	DATAFORM 일하는 사람 앞에서 연주하지 말자.
	DATAFORM 연회 중에 연주하면 쌈짓돈을 받을 수도 있지만, 솜씨가 모자라면 혼난다.
	DATAFORM 발원해서 인연 맺기를 원하면, 다음날 그 캐릭터가 내방해 온다.
	DATAFORM 일등의 예정은 능력 화면이나 캘린더에서 확인할 수 있다.
	DATAFORM 캐릭터에 따라서는 능력화면이나 화상 설정으로 다른 화상으로 변경할 수 있다.
	DATAFORM 타깃으로 하고 있는 캐릭터를 클릭하면 버튼 하나로 스탠딩과 얼굴 화상을 전환할 수 있다。
	DATAFORM 일부 캐릭터에게서 소재를 살 수 있다.
	DATAFORM 【요정】은 「자연」 선물을 좋아한다.
	DATAFORM 【유치】는 「캐릭터」 선물을 좋아한다.
	DATAFORM 「스펠 카드」 선물은 전투 능력이 없는 아이 말고는 좋아한다.
	DATAFORM 【요괴】【유령】은 대체로 「주술」이나 「법구」 선물을 싫어한다.
	DATAFORM 「요술」 선물은 다들 싫어한다.
	DATAFORM 「요술」「뼈」「피」「독」 선물은 요괴 이외의 대부분의 캐릭터가 싫어한다.
	DATAFORM 축제일은 노점 커맨드의 매출・이익률이 좋아진다.
	DATAFORM 지저에서는 채소가 비싸게 팔린다.
	DATAFORM 산정에서는 생선이 비싸게 팔린다.
	DATAFORM 【푸짐】한 요리는 포만도가 높고,【가벼운】요리는 적다。
ENDDATA
PRINTFORML 

@물가조정
#DIM DYNAMIC 변동치
IF RAND:2 == 0
	변동치 = 물가변동()
	IF FLAG:물가 + 변동치 <= 50
		IF 변동치 <= -30
			변동치 = -30
		ENDIF
		변동치 += 50 - FLAG:물가
	ENDIF
	IF FLAG:물가 + 변동치 >= 300
		변동치 = -10
	ENDIF
	FLAG:물가 += 변동치
	
	IF 변동치 == 0
		PRINTFORML 오늘의 물가는 변동 없습니다 (원가의 {FLAG:물가}\%)
	ELSEIF 변동치 > 0
		PRINTFORML 오늘의 물가는 {변동치}\%p 상승했습니다 (원가의 {FLAG:물가}\%)
	ELSEIF 변동치 < 0
		PRINTFORML 오늘의 물가는 {변동치* -1}\%p 하락했습니다 (원가의 {FLAG:물가}\%)
	ENDIF
ELSE
	PRINTFORML 오늘의 물가는 변동 없습니다 (원가의 {FLAG:물가}\%)
ENDIF

@물가변동
#FUNCTION
IF FLAG:물가 <= 150||명중판정(75)
	LOCAL = 2
	LOCAL += RAND:3
	LOCAL += FLAG:물가변동폭
	FLAG:물가변동폭 = 0
	IF LOCAL <= 0
		LOCAL = 1
	ENDIF
	RETURNF (RAND:LOCAL + 1) - (LOCAL/2)
ELSE
	;물가가 150% 보다 높으면 하향할 확률 증가, 이지만 그래도 이게 뜰 확률은 25% 밖에 안됨
	LOCAL = 2
	LOCAL += RAND:4
	LOCAL += FLAG:물가변동폭
	FLAG:물가변동폭 = 0
	IF LOCAL <= 0
		LOCAL = 1
	ENDIF
	RETURNF (RAND:LOCAL -1) - (LOCAL * 4 / 3)
ENDIF

PRINTFORML 

;虫リストを参考に
@SHOW_ALLCHILDLIST
#DIM CHILDNUM
#DIM START,10
#DIM ページ
#DIM 最後のページ
#DIM P調整
#DIM L調整
#DIM LASTNUM
#DIM CONST MAXLINE = 20
;各ページの頭番を設定する
IF !START
	P調整 = 0
	L調整 = 0
	LASTNUM = 0
	FOR LOCAL, 1, CHARANUM
		SIF !CFLAG:LOCAL:아이인원수
			CONTINUE
		L調整 ++
		IF L調整 > MAXLINE
			P調整 ++
			START:P調整 = LOCAL
			L調整 = 1
		ENDIF
		LASTNUM = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = P調整 + 1
	START:最後のページ = LASTNUM + 1
ENDIF

VARSET LOCAL
PRINTFORML ◆아이리스트 PAGE.{ページ} 총계{CFLAG:MASTER:아이인원수}인
DRAWLINE
FOR LOCAL, START, START:ページ
	SIF !CFLAG:LOCAL:아이인원수
		CONTINUE
	PRINTFORM [{LOCAL, 3}] %CALLNAME:LOCAL, 16, LEFT%
	CALL SHOW_CHILDDATA(LOCAL, "ICON")
	LOCAL:1 ++
NEXT
WHILE LOCAL:1 < MAXLINE
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [999] 다음페이지
PRINTL [998] 이전페이지
PRINTL [  0] 돌아간다
INPUT
;戻る
IF !RESULT
	VARSET START
	RETURN
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;閲覧
ELSEIF INRANGE(RESULT, 1, CHARANUM) && CFLAG:RESULT:아이인원수
	PRINTFORML %"━" * 50%
	PRINTFORML 　%CALLNAME:RESULT%의 아이 일람
	PRINTFORML %"━" * 50%
	PRINTFORML 
	CALL SHOW_CHILDDATA(RESULT)
	LOCAL:2 = RESULT
	DRAWLINE
	PRINTL [  0] 돌아간다
	INPUT
	CLEARLINE (MAXLINE + LOCAL:2 + 4)
ENDIF
CLEARLINE (MAXLINE + 7)
RESTART

;虫リストを参考に
@SHOW_SP_DISH_LIST
#DIM C_ID
#DIM START,10
#DIM ページ
#DIM 最後のページ
#DIM P調整
#DIM L調整
#DIM LASTNUM
#DIM CONST MAXLINE = 20
;各ページの頭番を設定する
IF !START
	P調整 = 0
	L調整 = 0
	LASTNUM = 0
	FOR LOCAL, 1, CHARANUM
		RESULT = 0
		TRYCALLFORM SPECIAL_MENU_{LOCAL}
		SIF !RESULT
			CONTINUE
		L調整 ++
		IF L調整 > MAXLINE
			P調整 ++
			START:P調整 = LOCAL
			L調整 = 1
		ENDIF
		LASTNUM = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = P調整 + 1
	START:最後のページ = LASTNUM + 1
ENDIF

VARSET LOCAL
PRINTFORML ◆고유요리 리스트 PAGE.{ページ}
DRAWLINE
FOR LOCAL, START, START:ページ
	RESULT = 0
	TRYCALLFORM SPECIAL_MENU_{LOCAL}
	SIF !RESULT
		CONTINUE
	SIF LEARNED_MENU:LOCAL > 100
		SETCOLOR C_YELLOW
	PRINTFORM  [{LOCAL, 3}] %CALLNAME:LOCAL, 16, LEFT%
	PRINTFORML %DISH_NAME% （%RESULTS%）
	LOCAL:1 ++
	RESETCOLOR
NEXT
WHILE LOCAL:1 < MAXLINE
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [999] 다음페이지
PRINTL [998] 이전페이지
PRINTL [  0] 돌아간다
INPUT
;戻る
IF !RESULT
	VARSET START
	RETURN
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
ELSEIF INRANGE(RESULT, 1, CHARANUM)
	C_ID = RESULT
	RESULT = 0
	TRYCALLFORM SPECIAL_MENU_{C_ID}
	IF RESULT
		PRINTFORML 【%DISH_NAME%】%DISH_COMMENT%
		CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")
		PRINTFORMW 
		CLEARLINE 3
	ENDIF
ENDIF
CALL RESET_DISH
CLEARLINE (MAXLINE + 7)
RESTART


@SHOP파일오염검사()
RETURN 69745882

@외부SHOP파일목록
FOR LOCAL,0,5
	TRYCALLFORM ExtSHOP_{LOCAL}_EXIST()
	IF RESULT >= 1000
		LOCAL = RESULT
		CALLFORM ExtSHOP_{LOCAL}("FNAME")
		LOCALS = RESULTS
		PRINTFORMLC [{LOCAL}] - %LOCALS,12,LEFT%
		LOCAL:1 ++
		IF LOCAL:1 == 3
			PRINTL
			LOCAL:1 = 0
		ENDIF
	ELSE
	ENDIF
NEXT

@외부SHOP파일호출(ARG)
TRYCALLFORM ExtSHOP_{ARG - 1000}_EXIST()
IF RESULT == ARG
	CALLFORM ExtSHOP_{LOCAL}("FDO")
	RETURN 1
ENDIF
RETURN 0