

@ITEM_BUY(ARG)
#DIM LCOUNT
#DIMS TYPE
#DIM SELECT_ITEM
#DIM 루프
;모두의 것↓은 거동 확인용 같았기 때문에 없고 좋다
;PRINTFORML {ITEMSALES:62}
;購入条件設定
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SALES")
	ITEMSALES:LOCAL = RESULT
NEXT
IF !루프
	DRAWLINE
	CALL LIST_ITEM("전부")
ENDIF
DRAWLINE
;우선 무조건 전부 표시로 했지만 TYPE 지정도 가능
PRINTFORML 구입할 아이템을 선택해주세요 소지금: %금액표시(MONEY)%
DRAWLINE
CALL SHOW_SHOPPING("통신판매", TYPE)
LCOUNT = RESULT
DRAWLINE
PRINTL [990] - 모두 표시　[991] - 조교 아이템　[992] - 소모품　[993] - 일용품　[994] - 식재료　[995] - 주류　[996] - 소질
PRINT [999] - 돌아간다
INPUT

;戻る
IF RESULT == 999
	루프 = 0
	RETURN
ELSEIF INRANGE(RESULT, 990, 996)
	SELECTCASE RESULT
	CASE 990
		TYPE = 
	CASE 991
		TYPE = 조교
	CASE 992
		TYPE = 소모품
	CASE 993
		TYPE = 일용품
	CASE 994
		TYPE = 식재료
	CASE 995
		TYPE = 주류
	CASE 996
		TYPE = 소질
	ENDSELECT
ELSEIF INRANGE(RESULT, 0, 1100)
	SELECT_ITEM = RESULT
	SELECTCASE ITEMSTOCK(SELECT_ITEM)
	CASE 1, 4
		PRINTW 유효하지 않은 입력입니다
	CASE 2
		PRINTW 품절입니다
	CASE 3
		PRINTW 소지금이 충분하지 않습니다
	CASE 5
		PRINTW 이미 충분히 가지고 있습니다
	CASEELSE
		CALL ITEMDATA(SELECT_ITEM, "구입")
		루프 = 0
		RESTART
	ENDSELECT
	LCOUNT ++
ENDIF
루프 = 1
CLEARLINE 7 + LCOUNT
RESTART


;----------------------------------------------------------------------
;기본 가게 메뉴
;----------------------------------------------------------------------
@SHOW_SHOPPING(판매점, TYPE, 할인)
#DIMS 판매점
#DIMS TYPE
#DIM 할인
#DIM 표시수
#DIM 표시행
#DIM 가격
표시수 = 0
표시행 = 0
FOR LOCAL, 0 , 1100
	CALL ITEMDATA(LOCAL, "SHOP:" + 판매점)
	IF RESULT
		IF TYPE != ""
			CALL ITEMDATA(LOCAL, "TYPE:" + TYPE)
		ELSE
			RESULT = 1
		ENDIF
		IF RESULT
			가격 = ITEMPRICE:LOCAL * (100 - 할인 + FLAG:물가) / 100
			SELECTCASE ITEMSTOCK(LOCAL, 가격)
			CASE 1
				LOCALS:1 = 존재하지 않음
				CONTINUE
			CASE 2
				LOCALS:1 = 매진
				SETCOLOR C_GRAY
			CASE 3
				LOCALS:1 = %금액표시(가격)%
				SETCOLOR C_GRAY
			CASE 4
				LOCALS:1 = 봉인
				CONTINUE
			CASE 5
				LOCALS:1 = 다 들 수 없다
				SETCOLOR C_GRAY
			CASEELSE
				LOCALS:1 = %금액표시(가격)%
			ENDSELECT
			LOCALS = [{LOCAL,2}]%ITEMNAME:LOCAL%
			PRINTFORM %LOCALS, 24, LEFT% %LOCALS:1, 8% 
			RESETCOLOR
			표시수 ++
			IF 표시수 % 3 == 0
				PRINTL 
				표시행 ++
			ENDIF
		ENDIF
	ENDIF
NEXT
IF 표시수 % 3 != 0
	PRINTL 
	표시행 ++
ENDIF
RETURN 표시행

;---------------------------------------------------------------------
;ITEM 소지수리스트 표시 함수
;---------------------------------------------------------------------
@LIST_ITEM(TYPE = "전부")
#DIMS TYPE
#DIM 표시수

SELECTCASE TYPE
CASE "전부"
	CALL LIST_ITEM("조교")
	CALL LIST_ITEM("일용품")
	CALL LIST_ITEM("소질")
	CALL LIST_ITEM("체질 변화")
	CALL LIST_ITEM("조합물")
	CALL LIST_ITEM("탄막")
	CALL LIST_ITEM("소모품")
	CALL LIST_ITEM("주류")
	CALL LIST_ITEM("식재료")
	CALL LIST_ITEM("야채")
	CALL LIST_ITEM("고기")
	CALL LIST_ITEM("초콜렛")
	CALL LIST_ITEM("회복")
	CALL LIST_ITEM("원예용품")
	CALL LIST_ITEM("소재")
	CALL LIST_ITEM("의상")
	CALL LIST_ITEM("서적")
	CALL LIST_ITEM("특수")
	CALL LIST_ITEM("그 외")
CASEELSE
	;全部回す
	표시수 = 0
	FOR LOCAL, 0, 1100
		CALL ITEMDATA(LOCAL, "TYPE:" + TYPE)
		IF RESULT && ITEM:LOCAL
			PRINTFORMLC %ITEMNAME:LOCAL%({ITEM:LOCAL})
			표시수 ++
			SIF 표시수 % 4 == 0
				PRINTL 
		ENDIF
	NEXT
	SIF 표시수 % 4 != 0
		PRINTL 
ENDSELECT

;----------------------------------------------------------------------
;買う前に最終チェックをするもの
;ITEMDATAから呼ばれる
;購入後どうなるかはアイテム次第（主に素質）
;----------------------------------------------------------------------
@SHOP_ASK(ARG, 할인)
#DIM 할인
#DIM price
PRINTFORML %조사처리(ITEMNAME:ARG,"를")% 구입하시겠습니까?
CALL ASK_YN
IF !RESULT
	IF ITEMPRICE:ARG * (100 - 할인 + FLAG:물가) / 100 <= MONEY
		price = ITEMPRICE:ARG * (100 - 할인 + FLAG:물가) / 100
		MONEY -= price
		PRINTFORMW %조사처리(ITEMNAME:ARG,"를")% %금액표시(price)%에 구입했습니다
	ELSE
		PRINTFORMW 돈이 부족하다…
		RETURN 2
	ENDIF
ENDIF
RETURN RESULT

;--------------------------------------------------------------
;아이템 정리하고 구매 함수
;任意の갯수で購入できるものはこれ
;우동 로부터 정리하고 사고 하고 싶었기 때문에 독립 함수화
;반드시 먼저@ITEMSTOCK로 판정해 구입 가능한 아이템만으로 호출하는 것
;단체 구입 전용이겠지만 정리하고 사 가능하겠지만 구입 가불가의 판정은 필요해서 먼저 공통으로 가 주세요
;ARG：구입 대상 아이템
;買ったら가격を返す
;--------------------------------------------------------------
@ITEM_MATOMEGAI(ARG, 할인)
#DIM 할인
#DIM 가격
PRINTFORM %조사처리(ITEMNAME:ARG,"를")% 몇 개 구입하겠습니까?(재고: {ITEMSALES:ARG} 소지수: {ITEM:ARG})
PRINTL 
가격 = ITEMPRICE:ARG * (100 - 할인 + FLAG:물가) / 100
;돈부족, 아이템수의 초과는 나중에 맞춘다
;텍붕이 패치 - 현재 아이템 갯수와 상관없이 일정한 숫자로 뜨게
SIF ITEMSALES:ARG
	PRINTL [ 1]  - 1　
SIF MIN(ITEMSALES:ARG, 소지수제한) >= 5
	PRINTL [ 5]  - 5　
SIF MIN(ITEMSALES:ARG, 소지수제한) >= 10
	PRINTL [10] - 10　
SIF MIN(ITEMSALES:ARG, 소지수제한) >= 20
	PRINTL [20] - 20　
SIF MIN(ITEMSALES:ARG, 소지수제한) >= 50
	PRINTL [50] - 50　
PRINTFORML [{MIN(ITEMSALES:ARG, 소지수제한), 2}] 전부
PRINTL [ 0]  - 그만둔다
INPUT
IF INRANGE(RESULT, 1, ITEMSALES:ARG)
	IF 가격 * RESULT > MONEY
		PRINTFORMW 돈이 부족하다…
		RETURN 0
	;소지수제한 제압
	;ELSEIF ITEM:ARG + RESULT > 소지수제한
	;	PRINTFORMW 그렇게 사면 다 들 수 없다！
	;	RETURN 0
	ENDIF
	MONEY -= 가격 * RESULT
	ITEMSALES:ARG -= RESULT
	ITEM:ARG += RESULT
	PRINTFORMW %조사처리(ITEMNAME:ARG,"를")% {RESULT}개, %금액표시(가격 *RESULT)%에 구입했습니다
	;쌀 리필
	ITEMSALES:1033 = 99
	RETURN 가격
ENDIF
RETURN 0

;----------------------------------------------------------------------
;アイテムをカートンで買う関数
;購入数が固定のものはこれ,
;1個でも最終チェックを飛ばして買うのに使う
;ARG：購入対象アイテム
;カートン商品は소지수제한を超えた場合でも余剰分を省いて購入できる
;品物を購入すると가격を返す、なんやかんやで買ってないと0
;----------------------------------------------------------------------
@ITEM_CARTON(ARG, 가격, 갯수, 할인)
#DIM 갯수
#DIM 가격
#DIM 할인
IF !갯수
	갯수 = 1
;소지수제한 제압
;ELSEIF ITEM:ARG + 갯수 > 소지수제한
;	갯수 = 소지수제한 - ITEM:ARG
ENDIF
SIF !가격
	가격 = ITEMPRICE:ARG * 갯수
가격 = 가격 * (100 - 할인 + FLAG:물가) / 100

IF 가격 > MONEY
	PRINTW 돈이 부족하다…
ELSEIF ITEMSALES:ARG == -1
	PRINTW 매진입니다
;소지수제한 제압
;ELSEIF ITEM:ARG >= 소지수제한
;	PRINTW 충분히 가지고 있다
ELSE
	MONEY -= 가격
	ITEMSALES:ARG -= 갯수
	ITEM:ARG += 갯수
	PRINTFORMW %조사처리(ITEMNAME:ARG,"를")% {갯수}개、%금액표시(가격)%에 구입했습니다
	RETURN 가격
ENDIF
RETURN 0

;----------------------------------------------------------------------
;설명문付きのアイテム陳列
;----------------------------------------------------------------------
@ITEM_SHOPPING(ARG, 설명문, 할인)
#DIMS 설명문
#DIM 할인
#DIM 가격
가격 = ITEMPRICE:ARG * (100 - 할인 + FLAG:물가) / 100
;在庫確認
CALL ITEMDATA(ARG, "SALES")
ITEMSALES:ARG = RESULT
LOCALS '= 금액표시(가격)
IF RESULT <= 0
	SETCOLOR C_GRAY
	설명문 = 매진
ENDIF
SIF 가격 > MONEY
	SETCOLOR C_GRAY
;소지수제한 제압
;IF ITEM:ARG >= 소지수제한
;	SETCOLOR C_GRAY
;	설명문 = 가질 수 없다
;ENDIF

LOCALS = [{ARG, 3}] %ITEMNAME:ARG, 22, LEFT% %금액표시(가격)%

;LOCALS = [{ARG, 3}] %ITEMNAME:ARG, 22, LEFT% %LOCALS%

PRINTFORML %LOCALS, 48, LEFT% %설명문%
RESETCOLOR

;----------------------------------------------------------------------
;カートン商品等の特殊なアイテム陳列
;----------------------------------------------------------------------
@EX_SHOPPING(ARG, 상품명, 설명문, 가격, 갯수, 표시번호)
#DIMS 상품명
#DIMS 설명문
#DIM 할인
#DIM 가격
#DIM 갯수
#DIM 표시번호
SIF !가격
	가격 = ITEMPRICE:ARG * 갯수
SIF !표시번호
	표시번호 = ARG
가격 = 가격 * (100 - 할인 + FLAG:물가) / 100
LOCALS = [{표시번호, 3}] %상품명, 22, LEFT% %금액표시(가격)%
CALL ITEMDATA(ARG, "SALES")
ITEMSALES:ARG = RESULT
SIF RESULT <= 0
	SETCOLOR C_GRAY
SIF 가격 > MONEY
	SETCOLOR C_GRAY
;소지수제한 제압
;SIF ITEM:ARG >= 소지수제한
;	SETCOLOR C_GRAY
PRINTFORML %LOCALS,48,LEFT%%설명문%
RESETCOLOR

;------------------------------------------------------------------------
;  쌀·술·모종의 재고 보충
;	싸구려식재는 1일의 마지막에+10, 가게에는 최대로 20이 보관됩니다
;	상급식재는 1일의 마지막에+7, 가게의 최대재고는 15입니다
;	고급식재는 1일의 마지막에+5, 가게의 최대재고는 10입니다
;----------------------------------------------------------------------
@EVENTTURNEND
#PRI
CALL 가게에입하(GETNUM(ITEM, "싸구려식재"),	10,	20)
CALL 가게에입하(GETNUM(ITEM, "상급식재"), 7, 15)
CALL 가게에입하(GETNUM(ITEM, "고급식재"), 5, 10)
CALL 가게에입하(GETNUM(ITEM, "감미재"), 5, 10)
CALL 가게에입하(GETNUM(ITEM, "짐승고기",), 5, 10)
CALL 가게에입하(GETNUM(ITEM, "새고기",), 5, 10)
;술집은 독자적으로 입하
CALL 술집의재고

CALL 가게에입하(GETNUM(ITEM, "거름"), 1, 3)
; 모종은 1종 1일 1개 입하, 최대재고도 3입니다
;	모종을 허술하게 하면(자) 할까 링에 화를 들어요?
FOR LOCAL, 0, 1100
	IF STRCOUNT(ITEMNAME:LOCAL, "의모종")
		CALL 가게에입하(LOCAL, 1, 3)
	ELSE
		;ついでに야채가게,入荷しないこともある
		CALL ITEMDATA(LOCAL, "SHOP:야채가게")
		SIF RESULT
			CALL 가게에입하(LOCAL, RAND:2, 5)
	ENDIF
NEXT
ITEMSALES:1033 = 99

; RETURN 1 해서는 안됩니다

;----------------------------------------------------------------------
; 가게에입하 함수
;	EVENTTURNEND에서의 입하와 술집에서의 매각때에 사용한다
;----------------------------------------------------------------------
@가게에입하(ARG, 입하량, 최대재고)
#DIM 입하량
#DIM 최대재고
SELECTCASE ITEMSALES:ARG
CASE IS >= 최대재고
	; 과잉 재고 때는 재고 입하하지 않는다
CASE -1, 0
	ITEMSALES:ARG = 입하량
CASEELSE
	ITEMSALES:ARG = MIN(ITEMSALES:ARG + 입하량, 최대재고)
ENDSELECT
