;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;MASTER 생성 처리
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_CHARAMAKE(ARG)
;NO는 적당하게 설정
NO:ARG = 0

;성별(1=여성 2= 남성 3=후타나리)
TALENT:ARG:2 = 1
;MASTER는 초기 성별=남자
SIF ARG == 0
	TALENT:ARG:2 = 2
;기력, 체력
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0
MAXBASE:ARG:1 = 2000
BASE:ARG:1 = MAXBASE:ARG:1

;모드 선택
$INPUT_LOOP
PRINTL ★★모드를 선택해 주세요★★
PRINTL [0]START [1]역극 모드
INPUT
IF RESULT == 0
	FLAG:5 = 0
ELSEIF RESULT == 1
	$LOOP_NARIKIRI
	FOR LOCAL, 1, CHARANUM
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	NEXT
	PRINTL
	DRAWLINE
	PRINTL [900] 그만둔다
	PRINTL 누구로 되겠습니까?
	INPUT
	SELECTCASE RESULT
		CASE 1 TO CHARANUM
			NAME:MASTER = %NAME:RESULT%
			CALLNAME:MASTER = %CALLNAME:RESULT%
			FOR LOCAL, 0,200
				TALENT:MASTER:LOCAL = TALENT:RESULT:LOCAL
			NEXT
			FOR LOCAL, 0,55
				ABL:MASTER:LOCAL = ABL:RESULT:LOCAL
			NEXT
			FOR LOCAL, 0,14
				MAXBASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
				BASE:MASTER:LOCAL = MAXBASE:RESULT:LOCAL
			NEXT
			FOR LOCAL, 0,100
				EXP:MASTER:LOCAL = EXP:RESULT:LOCAL
			NEXT
			CFLAG:MASTER:7 = RESULT
			CFLAG:MASTER:초기위치 = CFLAG:RESULT:초기위치
			CFLAG:RESULT:출금 = 1
			FLAG:역극 = RESULT
			PRINTFORMW %CALLNAME:RESULT%로서 게임을 개시합니다
			IF !TALENT:MASTER:처녀
				PRINTL 처녀가 되겠습니까?
				PRINTL [0]네 [1]아니오
				INPUT
				IF RESULT == 0
					TALENT:MASTER:처녀 = 1
					EXP:MASTER:V경험 = 0
					ABL:MASTER:V감각 = 0
					EXP:MASTER:V성교경험 = 0
					EXP:MASTER:V확장경험 = 0
					EXP:MASTER:정음경험 = 0
				ENDIF
			ENDIF
			IF MAXBASE:MASTER:체력 < 2000
				PRINTFORML 이 캐릭터는 체력이 낮은입니다. 2000까지 올립니까?
				CALL ASK_YN("올린다","이대로")
				SIF RESULT == 0
					MAXBASE:MASTER:체력 = 2000
			ENDIF
			IF MAXBASE:MASTER:기력 < 2000
				PRINTFORML 이 캐릭터는 기력이 낮은입니다. 2000까지 올립니까?
				CALL ASK_YN("올린다","이대로")
				SIF RESULT == 0
					MAXBASE:MASTER:기력 = 2000
			ENDIF
			CALL CLOTHES_CUSTOM
			RETURN 0
		CASE 900
			GOTO INPUT_LOOP
		CASEELSE
			GOTO LOOP_NARIKIRI
	ENDSELECT
ELSE
	GOTO INPUT_LOOP
ENDIF

;이름 결정
CALL NAME_CUSTOM(ARG)

;신분 설정
CALL CUSTOM_TERMINAL(ARG)



;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;CALL		USER
;ARG		NONE
;RETURN		VOID
;COMMENT	
;캐릭터를 추가했을 때에 하나 하나 여기를 만질 필요가 없게
;캐릭터수상한은 ERH 파일로 정의
;-----------------------------------------------------------
@ADD_ALL_CHARACTERS
FOR LOCAL, CHARANUM, 캐릭터수상한
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	ADDCHARA LOCAL
	TARGET = LOCAL
	CALL SET_MINIMUM_EXP_ALL(LOCAL)
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	SIF RESULT
		CFLAG:LOCAL:대사실장상황 = 1
NEXT
CVARSET BASE, GETNUM(BASE, "술기운"), 0

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;MASTER 신분 설정 처리
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CUSTOM_TERMINAL(ARG)
;배타적 연산
;MASTER가 여자, 후타나리일 때의 배타
IF TALENT:ARG:성별 & 1
	ABL:ARG:호모끼 = 0
	ABL:ARG:게이중독 = 0
	EXP:ARG:게이경험 = 0
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:사정경험 = 0
	SIF TALENT:ARG:성별 != 1
		TALENT:ARG:구현 = 0
;MASTER가 남자일 때의 배타
ELSE
	ABL:ARG:V감각 = 0
	ABL:ARG:레즈끼 = 0
	ABL:ARG:레즈중독 = 0
	EXP:ARG:레즈경험 = 0
	EXP:ARG:분유경험 = 0
	EXP:ARG:V경험 = 0
	EXP:ARG:파이즈리경험 = 0
	EXP:ARG:V성교경험 = 0
	EXP:ARG:V확장경험 = 0
	TALENT:ARG:V감도 = 0
	TALENT:ARG:처녀 = 0
	TALENT:ARG:모유체질 = 0
	TALENT:ARG:가슴사이즈 = -2
	TALENT:ARG:걸레 = 0
	TALENT:ARG:초경 = 0
	TALENT:ARG:구현 = 0
ENDIF
;자지 붙으면 정력 세트
IF TALENT:ARG:2 & 2
	MAXBASE:ARG:정력 = 1000
	BASE:ARG:정력 = 1000
	MAXBASE:ARG:발기 = 1500
	;실적 보너스분
	SIF ARG == MASTER && GETBIT(FLAG:702, 8)
		MAXBASE:MASTER:정력 += 100
ELSE
	TALENT:ARG:비동정 = 0
	MAXBASE:ARG:정력 = 0
	BASE:ARG:정력 = 0
ENDIF
;처녀라면 V계 경험 없음
IF TALENT:ARG:처녀 == 1
	ABL:ARG:V감각 = 0
	EXP:ARG:V경험 = 0
	EXP:ARG:V성교경험 = 0
	EXP:ARG:V확장경험 = 0
;비처녀는 V경험 1
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:V경험 == 0
	EXP:ARG:V경험 = 1
ENDIF

;===========
;화면 표시
;===========
DRAWLINE
PRINTFORML 이름：%NAME:ARG%　통칭：%CALLNAME:ARG%
CALL BASE_BAR(ARG, "체력", 18, "초록", "「개행」")
CALL BASE_BAR(ARG, "기력", 18, "파랑", "「개행」")
;능력·성기술표시
CALL PRINT_STATE_ABL(ARG)
PRINTL 
;소질표시
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
;경험표시
CALL PRINT_STATE_EXP(ARG)
PRINTL
;------------------------------
DRAWLINE
;최종결정
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL 정말 이것으로 괜찮겠습니까?
;이름의 설정
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
;베이스 설정
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
;소질 설정
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG, LOCAL:101)
;능력 설정
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
;경험 설정
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ELSEIF LOCAL:100 == 1006
	CALL CLOTHES_CUSTOM
ENDIF
;------------------------------
DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ?  네 # 결정\@  [1001] - 이름의 변경  [1002] - 체력, 기력의 설정
PRINTL [1003] - 소질의 설정  [1004] - 능력의 설정  [1005] - 경험의 설정
INPUT
;최종결정
IF RESULT == 1000 && LOCAL:100 == 1000
	SIF NO:MASTER == NO:ARG
		CALL CLOTHES_CUSTOM
	RETURN 0
;설정계 입력
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF

;베이스 설정
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 2500
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 2500
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
;소질 설정
ELSEIF LOCAL:100 == 1003
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;함락 소질 외 (1에서도 2에서도 안 돼)
	SIF 선택할수없는소질(RESULT, NO:ARG)
		RESTART
	;성별
	IF RESULT == GETNUM(TALENT, "성별")
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;성별기호
	ELSEIF RESULT == GETNUM(TALENT, "성별기호")
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;온 오프형의 소질
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT, 1) || RESULT == 1 || RESULT == 6
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
		;온 오프형으로 별도 추가가 필요한 소질
		;모유체질
		IF RESULT == 149
			IF TALENT:ARG:RESULT
				MAXBASE:ARG:모유 = 2000
			ELSE
				MAXBASE:ARG:모유 = 0
			ENDIF
		ENDIF
	;체형
	ELSEIF RESULT == GETNUM(TALENT, "체형")
		;체형이 「난쟁이(-5)」이라고 「거인(3)」의 경우를 독립 처리
		IF TALENT:ARG:RESULT == -5
			TALENT:ARG:RESULT = -2
		ELSEIF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -5
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;양적 소질
	ELSE
		;구석까지 가면―1을 건다
		IF STRLENS(GET_TALENTNAME(RESULT, TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;능력 설정
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;동성애 관계
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;중독 관계
	SIF LOCAL >= 30
		RESTART
	;홀수(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
	;짝수(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;동성애 관계
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		RESTART
	ENDIF
	PRINTFORM %EXPNAME:LOCAL%를 입력해주세요      
	PRINTBUTTON "[캔슬]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT, 30)
ELSEIF RESULT == 1006
ENDIF
RESTART

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;이름의 설정
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@NAME_CUSTOM(ARG)
PRINTL 캐릭터의 이름(NAME)을 입력해주세요
IF STRLENS(NAME:ARG)
	PRINTFORM 현재의 이름：%NAME:ARG%      
	PRINTBUTTON "[변경하지 않음]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL 캐릭터의 통칭(CALLNAME)을 입력해 주세요
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 현재의 통칭：%CALLNAME:ARG%      
	PRINTBUTTON "[변경하지 않음]",""
	PRINTL
ENDIF
INPUTS
IF RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;베이스의 설정
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@BASE_CUSTOM(ARG)
CALL BASE_BAR(ARG, "체력", 18, "초록")
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 2500
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
CALL BASE_BAR(ARG, "기력", 18, "파랑")
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 2500
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;소질의 설정
;CALL		USER
;ARG		0:MASTER 캐릭터 NO, 1:페이지수
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@TALENT_CUSTOM(ARG, ARG:1)
VARSET LOCAL
FOR LOCAL, 0, 157
	SETCOLOR 150, 150, 150
	IF 선택할수없는소질(LOCAL, NO:ARG) == 1
		CONTINUE
	ELSEIF 선택할수없는소질(LOCAL, NO:ARG) == 2
		;표시는 시킨다
		SETCOLOR 50, 50, 50
		LOCALS = [{LOCAL, 3}]%TALENTNAME:LOCAL%
		GOTO SKIP
	ENDIF
	SELECTCASE TALENTNAME:LOCAL
	;TALENT.CSV에 등록되지 않았다
	CASE ""
		CONTINUE
	;온 오프형의 소질(TALENTNAME와 소질표시가 일치)
	CASE GET_TALENTNAME(LOCAL, 1)
		SIF TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL, 3}]%TALENTNAME:LOCAL%
	CASE "비동정"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL, 3}]동정
	CASE "키스미경험"
		SIF !TALENT:ARG:LOCAL
			SETCOLOR C_AQUA
		LOCALS = [{LOCAL, 3}]%TALENTNAME:LOCAL%
	;양적 소질
	CASEELSE
		LOCALS =  [{LOCAL, 3}]%TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL))
			LOCALS:1 = 【%GET_TALENTNAME(LOCAL, TALENT:ARG:LOCAL)%】
			SETCOLOR C_AQUA
		ELSEIF TALENTNAME:LOCAL == "성별"
			LOCALS:1 = 【여자】
			SETCOLOR C_AQUA
		ELSE
			LOCALS:1 = 【－】
		ENDIF
		LOCALS += LOCALS:1
	ENDSELECT
	$SKIP
	SIF LOCAL:1 % 3 == 0
		PRINTL 
	IF 선택할수없는소질(LOCAL, NO:ARG) == 2
		PRINTPLAINFORM %LOCALS, 30, LEFT%
	ELSE
		PRINTFORM %LOCALS, 30, LEFT%
	ENDIF
	LOCAL:1 ++
NEXT
RESETCOLOR
PRINTL


;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;능력의 설정
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL, 0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;동성애 관계
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;중독도 없음
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL, 5, LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;경험의 설정
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL, 0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;동성애 관계
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	SIF LOCAL == 57 || (60 <= LOCAL && LOCAL <= 65)
		CONTINUE
	PRINTFORM [{LOCAL, 2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL, 5, LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

;FileName_SHOP_CUSTOM.ERB -------------------------- Rev1. 00
;복장의 설정
;CALL		USER
;ARG		MASTER 캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CLOTHES_CUSTOM(ARG)
;당신 이외라면 변경하지 않는다
SIF NO:ARG != NO:MASTER || FLAG:역극
	RETURN 0
;MASTER의 성별 판정
IF TALENT:ARG:2 & 1
	CFLAG:ARG:7 = 2
ELSE
	CFLAG:ARG:7 = 1
ENDIF
PRINTFORML 평상복을 선택해주세요.
DRAWLINE
$LOOP
PRINTL [0] - 전라
PRINTL [1] - 남장 세트
PRINTL [2] - 여장 세트
PRINTL [3] - 집사 세트
PRINTL [4] - 가정부 세트
PRINTL [5] - 신관 세트
PRINTL [6] - 작업복 세트
PRINTL [7] - 도복 세트
PRINTL [8] - 서생 세트
PRINTL [9] - 여학생 세트
INPUT
SELECTCASE RESULT
	CASE 0
	PRINTFORMW 너무 과격하잖아!
	GOTO LOOP
	CASE 1 TO 6
		CFLAG:MASTER:7 = RESULT
	CASE 7 TO 9
		CFLAG:MASTER:7 = RESULT + 1
	CASEELSE
		GOTO LOOP
ENDSELECT
RETURN 0

@선택할수없는소질(ARG, CHARA)
#FUNCTION
#DIM CHARA
;CHARA는 NO:의 형태로 건네받아 온다
;RETURNF 2는 표시는 시키는 것
SELECTCASE ARG
;정욕감지~, 보너스 소질 및 특수 함락, 종족, 잉태계
CASE IS >=157
	RETURNF 1
;당신은 선택할 수 없는 1
CASE GETNUM(TALENT, "키스미경험"), GETNUM(TALENT, "습득속도") TO GETNUM(TALENT, "바늘질"), GETNUM(TALENT, "약독내성"), GETNUM(TALENT, "즉각함락"), GETNUM(TALENT, "맹신"), GETNUM(TALENT, "소악마")
	SIF CHARA == NO:MASTER
		RETURNF 1
;당신은 선택할 수 없는 2
CASE GETNUM(TALENT, "여우") TO GETNUM(TALENT, "수수께끼의매력"), GETNUM(TALENT, "회복속도") TO GETNUM(TALENT, "이상한뿌리"), GETNUM(TALENT, "광기"), GETNUM(TALENT, "인기")
	SIF CHARA == NO:MASTER
		RETURNF 1
;당신은 선택하지 못하고, 여성기가 없으면 선택할 수 없다
CASE GETNUM(TALENT, "모유체질")
	SIF CHARA == NO:MASTER
		RETURNF 1
	SIF !GETBIT(TALENT:CHARA:성별, 0)
		RETURNF 2
;여성기가 없으면 선택할 수 없다
CASE GETNUM(TALENT, "처녀"), GETNUM(TALENT, "걸레"), GETNUM(TALENT, "V감도")
	SIF !GETBIT(TALENT:CHARA:성별, 0)
		RETURNF 2
;남성기가 없으면 선택할 수 없다
CASE GETNUM(TALENT, "비동정")
	SIF !GETBIT(TALENT:CHARA:성별, 1)
		RETURNF 2
;남성기가 있으면 선택할 수 없다
CASE GETNUM(TALENT, "구현")
	SIF GETBIT(TALENT:CHARA:성별, 1)
		RETURNF 2
;당신 이외는 선택할 수 없다
CASE GETNUM(TALENT, "연령")
	SIF CHARA != NO:MASTER
		RETURNF 1
;전원 선택할 수 없는 1
CASE GETNUM(TALENT, "연모") TO GETNUM(TALENT, "임신소망"), GETNUM(TALENT, "기분"), GETNUM(TALENT, "용모"), GETNUM(TALENT, "약점"), GETNUM(TALENT, "조합지식"), GETNUM(TALENT, "요호"), GETNUM(TALENT, "여우"), GETNUM(TALENT, "흡혈귀"), GETNUM(TALENT, "조제Lv"), GETNUM(TALENT, "채집Lv"), GETNUM(TALENT, "낚시Lv")
	RETURNF 1
;전원 선택할 수 없는 2
CASE GETNUM(TALENT, "자위중독") TO GETNUM(TALENT, "포르치오성감"), GETNUM(TALENT, "달변"), GETNUM(TALENT, "질투"), GETNUM(TALENT, "농후정액"), GETNUM(TALENT, "정애미각"), GETNUM(TALENT, "금단의지식"), GETNUM(TALENT, "절륜") TO GETNUM(TALENT, "모성")
	RETURNF 1
;여성기가 있어 유아나 유치가 없으면 선택할 수 없다
CASE GETNUM(TALENT, "초경")
	SIF !GETBIT(TALENT:CHARA:성별, 0)
		RETURNF 2
	SIF !TALENT:CHARA:애같음 && !TALENT:CHARA:유치
		RETURNF 2
ENDSELECT
RETURNF 0

;===========
;캐릭터 커스텀
;===========
@CHARA_CUSTOM

	PRINTL 캐릭터의 설정을 바꿀 수 있습니다
	FOR LOCAL, 0, CHARANUM
		IF FLAG:역극
			SIF LOCAL == FLAG:역극
				CONTINUE
		ENDIF
		PRINTFORMLC [{LOCAL}] - %CALLNAME:LOCAL%
	NEXT
	PRINTL [999] 설정 완료
	INPUT

	IF RESULT >= 0 && RESULT < CHARANUM
		SIF RESULT != FLAG:역극
			CALL CUSTOM_TERMINAL(RESULT)
	ELSEIF RESULT == 999
		PRINTL 괜찮겠습니까?
		PRINTL [0] 네
		PRINTL [1] 아니오
		PRINTL
		ONEINPUT
		IF RESULT == 0
			RETURN
		ENDIF
	ENDIF
	RESTART
RETURN 0

@LIMITED_CUSTOM(ARG)
$LOOP
PRINTFORML [0] 체형 변경 현재의 체형: %GET_TALENTNAME(100, TALENT:ARG:100)%
PRINTFORML [1] 가슴사이즈 변경 현재의 가슴사이즈: %GET_TALENTNAME(105, TALENT:ARG:105)%
PRINTFORM [2] 감도 상승             
	SELECTCASE TCVAR:ARG:감도상승부위
		CASE 101 TO 104,106
			PRINTFORML %TALENTNAME:(TCVAR:ARG:감도상승부위)% 선택중
		CASEELSE
			PRINTL
	ENDSELECT
DRAWLINE
PRINTL [100] 종료
INPUT
IF RESULT == 0
	PRINTLC [0] 난쟁이
	PRINTLC [3] 유아 체형
	PRINTLC [4] 작은 체형
	PRINTLC [5] 보통
	PRINTLC [6] 장신
	PRINTLC [7] 거구
	PRINTL
	DRAWLINE
	PRINTL [100] 돌아간다
	INPUT
	SELECTCASE RESULT
		CASE 0, 3 TO 7
			TALENT:ARG:100 = RESULT - 5
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 1
	PRINTLC [0] 절벽
	PRINTLC [1] 빈유
	PRINTLC [2] 평유
	PRINTLC [3] 거유
	PRINTLC [4] 폭유
	PRINTL
	DRAWLINE
	PRINTL [100] 돌아간다
	INPUT
	SELECTCASE RESULT
		CASE 0 TO 4
			TALENT:ARG:105 = RESULT - 2
	ENDSELECT
	GOTO LOOP
ELSEIF RESULT == 2
	PRINTL 한 부위만 감도를 올릴 수 있습니다
	PRINTFORMLC [101] C감도
	PRINTFORMLC [102] V감도
	PRINTFORMLC [103] A감도
	PRINTFORMLC [104] B감도
	PRINTFORMLC [106] M감도
	PRINTL
	DRAWLINE
	PRINTL [100] 돌아간다
	INPUT
	IF RESULT >= 101 && RESULT <= 104 || RESULT == 106
		IF TALENT:ARG:RESULT > 0
			PRINTW 벌써 민감합니다
		ELSE
			TCVAR:ARG:감도상승부위 = RESULT
		ENDIF
	ENDIF
	GOTO LOOP
ENDIF