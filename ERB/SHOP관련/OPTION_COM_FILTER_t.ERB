;FILTER용의 COMNAME 취득 함수
;우선 0~599의 범위에서 사용해주세요
@GET_COMNAME(ID_COM)
#FUNCTIONS
#DIM ID_COM
;파생 커맨드 용
SIF INRANGE(ID_COM, 500, 599)
	RETURNF STR:(1000 + ID_COM)
SIF INRANGE(ID_COM, 0, 499)
	RETURNF TRAINNAME:ID_COM
THROW 부정한 COM 번호가 건네받았던 {ID_COM}



@QUICKFILTER(ID_COM)
#FUNCTION
#DIM ID_COM
#DIM CONST BIT_V계       = 1
#DIM CONST BIT_A계       = 2
#DIM CONST BIT_봉사계     = 5
#DIM CONST BIT_SM계       = 6
#DIM CONST BIT_성희롱계 = 7
#DIM CONST BIT_가사계     = 9
#DIM CONST BIT_자위계     = 13  ;아마 사용하지 않다
SIF IS_COM_VAGINA(ID_COM)   && !CFLAG:TARGET:V언필터
	RETURNF 0
SIF IS_COM_ANAL(ID_COM)     && !CFLAG:TARGET:A언필터
	RETURNF 0
SIF IS_COM_HOUSI(ID_COM)    && !CFLAG:TARGET:봉사언필터
	RETURNF 0
SIF IS_COM_SM(ID_COM)       && !CFLAG:TARGET:SM언필터
	RETURNF 0
SIF IS_COM_KAJI(ID_COM)     && !FLAG:가사언필터
	RETURNF 0
SIF IS_COM_SEKUHARA(ID_COM) && !CFLAG:TARGET:성희롱언필터 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	RETURNF 0
;SIF IS_COM_ONANISM(ID_COM)  && !CFLAG:TARGET:자위 안필터
;	RETURNF 0
SIF IS_COM_KOJO(ID_COM) && !CFLAG:TARGET:allowKojoCom
	RETURNF 0
RETURNF 1



@필터표시
;Act_COMのラインと合体
SELECTCASE CFLAG:오토신음
	CASE 0
		LOCALS = 오토신음：하지 않는다
	CASE 1
		LOCALS = 오토신음：한다
	CASE 2
		LOCALS = 오토신음：상스럽다
ENDSELECT
PRINTFORM %"=" * 6% Act_COM ==
SIF STALKED == TARGET && STALKED
	SETCOLOR C_YELLOW
PRINTBUTTON "【★】", TARGET + 4000
RESETCOLOR
CALL PRINTBUTTON_EX("[V]", 831,!CFLAG:TARGET:V언필터)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[A]", 832,!CFLAG:TARGET:A언필터)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[SM]", 836,!CFLAG:TARGET:SM언필터)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[봉사]", 835,!CFLAG:TARGET:봉사언필터)
PRINTPLAIN =
;CALL PRINTBUTTON_EX("[자위]", 843,!CFLAG:TARGET:자위 안필터)
;PRINTPLAIN =
CALL PRINTBUTTON_EX("[성희롱]", 837,!CFLAG:TARGET:성희롱언필터)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[구상]",839,!CFLAG:TARGET:allowKojoCom)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[가사]", 840,!FLAG:가사언필터)
PRINTPLAIN =
CALL PRINTBUTTON_EX("[자동콘돔]",838,!CFLAG:자동고무)
PRINTPLAIN =
IF TARGET != MASTER
	CALL PRINTBUTTON_EX(@"[%LOCALS%]",4000,!CFLAG:오토신음)
	PRINTFORM %"=" * (41 - STRLENS(LOCALS))%
ELSE
	PRINTFORM %"=" * 43%
ENDIF
PRINTL =============================

;캐릭터라인 변수에 보존해 보았다
@OPTION_COMMAND_FILTER
#LOCALSIZE 600
#DIM ID_COM
CALL LOAD_COM_FILTER(LOCAL)
DRAWLINE
FOR ID_COM , 0, 600
	SIF !STRLENS(GET_COMNAME(ID_COM))
		CONTINUE
	CALL PRINTBUTTON_EXC(@"%GET_COMNAME(ID_COM)%[{ID_COM, 3}]", ID_COM, STRCOUNT(SAVESTR:0, @"/{ID_COM}/"))
NEXT
PRINTL 
DRAWLINE
PRINTFORML [1101] - V필터     [1102] - A필터       [1103] - 봉사 필터 
PRINTFORML [1104] - SM필터   [1105] - 성희롱필터 [1106] - 가사 필터 
PRINTFORML [1111] - ALL 필터
PRINTFORML [2000] - USERCOM필터
PRINTFORML [3000] - 필터의 보존 [3001] - 필터의 독입
PRINTFORML [1000] - 결정
PRINTL  
PRINTFORML [9999] - 리셋
PRINTL  
INPUT
SELECTCASE RESULT
CASE 1000
	RETURN 0
CASE 9999
	SAVESTR:0 = 
CASE 3000
	CALL SAVE_GLOBAL_COM_FILTER
CASE 3001
	CALL LOAD_GLOBAL_COM_FILTER
CASE 2000
	CALL SETUP_USERCOM_FILTER
CASE 1101
	CALL SET_FILTER_NG_VAGINA
CASE 1102
	CALL SET_FILTER_NG_ANAL
CASE 1103
	CALL SET_FILTER_NG_HOUSI
CASE 1104
	CALL SET_FILTER_NG_SM
CASE 1105
	CALL SET_FILTER_NG_SEKUHARA
CASE 1106
	CALL SET_FILTER_NG_KAJI
CASE 1111
	CALL SET_FILTER_NG_ALL
CASEELSE
	IF STRLENS(GET_COMNAME(RESULT))
		LOCAL:RESULT = !LOCAL:RESULT
		IF LOCAL:RESULT
			CALL ADD_COM_FILTER(RESULT)
		ELSE
			CALL DEL_COM_FILTER(RESULT)
			;SAVE_COM_FILTER(LOCAL)
		ENDIF
	ENDIF
ENDSELECT
RESTART



;필터에 NG인 COM 추가하는 함수
@ADD_COM_FILTER(NG_ID)
#DIM NG_ID
SIF SAVESTR:0 == ""
	SAVESTR:0 = /
SIF STRCOUNT(SAVESTR:0, @"/{NG_ID}/")
	RETURN
SAVESTR:0 += @"{NG_ID}/"

;필터로부터 NG인 COM 삭제하는 함수
;정규 표현이라든지 제대로 손댄 적 없지만 이것으로 움직이는 응?
@DEL_COM_FILTER(NG_ID)
#DIM NG_ID
SIF !STRCOUNT(SAVESTR:0, @"/{NG_ID}/")
	RETURN
SAVESTR:0 '= REPLACE(SAVESTR:0, @"%ESCAPE("/")%{NG_ID}%ESCAPE("/")%", ESCAPE("/"))



;REF 사용 COM_FILTER 보존 함수
;배열→캐릭터라인
;배열의 요소수가 600 미만이라고 화가 난다
@SAVE_COM_FILTER(NG_LIST)
#DIM REF NG_LIST
#DIM ID_COM
SAVESTR:0 = /
FOR ID_COM, 0, 600
	SIF !NG_LIST:ID_COM
		CONTINUE
	SIF STRCOUNT(SAVESTR:0, @"/{ID_COM}/")
		CONTINUE
	SAVESTR:0 += @"{ID_COM}/"
NEXT
;#;PRINTFORML SAVE_COM_FILTER：%SAVESTR:0%


;REF 사용 COM_FILTER독입함수
;캐릭터라인→배열
;배열의 요소수가 600 미만이라고 화가 난다
@LOAD_COM_FILTER(NG_LIST)
#DIM REF NG_LIST
#DIM ID_COM
VARSET NG_LIST
IF SAVESTR:0 == ""
	SAVESTR:0 = /
ELSE
	FOR ID_COM, 0, 600
		SIF STRCOUNT(SAVESTR:0, @"/{ID_COM}/")
			NG_LIST:ID_COM = 1
	NEXT
ENDIF
;#;PRINTFORML LOAD_COM_FILTER：%SAVESTR:0%


;NG캐릭터라인을 GLOBAL에 보존
@SAVE_GLOBAL_COM_FILTER
LOADGLOBAL
FOR LOCAL, 0, 3
	PRINTFORM [{LOCAL}] - 
	IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
		PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】
	ELSE
		PRINTL ----------
	ENDIF
NEXT
PRINTL [100] - 돌아간다
INPUT
DO
	LOCAL = RESULT
	SIF RESULT == 100
		RETURN
	SIF !INRANGE(RESULT, 0, 2)
		CONTINUE
	SIF STRLENS(GLOBALS:(RESULT * 2 + 1))
		PRINTFORML 【%GLOBALS:(RESULT * 2 + 1)%】 이 데이터에 덧쓰기합니다
	PRINTL 커맨드필터의 이름을 등록해 주세요
	INPUTS
	GLOBALS:(LOCAL * 2 + 1) = %RESULTS%
	GLOBALS:(LOCAL * 2) = %SAVESTR:0%
	PRINTFORMW 커맨드필터【%GLOBALS:(LOCAL * 2 + 1)%】를 등록했습니다
	SAVEGLOBAL
	RETURN
LOOP 1



;NG캐릭터라인을 GLOBAL로부터 독입
@LOAD_GLOBAL_COM_FILTER
LOADGLOBAL
FOR LOCAL, 0, 3
	PRINTFORM [{LOCAL}] - 
	IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
		PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】
	ELSE
		PRINTL ----------
	ENDIF
NEXT
PRINTL [100] - 돌아간다
INPUT
DO
	SIF RESULT == 100 
		RETURN
	SIF !INRANGE(RESULT, 0, 2)
		CONTINUE
	PRINTFORMW 커맨드필터 【%GLOBALS:(RESULT * 2 + 1)%】를 읽어들였습니다
	SAVESTR:0 = %GLOBALS:(RESULT * 2)%
	RETURN
LOOP 1



;솔직히 사용하지 않는 생각이 든다
@SETUP_USERCOM_FILTER
CALL PRINTBUTTON_EXC("이력 표시[800]",             800, GETBIT(FLAG:USERCOM필터, 12))
CALL PRINTBUTTON_EXC("불결 표시[801]",             801, GETBIT(FLAG:USERCOM필터,  0))
CALL PRINTBUTTON_EXC("상대를 바꾼다[802]",         802, GETBIT(FLAG:USERCOM필터,  1))
CALL PRINTBUTTON_EXC("능력 표시[803]",             803, GETBIT(FLAG:USERCOM필터,  2))
CALL PRINTBUTTON_EXC("능력 표시(마스터)[804]", 804, GETBIT(FLAG:USERCOM필터,  3))
CALL PRINTBUTTON_EXC("아이템 확인[805]", 805, GETBIT(FLAG:USERCOM필터,  4))
CALL PRINTBUTTON_EXC("자동 시간정지 이동[995]", 995, GETBIT(FLAG:USERCOM필터,  13))

;자동청소
CALL PRINTBUTTON_EXC("자동청소[996]", 996, GETBIT(FLAG:USERCOM필터,  14))

CALL PRINTBUTTON_EXC("커맨드필터[809]",     809, GETBIT(FLAG:USERCOM필터, 11))
PRINTL 
PRINTC 돌아오는 [0]
INPUT
SELECTCASE RESULT
CASE 0
	RETURN
CASE 801
	INVERTBIT FLAG:USERCOM필터, 0
CASE 802
	INVERTBIT FLAG:USERCOM필터, 1
CASE 803
	INVERTBIT FLAG:USERCOM필터, 2
CASE 804
	INVERTBIT FLAG:USERCOM필터, 3
CASE 805
	INVERTBIT FLAG:USERCOM필터, 4
CASE 809
	INVERTBIT FLAG:USERCOM필터, 11
	IF GETBIT (FLAG:USERCOM필터, 11)
		PRINTW 결정하면 수동으로 [809]를 입력하거나 OPTION으로부터 나오지 않으면 되돌릴 수 없게 됩니다
		CLEARLINE 1
	ENDIF
CASE 800
	INVERTBIT FLAG:USERCOM필터, 12
CASE 995
	INVERTBIT FLAG:USERCOM필터, 13
;自動掃除
CASE 996
	INVERTBIT FLAG:USERCOM필터, 14
ENDSELECT
CLEARLINE 3
RESTART


;V사용 커맨드 판정
;FLAG:1013, 1
@IS_COM_VAGINA(ID_COM)
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 3, 44, 60, 61, 65, 67, 68, 71, 122, 141, 143, 185, 316, 369, 503, 504, 506, 507, 513)
	RETURNF 1
RETURNF 0

;kojo custom com
@IS_COM_KOJO(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 270, 299)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_VAGINA
#DIM ID_COM
FOR ID_COM, 0, 600
	;V
	SIF !IS_COM_VAGINA(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT


;A사용 커맨드 판정
;FLAG:1013, 2
@IS_COM_ANAL(ID_COM)
#FUNCTION
#DIM ID_COM
SIF GROUPMATCH(ID_COM, 4, 5, 45, 62, 63, 66, 69, 70, 314, 142, 143, 146, 147, 148, 314, 369, 514, 515)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_ANAL
#DIM ID_COM
FOR ID_COM, 0, 600
	;A
	SIF !IS_COM_ANAL(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT




;봉사 커맨드 판정
;FLAG:1013, 5
@IS_COM_HOUSI(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 80, 85) || GROUPMATCH(ID_COM, 120, 121, 140, 501, 505, 508, 509, 510, 511, 512)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_HOUSI
#DIM ID_COM
FOR ID_COM, 0, 600
	;HOUSI
	SIF !IS_COM_HOUSI(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;SM 커맨드 판정
;FLAG:1013, 6
@IS_COM_SM(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 100, 119) || INRANGE(ID_COM, 140, 159)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_SM
#DIM ID_COM
FOR ID_COM, 0, 600
	;SM
	SIF !IS_COM_SM(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;성희롱 커맨드 판정
;FLAG:1013, 7
@IS_COM_SEKUHARA(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 305, 316) || INRANGE(ID_COM, 560, 563) || GROUPMATCH(ID_COM, 325,326,330, 350, 352,380, 429) ;KR판 오리지날 또는 구잔재, 325,326,380
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_SEKUHARA
#DIM ID_COM
FOR ID_COM, 0, 600
	;성희롱
	SIF !IS_COM_SEKUHARA(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;가사 커맨드 판정
;FLAG:1013, 9
@IS_COM_KAJI(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 410, 419) || GROUPMATCH(ID_COM, 403, 450)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_KAJI
#DIM ID_COM
FOR ID_COM, 0, 600
	SIF !IS_COM_KAJI(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;자위 커맨드 판정
;FLAG:1013, 13
@IS_COM_ONANISM(ID_COM)
#FUNCTION
#DIM ID_COM
SIF INRANGE(ID_COM, 700, 712) || INRANGE(ID_COM, 740, 752) || GROUPMATCH(ID_COM, 433)
	RETURNF 1
RETURNF 0

@SET_FILTER_NG_ONANISM
#DIM ID_COM
FOR ID_COM, 0, 600
	SIF !IS_COM_ONANISM(ID_COM)
		CONTINUE
	CALL ADD_COM_FILTER(ID_COM)
NEXT



;ALL 커맨드 판정
@SET_FILTER_NG_ALL
#DIM ID_COM
FOR ID_COM, 0, 600
	CALL ADD_COM_FILTER(ID_COM)
NEXT
