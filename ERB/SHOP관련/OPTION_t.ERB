;옵션 설정 처리
@OPTION
LOCAL:1 = 0
DRAWLINEFORM =
FOR LOCAL, 0, 100
	SIF OPTION_SETNAME(LOCAL) == ""
		CONTINUE
	PRINTFORML [{LOCAL, 2}] - %OPTION_SETNAME(LOCAL), 30, RIGHT%：%RESULTS:1%
	LOCAL:1 ++
NEXT
DRAWLINEFORM －
PRINTFORML [93] - 입맛 설정
PRINTFORML [94] - 오토신음일괄설정
PRINTFORML [95] - 간이 필터의 일괄 설정
;PRINTFORML [96] - 요리의 옵션 설정
PRINTFORML [97] - 그림 상세 설정
PRINTFORML [98] - 등장캐릭터변환
PRINTFORML [99] - 커맨드필터
DRAWLINE
IF DAY > 120 || FLAG:엔딩여부 ;KR판 오리지날, 엔딩여부 체크로 강하게 뉴게임 가능
	PRINTFORML [200] - 바이쳐 더스트
	LOCAL:1 ++
ENDIF
PRINTFORML [999] - 설정 완료
INPUT
IF OPTION_SETNAME(RESULT) != ""
	CALL OPTION_SETTING(RESULT)
	CLEARLINE 12 + LOCAL:1
ELSEIF RESULT == 93
	CALL 마스터_호불호선택
ELSEIF RESULT == 94
	CALL OPTION_오토신음일괄설정
ELSEIF RESULT == 95
	CALL 간이필터일괄설정
;ELSEIF RESULT == 96
;	CALL SET_OPTION_COOKING
ELSEIF RESULT == 97
	CALL OPTION_그림표시상세설정
ELSEIF RESULT == 98
	CALL 등장캐릭터변환
ELSEIF RESULT == 99
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 200 && (DAY > 120 || FLAG:엔딩여부)
	IF FLAG:채무
		LOCAL = 0
		LOCAL += 5000000
		LOCAL += (MONEY/1000)
		IF TALENT:69:연인
			LOCAL += 1000000
		ENDIF
		LOCAL -= (MARK:69:반발각인) * 500000
		IF LOCAL/5 < FLAG:채무
			PRINTFORML 「아따 지금 뭔 짓거리냥께?」
			PRINTFORML 「갈땐 가드라도 빚은 갚고 가야제, 어딜 튀려하는겨」
			PRINTFORMW 은행의 너구리에게 저지당했다...
			RESTART
		ENDIF
	ENDIF
	PRINTFORML 시간을 날려버려서 1일째로 되돌아가겠습니까?
	PRINTL 
	PRINTL [0] 「스위치」를 누르게 하지맛――
	PRINTL [1] 아냐! 한계다 누를래! 지금이닷!
	INPUT
	IF RESULT == 1
		CALL 강하게뉴게임
	ELSE
;		PRINTW 「스위치」를 누르는 건 멈추지 못했지만, 운 좋게도 옵션 선택까지밖에 돌아가지 않았다.
	ENDIF
ELSE
	RETURN
ENDIF
RESTART


@OPTION_SETNAME(ARG)
#FUNCTIONS
VARSET RESULTS
SELECTCASE ARG
CASE 0
	RESULTS:0 = 임신 유무※기본 OFF.
	IF FLAG:임신스위치
		RESULTS:1 = 임신한다 ※뭔가 발생했을 때 자기 책임으로　　
	ELSE
		RESULTS:1 = 임신하지 않는다
	ENDIF
CASE 1
	RESULTS:0 = 자식의성별
	SELECTCASE FLAG:자식의성별
	CASE 0
		RESULTS:1 = 랜덤
	CASE 1
		RESULTS:1 = 여자아이
	CASE 2
		RESULTS:1 = 남자아이
	ENDSELECT
CASE 2
	RESULTS:0 = 자식묘사
	IF FLAG:자식묘사
		RESULTS:1 = 있음
	ELSE
		RESULTS:1 = 없음
	ENDIF
CASE 3
	RESULTS:0 = 더부살이이벤트
	IF FLAG:더부살이이벤트
		RESULTS:1 = 없음
	ELSE
		RESULTS:1 = 있음
	ENDIF
CASE 4
	RESULTS:0 = 연회 발생
	IF FLAG:연회의유무
		RESULTS:1 = 있음
	ELSE
		RESULTS:1 = 없음
	ENDIF
CASE 5
	RESULTS:0 = 상대 쪽의 고백
	IF FLAG:고백금지
		RESULTS:1 = 고백하지 않는다
	ELSE
		RESULTS:1 = 고백한다
	ENDIF
CASE 6
	RESULTS:0 = 섹프계 함락에서 연모계 함락으로 변화
	IF FLAG:함락노선변화
		RESULTS:1 = 변화한다
	ELSE
		RESULTS:1 = 변화하지 않는다
	ENDIF
CASE 7
	RESULTS:0 = 이벤트로 [무지] 상실 발생
	IF FLAG:무지자동상실금지
		RESULTS:1 = 상실하지 않는다
	ELSE
		RESULTS:1 = 상실한다
	ENDIF
CASE 8
	RESULTS:0 = 텍스트 설정
	IF FLAG:정경텍스트설정 == 1 && FLAG:구상텍스트설정 == 2
		RESULTS:1 = 대사 텍스트＋정경 텍스트
	ELSEIF FLAG:정경텍스트설정 == 1 && FLAG:구상텍스트설정 == -1
		RESULTS:1 = 정경 텍스트만
	ELSEIF FLAG:정경텍스트설정 == 0 && FLAG:구상텍스트설정 == -1
		RESULTS:1 = 텍스트를 표시하지 않는다
	ENDIF
CASE 9
	RESULTS:0 = 대사색깔
	IF FLAG:대사색깔
		RESULTS:1 = 있음
	ELSE
		RESULTS:1 = 없음
	ENDIF
CASE 10
	RESULTS:0 = 불결도 표시
	IF FLAG:쓰레기표시OFF
		RESULTS:1 = 하지 않는다
	ELSE
		RESULTS:1 = 한다
	ENDIF
CASE 11
	RESULTS:0 = 카운터로 소질 변화
	IF FLAG:카운터지문제어
		RESULTS:1 = 소질이 변화한다
	ELSE
		RESULTS:1 = 변화하지 않는다
	ENDIF
CASE 12
	RESULTS:0 = 가슴 관련 텍스트 설정
	IF FLAG:Look표시용유방 == 1 && FLAG:Look표시용유두 == 1
		RESULTS:1 = 가슴 관련 전부 고정 묘사
	ELSEIF FLAG:Look표시용유방 == 1 && FLAG:Look표시용유두 == 0
		RESULTS:1 = 유방만 고정 묘사
	ELSEIF FLAG:Look표시용유방 == 0 && FLAG:Look표시용유두 == 1
		RESULTS:1 = 유두만 고정 묘사
	ELSEIF FLAG:Look표시용유방 == 0 && FLAG:Look표시용유두 == 0
		RESULTS:1 = 가슴 관련 랜덤 묘사
	ENDIF
CASE 13
	RESULTS:0 = 호의/이성의 벽 단축표시
	IF FLAG:호의이성단축
		RESULTS:1 = 단축한다
	ELSE
		RESULTS:1 = 단축하지 않는다
	ENDIF
CASE 14
	RESULTS:0 = 능력 표시 버튼 표기
	IF FLAG:능력표기
		RESULTS:1 = 숫자 표기
	ELSE
		RESULTS:1 = 기호 표기
	ENDIF
CASE 15
	RESULTS:0 = 턴마다 나오는 ABLUP 생략
	IF FLAG:ABLUP생략
		RESULTS:1 = 생략한다
	ELSE
		RESULTS:1 = 생략하지 않는다
	ENDIF
CASE 16
	RESULTS:0 = 첫키스 기록 상황 설정
	SELECTCASE FLAG:FK판정
	CASE 0
		RESULTS:1 = 키스만·자각했을 때만
	CASE 1
		RESULTS:1 = 키스만·미자각 포함
	CASE 2
		RESULTS:1 = 펠라 포함·자각했을 때만
	CASE 3
		RESULTS:1 = 펠라 포함·미자각 포함
	ENDSELECT
CASE 17
	RESULTS:0 = 이동시 멈춰 서기
	SELECTCASE FLAG:멈춰서기
	CASE 0
		RESULTS:1 = 무조건 멈춰 선다
	CASE 1
		RESULTS:1 = 대사가 존재할 경우에만
	CASE 2
		RESULTS:1 = 멈춰 서지 않는다
	ENDSELECT
CASE 18
	RESULTS:0 = 캐릭터 만남시 의뢰 표시
	IF FLAG:의뢰패스
		RESULTS:1 = 표시하지 않는다
	ELSE
		RESULTS:1 = 표시한다
	ENDIF
CASE 19
	RESULTS:0 = 소모품의 자동 구입
	IF FLAG:소모품자동구입
		RESULTS:1 = 한다
	ELSE
		RESULTS:1 = 하지 않는다
	ENDIF
CASE 20
	RESULTS:0 = 자동 시간정지 이동
	SELECTCASE FLAG:순간이동
	CASE 0
		RESULTS:1 = ＯＦＦ
	CASE 1
		RESULTS:1 = 외출지 산책시에만
	CASE 2
		RESULTS:1 = 거점내 이동시에만
	CASE 3
		RESULTS:1 = 외출지/거점내 양쪽 다
	ENDSELECT
CASE 21
	RESULTS:0 = 능력을 비교해서 덮쳐지지 않게 되는 판정
	IF FLAG:덮치기
		RESULTS:1 = 있음
	ELSE
		RESULTS:1 = 없음
	ENDIF
;CASE 22
;	RESULTS:0 = 경고장사용
;	IF FLAG:경고장사용
;		RESULTS:1 = 있음
;	ELSE
;		RESULTS:1 = 없음
;	ENDIF
CASE 23
	RESULTS:0 = 내방모드
	SELECTCASE FLAG:교류내방모드
	CASE 0
		RESULTS:1 = 통상
	CASE 1 ;KR판 구잔재, 일판은 내방플래그.ERB 변경되어 50
		RESULTS:1 = 호감도 0은 내방하지 않는다
	CASE 2
		RESULTS:1 = 함락된 캐릭터만
	CASE 3
		RESULTS:1 = 내방하지 않는다
	ENDSELECT
CASE 24
	RESULTS:0 = 탈의시 명령 선택
	SELECTCASE FLAG:탈의선택
	CASE 0
		RESULTS:1 = 매번 선택
	CASE 1
		RESULTS:1 = 전라
	CASE 2
		RESULTS:1 = 반라
	ENDSELECT
CASE 25
	RESULTS:0 = 시간정지 탈의시 팬티 처리
	SELECTCASE FLAG:팬티도둑
	CASE 0
		RESULTS:1 = 매번 선택
	CASE 1
		RESULTS:1 = 항상 훔친다
	CASE 2
		RESULTS:1 = 항상 벗기기만 할 뿐
	ENDSELECT
CASE 26
	RESULTS:0 = 일부 캐릭터의 니삭스 착용
	IF FLAG:니삭스착용
		RESULTS:1 = 있음
	ELSE
		RESULTS:1 = 없음
	ENDIF
CASE 27
	RESULTS:0 = 야한 속옷을 추가한다
	IF FLAG:섹시팬티추가
		RESULTS:1 = 추가한다
	ELSE
		RESULTS:1 = 추가하지 않는다
	ENDIF
CASE 28
	RESULTS:0 = 팬티 수집 상황 표시
	IF FLAG:팬티수집상황타입B
		RESULTS:1 = 미입수여도 표시한다
	ELSE
		RESULTS:1 = 미입수를 표시하지 않는다
	ENDIF
CASE 29
	RESULTS:0 = 지도 표시 타입
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RESULTS:1 = 컬러맵 + 영상모드
		ELSE
			RESULTS:1 = 컬러맵
		ENDIF
	ELSE
		RESULTS:1 = 디폴트
	ENDIF
CASE 30
	RESULTS:0 = 캐릭터리스트 표시 순서
	IF FLAG:정렬
		RESULTS:1 = 원작순
	ELSE
		RESULTS:1 = ＴＷ순
	ENDIF
CASE 31
	RESULTS:0 = 방뇨와 오줌싸개버릇의 유효화
	IF FLAG:오줌유효
		RESULTS:1 = 유효
	ELSE
		RESULTS:1 = 무효
	ENDIF
CASE 32
	RESULTS:0 = 채집시 재추첨 기능
	IF FLAG:채집재추첨
		RESULTS:1 = 유효
	ELSE
		RESULTS:1 = 무효
	ENDIF
CASE 36 ;KR판 오리지날
	RESULTS:0 = 일상 호감 신뢰 상승 배율 설정
	IF FLAG:호감신뢰상승률
		RESULTS:1 = (현재 {1+FLAG:호감신뢰상승률}배)
	ELSE
		RESULTS:1 = 디폴트
	ENDIF
CASE 37 ;KR판 오리지날
	RESULTS:0 = 의뢰 호감 신뢰 상승 배율 설정
	IF FLAG:의뢰호감신뢰상승률
		RESULTS:1 = (현재 {1+FLAG:의뢰호감신뢰상승률}배)
	ELSE
		RESULTS:1 = 디폴트
	ENDIF
CASE 38
	RESULTS:0 = 창녀의 경과 묘사 생략
	IF FLAG:창녀묘사간소화
		RESULTS:1 = (생략한다)
	ELSE
		RESULTS:1 = (생략하지 않는다)
	ENDIF
CASE 39
	RESULTS:0 = 창관 납치 이벤트
	IF FLAG:NTR이벤트
		RESULTS:1 = (발생한다)
	ELSE
		RESULTS:1 = (발생하지 않는다)
	ENDIF
CASE 40
	RESULTS:0 = 영양소 시스템
	IF !FLAG:영양소시스템미사용
		RESULTS:1 = (사용한다)
	ELSE
		RESULTS:1 = (사용하지 않는다)
	ENDIF
ENDSELECT
RETURNF RESULTS:0

@OPTION_SETTING(ARG)
SELECTCASE OPTION_SETNAME(ARG)
CASE "임신 유무※기본 OFF."
	FLAG:임신스위치 = !FLAG:임신스위치
CASE "텍스트 설정"
	IF FLAG:정경텍스트설정 == 1 && FLAG:구상텍스트설정 == 2
		FLAG:정경텍스트설정 = 1
		FLAG:구상텍스트설정 = -1
	ELSEIF FLAG:정경텍스트설정 == 1 && FLAG:구상텍스트설정 == -1
		FLAG:정경텍스트설정 = 0
		FLAG:구상텍스트설정 = -1
	ELSEIF FLAG:정경텍스트설정 == 0 && FLAG:구상텍스트설정 == -1
		FLAG:정경텍스트설정 = 1
		FLAG:구상텍스트설정 = 2
	ENDIF
CASE "소모품의 자동 구입"
	FLAG:소모품자동구입 = !FLAG:소모품자동구입
CASE "불결도 표시"
	FLAG:쓰레기표시OFF = !FLAG:쓰레기표시OFF
CASE "이동시 멈춰 서기"
	CALL 옵션바꿔치기("멈춰서기", 2)
CASE "능력을 비교해서 덮쳐지지 않게 되는 판정"
	FLAG:덮치기 = !FLAG:덮치기
CASE "자동 시간정지 이동"
	CALL 옵션바꿔치기("순간이동", 3)
CASE "더부살이이벤트"
	FLAG:더부살이이벤트 = !FLAG:더부살이이벤트
CASE "경고장사용"
	FLAG:경고장사용 = !FLAG:경고장사용
CASE "일부 캐릭터의 니삭스 착용"
	FLAG:니삭스착용 = !FLAG:니삭스착용
CASE "연회 발생"
	FLAG:연회의유무 = !FLAG:연회의유무
CASE "내방모드"
	CALL 옵션바꿔치기("교류내방모드", 3)
CASE "탈의시 명령 선택"
	CALL 옵션바꿔치기("탈의선택", 2)
CASE "시간정지 탈의시 팬티 처리"
	CALL 옵션바꿔치기("팬티도둑", 2)
CASE "대사색깔"
	FLAG:대사색깔 = !FLAG:대사색깔
CASE "가슴 관련 텍스트 설정"
	IF FLAG:Look표시용유방 == 1 && FLAG:Look표시용유두 == 1
		FLAG:Look표시용유방 = 1
		FLAG:Look표시용유두 = 0
	ELSEIF FLAG:Look표시용유방 == 1 && FLAG:Look표시용유두 == 0
		FLAG:Look표시용유방 = 0
		FLAG:Look표시용유두 = 1
	ELSEIF FLAG:Look표시용유방 == 0 && FLAG:Look표시용유두 == 1
		FLAG:Look표시용유방 = 0
		FLAG:Look표시용유두 = 0
	ELSEIF FLAG:Look표시용유방 == 0 && FLAG:Look표시용유두 == 0
		FLAG:Look표시용유방 = 1
		FLAG:Look표시용유두 = 1
	ENDIF
CASE "능력 표시 버튼 표기"
	FLAG:능력표기 = !FLAG:능력표기
CASE "카운터로 소질 변화"
	FLAG:카운터지문제어 = !FLAG:카운터지문제어
CASE "야한 속옷을 추가한다"
	FLAG:섹시팬티추가 = !FLAG:섹시팬티추가
	IF !FLAG:섹시팬티추가
		PRINTFORMW 환상향에 찾아든 야한 속옷 붐이 종식되었습니다
		PRINTL 
		PRINTFORML 보유하고 있는 야한 속옷을 삭제하겠습니까?
		PRINTFORML 삭제 대상：스캔티, 훈도시, G스트링, 시스루 쇼츠, 오픈 쇼츠, 반창고, 드로워즈(가랑이 뚫림)
		PRINTL 
		PRINTL [0] 이젠 필요 없어
		PRINTL [1] 소중히 보관한다
		INPUT
		IF RESULT == 0
			CALL DELETE_SEXYPANTS
			CLEARLINE 10
		ELSE
			CLEARLINE 8
		ENDIF
	ENDIF
CASE "섹프계 함락에서 연모계 함락으로 변화"
	FLAG:함락노선변화 = !FLAG:함락노선변화
CASE "상대 쪽의 고백"
	FLAG:고백금지 = !FLAG:고백금지
CASE "턴마다 나오는 ABLUP 생략"
	FLAG:ABLUP생략 = !FLAG:ABLUP생략
CASE "이벤트로 [무지] 상실 발생"
	FLAG:무지자동상실금지 = !FLAG:무지자동상실금지
CASE "호의/이성의 벽 단축표시"
	FLAG:호의이성단축 = !FLAG:호의이성단축
CASE "자식묘사"
	FLAG:자식묘사 = !FLAG:자식묘사
CASE "자식의성별"
	CALL 옵션바꿔치기("자식의성별", 2)
CASE "팬티 수집 상황 표시"
	FLAG:팬티수집상황타입B = !FLAG:팬티수집상황타입B
CASE "첫키스 기록 상황 설정"
	CALL 옵션바꿔치기("FK판정", 3)
CASE "캐릭터 만남시 의뢰 표시"
	FLAG:의뢰패스 = !FLAG:의뢰패스
CASE "지도 표시 타입"
	IF RECOLOREDMAPS
		IF ANIMATERECOLOREDMAPS
			RECOLOREDMAPS = 0
			ANIMATERECOLOREDMAPS = 0
		ELSE
			ANIMATERECOLOREDMAPS = 1000
		ENDIF
	ELSE
		RECOLOREDMAPS = 1
	ENDIF
CASE "캐릭터리스트 표시 순서"
	FLAG:정렬 = !FLAG:정렬
CASE "방뇨와 오줌싸개버릇의 유효화"
	FLAG:오줌유효 = !FLAG:오줌유효
CASE "채집시 재추첨 기능"
	FLAG:채집재추첨 = !FLAG:채집재추첨
CASE "일상 호감 신뢰 상승 배율 설정"
	PRINTFORML 변경할 일반 호감도/신뢰 추가 획득 배율 입력 『현재 : {1 + FLAG:호감신뢰상승률}x, 최대 20』
	INPUT
	FLAG:호감신뢰상승률 = LIMIT(RESULT, 0, 20)
CASE "의뢰 호감 신뢰 상승 배율 설정"
	PRINTFORML 변경할 의뢰 호감도/신뢰 추가 획득 배율 입력 『현재 : {1 + FLAG:의뢰호감신뢰상승률}x, 최대 10』
	INPUT
	FLAG:의뢰호감신뢰상승률 = LIMIT(RESULT, 0, 10)
CASE "창녀의 경과 묘사 생략"
	FLAG:창녀묘사간소화 = !FLAG:창녀묘사간소화
CASE "창관 납치 이벤트"
	FLAG:NTR이벤트 = !FLAG:NTR이벤트
CASE "영양소 시스템"
	FLAG:영양소시스템미사용 = !FLAG:영양소시스템미사용
ENDSELECT

@옵션바꿔치기(ARGS, ARG)
FLAG:ARGS ++
SIF FLAG:ARGS > ARG
	FLAG:ARGS = 0

@OPTION_오토신음일괄설정
CALL ASK_M("전원없음",1,"전원있음（천박함 없음）",1,"전원있음（천박함 있음）",1, "취소한다",1)
IF INRANGE(RESULT, 0, 2)
	FOR LOCAL,1,CHARANUM
		CFLAG:LOCAL:오토신음 = RESULT
	NEXT
	PRINTFORMW 일괄설정했습니다.
ENDIF

@등장캐릭터변환
PRINTL 　지정한 캐릭터가 등장하지 않도록 합니다. ("*"는 대사가 있는 캐릭터)
DRAWLINE
SIF FLAG:정렬
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:정렬
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF TALENT:LOCAL:스침
		CONTINUE
	SIF TALENT:LOCAL:사망
		CONTINUE
	SIF CFLAG:LOCAL:출금
		SETCOLOR C_D_RED
	RESULT = 0
	TRYCALLFORM M_KOJO_K{NO:LOCAL}
	PRINTFORMLC [{LOCAL, 3}] \@ RESULT ? * # \@%CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTL 　
DRAWLINE
PRINTFORML [999] 설정 종료　출연금지 캐릭터：{FLAG:출금인원수}명　[998]리셋
$LOOP
INPUT
IF RESULT >= 1 && RESULT <= CHARANUM
	IF CFLAG:RESULT:출금 && FLAG:역극 != RESULT
		CFLAG:RESULT:출금 = 0
		SIF CFLAG:RESULT:현재위치 == CFLAG:RESULT:초기위치 && CFLAG:RESULT:수면
			LOCK:(CFLAG:RESULT:초기위치) = 1
		FLAG:출금인원수 --
	ELSEIF TALENT:RESULT:스침
		PRINTFORMW 이 캐릭터는 출연금지를 해제할 수 없습니다
		GOTO LOOP
	ELSEIF FLAG:역극 == RESULT
		IF CFLAG:(FLAG:역극):출금 == 1
			PRINTFORML 현재 그 캐릭터의 역극 중이기에, %CALLNAME:RESULT%가 두 명 있게 됩니다만…
			PRINTL 그래도 등장시키겠습니까?
			PRINTL [0]네 [1]아니오
			INPUT
			IF RESULT == 0
				CFLAG:(FLAG:역극):출금 = 0
				SIF CFLAG:(FLAG:역극):현재위치 == CFLAG:(FLAG:역극):초기위치 && CFLAG:(FLAG:역극):수면
					LOCK:(CFLAG:(FLAG:역극):초기위치) = 1
				FLAG:출금인원수 --
				PRINTFORMW %CALLNAME:(FLAG:역극)%를 등장시킵니다
			ELSE
				GOTO LOOP
			ENDIF
		ELSE
			CFLAG:(FLAG:역극):출금 = 1
			SIF LOCK:(CFLAG:(FLAG:역극):초기위치) && CFLAG:(FLAG:역극):초기위치 < 100
				LOCK:(CFLAG:(FLAG:역극):초기위치) = 0
			FLAG:출금인원수 ++
		ENDIF
	ELSE
		CFLAG:RESULT:출금 = 1
		SIF LOCK:(CFLAG:RESULT:초기위치) && CFLAG:RESULT:초기위치 < 100
			LOCK:(CFLAG:RESULT:초기위치) = 0
		FLAG:출금인원수 ++
	ENDIF
	RESTART
ELSEIF RESULT == 999
	PRINTW 설정 종료했습니다.
ELSEIF RESULT == 998
	FOR LOCAL,1,CHARANUM
		SIF FLAG:역극 == LOCAL
			CONTINUE
		SIF TALENT:LOCAL:스침
			CONTINUE
		CFLAG:LOCAL:출금 = 0
	NEXT
	FLAG:출금인원수 = 0
	RESTART
ELSE
	GOTO LOOP
ENDIF

;간이 필터 제어 함수
;사양상의 문제로서 설정 항목이 너무 증가하면 설계를 다시 할 필요가 있다
@간이필터일괄설정
#DIM OP_CFLAG_V언필터
#DIM OP_CFLAG_A언필터
#DIM OP_CFLAG_SM언필터
#DIM OP_CFLAG_봉사언필터
#DIM OP_CFLAG_성희롱언필터
#DIM OP_SETTING
IF !LOCAL
	OP_CFLAG_V언필터       = GETNUM(CFLAG, "V언필터")
	OP_CFLAG_A언필터       = GETNUM(CFLAG, "A언필터")
	OP_CFLAG_SM언필터     = GETNUM(CFLAG, "SM언필터")
	OP_CFLAG_봉사언필터     = GETNUM(CFLAG, "봉사언필터")
	OP_CFLAG_성희롱언필터 = GETNUM(CFLAG, "성희롱언필터")
	LOCAL ++
ENDIF
PRINTFORML ～간이 필터 설정～
PRINTFORML ※개별적인 커맨드필터는 이것과는 별도로 설정되어 있습니다
PRINTFORML 
PRINTFORML [29] 전 캐릭터의 모든 간이 필터 오프
PRINTFORML [28] 전 캐릭터별 [V]를 개방
PRINTFORML [27] 전 캐릭터별 [A]를 개방
PRINTFORML [26] 전 캐릭터별 [SM]를 개방
PRINTFORML [25] 전 캐릭터별 [봉사]를 개방
PRINTFORML [24] 전 캐릭터별 [성희롱]을 개방
PRINTFORML [23] [가사]를 개방
PRINTFORML 
PRINTFORML [19] 전 캐릭터의 모든 간이 필터 온
PRINTFORML [18] 전 캐릭터별 [V]를 제한
PRINTFORML [17] 전 캐릭터별 [A]를 제한
PRINTFORML [16] 전 캐릭터별 [SM]를 제한
PRINTFORML [15] 전 캐릭터별 [봉사]를 제한
PRINTFORML [14] 전 캐릭터별 [성희롱]을 제한
PRINTFORML [13] [가사]를 제한
PRINTFORML 
PRINTFORML [99] 설정 종료
DO
	INPUT
	OP_SETTING = (RESULT / 10) - 1
	;20그리고 1, 10으로 0
	SELECTCASE RESULT
	CASE 29, 19
		CVARSET CFLAG, OP_CFLAG_V언필터, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_A언필터, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_SM언필터, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_봉사언필터, OP_SETTING
		CVARSET CFLAG, OP_CFLAG_성희롱언필터, OP_SETTING
		FLAG:가사언필터 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터의 모든 간이 필터를 ＜OFF＞했습니다
		ELSE
			PRINTFORMW 전 캐릭터의 모든 간이 필터를 ＜ON＞했습니다
		ENDIF
	CASE 28, 18
		CVARSET CFLAG, OP_CFLAG_V언필터, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터별 [V] 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 전 캐릭터별 [V] 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 27, 17
		CVARSET CFLAG, OP_CFLAG_A언필터, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터별 [A] 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 전 캐릭터별 [A] 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 26, 16
		CVARSET CFLAG, OP_CFLAG_SM언필터, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터별 [SM] 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 전 캐릭터별 [SM] 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 25, 15
		CVARSET CFLAG, OP_CFLAG_봉사언필터, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터별 [봉사] 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 전 캐릭터별 [봉사] 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 24, 14
		CVARSET CFLAG, OP_CFLAG_성희롱언필터, OP_SETTING
		IF OP_SETTING
			PRINTFORMW 전 캐릭터별 [성희롱] 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 전 캐릭터별 [성희롱] 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 23, 13
		FLAG:가사언필터 = OP_SETTING
		IF OP_SETTING
			PRINTFORMW 가사 커맨드를 ＜개방＞합니다
		ELSE
			PRINTFORMW 가사 커맨드를 ＜제한＞합니다
		ENDIF
	CASE 99
		PRINTFORMW 간이 필터의 설정을 종료합니다
		RETURN
	CASEELSE
		PRINTFORML 무효인 입력입니다
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;---------------------------------------------------------------
;	그림 상세 설정
;	설정이 증가했으므로 신설
;---------------------------------------------------------------
@OPTION_그림표시상세설정
TARGET = [[레이무]]
FOR LOCAL, 1, 11
	TARGET:LOCAL = LOCAL
NEXT
PRINTL ▼샘플▼
CALL PRINT_TARGET_IMAGE(TARGET)
PRINTL 
PRINTFORML [ 0] - 그림표시를 \@ FLAG:그림표시 ? 한다 # 안한다 \@
IF FLAG:그림표시
	PRINTFORML [ *]┬ 현재의 \@ FLAG:스탠딩종류 ? 얼굴 그림 # 스탠딩 \@의 표시 인원수 {FLAG:그림표시}인 화상사이즈{100 + isModifier}％
	PRINTFORML [ 1]├ 늘린다
	PRINTFORML [ 2]└ 줄인다
	PRINTFORML [ 3] - 캐릭터 그림의 타입 \@ FLAG:스탠딩종류 ? [얼굴] # [스탠딩] \@
	PRINTFORML [ 4]┬ 현재의 화상 사이즈[\@ FLAG:멀티사이즈? 원본크기 # {デフォルトキャラ画像横幅} \@]：{100 + isModifier}％
	PRINTFORML [ 5]├ 확대
	PRINTFORML [ 6]└ 축소
	PRINTFORML [ 7] - \@ FLAG:스탠딩종류 ? 얼굴 그림 # 스탠딩 \@의 표시 위치 [\@ FLAG:그림표시위치 ? 왼쪽 맞춤# 중앙 맞춤 \@]
	PRINTFORML [ 8] - 그림 표시에 테두리를 친다 \@ FLAG:화상범위표시 ? ON # OFF \@
	PRINTFORML [ 9] - \@ FLAG:거유차이표시? 거유 이상의 별도 화상이 있는 캐릭터라면 표시한다# 거유차이를 표시하지 않는다\@
	PRINTFORML [10] - \@ FLAG:追加エフェクト? 추가 이펙트를 표시한다# 추가 이펙트를 표시하지 않는다\@
	PRINTFORML [11] - 취침후의 \@ FLAG:自慰画像非表示? 자위화상을 표시하지 않는다 # 자위화상을 표시한다\@
	PRINTFORML [12] - 얼굴 그림이 첨부된 메시지 \@ 顔絵付きメッセージ ? 있음 # 없음\@
	PRINTL 
	PRINTFORML [13]\@ FLAG:그림표시 ? ┬ # %" "%- \@ 삽화를 표시한다 \@ FLAG:삽화표시 ? ON # OFF \@
	IF FLAG:그림표시
		PRINT [14]└ 사정 그림의 종류
		SELECTCASE FLAG:사정화상종류
			CASE 0
				PRINTL  [단면＋효과음]
			CASE 1
				PRINTL  [단면만]
			CASE 2
				PRINTL  [효과음만]
		ENDSELECT
	ELSE
		PRINTL
	ENDIF
	PRINTFORML [15]\@ FLAG:애니메이션 ? ┬ # %" "%- \@애니메이션 설정 \@ FLAG:애니메이션 ? 반복 회수 {FLAG:애니메이션}회 # OFF \@
	IF FLAG:애니메이션
		PRINTFORML [ 7]└삽입중에는 매턴 애니메이션 표시：\@FLAG:삽입애니메이션필터 & 아니메필터_삽입 ? [×]# [O]\@
	ELSE
		PRINTL 
	ENDIF
ELSE
	FOR LOCAL, 1, 13
		PRINTL 
	NEXT
ENDIF
PRINTFORML [999] - 돌아간다
INPUT
IF RESULT == 999
	VARSET TARGET
	RETURN 0
ELSEIF RESULT == 0
	FLAG:그림표시 = !FLAG:그림표시
ELSEIF RESULT == 1
	FLAG:그림표시++
	SIF FLAG:그림표시 > 10
		FLAG:그림표시 = 10
ELSEIF RESULT == 2
	FLAG:그림표시--
	SIF FLAG:그림표시 < 1
		FLAG:그림표시 = 1
ELSEIF RESULT == 3
	FLAG:스탠딩종류 = !FLAG:스탠딩종류
ELSEIF RESULT == 4
	IF FLAG:멀티사이즈
		FLAG:멀티사이즈 = !FLAG:멀티사이즈
		デフォルトキャラ画像横幅 = 180
	ELSEIF デフォルトキャラ画像横幅 == 180
		デフォルトキャラ画像横幅 = 270
	ELSEIF デフォルトキャラ画像横幅 == 270
		デフォルトキャラ画像横幅 = 360
		ELSEIF デフォルトキャラ画像横幅 == 360
		デフォルトキャラ画像横幅 = 540
		ELSEIF デフォルトキャラ画像横幅 == 540
		デフォルトキャラ画像横幅 = 720
	ELSE
		FLAG:멀티사이즈 = !FLAG:멀티사이즈
	ENDIF
ELSEIF RESULT == 5
	isModifier = MIN(isModifier + 10, 50)
ELSEIF RESULT == 6
	isModifier = MAX(isModifier - 10, -50)
ELSEIF RESULT == 7
	FLAG:그림표시위치 = !FLAG:그림표시위치
ELSEIF RESULT == 8
	FLAG:화상범위표시 = !FLAG:화상범위표시
ELSEIF RESULT == 9
	FLAG:거유차이표시 = !FLAG:거유차이표시
ELSEIF RESULT == 10
	FLAG:追加エフェクト = !FLAG:追加エフェクト
ELSEIF RESULT == 11
	FLAG:自慰画像非表示 = !FLAG:自慰画像非表示
ELSEIF RESULT == 12
	INVERTBIT 顔絵付きメッセージ, 0
ELSEIF RESULT == 13
	FLAG:삽화표시 = !FLAG:삽화표시
ELSEIF RESULT == 14 && FLAG:삽화표시
	FLAG:사정화상종류++
	SIF FLAG:사정화상종류 >= 3
		FLAG:사정화상종류 = 0
ELSEIF RESULT == 15
	FLAG:애니메이션++
	SIF FLAG:애니메이션 >= 6
		FLAG:애니메이션 = 0
ELSEIF RESULT == 16
	INVERTBIT FLAG:삽입애니메이션필터, 0
	;CALL ASK_M("삽입 표시 전환", 1,"절정 표시 전환", 1,"사정 표시 전환", 1,"사정 표시 전환", 1,"돌아간다", 1)
	;SIF RESULT == 4
	;	RESTART
	;INVERTBIT FLAG:가사언필터, RESULT
	RESTART
ENDIF
RESTART






@SIMATTYAUOJISAN
;TW414_이동 패치에서 오른쪽 클릭이나 매크로에 의한 스킵도 끝내버리도록 수정
;PRINT에서의 웨이트를 없애는 대신에 FORCEWAIT로 스킵 해제하도록
;
FORCEWAIT
PRINT 「화면상에 표시되지 않는 숫자를 입력한 아이가 여기 있어?」
FORCEWAIT
PRINT 치워버리자 아저씨가 나타났다!
FORCEWAIT
PRINT 「자, 화면상에 표시되지 않는 숫자를 입력한 아이의 돈은, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM 치워버리자 아저씨는 %CALLNAME:MASTER%의 있는 돈을 모조리 치워버렸다
MONEY = 0
FORCEWAIT
PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 아이템도, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%의 아이템은 치워져버렸다
FOR LOCAL, 0,500
	ITEM:LOCAL = 0
NEXT
FORCEWAIT
PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 기교도, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%의 기교는 치워져버렸다
FORCEWAIT
FOR LOCAL, 0,100
	ABL:0:LOCAL = 0
NEXT
PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 경험도, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%의 경험은 치워져버렸다
FORCEWAIT
FOR LOCAL, 0,100
	EXP:0:LOCAL = 0
NEXT
;하는 김에 콜렉션도 치워버리자(eraTW4.15 팬티 대책 패치에서 추가)
;
PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 팬티 콜렉션도, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM %마스터가% 정성들여 수집한, 주옥같은 팬티 콜렉션은 치워져버렸다
FORCEWAIT
FLAG:700 = 0
FOR LOCAL, 1, CHARANUM
	FOR LOCAL:1, 0, MAXPANTS
		CFLAG:LOCAL:(LOCAL:1 + 팬티관리) = 0
	NEXT
NEXT
PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 신뢰도, 자꾸자꾸 치워버릴 거란다」
FORCEWAIT
PRINTFORM %CALLNAME:MASTER%에게 소녀들이 보내는 신뢰는 치워져버렸다
FOR LOCAL, 1, CHARANUM
	CFLAG:LOCAL:신뢰도 = 0
NEXT
FORCEWAIT
;
;
;PRINT 「화면상에 표시되지 않는 숫자를 입력하는 아이의 기초 능력도, 자꾸자꾸 치워버릴 거란다」
;FORCEWAIT
;PRINTFORM %CALLNAME:MASTER%의 기초 능력은 치워져버렸다
;FOR LOCAL, 0,9
;	MAXBASE:0:LOCAL = 99
;	BASE:0:LOCAL = min(MAXBASE:0:LOCAL, BASE:0:LOCAL)
;NEXT
;과연 너무 하고 는 좋지 않다고 생각했으므로 클릭 연타로 용서(허락)해 보았다
;작자님에게 마음대로 맞힌 패치이므로 불쾌하다면 이 부분만큼 패치 앞에 되돌려 주셔 상관하지 않습니다.
;
;
FORCEWAIT
PRINTW 다 망했어∼~~~


;섹시 팬티 삭제
@DELETE_SEXYPANTS
#DIM 대상
#DIM 종류
#DIM 소유수

VARSET RESULT

FOR 대상, 1, CHARANUM
	FOR 종류, 20,26
		소유수 = CFLAG:대상:(종류 + 100)
		IF 소유수 >= 1
			RESULT:0 += (소유수)
			CFLAG:대상:(종류 + 100) = 0
		ENDIF
	NEXT
NEXT

;바이브 팬티 삭제
@DELETE_VIBEPANTS
#DIM 대상
#DIM 종류
#DIM 소유수

VARSET RESULT

FOR 대상, 1, CHARANUM
	FOR 종류, 27,30
		소유수 = CFLAG:대상:(종류 + 100)
		IF 소유수 >= 1
			RESULT:0 += (소유수)
			CFLAG:대상:(종류 + 100) = 0
		ENDIF
	NEXT
NEXT


