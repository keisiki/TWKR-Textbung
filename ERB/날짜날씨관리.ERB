;FileName_COMMON.ERB ------------------------------- Rev1. 00
;계절 관리
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_MONTH
#DIM 운세뽑기값
;일자가 달을 넘으면(자) 달변경
IF DAY:3 > 31
	DAY:3 = 1
	DAY:2 ++
	탄막놀이축제발동 = 0
	IF FLAG:범죄율
		FLAG:범죄율 = MAX(0,FLAG:범죄율 - MAX(5,FLAG:범죄율/10))
	ENDIF
	CALL 직장처리
	CALL 세금징수
	IF 은행잔고 >= 100
		PRINTFORML 은행 이자가 들어왔습니다
		은행잔고 += MIN(500000,은행잔고/100 * 5)
	ENDIF
	CALL 부양의무
	CALL 행정집행
	대국민스택 = 1
	;겨울의 다음은 봄
	IF DAY:2 > 4
		DAY:2 = 1
		FLAG:연시대출금 = FLAG:지주대출금
		;(天候パッチ)長期予報を行う
		CALL WEATHER_FORECAST_1YEAR
	ENDIF
	FLAG:은행털기기회 = 0
	;여름이 되면 발렌타인 플래그 리셋
	SIF DAY:2 == 2
		CVARSET CFLAG, 383
	;징수 발생
	SIF FLAG:지주
		FLAG:연공 = 1
	IF FLAG:물주기대행
		FLAG:물주기대행 = 0
		PRINTFORMW 물주기대행의 기한이 지났습니다.
	ENDIF
	IF FLAG:식재료구독
		IF MONEY < 20000 && MONEY:2 < 30
			PRINTFORML 돈이 부족하기에 식재료의 구독을 정지합니다.
		ELSE
			PRINTL
			PRINTFORML ★식재료 배달 서비스를 연장하겠습니까? (월액%금액표시(20000)% or 30 카리스마)
			CALL ASK_M("해약", 1,"갱신(%금액표시(20000)%)", MONEY >= 20000,"갱신(30 카리스마)", MONEY:2 >= 30)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 구독을 정지합니다.
					FLAG:식재료구독 = 0
				CASE 1
					MONEY -= 20000
					PRINTFORML 이번 달도 구독합니다.
				CASE 2
					MONEY:2 -= 300
					PRINTFORML 이번 달도 구독합니다.
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:신문구독
		IF MONEY < 3000 && MONEY:2 < 10
			PRINTFORML 돈이 부족하기에 붕붕마루 신문의 구독을 정지합니다.
		ELSE
			PRINTL
			PRINTFORML ★붕붕마루 신문을 계속 구독하겠습니까? (월액%금액표시(3000)% or 10 카리스마)
			CALL ASK_M("해약", 1,"갱신(%금액표시(3000)%)", MONEY >= 3000,"갱신(10 카리스마)", MONEY:2 >= 10)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 구독을 정지합니다.
					FLAG:신문구독 = 0
				CASE 1
					MONEY -= 3000
					PRINTFORML 이번 달도 구독합니다.
				CASE 2
					MONEY:2 -= 10
					PRINTFORML 이번 달도 구독합니다.
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:건강보험
		IF MONEY < 건보가격(80000)
			PRINTFORML 돈이 부족하기에 건강보험의 갱신을 정지합니다.
		ELSE
			PRINTL
			PRINTFORML ★건강보험을 유지하겠습니까? (월액 {%금액표시(건보가격(80000))%} 원)
			CALL ASK_M("해약", 1,"갱신", MONEY >= 건보가격(80000))
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 건강보험을 취소합니다.
					FLAG:건강보험 = 0
				CASE 1
					MONEY -= 건보가격(80000)
					PRINTFORML 건강보험을 유지합니다.
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:전화
		IF MONEY < 5000
			PRINTFORML 돈이 부족하기에 전화 계약을 해지합니다.
		ELSE
			PRINTL
			PRINTFORML ★전화 서비스를 유지하겠습니까? (월액 %금액표시(5000)%)
			CALL ASK_M("해약", 1,"갱신", MONEY >= 50000,"갱신(50 카리스마)", MONEY:2 >= 50)
			SELECTCASE RESULT
				CASE 0
					PRINTFORML 해약했습니다
					FLAG:전화 = 0
				CASE 1
					MONEY -= 5000
					PRINTFORML 계약을 연장했습니다
				CASE 2
					MONEY:2 -= 50
					PRINTFORML 계약을 연장했습니다
			ENDSELECT
		ENDIF
	ENDIF
	IF FLAG:운세뽑기계약
		운세뽑기값 = 50
		SIF !MAIN_MAP
			운세뽑기값 = 30
		IF MONEY:2 < 운세뽑기값
			PRINTFORML 카리스마가 부족하기 때문에, 운세뽑기계약이 중지되었습니다
			FLAG:운세뽑기계약 = 0
		ELSE
			PRINTFORML 운세뽑기계약을 계속하겠습니까?
			CALL ASK_YN
			IF !RESULT
				PRINTFORML 계약을 갱신했습니다
				MONEY:2 -= 운세뽑기값
			ELSE
				FLAG:운세뽑기계약 = 0
				PRINTFORML 계약을 종료합니다
			ENDIF
		ENDIF
	ENDIF
	IF FLAG:벌레수집서비스
		PRINTFORMDL
		PRINTFORMW 벌레 수집 서비스의 계약 기간이 만료되었습니다.
		PRINTFORMW 벌레 수집 서비스를 계속하시겠습니까?
		PRINTFORML 소지금:%금액표시(MONEY)%
		LOCAL = CALCULATE_IFS_PRICE()
		CALL ASK_M("갱신하지 않는다",1,@"갱신한다 (%금액표시(LOCAL)%)",MONEY >= LOCAL)
		IF RESULT > 0
			MONEY -= LOCAL
			PRINTFORMW 계약을 갱신했습니다.
			PRINTFORMDL
			PRINTFORML 현재 벌레 채집 장소는 %GET_MAPNAME(FLAG:벌레수집서비스 - 1)%입니다.
			PRINTFORMW 변경하고 싶다면 %CALLNAME:[[リグル]]%에게 말을 걸어주세요.
		ELSE
			FLAG:벌레수집서비스 = 0
		ENDIF
		PRINTFORMDL
	ENDIF
ENDIF
;이상치를 고정 수정
SIF DAY:3 < 1
	DAY:3 = 1
SIF DAY:2 < 1 || DAY:2 > 4
	DAY:2 = 1

;FileName_COMMON.ERB ------------------------------- Rev1. 00
;시간대 관리
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_TIMEZONE
#DIM 現在時間帯
;SELECTCASE TIME
;	CASE 240 TO 599
;		LOCAL = 1
;	CASE 600 TO 1139
;		LOCAL = 2
;	CASE 1140 TO 1440 , 0 TO 239
;		LOCAL = 3
;ENDSELECT

SELECTCASE TIME
	;AM4~6　이른 아침
	CASE 240 TO 359
		LOCAL = 1
	;AM6~12　정오전
	CASE 360 TO 719
		LOCAL = 2
	;PM12~3　낮
	CASE 720 TO 899
		LOCAL = 3
	;PM3~6　오후
	CASE 900 TO 1079
		LOCAL = 4
	;PM6~8　저녁
	CASE 1080 TO 1199
		LOCAL = 5
	;PM8~11　밤
	CASE 1200 TO 1379
		LOCAL = 6
	;PM11~AM4　심야
	CASE 0 TO 239, IS >= 1380
		LOCAL = 7
ENDSELECT
;(天候パッチ)天候システム上の時間帯管理
SELECTCASE TIME
	;AM0~3　未明
	CASE 0 TO 179
		現在時間帯 = 1
	;AM3~6　明け方
	CASE 180 TO 359
		現在時間帯 = 2
	;AM6~9　朝
	CASE 360 TO 539
		現在時間帯 = 3
	;AM9~12　昼前
	CASE 540 TO 719
		現在時間帯 = 4
	;PM0~3　昼過ぎ
	CASE 720 TO 899
		現在時間帯 = 5
	;PM3~6　夕方
	CASE 900 TO 1079
		現在時間帯 = 6
	;PM6~9　夜のはじめ頃
	CASE 1080 TO 1259
		現在時間帯 = 7
	;PM9~12　夜遅く
	CASE IS >= 1260
		現在時間帯 = 8
ENDSELECT

;(天候パッチ)現在の天候傾向を判定
CALL CHANGE_WEATHER_SYSTEM

IF LOCAL != TIME:2
	TIME:2 = LOCAL
	TRYCALLFORM MOB_PLACE_{MAIN_MAP}
	CALL TIMEZONE_MOVE
ENDIF
;(天候パッチ)天候システム上の時間帯変動・天候変化
IF WEATHER_TIMEZOME != 現在時間帯
	WEATHER_TIMEZOME = 現在時間帯
	CALL CHANGE_WEATHER
ENDIF
;(天候パッチ)時刻・天候に応じて背景色を変更
CALL SET_MAP_WEATHER_BGCOLOR

;FileName_COMMON.ERB ------------------------------- Rev1.00
;계절 이름 취득
;CALL		USER
;ARG		인수가 없는 경우 DAY:2
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_MONTH(ARG)
#FUNCTIONS
SIF !ARG
	ARG = DAY:2
SIF !STRLENS(LOCALS)
	SPLIT "무의 달/봄의 달/여름의 달/가을의 달/겨울의 달", "/", LOCALS
RETURNF LOCALS:(ARG)

;FileName_COMMON.ERB ------------------------------- Rev1. 00
;날씨 이름 취득
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@GET_WEATHER
#FUNCTIONS
#DIMS 표시_이상기상, 15
;(天候パッチ)台風・猛吹雪は表示しない
IF FLAG:이상기상 && FLAG:이상기상 != 9 && FLAG:이상기상 != 10
	;SPLIT " /Ｃ敏感/Ｖ敏感/Ａ敏感/Ｂ敏感/Ｍ敏感/スカート/パンツ/桃色の風/台風/猛吹雪", "/", 表示_異常気象
	SPLIT " /C민감/V민감/A민감/B민감/M민감/치마/팬티/분홍바람/태풍/비바람/아파테이아/아타락시아/소나기/", "/", 표시_이상기상
	RETURNF 표시_이상기상:(FLAG:이상기상)
ENDIF
SIF !STRLENS(LOCALS)
	SPLIT "맑음/쾌청/약간흐림/흐림/비/폭우/이슬비/안개/눈/폭설/가루눈/안개눈/진눈깨비/싸라기눈/호우/우박", "/", LOCALS
	;SPLIT "晴れ/快晴/薄曇   /曇り/雨/大雨/霧雨  /霧  /雪/大雪/細雪  /霧雪  /みぞれ /あられ  /豪雨/ひょう", "/", LOCALS
RETURNF LOCALS:(TIME:5)

;-------------------------------------------------
;평일 판정(ARG)
;DAY를 건네주어 츠키～ 금요일이라면 1
;-------------------------------------------------
@IS_WEEKDAY(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;주말 판정(ARG)
;DAY를 건네주어 토일요일이라면 1
;-------------------------------------------------
@IS_WEEKEND(ARG)
#FUNCTION
SIF !ARG
	ARG = DAY
RETURNF !INRANGE(ARG % 7, 1, 5)

;-------------------------------------------------
;요일 일치 판정(ARG)
;DAY와 캐릭터라인을 건네주어, 일치하고 있으면 1
;판정용 캐릭터라인은 세월화수목금흙에서 각 요일을 나타낸다(=복수 지정 가능)
;-------------------------------------------------
@YOUBI_MATCH(ARG, CHECK_STR)
#FUNCTION
#DIMS  CHECK_STR
SIF !ARG
	ARG = DAY
SIF STRCOUNT(CHECK_STR, GET_DAY(ARG))
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;함수명 :GET_DAY
;개요:요일을 나타내는 캐릭터라인을 취득하는 함수
;인수:인수가 없는 경우 DAY
;반환값:요일을 나타내는 캐릭터라인(일/월/화/수/목/금/토)
;비고:문중 함수
;-------------------------------------------------
@GET_DAY(ARG)
#FUNCTIONS
#DIMS CONST YOUBI = "일", "월", "화", "수", "목", "금", "토"
SIF !ARG
	ARG = DAY
RETURNF YOUBI:(ARG % 7)

;FileName_COMMON.ERB ------------------------------- Rev1. 00
;날씨 관리
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHANGE_WEATHER
#DIM 지저
#DIM 입수팬티
#DIM 소유자
#DIM 루프개시
#DIM 루프종료
#DIM 루프가산
#DIM DYNAMIC 날씨변경임시방지
DEBUGPRINTL 【CHANGE_WEATHER】CALL CHANGE_WATHER
;날씨 관리에 메시지 표시 처리시켜 죄송합니다＞＜；
;우후후 안은 공기를 읽는 정도의 능력
;날씨 변화하지 않아도 무지개는 사라진다
지저 = 0
IF CFLAG:MASTER:데이트중 == 9
	지저 = 1
ELSEIF CFLAG:MASTER:현재위치 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:초대, 36, 37, 38, 49, 60, 64, 83, 84)
	지저 = 1
ENDIF
TIME:7 = 0
IF !지저
	SELECTCASE FLAG:이상기상
		CASE 7
			;이러해도 하지 않으면 또 노팬티가 날라와서 끝낸다고 생각해
			DO
				CALL RAND_CHARASELECT("주인공 이외의캐릭터리스트")
				소유자 = RESULT
				입수팬티 = 오늘의팬티(소유자)
			LOOP !입수팬티
			SIF CFLAG:소유자:(입수팬티 + 팬티관리) == 0
				SETCOLOR C_PINK
			PRINTFORML 회오리 바람이 %CALLNAME:소유자%의 %조사처리(PANTSNAME(입수팬티, 소유자),"를")% 옮겨 왔다
			RESETCOLOR
			CALL ASK_YN("줍는다","줍지 않는다")
			IF !RESULT
				PRINTFORMW %마스터는% %CALLNAME:소유자%의 %조사처리(PANTSNAME(입수팬티, 소유자),"를")% 주웠다
				CALL PANTS_GETTING(소유자, 입수팬티)
			ELSE
				PRINTFORML %PANTSNAME(입수팬티, 소유자)%는 그대로 어딘가에 날아 갔다
			ENDIF
		CASE 8
			SETCOLOR 255,100,255
			FONTBOLD
			PRINTFORMW 환상향에 분홍색의 바람이 불었다!
			FOR LOCAL, 1, CHARANUM
				IF !RAND:8 && !TALENT:LOCAL:임신
					PRINTFORML 분홍색의 바람을 받은 %조사처리(CALLNAME:LOCAL,"는")% 이상한 기분이 되었다…
					CFLAG:LOCAL:욕구불만도 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:욕망, 200,1000)
					SIF !TALENT:LOCAL:인형
						TCVAR:LOCAL:발정 = 1
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
		CASE 11
			SETCOLOR 0x66FFFF
			FONTBOLD
			PRINTFORMW 환상향에 푸른색의 바람이 불었다!
			FOR LOCAL, 1, CHARANUM
				IF !RAND:7
					PRINTFORML 푸른색의 바람을 받은 %조사처리(CALLNAME:LOCAL,"는")% 이상한 기분이 되었다…
					CFLAG:LOCAL:욕구불만도 -= LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:욕망, 200,1000)
					IF CFLAG:LOCAL:욕구불만도 < 0
						CFLAG:LOCAL:욕구불만도 = 0
					ENDIF
					SIF !TALENT:LOCAL:인형&&TCVAR:LOCAL:발정 == 1&&RAND:2 == 0
						TCVAR:LOCAL:발정 = 0
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
		CASE 12
			SETCOLOR 0xF0F0F0
			FONTBOLD
			PRINTFORMW 환상향에 흰색의 바람이 불었다!
			FOR LOCAL, 1, CHARANUM
				IF !RAND:15
					PRINTFORML 흰색의 바람을 받은 %조사처리(CALLNAME:LOCAL,"는")% 이상한 기분이 되었다…
					SIF ABL:LOCAL:욕망
						ABL:LOCAL:욕망 = MAX(ABL:LOCAL:욕망 - 1 , 1)
					SIF !TALENT:LOCAL:인형&&TCVAR:LOCAL:발정 == 1
						TCVAR:LOCAL:발정 = 0
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
		CASE 13
			SETCOLOR 0x9090FF
			FONTBOLD
			IF TIME:5 != 4
				PRINTFORMW 소나기가 내리기 시작했다!
				TIME:5 = 4
				날씨변경금지시간 = DAY * 1440 + TIME + 30 + RAND:30
			ELSE
				PRINTFORMW 빗줄기가 거세진다!
				날씨변경임시방지 = RAND:2
			ENDIF
			FOR LOCAL, 1, CHARANUM
				SIF !CAN_MEET(LOCAL)
					CONTINUE
				SIF !OUTROOF(CFLAG:LOCAL:현재위치)
					CONTINUE
				SIF CFLAG:LOCAL:수면 || !VISIT(LOCAL)
					CONTINUE
				SIF NAME:LOCAL == "모브코"
					CONTINUE
				IF !RAND:40
					PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% 비에 홀딱 젖어버렸다...
					TCVAR:LOCAL:옷젖음 = 210
				ENDIF
			NEXT
			RESETCOLOR
			FONTREGULAR
	ENDSELECT
ENDIF

SIF FLAG:이상기상 >= 1 && FLAG:이상기상 <= 8
	RETURN
SIF TIME:5 > 15
	TIME:5 = 0
;(天候パッチ)天候変更禁止ならば処理終了
SIF FORBIDDEN_CHANGE_WEATHER == 1
	RETURN
	
;(天候パッチ)新しい天候管理システムへ
SIF 날씨변경금지시간 < DAY * 1440 + TIME && !날씨변경임시방지
	TRYCALLFORM CHANGE_WEATHER_SYSTEM_{NOW_WEATHER_CONTROL}

IF FLAG:이상기상 == 13 && !GROUPMATCH(TIME:5,4,5,6,7,15)
	PRINTFORML 소나기는 멈췄다
	FLAG:이상기상 = 0
ELSEIF FLAG:이상기상 == 13 && RAND:100 > 95
	PRINTFORML 소나기는 멈췄다
	FLAG:이상기상 = 0
	TRYCALLFORM CHANGE_WEATHER_SYSTEM_{NOW_WEATHER_CONTROL}
ENDIF
날씨변경임시방지 = 0

[SKIPSTART]
@현재기온설정
;높낮이차이를 5 분할
SELECTCASE TIME
	;PM11~AM4
	CASE 0 TO 299, IS >= 1380
		LOCAL = FLAG:62
	;AM5~7
	CASE 300 TO 479
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
	;AM8~9
	CASE 480 TO 599
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;AM10~11
	CASE 600 TO 719
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 3)
	;PM12~2
	CASE 720 TO 899
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM3~5
	CASE 900 TO 1079
		LOCAL = FLAG:61
	;PM6~7
	CASE 1080 TO 1199
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 4)
	;PM8~9
	CASE 1200 TO 1259
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5 * 2)
	;PM9~10
	CASE 1260 TO 1379
		LOCAL = FLAG:62 + ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;해가 나와 있지 않으면 온도 저하
SELECTCASE TIME
	;AM5~PM7
	CASE 300 TO 1199
		SIF TIME:5 > 1
			LOCAL -= ((FLAG:61 - FLAG:62) / 5)
ENDSELECT
;----------------------------------------------------------------
@연간기초최고기온(ARG, ARG:1)
#FUNCTION
;ARG   지정의 달
;ARG:1 지정의 날
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 17
			CASE 4, 5
				RETURNF 18
			CASE 6 TO 8
				RETURNF 19
			CASE 9 TO 11
				RETURNF 20
			CASE 12 TO 14
				RETURNF 21
			CASE 15 TO 18
				RETURNF 22
			CASE 19 TO 22
				RETURNF 23
			CASE 23 TO 27
				RETURNF 24
			CASE 28 TO 30
				RETURNF 25
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 25
			CASE 3 TO 7
				RETURNF 26
			CASE 8 TO 13
				RETURNF 27
			CASE 14 TO 17
				RETURNF 28
			CASE 18 TO 22
				RETURNF 29
			CASE 23 TO 28
				RETURNF 30
			CASE 29, 30
				RETURNF 31
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 31
			CASE 3, 4
				RETURNF 30
			CASE 5 TO 7
				RETURNF 29
			CASE 8 TO 10
				RETURNF 28
			CASE 11, 12
				RETURNF 27
			CASE 13, 14
				RETURNF 26
			CASE 15, 16
				RETURNF 25
			CASE 17 TO 19
				RETURNF 24
			CASE 20 TO 22
				RETURNF 23
			CASE 23, 24
				RETURNF 22
			CASE 25 TO 27
				RETURNF 21
			CASE 28, 29
				RETURNF 20
			CASE 30
				RETURNF 19
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 4
				RETURNF 19
			CASE 5 TO 7
				RETURNF 18
			CASE 8 TO 10
				RETURNF 17
			CASE 11, 12
				RETURNF 16
			CASE 13, 14
				RETURNF 15
			CASE 15, 16
				RETURNF 14
			CASE 17, 18
				RETURNF 13
			CASE 19, 20
				RETURNF 12
			CASE 21, 22
				RETURNF 11
			CASE 23 TO 25
				RETURNF 10
			CASE 26, 27
				RETURNF 9
			CASE 28, 29
				RETURNF 8
			CASE 30
				RETURNF 7
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 7
			CASE 4 TO 9
				RETURNF 6
			CASE 10 TO 14
				RETURNF 5
			CASE 15 TO 21
				RETURNF 4
			CASE 22 TO 28
				RETURNF 5
			CASE 29, 30
				RETURNF 6
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 6
				RETURNF 6
			CASE 7 TO 12
				RETURNF 7
			CASE 13, 14
				RETURNF 8
			CASE 15, 16
				RETURNF 9
			CASE 17, 18
				RETURNF 10
			CASE 19, 20
				RETURNF 11
			CASE 21, 22
				RETURNF 12
			CASE 23, 24
				RETURNF 13
			CASE 25, 26
				RETURNF 14
			CASE 27, 28
				RETURNF 15
			CASE 29
				RETURNF 16
			CASE 30
				RETURNF 17
		ENDSELECT
ENDSELECT

;----------------------------------------------------------------
@연간기초최저기온(ARG, ARG:1)
#FUNCTION
;ARG   지정의 달
;ARG:1 지정의 날
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 11
			CASE 4 TO 6
				RETURNF 12
			CASE 7, 8
				RETURNF 13
			CASE 9 TO 11
				RETURNF 14
			CASE 12 TO 14
				RETURNF 15
			CASE 15 TO 17
				RETURNF 16
			CASE 18 TO 20
				RETURNF 17
			CASE 21 TO 23
				RETURNF 18
			CASE 24 TO 27
				RETURNF 19
			CASE 28 TO 30
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 20
			CASE 4 TO 7
				RETURNF 21
			CASE 8 TO 10
				RETURNF 22
			CASE 11 TO 14
				RETURNF 23
			CASE 15 TO 22
				RETURNF 24
			CASE 23 TO 30
				RETURNF 25
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1 TO 5
				RETURNF 25
			CASE 6 TO 9
				RETURNF 24
			CASE 10 TO 12
				RETURNF 23
			CASE 13, 14
				RETURNF 22
			CASE 15 TO 17
				RETURNF 21
			CASE 18 TO 20
				RETURNF 20
			CASE 21 TO 23
				RETURNF 19
			CASE 24, 25
				RETURNF 18
			CASE 26
				RETURNF 17
			CASE 27, 28
				RETURNF 16
			CASE 29
				RETURNF 15
			CASE 30
				RETURNF 14
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 14
			CASE 4, 5
				RETURNF 13
			CASE 6, 7
				RETURNF 12
			CASE 8, 9
				RETURNF 11
			CASE 10, 11
				RETURNF 10
			CASE 12 TO 14
				RETURNF 9
			CASE 15 TO 17
				RETURNF 8
			CASE 18 TO 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 6
			CASE 24 TO 26
				RETURNF 5
			CASE 27 TO 29
				RETURNF 4
			CASE 30
				RETURNF 3
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 3
			CASE 3 TO 6
				RETURNF 2
			CASE 7 TO 10
				RETURNF 1
			CASE 11 TO 13
				RETURNF 0
			CASE 14 TO 21
				RETURNF -1
			CASE 22 TO 30
				RETURNF 0
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 0
			CASE 4 TO 6
				RETURNF 1
			CASE 7, 8
				RETURNF 2
			CASE 9, 10
				RETURNF 3
			CASE 11 TO 14
				RETURNF 4
			CASE 15, 16
				RETURNF 5
			CASE 17, 18
				RETURNF 6
			CASE 19, 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 8
			CASE 24, 25
				RETURNF 9
			CASE 26 TO 28
				RETURNF 10
			CASE 29, 30
				RETURNF 11
		ENDSELECT
ENDSELECT
[SKIPEND]

;정시 조우위치 재설정
@TIMEZONE_MOVE
#DIMS 이동전
#DIMS 이동후
FOR LOCAL, 1, CHARANUM
	SIF 출현상태(LOCAL) != 1
		CONTINUE
	SIF !CHARA_HOLIDAY(LOCAL)
		CONTINUE
	SIF CFLAG:LOCAL:초대
		CONTINUE
	SIF CFLAG:LOCAL:동행 || CHK_DATENOW(CFLAG:LOCAL:데이트중)
		CONTINUE
	SIF !CFLAG:LOCAL:전턴위치
		CONTINUE
	SIF CFLAG:LOCAL:수면
		CONTINUE
	;당신과 같은 장소에 있으면 확률이 떨어집니다
	IF CFLAG:MASTER:현재위치 == CFLAG:LOCAL:현재위치
		IF TALENT:LOCAL:연모
			;恋慕は5分の1
			SIF RAND:5
				CONTINUE
		ELSEIF TALENT:LOCAL:사모
			;思慕は4分の1
			SIF RAND:4
				CONTINUE
		ELSE
			;それ以外は3分の1
			SIF RAND:3
				CONTINUE
		ENDIF
	ELSE
		;見てない場所なら2分の1
		SIF RAND:2
			CONTINUE
	ENDIF
	CALL 만남플래그결정(LOCAL)
NEXT
;全キャラ決定後、連動キャラを設定する
FOR LOCAL, 1, CHARANUM
	VARSET RESULT
	CALLFORM CHARAMOVE_DATA_{LOCAL}(1)
	;依存連動
	IF RESULT
		CFLAG:LOCAL:만난위치 = CFLAG:RESULT:만난위치
	;対等連動
	ELSEIF RESULT:1
		;連動設定する
		CFLAG:LOCAL:만난위치 = CFLAG:연동캐릭터설정(RESULT:1, RESULT:2, RESULT:3):만난위치
	ENDIF
NEXT

