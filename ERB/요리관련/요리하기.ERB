@임시영양소표시
VARSET YN_탄수화물
VARSET YN_단백질
VARSET YN_지방
VARSET YN_비타민
VARSET YN_나트륨
VARSET TEMP요리영양소
IF 주재료
    CALL 영양소표(주재료)
    ;탄단지비나
    CALL TEMP영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
    CALL TEMP기타영양소올리기(YN_요력)
ENDIF
;PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
FOR LOCAL,0 ,10
    SIF !부재료:LOCAL
        CONTINUE
    CALL 영양소표(부재료:LOCAL)
    CALL TEMP영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
    CALL TEMP기타영양소올리기(YN_요력)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT
FOR LOCAL,0 ,10
    SIF !양념:LOCAL
        CONTINUE
    CALL 영양소표(양념:LOCAL,1)
    CALL TEMP영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
    CALL TEMP기타영양소올리기(YN_요력)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT

@TEMP영양소올리기(ARG:0,ARG:1,ARG:2,ARG:3,ARG:4)
TEMP요리영양소:0 += ARG:0 ;탄수화물
TEMP요리영양소:1 += ARG:1 ;단백질
TEMP요리영양소:2 += ARG:2 ;지방
TEMP요리영양소:3 += ARG:3 ;비타민
TEMP요리영양소:4 += ARG:4 ;나트륨

@TEMP기타영양소올리기(ARG:0)
TEMP요리영양소:5 += ARG:0 ;요력

@요리하기
CALL Y_PRE_ERROR_CORRECT
VARSET 사용된재료
VARSET 주재료
VARSET 주재료요리법
VARSET 부재료
VARSET 부재료요리법
VARSET 양념
기본제공품사용료 = 0
$LOOP
DRAWLINE
;주재료
PRINTFORM [주재료]
PRINTFORM 　　　　　　　　　
PRINTBUTTON "[요리명]",101
PRINTFORM :%DISH_NAME%
PRINTFORM 　　　　　　　　　
PRINTBUTTON "[요리종류]",102
PRINTFORM : \@ DISH_TYPE != "" ? %DISH_TYPE% # 주식\@
PRINTL
PRINTFORML [100] %사용된재료표시(주재료요리법,주재료),20,LEFT%
DRAWLINE
PRINTFORMLC %"[부재료]", 26, LEFT%
PRINTFORMLC %"[양　념]", 26, LEFT%
PRINTFORMLC %"[첨가물]", 26, LEFT%
CALL 임시영양소표시
PRINTFORML
FOR LOCAL, 0, 10
    ;200대 : 부재료
    PRINTFORMLC [{200+LOCAL}] %사용된재료표시(부재료요리법:LOCAL,부재료:LOCAL),20,LEFT%
    ;300대 : 양념
    PRINTFORMLC [{300+LOCAL}] %사용된재료표시(-1,양념:LOCAL),20,LEFT%
    ;400대 : 약물등의 첨가물
    IF LOCAL == 0
        LOCAL:1 = DISH_DRG
        SIF LOCAL:1 <= 0
            LOCAL:1 = DISH_DRG_2
        PRINTFORMLC [{LOCAL+400}] %사용된재료표시(-2,LOCAL:1),20,LEFT%
    ENDIF
    SELECTCASE LOCAL
        CASE 2
            PRINTFORMLC 탄수화물 : {TEMP요리영양소:0}
        CASE 3
            PRINTFORMLC 단백질　 : {TEMP요리영양소:1}
        CASE 4
            PRINTFORMLC 지방　　 : {TEMP요리영양소:2}
        CASE 5
            PRINTFORMLC 비타민　 : {TEMP요리영양소:3}
        CASE 6
            PRINTFORMLC 나트륨　 : {TEMP요리영양소:4}
        CASE 7
            SIF 요리영양소:5
                PRINTFORMLC 요력　　 : {TEMP요리영양소:5}
    ENDSELECT
    PRINTFORML
NEXT
DRAWLINE
IF 기본제공품사용료
    PRINTFORML [예상 비용 : {기본제공품사용료}￦]
ENDIF
PRINTFORM [997] 레시피　　　
IF 주재료
    PRINTFORM [998] 완성!　　　
ENDIF
PRINTFORML [999] 취소
INPUT
IF RESULT == 100
    CALL 주재료선택
ELSEIF RESULT == 101
    CALL 요리이름결정
ELSEIF RESULT == 102
    SELECTCASE DISH_TYPE
        CASE "주식"
            DISH_TYPE '= "경식"
        CASE "경식"
            DISH_TYPE '= "디저트"
        CASEELSE ; "디저트" 포함
            DISH_TYPE '= "주식"
    ENDSELECT
    IF 기본제공품사용료
        CLEARLINE 19
    ELSE
        CLEARLINE 18
    ENDIF
ELSEIF RESULT / 100 == 2
    CALL 서브재료선택(RESULT % 200)
ELSEIF RESULT / 100 == 3
    CALL 양념선택(RESULT % 300)
ELSEIF RESULT == 400
    CALL 첨가물선택
ELSEIF RESULT == 997
    CALL 요리프리셋
    IF RESULT == 5
        RETURN
    ENDIF
ELSEIF RESULT == 998
    CALL MAKECOOK
    RETURN 1
ELSEIF RESULT == 999
    CALL RESET_DISH
    RETURN -1
ENDIF
GOTO LOOP

@요리이름결정
PRINT 요리의 이름을 입력해주세요:
INPUTS
DISH_NAME = %RESULTS%
DISH_NAME '= REPLACE(DISH_NAME, "/", "")
IF DISH_NAME == ""
    DISH_NAME = 수수께끼 요리
ENDIF
DISH_NAME += "​"
PRINTL

@사용된재료표시(조리법,재료)
#FUNCTIONS
#DIM DYNAMIC 조리법
#DIM DYNAMIC 재료
#DIMS DYNAMIC 출력문
IF 조리법 == 0
    출력문 = ----------
ELSEIF 조리법 == -2
    IF 재료
        출력문 = %ITEMNAME:재료%
    ELSE
        출력문 = ----------
    ENDIF
ELSEIF 조리법 == -1
    IF 재료 < 20
        SELECTCASE 재료
            CASE Y_소금
                출력문 += "소금"
            CASE Y_후추
                출력문 += "후추"
            CASE Y_설탕
                출력문 += "설탕"
            CASE Y_식초
                출력문 += "식초"
            CASE Y_된장
                출력문 += "된장"
            CASE Y_기름
                출력문 += "기름"
            CASE Y_간장
                출력문 += "간장"
            CASE Y_고춧가루
                출력문 += "고춧가루"
            CASE Y_젓갈
                출력문 += "젓갈"
            CASE Y_버터
                출력문 += "버터"
            CASE Y_강황
                출력문 += "강황"
            CASEELSE
                출력문 += "----------"
        ENDSELECT
    ELSE
        출력문 += ITEMNAME:재료
    ENDIF
ELSE
    SELECTCASE 조리법
        CASE Y_굽기
            출력문 += "구운 "
        CASE Y_삶기
            출력문 += "삶은 "
        CASE Y_튀기기
            출력문 += "튀긴 "
        CASE Y_끓이기
            출력문 += "끓인 "
        CASE Y_찌기
            출력문 += "찐 "
        CASE Y_조리기
            출력문 += "조린 "
        CASE Y_볶기
            출력문 += "볶은 "
        CASE Y_그대로
            
        CASEELSE

    ENDSELECT
    IF 재료 < 20
        SELECTCASE 재료
            CASE Y_국수
                출력문 += "국수"
            CASE Y_빵
                출력문 += "빵"
            CASE Y_육수
                출력문 += "육수"
            CASE Y_밀가루
                출력문 += "밀가루"
            CASE Y_계란
                출력문 += "계란"
            CASE Y_콩
                출력문 += "콩"
            CASE Y_양파
                출력문 += "양파"
            CASE Y_마늘
                출력문 += "마늘"
            CASE Y_파프리카
                출력문 += "파프리카"
            CASE Y_치즈
                출력문 += "치즈"
            CASEELSE
                출력문 += "----------"
        ENDSELECT
    ELSE
        출력문 += ITEMNAME:재료
    ENDIF
ENDIF
RETURNF 출력문

@요리재료표시함수(ARG)
IF ARG <= 0
    RETURN -1
ENDIF
IF ITEM:ARG <= 0
    SETCOLOR 0x808080
ENDIF
PRINTFORMLC [{ARG, 4}] %ITEMNAME:ARG% x{ITEM:ARG - 사용된재료:ARG}

RESETCOLOR
RETURN 1

@기본재료표시함수(ARGS,ARG = 0)
#DIMS MAIN_ING, 10 = "국수", "빵", "육수", "밀가루", "계란", "콩"
#DIMS SUB_ING, 10 = "양파", "마늘","파프리카","치즈"
#DIMS SPICES, 12 = "소금", "후추", "설탕", "식초", "된장", "기름", "간장" , "고춧가루" , "젓갈" , "버터" , "강황"

VARSET LOCAL
IF ARG
    IF ARGS == "주재료"||ARGS == "부재료"
        FOR LOCAL, 0, 10
            IF MAIN_ING:LOCAL != ""
                LOCAL:1 ++
            ENDIF
        NEXT
        IF ARGS == "부재료"
            FOR LOCAL, 0, 10
                IF SUB_ING:LOCAL != ""
                    LOCAL:1 ++
                ENDIF
            NEXT
        ENDIF
    ELSEIF ARGS == "양념"
        FOR LOCAL, 0, 12
            IF SPICES:LOCAL != ""
                LOCAL:1 ++
            ENDIF
        NEXT
    ENDIF
ELSE
    PRINT 기본 
    IF ARGS == "주재료"||ARGS == "부재료"
        PRINTFORML 재료 (%금액표시(500)%)
        DRAWLINE
        FOR LOCAL, 0, 10
            SIF LOCAL && !(LOCAL % 4) ;5번째줄마다 빈줄
                PRINTL
            IF MAIN_ING:LOCAL != ""
                PRINTFORMLC [{LOCAL+1, 4, RIGHT}] %MAIN_ING:LOCAL%
                LOCAL:1 ++
            ENDIF
        NEXT
        IF ARGS == "부재료"
            FOR LOCAL, 0, 10
                SIF LOCAL && !(LOCAL % 4) ;5번째줄마다 빈줄
                    PRINTL
                IF SUB_ING:LOCAL != ""
                    PRINTFORMLC [{LOCAL:1+1, 4, RIGHT}] %SUB_ING:LOCAL%
                    LOCAL:1 ++
                ENDIF
            NEXT
        ENDIF
    ELSEIF ARGS == "양념"
        PRINTFORML 양념 (%금액표시(100)%)
        DRAWLINE
        FOR LOCAL, 0, 12
            SIF LOCAL && !(LOCAL % 4) ;5번째줄마다 빈줄
                PRINTL
            IF SPICES:LOCAL != ""
                PRINTFORMLC [{LOCAL+1, 4, RIGHT}] %SPICES:LOCAL%
                LOCAL:1 ++
            ENDIF
        NEXT
    ENDIF
    PRINTL
ENDIF
RETURN LOCAL:1

@첨가물선택
#DIM 표시되는재료, 100
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료 = GETNUM(ITEM,"미약"), GETNUM(ITEM,"이뇨제"), GETNUM(ITEM,"춘약"), GETNUM(ITEM,"수면제")
표시되는재료:4 = GETNUM(ITEM,"보테이지"), GETNUM(ITEM,"배란유발제")
표시되는재료:6 = GETNUM(ITEM,"로터스"), GETNUM(ITEM,"로터스분말"), GETNUM(ITEM,"로터스즙")
표시되는재료:10 = GETNUM(ITEM,"히스테리큐"), GETNUM(ITEM,"영양강화제")
표시되는재료:12 = GETNUM(ITEM,"소마"), GETNUM(ITEM,"소마즙")
표시되는재료:14 = GETNUM(ITEM,"암브로시아"), GETNUM(ITEM,"암브로시아즙")
표시되는재료:16 = GETNUM(ITEM,"프랑부아즈"), GETNUM(ITEM,"다크럼"), GETNUM(ITEM,"브랜디VS"), GETNUM(ITEM,"브랜디VSOP"), GETNUM(ITEM,"브랜디XO")
사용된재료:DISH_DRG = MIN(사용된재료:DISH_DRG - 1, 0)
사용된재료:DISH_DRG_2 = MIN(사용된재료:DISH_DRG_2 - 1, 0)
DISH_DRG = 0
DISH_DRG_2 = 0

PRINTFORML 무엇인가 섞을까?
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 1
ELSE
    FOR LOCAL, 0, 100
        IF (RESULT == 표시되는재료:LOCAL && RESULT != 0 )
            IF ITEM:RESULT > 0
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    $MAKE
    SELECTCASE RESULT
        CASE GETNUM(ITEM,"미약"), GETNUM(ITEM,"이뇨제"), GETNUM(ITEM,"춘약"), GETNUM(ITEM,"수면제"), GETNUM(ITEM,"로터스"), GETNUM(ITEM,"로터스분말"), GETNUM(ITEM,"로터스즙"), GETNUM(ITEM,"소마"), GETNUM(ITEM,"소마즙"), GETNUM(ITEM,"암브로시아"), GETNUM(ITEM,"암브로시아즙")
            DISH_DRG = RESULT
        CASE GETNUM(ITEM,"보테이지"), GETNUM(ITEM,"배란유발제")
            DISH_DRG = RESULT
        CASE GETNUM(ITEM,"히스테리큐"), GETNUM(ITEM,"영양강화제")
            DISH_DRG_2 = RESULT
    ENDSELECT
    사용된재료:RESULT ++
ENDIF

@주재료선택
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
IF 주재료
    사용된재료:주재료 --
    SIF INRANGE(주재료,1,50)
        기본제공품사용료 -= 500
ENDIF
VARSET 주재료
VARSET 주재료요리법



표시되는재료:0 = GETNUM(ITEM,"싸구려식재"), GETNUM(ITEM,"상급식재"), GETNUM(ITEM,"고급식재")
표시되는재료:3 = GETNUM(ITEM,"쌀"),  GETNUM(ITEM,"고기")
표시되는재료:5 = GETNUM(ITEM,"물고기"), GETNUM(ITEM,"맛있는물고기")
표시되는재료:7 = GETNUM(ITEM,"보통버섯"), GETNUM(ITEM,"간")
표시되는재료:9 = GETNUM(ITEM,"당근"), GETNUM(ITEM,"순무"),  GETNUM(ITEM,"죽순")
표시되는재료:12 = GETNUM(ITEM,"아이스크림"), GETNUM(ITEM,"고구마"), GETNUM(ITEM,"개구리")
표시되는재료:15 = GETNUM(ITEM,"감자"),GETNUM(ITEM,"뼈")
표시되는재료:17 = GETNUM(ITEM,"염통")
표시되는재료:18 = GETNUM(ITEM,"허파")
표시되는재료:19 = GETNUM(ITEM,"창자")
표시되는재료:20 = GETNUM(ITEM,"치르밀")
표시되는재료:21 = GETNUM(ITEM,"로터")
표시되는재료:22 = GETNUM(ITEM,"딸기"),GETNUM(ITEM,"초콜릿"),GETNUM(ITEM,"고급초콜릿"),GETNUM(ITEM,"마법의버섯")
$LOOP
PRINTFORML 어떤 메인 재료를 사용할까?
DRAWLINE
CALL 기본재료표시함수("주재료")
기본제공품 = RESULT
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다

WHILE 1
    INPUT
    IF RESULT == 9999 ;돌아간다
        RETURN -1
    ELSEIF RESULT == 0 ;선택 대상 없음. 로터를 고른다면 또 모를까.
        CONTINUE
    ENDIF

    FOR LOCAL, 0, 100
        IF 표시되는재료:LOCAL == -1 ; 표시되는재료 배열 공란 발견시
            CONTINUE
        ELSEIF (RESULT == 표시되는재료:LOCAL && ITEM:RESULT > 0 )|| INRANGE(RESULT,1,기본제공품)
            IF INRANGE(RESULT,1,기본제공품)
                기본제공품사용료 += 500
                IF MONEY < 기본제공품사용료
                    PRINTFORMW 돈이 부족합니다
                    기본제공품사용료 -= 500
                    GOTO LOOP
                ENDIF
            ENDIF
            사용된재료:RESULT ++
            주재료 = RESULT
            CALL 요리법선택
            주재료요리법 = RESULT
            RETURN 1
        ENDIF
    NEXT
WEND

@요리법선택
PRINTL 어떻게 요리할까...?
DRAWLINE
PRINTFORML [{Y_굽기, 3, RIGHT}] 굽기
PRINTFORML [{Y_삶기, 3, RIGHT}] 삶기
PRINTFORML [{Y_튀기기, 3, RIGHT}] 튀기기
PRINTFORML [{Y_끓이기, 3, RIGHT}] 끓이기
PRINTFORML [{Y_찌기, 3, RIGHT}] 찌기
PRINTFORML [{Y_조리기, 3, RIGHT}] 조리기
PRINTFORML [{Y_볶기, 3, RIGHT}] 볶기
PRINTFORML [{Y_그대로, 3, RIGHT}] 그대로
DRAWLINE
WHILE 1
    INPUT
    IF INRANGE(RESULT, 1, 8)
        RETURN RESULT
    ELSE
        PRINTW 잘못된 입력입니다.
    ENDIF
WEND

@서브재료선택(ARG)
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
IF 부재료:ARG != 0
    사용된재료:(부재료:ARG) --
    SIF INRANGE(부재료:ARG,1,100)
        기본제공품사용료 -= 250
ENDIF
부재료:ARG = 0
부재료요리법:ARG = 0

VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료:0 = GETNUM(ITEM,"가지")
표시되는재료:1 = GETNUM(ITEM,"파")
표시되는재료:2 = GETNUM(ITEM,"순무")
표시되는재료:3 = GETNUM(ITEM,"당근")
표시되는재료:4 = GETNUM(ITEM,"고기")
표시되는재료:5 = GETNUM(ITEM,"약초")
표시되는재료:6 = GETNUM(ITEM,"고려인삼")
표시되는재료:7 = GETNUM(ITEM,"간")
표시되는재료:8 = GETNUM(ITEM,"양배추")
표시되는재료:9 = GETNUM(ITEM,"참마")
표시되는재료:10 = GETNUM(ITEM,"오이")
표시되는재료:11 = GETNUM(ITEM,"토마토")
표시되는재료:12 = GETNUM(ITEM,"고구마")
표시되는재료:13 = GETNUM(ITEM,"개구리")
표시되는재료:14 = GETNUM(ITEM,"우유")
표시되는재료:15 = GETNUM(ITEM,"감자")
표시되는재료:16 = GETNUM(ITEM,"뼈")
표시되는재료:17 = GETNUM(ITEM,"염통")
표시되는재료:18 = GETNUM(ITEM,"허파")
표시되는재료:19 = GETNUM(ITEM,"창자")
표시되는재료:20 = GETNUM(ITEM,"로터")
PRINTL
DRAWLINE
PRINTL 추가로 뭘 넣을까?
DRAWLINE
CALL 기본재료표시함수("부재료")
기본제공품 = RESULT
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSEIF RESULT == 0 ;선택 대상 없음. 로터를 고른다면 또 모를까.
    GOTO LOOP1
ELSE
    FOR LOCAL, 0, 100
        IF (RESULT == 표시되는재료:LOCAL && ITEM:RESULT > 0 )|| INRANGE(RESULT,1,기본제공품)
            IF ITEM:RESULT > 0|| INRANGE(RESULT,1,기본제공품)
                IF INRANGE(RESULT,1,기본제공품)
                    기본제공품사용료 += 250
                    IF MONEY < 기본제공품사용료
                        PRINTFORMW 돈이 부족합니다
                        기본제공품사용료 -= 250
                        GOTO LOOP1
                    ENDIF
                ENDIF
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    GOTO LOOP1
    $MAKE
    사용된재료:RESULT ++
    부재료:ARG = RESULT
    CALL 요리법선택
    부재료요리법:ARG = RESULT
ENDIF

@양념선택(ARG)
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
IF 양념:ARG != 0
    사용된재료:(양념:ARG) --
    SIF INRANGE(양념:ARG,1,100)
        기본제공품사용료 -= 100
ENDIF
양념:ARG = 0
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료:0 = GETNUM(ITEM,"벌꿀")
표시되는재료:1 = GETNUM(ITEM,"소주")
표시되는재료:2 = GETNUM(ITEM,"맥주")
표시되는재료:3 = GETNUM(ITEM,"와인")
표시되는재료:4 = GETNUM(ITEM,"미원")
표시되는재료:5 = GETNUM(ITEM,"정액")

PRINTL 무슨 양념?
DRAWLINE
CALL 기본재료표시함수("양념")
기본제공품 = RESULT
DRAWLINE
PRINTL 고급 양념
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [9999] 이대로 끝
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    IF RESULT > 0&&RESULT <= 기본제공품
        기본제공품사용료 += 100
        IF MONEY < 기본제공품사용료
            PRINTFORMW 돈이 부족합니다
            기본제공품사용료 -= 100
            GOTO LOOP1
        ENDIF
        GOTO MAKE
    ENDIF
    FOR LOCAL, 0, 100
         IF RESULT == 표시되는재료:LOCAL&&RESULT != 0
            IF ITEM:RESULT > 0
                사용된재료:RESULT ++
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    GOTO LOOP1
    $MAKE
    양념:ARG = RESULT
ENDIF



@MAKECOOK
#DIM 도구력
#DIM 보너스점수
#DIM 감점
#DIMS 이름백업
#DIM DYNAMIC 레시피보너스

PRINTL
PRINTL
PRINTL
DRAWLINE
PRINTL 조리중
DRAWLINE
;===============
; PHASE 0 - 재료를 삭제
;===============
IF 기본제공품사용료 > 0
    IF MONEY < 기본제공품사용료
        PRINTFORMW 돈이 부족해서 만들수 없습니다
        RETURN -1
    ENDIF
    MONEY = MONEY - 기본제공품사용료
    PRINTFORML 재료 구입비로 {기본제공품사용료}\ 냈다
ENDIF
IF !INRANGE(주재료,1,기본제공재료)
    ITEM:(주재료) --
ENDIF
FOR LOCAL, 0 ,10
    SIF 부재료:LOCAL&&!INRANGE(부재료:LOCAL,1,기본제공재료)
        ITEM:(부재료:LOCAL) --
    SIF 양념:LOCAL > 10&&!INRANGE(양념:LOCAL,1,기본제공재료)
        ITEM:(양념:LOCAL) --
NEXT
SIF DISH_DRG
    ITEM:DISH_DRG --
SIF DISH_DRG_2
    ITEM:DISH_DRG_2 --
;===============
; PHASE 1 - 레시피를 읽어와 일치하는게 있는지 확인, 없다면 그대로 창작요리- 이름 책정
;===============
이름백업 '= DISH_NAME
FOR LOCAL,0,1000
    CALL 요리레시피(LOCAL)
    IF RESULT == 1
        PRINTFORML %조사처리(RCP_이름,"을")% 만들었다!
        DISH_TASTE '= RCP_TASTE + "/"
        DISH_NAME '= RCP_이름
        레시피보너스 = RCP_보너스
        EXP:MASTER:요리경험 += 3
        SIF DISH_ASSI
            EXP:DISH_ASSI:요리경험 += 3
        GOTO 이름백업스킵
    ENDIF
NEXT
DISH_NAME '= 이름백업
$이름백업스킵
IF DISH_NAME == ""
    DISH_NAME '= 자동요리이름()
ENDIF

;===============
; PHASE 2 - 영양정보 확인
;===============
VARSET 요리영양소
VARSET YN_탄수화물
VARSET YN_단백질
VARSET YN_지방
VARSET YN_비타민
VARSET YN_나트륨
CALL 영양소표(주재료)
;탄단지비나
CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
CALL 기타영양소올리기(YN_요력)
;PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
FOR LOCAL,0 ,10
    VARSET YN_탄수화물
    VARSET YN_단백질
    VARSET YN_지방
    VARSET YN_비타민
    VARSET YN_나트륨
    CALL 영양소표(부재료:LOCAL)
    CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
    CALL 기타영양소올리기(YN_요력)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT
FOR LOCAL,0 ,10
    VARSET YN_탄수화물
    VARSET YN_단백질
    VARSET YN_지방
    VARSET YN_비타민
    VARSET YN_나트륨
    CALL 영양소표(양념:LOCAL,1)
    CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
    CALL 기타영양소올리기(YN_요력)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT
IF DISH_DRG_2 == GETNUM(ITEM,"영양강화제")
    CALL 영양소올리기(10,50,0,45,0)
ENDIF
;===============
; PHASE 3 - 맛 책정 안되있으면 자동으로
;===============
IF DISH_TASTE == ""
    SELECTCASE DISH_TYPE
        CASE "주식"
            DISH_TASTE += "주식/"
            TIME += 30
        CASE "경식"
            DISH_TASTE += "경식/"
            TIME += 15
        CASE "디저트"
            DISH_TASTE += "디저트/"
        CASEELSE
            DISH_TASTE += "주식/"
            DISH_TYPE = 주식
            TIME += 30
    ENDSELECT
    
    DISH_TASTE += DISH_NAME + "/"
    CALL 음식맛책정("주재료",주재료,주재료요리법)
    FOR LOCAL,0,9
        CALL 음식맛책정("부재료",부재료:LOCAL,부재료요리법:LOCAL)
        CALL 음식맛책정("양념",양념:LOCAL,0)
    NEXT
    LOCAL:1 = 0
    IF 주재료
        LOCAL:1++
    ENDIF
    FOR LOCAL,0,9
        IF 부재료:LOCAL
            LOCAL:1 ++
        ENDIF
        IF 양념:LOCAL
            LOCAL:1 ++
        ENDIF
    NEXT
    IF LOCAL:1 <= 5
        DISH_TASTE += "심플/"
        TIME -= 5
    ELSEIF LOCAL:1 >= 10
        DISH_TASTE += "푸짐/"
        TIME += 10
    ENDIF
ELSEIF DISH_TYPE == ""
    IF STRFIND(DISH_TASTE,"주식")
        DISH_TYPE = 주식
    ELSEIF STRFIND(DISH_TASTE,"경식")
        DISH_TYPE = 경식
    ELSEIF STRFIND(DISH_TASTE,"디저트")
        DISH_TYPE = 디저트
    ELSE
        DISH_TYPE = 주식
    ENDIF
ENDIF

;갓만든 요리 표시용
DISH_TASTE += "갓만든/"
;레시피에서 조리 시간이 설정되지 않았을 때
IF !DISH_TIME
    SELECTCASE DISH_TYPE
        CASE "경식"
            DISH_TIME = 20
        CASE "주식"
            DISH_TIME = 40
        CASE "디저트"
            DISH_TIME = 40
    ENDSELECT
ENDIF
SIF FLAG:역극
	CALL DISH_TASTE_FROM_CHARA(FLAG:역극)
SIF DISH_ASSI
	CALL DISH_TASTE_FROM_CHARA(DISH_ASSI)
    
;레시피별 최대유통기간이 설정되지 않았을 때
SIF !DISH_MAXLIMIT
	DISH_MAXLIMIT = 120
IF ITEM:월제다회용도시락통
    DISH_MAXLIMIT *= 24
ELSEIF ITEM:보온전기밥통
	TIMES DISH_MAXLIMIT, 1.5
ENDIF
;신선도 설정, 조리 시간을 추가해 둔다.
DISH_LIMIT = DISH_MAXLIMIT + DISH_TIME

DISHNAME = %DISH_NAME%
DISH_RANK = 3

;기본 점수 계산
도구력 = ITEM:은행나무도마
보너스점수 = 0
감점 = 0
SIF STRCOUNT(DISH_TASTE, "도구의공명/")
	TIMES 도구력, 2.0

SIF STRCOUNT(DISH_TASTE, "제철/")
	보너스점수 += 2
SIF STRCOUNT(DISH_TASTE, "옥토끼풍/") && STRCOUNT(DISH_TASTE, "당근/")
	보너스점수 += 2
SIF STRCOUNT(DISH_TASTE, "프로의 맛/")
	보너스점수 += 2
SIF STRCOUNT(DISH_TASTE, "반짝임/")
	보너스점수 += 1
;자신있는 요리나 날카로움은 해당되지 않을 경우 소거되어 있으므로 이것으로 OK, 자신있는 요리는 "/"를 제외합니다.
SIF STRCOUNT(DISH_TASTE, "특기요리")
	보너스점수 += 2
SIF STRCOUNT(DISH_TASTE, "섬세한맛/")
	보너스점수 += 1
SIF STRCOUNT(DISH_TASTE, "맛있는물/")
	보너스점수 += 1
;질투,숨겨진 맛의 효과는 먹을 때
IF 재료첨가여부("양념",GETNUM(ITEM,"미원"),0)
    보너스점수 += 1
ENDIF
SIF DISH_DRG
	감점 += 1
IF DISH_DRG_2
	IF ITEM:DISH_DRG_2 == ITEM:영양강화제
		감점 += 1
	;(酒造パッチ)洋酒にボーナス点
	ELSEIF ITEM:DISH_DRG_2 == ITEM:프랑부아즈 || ITEM:DISH_DRG_2 == ITEM:다크럼 || ITEM:DISH_DRG_2 == ITEM:브랜디VS
		보너스점수 += 2
	ELSEIF ITEM:DISH_DRG_2 == ITEM:브랜디VSOP
		보너스점수 += 3
	ELSEIF ITEM:DISH_DRG_2 == ITEM:브랜디XO
		보너스점수 += 4
	ENDIF
ENDIF
보너스점수 += RCP_보너스
;DISH_ASSI가 0이면 MASTER니까 분기 필요없지만 일단
IF STRCOUNT(DISH_TASTE, "치르밀/")
    IF DISH_ASSI
        DISH_BASEPOINT = MIN(ABL:MASTER:요리기능 + ABL:DISH_ASSI:요리기능,2) + DISH_RANK + (보너스점수/2) - 감점 + 11
        DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
        PRINTFORM 솜씨：
        CALL 만듦새(DISH_BASEPOINT)
        PRINTFORML 　／%CALLNAME:MASTER%의 능력({ABL:MASTER:요리기능}) + %CALLNAME:DISH_ASSI%의 능력({ABL:DISH_ASSI:요리기능}) + 식재료의 품질({DISH_RANK}) \@ 도구력 ? + 도구의 품질({도구력}) # \@ \@ 보너스점수 ? + 보너스점수({보너스점수/2}) # \@ \@ 감점 ? - 첨가물({감점}) # \@ + 치르밀(11)
    ELSE
       DISH_BASEPOINT = MIN(ABL:MASTER:요리기능 + ABL:MASTER:요리기능,2) + DISH_RANK + (보너스점수/2) - 감점 + 11
        DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
        PRINTFORM 솜씨：
        CALL 만듦새(DISH_BASEPOINT)
        PRINTFORML 　／%CALLNAME:MASTER%의 능력 x 2({ABL:MASTER:요리기능 * 2}) + 식재료의 품질({DISH_RANK}) \@ 도구력 ? + 도구의 품질({도구력}) # \@ \@ 보너스점수 ? + 보너스점수({보너스점수/2}) # \@ \@ 감점 ? - 첨가물({감점}) # \@ + 치르밀(11)
    ENDIF
ELSEIF DISH_ASSI
	DISH_BASEPOINT = ABL:MASTER:요리기능 + ABL:DISH_ASSI:요리기능 + DISH_RANK + 도구력 + 보너스점수 - 감점
	DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
	PRINTFORM 솜씨：
	CALL 만듦새(DISH_BASEPOINT)
	PRINTFORML 　／%CALLNAME:MASTER%의 능력({ABL:MASTER:요리기능}) + %CALLNAME:DISH_ASSI%의 능력({ABL:DISH_ASSI:요리기능}) + 식재료의 품질({DISH_RANK}) \@ 도구력 ? + 도구의 품질({도구력}) # \@ \@ 보너스점수 ? + 보너스점수({보너스점수}) # \@ \@ 감점 ? - 첨가물({감점}) # \@
ELSE
	DISH_BASEPOINT = ABL:MASTER:요리기능 + ABL:MASTER:요리기능 + DISH_RANK + 도구력 + 보너스점수 - 감점
	DISH_BASEPOINT = MIN(300 + (DISH_BASEPOINT * 20), 700)
	PRINTFORM 솜씨：
	CALL 만듦새(DISH_BASEPOINT)
	PRINTFORML 　／%CALLNAME:MASTER%의 능력 x 2({ABL:MASTER:요리기능 * 2}) + 식재료의 품질({DISH_RANK}) \@ 도구력 ? + 도구의 품질({도구력}) # \@ \@ 보너스점수 ? + 보너스점수({보너스점수}) # \@ \@ 감점 ? - 첨가물({감점}) # \@
ENDIF
PRINTL 
LOCAL = (DISH_BASEPOINT - 300) * 100 / 400
IF 명중판정(MIN(90,3 * LOCAL / 2) + 1)
    CALL COLORMESSAGE("이번 요리는 특별히 잘 된 것 같다!",C_YELLOW,1)
    요리영양소:0 += 2
    요리영양소:1 += 2
    요리영양소:2 += 2
    요리영양소:3 += 2
    요리영양소:4 += 2
    요리영양소:5 += 2
    TIMES 요리영양소:0 , 1.5
    TIMES 요리영양소:1 , 1.5
    TIMES 요리영양소:2 , 1.5
    TIMES 요리영양소:3 , 1.5
    TIMES 요리영양소:4 , 1.5
    TIMES 요리영양소:5 , 1.5
    DISH_BASEPOINT += 20
ENDIF
;첨가물
;#DIM CONST BIT_MIX_DRUG = 0, 21, 22, 23, 24
IF DISH_DRG != 0
    SELECTCASE DISH_DRG
        CASE 41
            ITEM:미약 --
        CASE 42
            ITEM:이뇨제 --
        CASE 250
            ITEM:춘약 --
        CASE 252
            ITEM:수면제 --
        CASE 91
            ITEM:수면제 --
        CASE 1021
            ITEM:보테이지 --
    ENDSELECT
ELSEIF DISH_DRG_2 != 0
    SELECTCASE DISH_DRG_2
        CASE 1022
            ITEM:히스테리큐 --
        CASE 518
            ITEM:영양강화제 --
    ENDSELECT
ENDIF
;
; PHASE 4 경험증가
;
PRINTFORML 요리의 맛 : %DISH_TASTE%
EXP:MASTER:요리경험 += 2
SIF DISH_ASSI
    EXP:DISH_ASSI:요리경험 += 2
TIME += 15

IF STRCOUNT(DISH_TASTE, "로터/")
	TFLAG:194 = 100
ELSEIF DISH_TYPE == "디저트"
	TFLAG:194 = 3
ELSEIF DISH_TYPE == "주식"
	TFLAG:194 = 2
ELSE
	TFLAG:194 = 1
ENDIF

;표시용 음식명
EX_DISH_NAME = %DISH_NAME%
PRINTFORM 영양정보 : 탄수화물 {요리영양소:0} / 단백질 {요리영양소:1} / 지방 {요리영양소:2} / 나트륨 {요리영양소:4} / 비타민 {요리영양소:3}
IF 요리영양소:5
    PRINTFORM  / 요력 {요리영양소:5}
ENDIF
FLAG:요리 = 1
PRINTL

@요리법포함여부(ARG)
#FUNCTION
IF 주재료요리법 == ARG
    RETURNF 1
ELSE
    FOR LOCAL,0,10
        IF 부재료요리법:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
ENDIF
RETURNF 0

@재료첨가여부(ARGS,ARG,ARG:1 = 0)
#FUNCTION
;대상 재료를 넣었는지 안 넣었는지
IF ARGS == "모두"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        FOR LOCAL, 0, 10
            IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
                RETURNF 1
            ENDIF
            IF 양념:LOCAL == ARG
                RETURNF 1
            ENDIF
        NEXT
    ENDIF
    RETURNF 0
ELSEIF ARGS == "주재료"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        RETURNF 0
    ENDIF
ELSEIF ARGS == "부재료"
    FOR LOCAL, 0, 10
        IF ARG == -1&&부재료:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
            RETURNF 1
        ENDIF
    NEXT
    SIF ARG == -1
        RETURNF 1
    RETURNF 0
ELSEIF ARGS == "양념"
    FOR LOCAL, 0, 10
        IF ARG == -1&&양념:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 양념:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
    SIF ARG == -1
        RETURNF 1
    RETURNF 0
ENDIF

@영양소올리기(ARG:0,ARG:1,ARG:2,ARG:3,ARG:4)
요리영양소:0 += ARG:0 ;탄수화물
요리영양소:1 += ARG:1 ;단백질
요리영양소:2 += ARG:2 ;지방
요리영양소:3 += ARG:3 ;비타민
요리영양소:4 += ARG:4 ;나트륨

@기타영양소올리기(ARG:0)
요리영양소:5 += ARG:0 ;요력


@자동요리이름
#FUNCTIONS
#DIMS DYNAMIC 요리이름
;자동으로 요리 이름을 선정해주는 함수
FOR LOCAL, 0, 9
    IF 부재료:LOCAL != 0
        LOCALS = %조사처리(사용된재료표시(부재료요리법:LOCAL,부재료:LOCAL),"를")%
        요리이름 = %LOCALS%
        요리이름 += " 곁들인 "
        BREAK
    ENDIF
NEXT
IF 주재료
    LOCALS = %사용된재료표시(주재료요리법,주재료)%
    요리이름 += LOCALS + " 요리"
ELSE
    요리이름 = 수수께끼 요리
ENDIF
RETURNF 요리이름


@맛의종류(ARG = 0)
#DIMS TASTE_STR               ;표시 대상의 맛데이터
#DIMS DYNAMIC TASTE_LIST, 100 ;미 리스트
#DIM  DYNAMIC TASTE_CNT,  100 ;미 리스트 각 요소의 중복 개수
#DIM  DYNAMIC TASTE_NUM       ;미의 종류의 총수
#DIMS DYNAMIC 맛리스트,1000
#DIM DYNAMIC 카운터
FOR LOCAL,1,CHARANUM
    TASTE_STR '= GET_STR(, "캐릭터데이터", LOCAL, "요리：좋아하는 맛")
    CALLF CNT_SPLIT(TASTE_STR, TASTE_LIST, TASTE_CNT, TASTE_NUM)
    FOR LOCAL:2,0,100
        LOCAL:3 = 0
        FOR LOCAL:1,0,카운터+1
            IF 맛리스트:(LOCAL:1) == TASTE_LIST:(LOCAL:2)
                LOCAL:3 = 1
                BREAK
            ELSE
                LOCAL:3 = 0
            ENDIF
        NEXT
        IF LOCAL:3 == 0
            맛리스트:카운터 '= TASTE_LIST:(LOCAL:2)
            카운터++
        ENDIF
    NEXT
    TASTE_STR '= GET_STR(, "캐릭터데이터", LOCAL, "요리：싫어하는 맛")
    CALLF CNT_SPLIT(TASTE_STR, TASTE_LIST, TASTE_CNT, TASTE_NUM)
    FOR LOCAL:2,0,100
        LOCAL:3 = 0
        FOR LOCAL:1,0,카운터+1
            IF 맛리스트:(LOCAL:1) == TASTE_LIST:(LOCAL:2)
                LOCAL:3 = 1
                BREAK
            ELSE
                LOCAL:3 = 0
            ENDIF
        NEXT
        IF LOCAL:3 == 0
            맛리스트:카운터 '= TASTE_LIST:(LOCAL:2)
            카운터++
        ENDIF
    NEXT
    TASTE_STR '= GET_STR(, "캐릭터데이터", LOCAL, "요리：조수 효과")
    SPLIT TASTE_STR, "＊", RESULTS
    FOR LOCAL:1,0,카운터+1
        IF 맛리스트:(LOCAL:1) == RESULTS:1
            LOCAL:3 = 1
            BREAK
        ELSE
            LOCAL:3 = 0
        ENDIF
    NEXT
    IF LOCAL:3 == 0
        맛리스트:카운터 '= RESULTS:1
        카운터++
    ENDIF
NEXT
IF ARG == 1
    LOCAL:1 = 0
    FOR LOCAL,0,카운터+1
        PRINTFORM [%맛리스트:LOCAL,20,RIGHT%]　
        LOCAL:1++
        IF LOCAL:1 % 5 == 0
            PRINTL
        ENDIF
    NEXT
    PRINTL
    PRINTL
ENDIF