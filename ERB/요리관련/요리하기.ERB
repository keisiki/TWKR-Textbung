@요리하기
VARSET 사용된재료
VARSET 주재료
VARSET 주재료요리법
VARSET 부재료
VARSET 부재료요리법
VARSET 양념
VARSET 첨가물
기본제공품사용료 = 0
$LOOP
DRAWLINE
;주재료
PRINTFORM [주재료]
PRINTFORM 　　　　　　　　　
PRINTBUTTON "[요리명]",101
PRINTFORM :%DISH_NAME%
PRINTFORM 　　　　　　　　　
PRINTBUTTON "[요리종류]",102
IF DISH_TYPE
    PRINTFORM : %DISH_TYPE%
ELSE ;DISH_TYPE == ""
    PRINTFORM : 주식
ENDIF
PRINTL
PRINTFORML [100] %사용된재료표시(주재료요리법,주재료),20,LEFT%
DRAWLINE
PRINTFORM [부재료]
PRINTFORM 　　　　　　　　　
PRINTFORM [양　념]
PRINTFORM 　　　　　　　　　
PRINTFORM [첨가물]
PRINTFORML
;200대 : 부재료
PRINTFORM [200] %사용된재료표시(부재료요리법:0,부재료:0),20,LEFT%
;300대 : 양념
PRINTFORM [300] %사용된재료표시(-1,양념:0),20,LEFT%
;400대 : 약물등의 첨가물
PRINTFORM [400] %사용된재료표시(-2,첨가물),20,LEFT%
PRINTFORML
PRINTFORM [201] %사용된재료표시(부재료요리법:1,부재료:1),20,LEFT%

PRINTFORM [301] %사용된재료표시(-1,양념:1),20,LEFT%
PRINTFORML
PRINTFORM [202] %사용된재료표시(부재료요리법:2,부재료:2),20,LEFT%

PRINTFORM [302] %사용된재료표시(-1,양념:2),20,LEFT%
PRINTFORML
PRINTFORM [203] %사용된재료표시(부재료요리법:3,부재료:3),20,LEFT%

PRINTFORM [303] %사용된재료표시(-1,양념:3),20,LEFT%
PRINTFORML
PRINTFORM [204] %사용된재료표시(부재료요리법:4,부재료:4),20,LEFT%

PRINTFORM [304] %사용된재료표시(-1,양념:4),20,LEFT%
PRINTFORML
PRINTFORM [205] %사용된재료표시(부재료요리법:5,부재료:5),20,LEFT%

PRINTFORM [305] %사용된재료표시(-1,양념:5),20,LEFT%
PRINTFORML
PRINTFORM [206] %사용된재료표시(부재료요리법:6,부재료:6),20,LEFT%

PRINTFORM [306] %사용된재료표시(-1,양념:6),20,LEFT%
PRINTFORML
PRINTFORM [207] %사용된재료표시(부재료요리법:7,부재료:7),20,LEFT%

PRINTFORM [307] %사용된재료표시(-1,양념:7),20,LEFT%
PRINTFORML
PRINTFORM [208] %사용된재료표시(부재료요리법:8,부재료:8),20,LEFT%

PRINTFORM [308] %사용된재료표시(-1,양념:8),20,LEFT%
PRINTFORML
PRINTFORM [209] %사용된재료표시(부재료요리법:9,부재료:9),20,LEFT%

PRINTFORM [309] %사용된재료표시(-1,양념:9),20,LEFT%
PRINTFORML
DRAWLINE
IF 주재료
    PRINTFORML [998] 완성!
ENDIF
PRINTFORML [999] 취소
INPUT
IF RESULT == 100
    CALL 주재료선택
ELSEIF RESULT == 101
    CALL 요리이름결정
ELSEIF RESULT == 102
    SELECTCASE DISH_TYPE
        CASE "주식"
            DISH_TYPE '= "경식"
        CASE "경식"
            DISH_TYPE '= "디저트"
        CASEELSE ; "디저트" 포함
            DISH_TYPE '= "주식"
    ENDSELECT
    CLEARLINE 18
ELSEIF RESULT / 100 == 2
    CALL 서브재료선택(RESULT % 200)
ELSEIF RESULT / 100 == 3
    CALL 양념선택(RESULT % 300)
ELSEIF RESULT == 400
    CALL 첨가물선택
ELSEIF RESULT == 998
    CALL MAKECOOK
    RETURN 1
ELSEIF RESULT == 999
    RETURN -1
ENDIF
GOTO LOOP

@요리이름결정
PRINT 요리의 이름을 입력해주세요:
INPUTS
DISH_NAME = %RESULTS%
IF DISH_NAME == ""
    DISH_NAME = 수수께끼 요리
ENDIF
PRINTL

@사용된재료표시(조리법,재료)
#FUNCTIONS
#DIM DYNAMIC 조리법
#DIM DYNAMIC 재료
#DIMS DYNAMIC 출력문
IF 조리법 == 0
    출력문 = ----------
ELSEIF 조리법 == -2
    IF 재료
        출력문 = %ITEMNAME:재료%
    ELSE
        출력문 = ----------
    ENDIF
ELSEIF 조리법 == -1
    IF 재료 < 20
        SELECTCASE 재료
            CASE Y_소금
                출력문 += "소금"
            CASE Y_후추
                출력문 += "후추"
            CASE Y_설탕
                출력문 += "설탕"
            CASE Y_식초
                출력문 += "식초"
            CASE Y_된장
                출력문 += "된장"
            CASE Y_기름
                출력문 += "기름"
            CASE Y_간장
                출력문 += "간장"
            CASE Y_고춧가루
                출력문 += "고춧가루"
            CASE Y_젓갈
                출력문 += "젓갈"
            CASE Y_버터
                출력문 += "버터"
            CASE Y_강황
                출력문 += "강황"
            CASEELSE
                출력문 += "----------"
        ENDSELECT
    ELSE
        출력문 += ITEMNAME:재료
    ENDIF
ELSE
    SELECTCASE 조리법
        CASE Y_굽기
            출력문 += "구운 "
        CASE Y_삶기
            출력문 += "삶은 "
        CASE Y_튀기기
            출력문 += "튀긴 "
        CASE Y_끓이기
            출력문 += "끓인 "
        CASE Y_찌기
            출력문 += "찐 "
        CASE Y_조리기
            출력문 += "조린 "
        CASE Y_볶기
            출력문 += "볶은 "
        CASE Y_그대로
            
        CASEELSE

    ENDSELECT
    IF 재료 < 20
        SELECTCASE 재료
            CASE Y_국수
                출력문 += "국수"
            CASE Y_빵
                출력문 += "빵"
            CASE Y_육수
                출력문 += "육수"
            CASE Y_밀가루
                출력문 += "밀가루"
            CASE Y_계란
                출력문 += "계란"
            CASE Y_감자
                출력문 += "감자"
            CASE Y_콩
                출력문 += "콩"
            CASE Y_양파
                출력문 += "양파"
            CASE Y_마늘
                출력문 += "마늘"
            CASE Y_우유
                출력문 += "우유"
            CASE Y_파프리카
                출력문 += "파프리카"
            CASE Y_치즈
                출력문 += "치즈"
            CASEELSE
                출력문 += "----------"
        ENDSELECT
    ELSE
        출력문 += ITEMNAME:재료
    ENDIF
ENDIF
RETURNF 출력문

@요리재료표시함수(ARG)
IF ARG <= 0
    RETURN -1
ENDIF
IF ITEM:ARG <= 0
    SETCOLOR 0x808080
ENDIF
PRINTFORMLC [{ARG, 4}] %ITEMNAME:ARG% x{ITEM:ARG - 사용된재료:ARG}

RESETCOLOR
RETURN 1

@기본재료표시함수(ARGS)
#DIMS MAIN_ING, 10 '= "국수", "빵", "육수", "밀가루", "계란", "감자", "콩"
#DIMS SUB_ING, 10 '= "양파", "마늘", "우유", "파프리카"
#DIMS SPICES '= "소금", "후추", "설탕", "식초", "된장", "기름", "간장"

VARSET LOCAL
PRINT 기본 
IF ARGS == "주재료"||ARGS == "부재료"
    PRINTFORML 재료 (%금액표시(500)%)
    DRAWLINE
    FOR LOCAL, 0, 10
        SIF LOCAL && LOCAL % 4 ;5번째줄마다 빈줄
            PRINTL
        IF MAIN_ING:LOCAL != ""
            PRINTFORMLC [{LOCAL+1, 4, RIGHT}] %MAIN_ING::LOCAL%
            LOCAL:1 ++
        ENDIF
    NEXT
    IF ARGS == "부재료"
        FOR LOCAL, 0, 10
            SIF LOCAL && LOCAL % 4 ;5번째줄마다 빈줄
                PRINTL
            IF SUB_ING:LOCAL != ""
                PRINTFORMLC [{LOCAL+1, 4, RIGHT}] %SUB_ING:LOCAL%
                LOCAL:1 ++
            ENDIF
        NEXT
    ENDIF
ELSEIF ARGS == "양념"
    PRINTFORML 양념 (%금액표시(100)%)
    DRAWLINE
    FOR LOCAL, 0, 12
        SIF LOCAL && LOCAL % 4 ;5번째줄마다 빈줄
            PRINTL
        IF SPICES:LOCAL != ""
            PRINTFORMLC [{LOCAL+1, 4, RIGHT}] %SPICES:LOCAL%
            LOCAL:1 ++
        ENDIF
    NEXT
ENDIF
PRINTL
RETURN LOCAL:1

@첨가물선택
#DIM 표시되는재료, 100
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료 = GETNUM(ITEM,"미약"), GETNUM(ITEM,"이뇨제"), GETNUM(ITEM,"춘약"), GETNUM(ITEM,"수면제")
표시되는재료:4 = GETNUM(ITEM,"보테이지"), GETNUM(ITEM,"배란유발제")
표시되는재료:10 = GETNUM(ITEM,"히스테리큐"), GETNUM(ITEM,"영양강화제")

사용된재료:첨가물 --
첨가물 = 0

PRINTFORML 무엇인가 섞을까?
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 1
ELSE
    FOR LOCAL, 0, 100
        IF (RESULT == 표시되는재료:LOCAL && RESULT != 0 )
            IF ITEM:RESULT > 0
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    $MAKE
    사용된재료:RESULT ++
    첨가물 = RESULT
ENDIF

@주재료선택
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
IF 주재료
    사용된재료:주재료 --
    기본제공품사용료 -= 500
ENDIF
VARSET 주재료
VARSET 주재료요리법



표시되는재료:0 = GETNUM(ITEM,"싸구려식재")
표시되는재료:1 = GETNUM(ITEM,"상급식재")
표시되는재료:2 = GETNUM(ITEM,"고급식재")
표시되는재료:3 = GETNUM(ITEM,"쌀")
표시되는재료:4 = GETNUM(ITEM,"고기")
표시되는재료:5 = GETNUM(ITEM,"물고기")
표시되는재료:6 = GETNUM(ITEM,"맛있는물고기")
표시되는재료:7 = GETNUM(ITEM,"보통버섯")
표시되는재료:8 = GETNUM(ITEM,"간")
표시되는재료:9 = GETNUM(ITEM,"당근")
표시되는재료:10 = GETNUM(ITEM,"순무")
표시되는재료:11 = GETNUM(ITEM,"죽순")
표시되는재료:12 = GETNUM(ITEM,"아이스크림")
표시되는재료:13 = GETNUM(ITEM,"고구마")
표시되는재료:14 = GETNUM(ITEM,"개구리")


$LOOP
PRINTFORML 어떤 메인 재료를 사용할까?
DRAWLINE
CALL 기본재료표시함수("주재료")
기본제공품 = RESULT
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다

WHILE 1
    INPUT
    IF RESULT == 9999 ;돌아간다
        RETURN -1
    ELSEIF RESULT == 0 ;선택 대상 없음. 로터를 고른다면 또 모를까.
        CONTINUE
    ENDIF

    FOR LOCAL, 0, 100
        IF 표시되는재료:LOCAL == -1 ; 표시되는재료 배열 공란 발견시
            CONTINUE
        ELSEIF (RESULT == 표시되는재료:LOCAL && ITEM:RESULT > 0 )|| INRANGE(RESULT,1,기본제공품)
            IF INRANGE(RESULT,1,기본제공품)
                기본제공품사용료 += 500
                IF MONEY < 기본제공품사용료
                    PRINTFORMW 돈이 부족합니다
                    기본제공품사용료 -= 500
                    GOTO LOOP
                ENDIF
            ENDIF
            사용된재료:RESULT ++
            주재료 = RESULT
            CALL 요리법선택
            주재료요리법 = RESULT
            RETURN 1
        ENDIF
    NEXT
WEND

@요리법선택
PRINTL 어떻게 요리할까...?
DRAWLINE
PRINTFORML [{Y_굽기, 3, RIGHT}] 굽기
PRINTFORML [{Y_삶기, 3, RIGHT}] 삶기
PRINTFORML [{Y_튀기기, 3, RIGHT}] 튀기기
PRINTFORML [{Y_끓이기, 3, RIGHT}] 끓이기
PRINTFORML [{Y_찌기, 3, RIGHT}] 찌기
PRINTFORML [{Y_조리기, 3, RIGHT}] 조리기
PRINTFORML [{Y_볶기, 3, RIGHT}] 볶기
PRINTFORML [{Y_그대로, 3, RIGHT}] 그대로
DRAWLINE
WHILE 1
    INPUT
    IF INRANGE(RESULT, 1, 8)
        RETURN RESULT
    ELSE
        PRINTW 잘못된 입력입니다.
    ENDIF
WEND

@서브재료선택(ARG)
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
IF 부재료:ARG != 0
    사용된재료:(부재료:ARG) --
    기본제공품사용료 -= 500
ENDIF
부재료:ARG = 0
부재료요리법:ARG = 0

VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료:0 = GETNUM(ITEM,"가지")
표시되는재료:1 = GETNUM(ITEM,"파")
표시되는재료:2 = GETNUM(ITEM,"순무")
표시되는재료:3 = GETNUM(ITEM,"당근")
표시되는재료:4 = GETNUM(ITEM,"고기")
표시되는재료:5 = GETNUM(ITEM,"약초")
표시되는재료:6 = GETNUM(ITEM,"고려인삼")
표시되는재료:7 = GETNUM(ITEM,"간")
표시되는재료:8 = GETNUM(ITEM,"양배추")
표시되는재료:9 = GETNUM(ITEM,"참마")
표시되는재료:10 = GETNUM(ITEM,"오이")
표시되는재료:11 = GETNUM(ITEM,"토마토")
표시되는재료:12 = GETNUM(ITEM,"고구마")
표시되는재료:13 = GETNUM(ITEM,"개구리")
PRINTL
DRAWLINE
PRINTL 추가로 뭘 넣을까?
DRAWLINE
CALL 기본재료표시함수("부재료")
기본제공품 = RESULT
DRAWLINE
LOCAL:1 = 0

FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
RESETCOLOR
DRAWLINE
PRINTL [9999] 돌아간다
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    FOR LOCAL, 0, 100
        IF (RESULT == 표시되는재료:LOCAL && RESULT != 0 )|| INRANGE(RESULT,1,기본제공품)
            IF ITEM:RESULT > 0|| INRANGE(RESULT,1,기본제공품)
                IF INRANGE(RESULT,1,기본제공품)
                    기본제공품사용료 += 500
                    IF MONEY < 기본제공품사용료
                        PRINTFORMW 돈이 부족합니다
                        기본제공품사용료 -= 500
                        GOTO LOOP1
                    ENDIF
                ENDIF
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    $MAKE
    사용된재료:RESULT ++
    부재료:ARG = RESULT
    CALL 요리법선택
    부재료요리법:ARG = RESULT
ENDIF

@양념선택(ARG)
#DIM 표시되는재료, 100
#DIM DYNAMIC 기본제공품
IF 양념:ARG != 0
    사용된재료:(양념:ARG) --
    기본제공품사용료 -= 100
ENDIF
양념:ARG = 0
VARSET 표시되는재료, -1 ;표시되는재료 배열의 공란은 -1로 처리
표시되는재료:0 = GETNUM(ITEM,"벌꿀")
표시되는재료:1 = GETNUM(ITEM,"소주")
표시되는재료:2 = GETNUM(ITEM,"맥주")
표시되는재료:3 = GETNUM(ITEM,"와인")

PRINTL 무슨 양념?
DRAWLINE
CALL 기본재료표시함수("양념")
기본제공품 = RESULT
DRAWLINE
PRINTL 고급 양념
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 0, 100
    CALL 요리재료표시함수(표시되는재료:LOCAL)
    IF RESULT == 1
        LOCAL:1++
    ENDIF
    IF LOCAL:1 == 4
        PRINTL
        LOCAL:1 = 0
    ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [9999] 이대로 끝
$LOOP1
INPUT
IF RESULT == 9999
    RETURN 0
ELSE
    IF RESULT > 0&&RESULT <= 기본제공품
        기본제공품사용료 += 100
        IF MONEY < 기본제공품사용료
            PRINTFORMW 돈이 부족합니다
            기본제공품사용료 -= 100
            GOTO LOOP1
        ENDIF
        GOTO MAKE
    ENDIF
    FOR LOCAL, 0, 100
         IF RESULT == 표시되는재료:LOCAL&&RESULT != 0
            IF ITEM:RESULT > 0
                사용된재료:RESULT ++
                GOTO MAKE
            ELSE
                GOTO LOOP1
            ENDIF
        ENDIF
    NEXT
    GOTO LOOP1
    $MAKE
    양념:ARG = RESULT
ENDIF



@MAKECOOK
#DIM DYNAMIC 레시피있음
PRINTL
PRINTL
PRINTL
DRAWLINE
PRINTL 조리중
DRAWLINE
;===============
; PHASE 0 - 재료를 삭제
;===============
IF 기본제공품사용료
    IF MONEY < 기본제공품사용료
        PRINTFORMW 돈이 부족해서 만들수 없습니다
        RETURN -1
    ENDIF
    MONEY = MONEY - 기본제공품사용료
    PRINTFORML 재료 구입비로 {기본제공품사용료}\ 냈다
ENDIF
IF !INRANGE(주재료,1,기본제공재료)
    ITEM:(주재료) --
ENDIF
FOR LOCAL, 0 ,10
    SIF 부재료:LOCAL&&!INRANGE(부재료:LOCAL,1,기본제공재료)
        ITEM:(부재료:LOCAL) --
    SIF 양념:LOCAL > 10&&!INRANGE(양념:LOCAL,1,기본제공재료)
        ITEM:(양념:LOCAL) --
NEXT
;===============
; PHASE 1 - 레시피를 읽어와 일치하는게 있는지 확인, 없다면 그대로 창작요리- 이름 책정
;===============
FOR LOCAL,0,1000
    TRYCALLFORM 요리레시피{LOCAL}
    IF RESULT == 1
        TRYCALLFORM 요리명{LOCAL}
        DISH_NAME = %RESULTS%
        PRINTFORML %조사처리(RESULTS,"을")% 만들었다!
        레시피있음 = LOCAL
        BREAK
    ENDIF
NEXT
IF DISH_NAME == ""
    DISH_NAME '= 자동요리이름()
    
ENDIF

;===============
; PHASE 2 - 영양정보 확인
;===============
VARSET 요리영양소
VARSET YN_탄수화물
VARSET YN_단백질
VARSET YN_지방
VARSET YN_비타민
VARSET YN_나트륨
CALL 영양소표(주재료)
;탄단지비나
CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
;PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
FOR LOCAL,0 ,10
    VARSET YN_탄수화물
    VARSET YN_단백질
    VARSET YN_지방
    VARSET YN_비타민
    VARSET YN_나트륨
    CALL 영양소표(부재료:LOCAL)
    CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT
FOR LOCAL,0 ,10
    VARSET YN_탄수화물
    VARSET YN_단백질
    VARSET YN_지방
    VARSET YN_비타민
    VARSET YN_나트륨
    CALL 영양소표(양념:LOCAL,1)
    CALL 영양소올리기(YN_탄수화물,YN_단백질,YN_지방,YN_비타민,YN_나트륨)
;    PRINTFORMW {YN_탄수화물},{YN_단백질},{YN_지방},{YN_비타민},{YN_나트륨}
NEXT

;===============
; PHASE 3 - 맛 책정 안되있으면 자동으로
;===============
IF 레시피있음 == 0
    $맛책정
    SELECTCASE DISH_TYPE
        CASE "주식"
            DISH_TASTE += "주식/"
            TIME += 30
        CASE "경식"
            DISH_TASTE += "경식/"
            TIME += 15
        CASE "디저트"
            DISH_TASTE += "디저트/"
        CASEELSE
            DISH_TASTE += "주식/"
            DISH_TYPE = 주식
            TIME += 30
    ENDSELECT
    
    DISH_TASTE += DISH_NAME + "/"
    CALL 음식맛책정("주재료",주재료,주재료요리법)
    FOR LOCAL,0,9
        CALL 음식맛책정("부재료",부재료:LOCAL,부재료요리법:LOCAL)
        CALL 음식맛책정("양념",양념:LOCAL,0)
    NEXT
    LOCAL:1 = 0
    IF 주재료
        LOCAL:1++
    ENDIF
    FOR LOCAL,0,9
        IF 부재료:LOCAL
            LOCAL:1 ++
        ENDIF
        IF 양념:LOCAL
            LOCAL:1 ++
        ENDIF
    NEXT
    IF LOCAL:1 <= 5
        DISH_TASTE += "심플/"
        TIME -= 5
    ENDIF
ELSE
    CALLFORM 요리맛책정{레시피있음}
    IF RESULTS == ""
        GOTO 맛책정
    ENDIF
    DISH_TASTE = RESULTS
ENDIF
DISHNAME '= DISH_NAME
DISH_BASEPOINT = 3 + MIN(RAND:((ABL:MASTER:요리기능/2) + 1),3)
DISH_RANK = 3

;첨가물
;#DIM CONST BIT_MIX_DRUG = 0, 21, 22, 23, 24
IF 첨가물
    SELECTCASE 첨가물
        CASE 41
            SELECT_DRG = GETNUM(ITEM,"미약")
            ITEM:미약 --
        CASE 42
            SELECT_DRG = GETNUM(ITEM,"이뇨제")
            ITEM:이뇨제 --
        CASE 250
            SELECT_DRG = GETNUM(ITEM,"춘약")
            ITEM:춘약 --
        CASE 252
            SELECT_DRG = GETNUM(ITEM,"수면제")
            ITEM:수면제 --
        CASE 91
            SELECT_DRG = GETNUM(ITEM,"배란유발제")
            ITEM:수면제 --
        CASE 1021
            SELECT_DRG = GETNUM(ITEM,"보테이지")
            ITEM:보테이지 --
        CASE 1022
            SELECT_DRG = GETNUM(ITEM,"히스테리큐")
            ITEM:히스테리큐 --
        CASE 518
            SELECT_DRG = GETNUM(ITEM,"영양강화제")
            ITEM:영양강화제 --
    ENDSELECT
ENDIF
;
; PHASE 4 경험증가
;
PRINTFORML 요리의 맛 : %DISH_TASTE%
EXP:MASTER:요리경험 += 2
SIF TARGET
    EXP:TARGET:요리경험 += 2
TIME += 15

IF STRCOUNT(DISH_TASTE, "로터/")
	TFLAG:194 = 100
ELSEIF DISH_TYPE == "디저트"
	TFLAG:194 = 3
ELSEIF DISH_TYPE == "주식"
	TFLAG:194 = 2
ELSE
	TFLAG:194 = 1
ENDIF

@요리법포함여부(ARG)
#FUNCTION
IF 주재료요리법 == ARG
    RETURNF 1
ELSE
    FOR LOCAL,0,10
        IF 부재료요리법:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
ENDIF
RETURNF 0

@재료첨가여부(ARGS,ARG,ARG:1 = 0)
#FUNCTION
;대상 재료를 넣었는지 안 넣었는지
IF ARGS == "모두"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        FOR LOCAL, 0, 10
            IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
                RETURNF 1
            ENDIF
            IF 양념:LOCAL == ARG
                RETURNF 1
            ENDIF
        NEXT
    ENDIF
    RETURNF 0
ELSEIF ARGS == "주재료"
    IF 주재료 == ARG&&(ARG:1 == 0 || ARG:1 == 주재료요리법)
        RETURNF 1
    ELSE
        RETURNF 0
    ENDIF
ELSEIF ARGS == "부재료"
    FOR LOCAL, 0, 10
        IF ARG == -1&&부재료:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 부재료:LOCAL == ARG&&(ARG:1 == 0 || ARG:1 == 부재료요리법:LOCAL)
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0
ELSEIF ARGS == "양념"
    FOR LOCAL, 0, 10
        IF ARG == -1&&양념:LOCAL != 0
            RETURNF 0
        ENDIF
        IF 양념:LOCAL == ARG
            RETURNF 1
        ENDIF
    NEXT
    RETURNF 0
ENDIF

@영양소올리기(ARG:0,ARG:1,ARG:2,ARG:3,ARG:4)
요리영양소:0 += ARG:0 ;탄수화물
요리영양소:1 += ARG:1 ;단백질
요리영양소:2 += ARG:2 ;지방
요리영양소:3 += ARG:3 ;비타민
요리영양소:4 += ARG:4 ;나트륨

@자동요리이름
#FUNCTIONS
#DIM 요리이름
;자동으로 요리 이름을 선정해주는 함수
FOR LOCAL, 0, 9
    IF 부재료:LOCAL != 0
        요리이름 = %조사처리(사용된재료표시(부재료요리법:LOCAL,부재료:LOCAL),"를")%
        요리이름 += " 곁들인 "
    ENDIF
NEXT
IF 주재료
    LOCALS = %사용된재료표시(주재료요리법,주재료)%
    요리이름 += LOCALS + " 요리"
ELSE
    요리이름 = 수수께끼 요리
ENDIF
RETURNF 요리이름