@EVENTTURNEND
#DIM 양육비
#DIM 채무계산용변수
;연회 당일에 하루가 끝나면 연회 플래그 OFF
;IF DAY >= FLAG:34 && FLAG:연회개최플래그

CALL 카지노소유금자동변동
PRINTL
FOR LOCAL, 1, CHARANUM
	;전원 대상 검사 실시
	TBCFLAG:LOCAL:강제탈의해제금지 = MAX(0,TBCFLAG:LOCAL:강제탈의해제금지 - 1)
	;소지금 증가 처리
	SIF CFLAG:LOCAL:소지금 < CFLAG:LOCAL:시작소지금
		CFLAG:LOCAL:소지금 = (CFLAG:LOCAL:시작소지금 / 5) + RAND:1001	
	CFLAG:LOCAL:오늘훔친횟수 = 0
	;CFLAG:소지물품 = RAND:4
	;종속/주도도 검사
	CALL 노예화(LOCAL)
	IF CFLAG:LOCAL:감금됨
		CALL 감금실정책수행(LOCAL)
		CFLAG:LOCAL:감금된날 ++
		IF CFLAG:LOCAL:감금된날 > 10 &&TALENT:LOCAL:붕괴 == 0
			PRINTFORMW 지나치게 오래 감금되어 있던 %CALLNAME:LOCAL%의 상태가 이상하다...
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"은")% [붕괴]를 취득했다
			CFLAG:LOCAL:감금된날 += 5
			TALENT:LOCAL:붕괴 = 1
		ENDIF
	ELSEIF CFLAG:LOCAL:감금된날
		CFLAG:LOCAL:감금된날--
		IF CFLAG:LOCAL:감금된날 == 0&&TALENT:LOCAL:붕괴
			PRINTFORMW %CALLNAME:LOCAL%의 정서불안이 호전되었다...
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"은")% [붕괴]를 극복했다
			TALENT:LOCAL:붕괴 = 0
		ENDIF
	ENDIF
	IF TALENT:LOCAL:당신의노예
		CALL 노예정책수행(LOCAL)
	ENDIF
	IF TCVAR:LOCAL:히스테리큐
		PRINTL
		PRINTFORMW %CALLNAME:LOCAL%의 상태가 이상하다...
		SELECTCASE RAND:100
			CASE IS < 5
				PRINTFORMW 히스테리큐의 효능으로 %마스터를% 대하는 태도가 조금 호전된 것 같다...
				IF TALENT:LOCAL:연모
					PRINTFORMW 호감도와 신뢰도가 350만큼 상승하였다
					CFLAG:LOCAL:호감도 += 350
					CFLAG:LOCAL:신뢰도 += 350
				ELSEIF TALENT:LOCAL:사모
					PRINTFORMW [연모]를 취득했다
					TALENT:LOCAL:연모 = 1
					TALENT:LOCAL:사모 = 0
					CFLAG:LOCAL:호감도 += 150
					CFLAG:LOCAL:신뢰도 += 150
				ELSEIF TALENT:LOCAL:애욕
					PRINTFORMW [애욕]을 잃고 [연모]를 취득했다
					TALENT:LOCAL:애욕 = 0
					TALENT:LOCAL:연모 = 1
					CFLAG:LOCAL:호감도 += 150
					CFLAG:LOCAL:신뢰도 += 150
				ELSEIF TALENT:LOCAL:섹프
					PRINTFORMW [사모]를 취득했다
					TALENT:LOCAL:사모 = 1
					CFLAG:LOCAL:호감도 += 150
					CFLAG:LOCAL:신뢰도 += 150
				ELSE
					PRINTFORMW [사모]를 취득했다
					TALENT:LOCAL:사모 = 1
					CFLAG:LOCAL:호감도 += 150
					CFLAG:LOCAL:신뢰도 += 150
				ENDIF
			CASE IS < 30
				PRINTFORMW 히스테리큐의 효능으로 %마스터를% 대하는 태도가 조금 호전된 것 같다...
				CFLAG:LOCAL:호감도 += 150
				CFLAG:LOCAL:신뢰도 += 150
				MARK:LOCAL:반발각인 = MAX(0,MARK:LOCAL:반발각인 - 1)
				ABL:LOCAL:친밀 = MIN(7,ABL:LOCAL:친밀 + 1)
				ABL:LOCAL:순종 = MIN(7,ABL:LOCAL:순종 + 1)
			CASE IS < 80
				PRINTFORMW 히스테리큐의 효능으로 %마스터를% 조금 더 부드럽게 대해주기로 한 것 같다...
				CFLAG:LOCAL:호감도 += 100
				CFLAG:LOCAL:신뢰도 += 100
				MARK:LOCAL:반발각인 = MAX(0,MARK:LOCAL:반발각인 - 1)
			CASE IS < 100
				PRINTFORMW 히스테리큐가 몸에 안 맞아 거부반응이 나는 모양이다...
				PRINTFORMW 호감도와 신뢰도가 50 감소하였다
				CFLAG:LOCAL:호감도 -= 50
				CFLAG:LOCAL:신뢰도 -= 50
				CFLAG:LOCAL:타락징조 += 45
		ENDSELECT
		CFLAG:LOCAL:타락징조 += 50
	ENDIF
	IF TCVAR:LOCAL:무조건임신
		PRINTFORMW %CALLNAME:LOCAL%에게 투약된 보테이지는 몸과 마음에 상당한 무리를 주고 있다...
		CFLAG:LOCAL:타락징조 += 20
	ENDIF
	IF CFLAG:LOCAL:타락징조 >= 100&&TALENT:LOCAL:타락 == 0&&!TALENT:LOCAL:창녀&&!TALENT:LOCAL:렌탈창녀
		PRINTL
		PRINTFORMW 최근, %CALLNAME:LOCAL%에게는 이런 저런 약이 투여되었다...
		PRINTFORMW 그 탓에 몸도 마음도 서서히 뒤틀려간 %조사처리(CALLNAME:LOCAL,"은")% 돌연 문란한 성애에 대한 동경심이 들었다
		PRINTFORMW 늦은밤, %조사처리(CALLNAME:LOCAL,"은")% 스스로 매음굴에 들어섰다...
		TALENT:LOCAL:타락 = 1
		CFLAG:LOCAL:타락징조 += 155
		CALL 창녀전락(LOCAL,1)
	ENDIF
	
	IF CFLAG:LOCAL:타락징조
		CFLAG:LOCAL:타락징조 -= 5
	ENDIF
	;노파심에
	TCVAR:LOCAL:미약정액 = 0
NEXT
TCVAR:MASTER:미약정액 = 0
CALL TG_선물본체
LOCAL = RAND:4 + 2
IF FLAG:불소도포
	LOCAL += RAND:4 + 5
	FLAG:불소도포 --
ENDIF
IF 건치의여신
	LOCAL += 건치의여신/2
	건치의여신 --
ENDIF
IF FLAG:충치진행도 > 0
	IF RAND:(FLAG:충치진행도) >= LOCAL&&FLAG:충치 <= 28
		IF FLAG:불소도포 && RAND:2 == 0
			FLAG:충치진행도 = MAX(FLAG:충치진행도 - 2 - RAND:3, 0)
		ELSE
			PRINTFORMW 돌연, 강한 치통을 느끼고 거울로 가 보니 이 하나가 썩어있었다!
			FLAG:충치++
			FLAG:충치진행도 = 0
		ENDIF
	ELSE
		FLAG:충치진행도++
		IF FLAG:불소도포 && RAND:2 == 0
			FLAG:충치진행도 = MAX(FLAG:충치진행도 - 2 - RAND:3 , 0)
		ENDIF
	ENDIF
ENDIF
FLAG:홍마관출입증 = 0
FLAG:지저통행권 = 0

IF TIME + 1440 * DAY >= FLAG:개시시간 + 1440 * FLAG:개시일 && FLAG:연회개최플래그
	CALL ENKAI_END()
ENDIF

IF FLAG:훈련강화
	FLAG:훈련강화 = 0
ENDIF

;오늘 밥 먹었나
IF FLAG:영양소시스템미사용
	;난이도 낮추는 플래그
	IF FLAG:오늘밥 == 0
		PRINTFORML 
		PRINTFORML 오늘은 쫄쫄 굶었다...
		IF MAXBASE:MASTER:기력 > 300
			PRINTFORML %마스터의% 건강이 악화되는 기분이다
			MAXBASE:MASTER:기력 -= 50
			FLAG:굶어서감소기력 += 50
		ELSE
			PRINTFORML %마스터의% 몸은 쇠약해졌다...
			CFLAG:MASTER:철야 = 1
		ENDIF
	ELSE
		FLAG:오늘밥 = 0
		IF FLAG:굶어서감소기력 >= 25
			PRINTFORML 밥을 먹으니 건강이 조금 개선되는 느낌이다!
			FLAG:굶어서감소기력 -= 25
			MAXBASE:MASTER:기력 += 25
		ENDIF
	ENDIF
ELSE
	CALL 영양실조
ENDIF
IF FLAG:만기일 < 0
	FLAG:만기일 = 0
ENDIF
IF FLAG:연체일 < 0
	FLAG:연체일 = 0
ENDIF
IF FLAG:채무 > 0&&FLAG:연체일 > 0
	PRINTFORML 
	PRINTFORMW 대출금을 갚으라고 너구리가 찾아왔다...
	PRINTFORM %마스터의% 채무 : {FLAG:채무}
	PRINTFORML 현재 돈 : {MONEY}
	IF MONEY >= FLAG:채무
		PRINTFORMW 너구리는 %CALLNAME:MASTER%에게 영수증을 뜯어주곤 돈을 받아 돌아갔다
		MONEY -= FLAG:채무
		FLAG:채무 = 0
		FLAG:연체일 = 0
	ELSEIF FLAG:연체일 == 7
		PRINTFORMW 빚을 갚을수 없었다...
		PRINTFORMW 그러자 너구리는 상사를 불러왔다!
		PRINTFORMW 상사는 %마스터의% 채무를 계산하고는 차압딱지를 들었다
		PRINTFORML
		PRINTFORMW %마스터의% 돈을 모조리 압수했다...
		채무계산용변수 = MONEY
		MONEY -= FLAG:채무
		FLAG:채무 -= 채무계산용변수
		IF FLAG:채무 <= 0
			FLAG:채무 = 0
		ENDIF
		PRINTFORMW 
		IF FLAG:채무 > 0
			LOCAL = 0
			FOR LOCAL, 0, CHARANUM
				;노파심에
				SIF FLAG:채무 <= 0
					CONTINUE
				SIF LOCAL <= 0
					CONTINUE
				;아내가 아니면 제외
				SIF TALENT:(LOCAL):연인 != 2
					CONTINUE
				;마미조는 제외
				SIF LOCAL == 69
					CONTINUE
				;TARGET = LOCAL
				IF TALENT:LOCAL:연인 == 2
					PRINTFORMW %CALLNAME:LOCAL%이 창관에 팔려갔다... 
					CALL 창녀전락(LOCAL,1)
					FLAG:채무 -= 10000
					IF FLAG:채무 <= 0
						FLAG:채무 = 0
					ENDIF 
				ENDIF
			NEXT
		ENDIF
		IF FLAG:채무 > 0
			PRINTFORML
			PRINTFORMW 그럼에도 빚을 전부 갚지 못한 %마스터는% 결국 어딘가에 노예시장에 강제로 팔려가게 되었다...
			PRINTFORML
			PRINTFORML
			PRINTFORMW  ―――채무는 {FLAG:채무}원. 상환능력 상실―――
			PRINTW ………………
			PRINTW …………
			PRINTW ……
			IF TALENT:MASTER:성별 == 2
				PRINTFORMW 노예상이 건내준 무지개빛 사탕을 먹고 정신을 잃은지 얼마나 지났을까
				PRINTFORMW %마스터는% 빛 한점 없는 방 안에서 무거운 몸을 일으켰다
				PRINTFORMW 상황 파악이 덜 된 %마스터는% 몸을 일으키려다 문득 다리 사이에 무언가가 사라진 것을 눈치챘다
				PRINTFORMW 놀라서 비명을 지른 %마스터는% 자신의 목소리가 여자의 것으로 변해있었다는걸 알고 패닉에 빠졌다...
				PRINTFORML
				PRINTFORMW 그런 %마스터를% 향해 천천히 다가오는 그 사람-
				PRINTFORMW 아나타의 비열한 웃음이 %마스터의% 눈에 들어왔고, 그 후로 %마스터의% 행방을 아는 사람은 없었다...
				PRINTW 　　　　　　　　―― BAD Ending (노예 엔딩)
			ELSE
				PRINTFORMW 오늘도 수십명의 남성을 상대한 %마스터는% 더이상 숨 쉬기도 곤란할 정도로 지쳐버렸다
				PRINTFORMW %마스터를% 사들인 주인은 %마스터를% 창관에서 일하게 하였다
				PRINTFORMW 능력도, 도망칠 기력도 잃은 %마스터는% 그에게서 벗어날수 없었다
				PRINTFORMW 그로부터 매일매일 수십, 수백의 남성들에게 범해지는 일상이 되었다
				PRINTFORMW 오늘 상대한 사람 수도 양 손으로 꼽을수 없을 수준인데, 내일은 더 많은 사람이 오겠지
				PRINTFORMW %마스터는% 공포심에 바들바들 떨 수 밖에 없었다...
				PRINTW 　　　　　　　　―― BAD Ending (창관 엔딩)
			ENDIF
			QUIT
		ENDIF
	ELSE
		PRINTFORMW 돈이 부족하다...
		PRINTFORMW 너구리는 한숨을 쉬며 {7-FLAG:연체일}일 뒤까지 꼭 갚으라고 경고한뒤 돌아갔다
		FLAG:연체일++
	ENDIF
ENDIF
IF FLAG:채무 > 0&&FLAG:만기일 == 0&&FLAG:연체일 == 0
	PRINTFORML 
	PRINTFORMW 대출금을 갚으라고 너구리가 찾아왔다...
	PRINTFORML %마스터의% 채무 : {FLAG:채무}
	PRINTFORML 현재 돈 : {MONEY}
	IF MONEY >= FLAG:채무
		PRINTFORMW 너구리는 %CALLNAME:MASTER%에게 영수증을 뜯어주곤 돈을 받아 돌아갔다
		MONEY -= FLAG:채무
		FLAG:채무 = 0
	ELSE
		PRINTFORMW 돈이 부족하다...
		PRINTFORMW 너구리는 한숨을 쉬며 다음주까지 갚으라고 경고한뒤 돌아갔다
		FLAG:연체일++
		FLAG:이율 += 5
	ENDIF 
ELSEIF FLAG:채무 > 0&&FLAG:만기일 > 0
	IF (FLAG:채무 * FLAG:이율)/100 == 0&&FLAG:이율 != 0
		;못해도 하루 10원씩 이자가 붙어
		FLAG:채무 += 10
	ELSE
		FLAG:채무 = FLAG:채무 + ((FLAG:채무 * FLAG:이율)/100)
	ENDIF
	FLAG:만기일--
ENDIF

;사당 플래그 OFF
FLAG:17 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL,0,CHARANUM
	;이 3가지는 당신도 취득가능
	IF EXP:LOCAL:키스경험 > 300 && ABL:LOCAL:M감각 > 5 && !TALENT:LOCAL:키스마
		PRINTFORML 수많은 키스를 나누고 마음의 지주를 찾은 %조사처리(CALLNAME:LOCAL,"는")%、키스를 하지 않으면 불안해서 견딜 수 없게 되어 버렸다…
		;陥落素質取得口上,6,キス魔
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 6)
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [키스마]를 얻었다
		TALENT:LOCAL:키스마 = 1
	ENDIF
	IF EXP:LOCAL:조교자위경험 > 200 && EXP:LOCAL:자위경험 > 500 && !TALENT:LOCAL:자위중독
		PRINTFORML 욕망을 내뱉는 법을 알게 된 %조사처리(CALLNAME:LOCAL,"는")%、늘 자위를 생각하게 되었다…
		;陥落素質取得口上,7,自慰狂い
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 7)
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [자위중독]을 얻었다
		TALENT:LOCAL:자위중독 = 1
	ENDIF
	IF EXP:LOCAL:자궁구개발경험 > 50 && !TALENT:LOCAL:포르치오성감
		PRINTFORML 계속해서 자궁구를 공격당한 %조사처리(CALLNAME:LOCAL,"는")%, 고통보다는 쾌락을 느끼게 된 것 같다…
		;陥落素質取得口上,8,ポルチオ性感
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 8)
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [포르치오성감]을 얻었다
		TALENT:LOCAL:포르치오성감 = 1
	ENDIF
;	IF FLAG:おもらし有効 > 0 && EXP:LOCAL:放尿経験 > 1000 * (10 - 2 * TALENT:LOCAL:中毒しやすい) / 10 && !TALENT:LOCAL:おもらし癖
;		PRINTFORML 매 절정 마다 계속 방뇨해 온 %CALLNAME:LOCAL%의 몸은, 이제 방뇨하는 것이 버릇이 되어 버릴 정도로 느슨해져 버렸다…
;		;陥落素質取得口上,9,おもらし癖
;		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 9)
;		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [오줌싸개버릇]을 얻었다
;		TALENT:LOCAL:おもらし癖 = 1
;	ENDIF
	;이하는 아나타는 건너뛰기
	SIF LOCAL == MASTER
		CONTINUE
	;연모 취득
	IF 연모취득조건(LOCAL)
		;멘레이키는 특별 취급
		IF LOCAL == 57
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% %CALLNAME:MASTER%에 대한 마음을 자각함과 동시에, 입꼬리가 느슨해진다
			PRINTFORMW 자연스러운 미소를 짓는 %조사처리(CALLNAME:LOCAL,"는")%, 표정을 얻은 것 같다
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%의 \@ TALENT:LOCAL:사모 ? [사모]는 # [애욕]은 \@ [연모]가 되었다
			FLAG:10 ++
		;애욕의 유무로 분기
		ELSEIF TALENT:LOCAL:애욕
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 몸도 마음도 %마스터로% 채워지는 것에 기쁨을 느끼고 있다…
			PRINTL 문란한 관계의 끝에 맺어진 연심, 그것 또한 하나의 사랑의 형태…
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%의 [애욕]은 [연모]가 되었다
		ELSE
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터와% 보내는 시간에 행복을 느끼는 것 같다…
			;陥落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %CALLNAME:LOCAL%의 [사모]는 [연모]가 되었다
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:시간각인 && !TALENT:LOCAL:애욕 && !CFLAG:LOCAL:수면간 && !CFLAG:LOCAL:만취간
			CFLAG:LOCAL:깨끗한교제 = 1
		TALENT:LOCAL:연모 = 1
		TALENT:LOCAL:사모 = 0
		TALENT:LOCAL:애욕 = 0
		TALENT:LOCAL:섹프 = 0
		
		;악보 입수 처리
		PRINTL
		RESULT = 악보입수_함락(LOCAL)
		SIF RESULT != -1
			PRINTL
	;사모 취득
	;섹프/정부의 취득으로 분기
	ELSEIF 사모취득조건(LOCAL)
		IF TALENT:LOCAL:섹프
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %CALLNAME:MASTER%의 신체 이외에도 흥미가 있는 것 같다
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [사모]를 얻었다
		ELSE
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터가% 신경쓰이는 것 같다…
			;陥落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [사모]를 얻었다
		ENDIF
		TALENT:LOCAL:사모 = 1
	;애욕 취득
	;봉사쾌락의 분기가 아직 없으므로, 취득조건에 병합
	ELSEIF 애욕취득조건(LOCAL)
		;사모하며, 진심으로 신체를 선택한
		IF TALENT:LOCAL:사모
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터와% 마음 이상으로 몸의 연결에 만족하고 있는 것 같다…
			PRINTFORML 눈에 보이지 않는 정보다도 눈에 보이는 증표를 원하며 앞으로도 육체를 겹칠 것이다
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [애욕]을 얻었다
		ELSE
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터와%의 정욕에 빠져있는것 같다…
			;陥落素質取得口上,3,愛欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [애욕]을 얻었다
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:애욕 = 1
			TALENT:LOCAL:사모 = 0
			
			;악보 입수 처리
			PRINTL
			RESULT = 악보입수_함락(LOCAL)
			SIF RESULT != -1
				PRINTL
	;섹프/정부 취득
	;섹프와 정부의 차이는 사모의 유무
	ELSEIF 섹프취득조건(LOCAL)
		;사모 취득 완료＋섹프 미취득
		IF TALENT:LOCAL:사모 && !TALENT:LOCAL:섹프
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터라%면 쾌락에 빠져도 괜찮다고 생각하기 시작한 것 같다…
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% %CALLNAME:MASTER%의 [정부]가 되었다
			TALENT:LOCAL:섹프 = 2
		;사모 취득 완료＋섹프  취득 완료
		ELSEIF TALENT:LOCAL:사모 && TALENT:LOCAL:섹프 == 1
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %CALLNAME:MASTER%에게 단순한 육체 관계 이상의 것을 바라고 있는 것 같다…
			;陥落素質取得口上,4,愛人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [정부]가 되었다
			TALENT:LOCAL:섹프 = 2
		;사모 미취득＋섹프 미취득이라면 우선 섹프 취득
		ELSEIF !TALENT:LOCAL:섹프
			PRINTFORML %조사처리(CALLNAME:LOCAL,"는")% %마스터가% 주는 쾌락에 빠져들고 있다…
			;陥落素質取得口上,5,セフレ
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 5)
			PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% %CALLNAME:MASTER%의 [섹프]가 되어버렸다
			TALENT:LOCAL:섹프 = 1
		ENDIF
	ENDIF
	IF EXP:LOCAL:연주경험 >= 1000 && ABL:LOCAL:기교 > 3 && !TALENT:LOCAL:음악지식
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 악기 연주에 조예가 깊어졌다
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [악기지식]을 얻었다
		;PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 음악에 조예가 깊어졌다
		;PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [음악지식]을 얻었다	
		TALENT:LOCAL:음악지식 = 1
		;技能レベルアップ口上,7-1,音楽知識
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 1)
	ELSEIF EXP:LOCAL:연주경험 >=5000 && !TALENT:LOCAL:음감
		PRINTFORMW %CALLNAME:LOCAL%의 음감은 상당히 단련된 것 같다
		PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% [상대음감]을 얻었다
		TALENT:LOCAL:음감 = 1
		;技能レベルアップ口上,7-2,相対音感
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 2)
	ENDIF
	IF CFLAG:LOCAL:언짢음
		CFLAG:LOCAL:언짢음 --
		IF !CFLAG:LOCAL:언짢음
			SIF MARK:LOCAL:반발각인
				CFLAG:LOCAL:언짢음 ++
		ENDIF
	ENDIF
	;EX캐릭터 연모 시에는 멤버 추가 이벤트 플래그를 세운다
	IF !CFLAG:LOCAL:신사거주
		SIF TALENT:LOCAL:연모 && EXP:LOCAL:애정경험 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:이상청소경험 > (500 + 100 * FLAG:850) && ABL:MASTER:청소기능 > 3 && !TALENT:MASTER:청소중독
	PRINTFORMW %마스터는% 무서운 것을 깨닫고 말았다
	PRINTFORMW 자나 깨나, 시간을 멈춘 사이나, 소녀들과 살을 겹치고 있을 때조차도,
	PRINTFORMW 방이 어질러져 있지는 않은지 신경이 쓰여서 견딜 수 없다
	PRINTFORMW 벌써 %CALLNAME:MASTER%의 마음이 편안해지는 것은 이렇게 청소를 하는 동안 뿐
	PRINTFORMW %CALLNAME:MASTER%의 청소에 대한 집착은, 돌이킬 수 없는 경지에 도달해 있었다
	PRINTFORMW %마스터는% [청소중독]을 얻었다
	TALENT:MASTER:청소중독 = 1
ENDIF
;MASTER을 제외한 전원의 프리셋 처리
FOR LOCAL,1,CHARANUM
	;기력의 회복
	BASE:LOCAL:기력 = MAXBASE:LOCAL:기력

	LOCAL:1 = MAXBASE:LOCAL:체력 / 3

	;회복 빠름, 회복 느림
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:회복속도)
	
	;동침하고 있으면 회복속도 향상
	SIF CFLAG:LOCAL:동침중
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:철야
		LOCAL:1 /= 2
	CFLAG:LOCAL:철야 = 0
	BASE:LOCAL:체력 = MIN(MAXBASE:LOCAL:체력, BASE:LOCAL:체력 + LOCAL:1)

	;나무삼 카드 발행자가 있으면 발행 플래그 OFF
	CFLAG:LOCAL:328 = 0
NEXT
;전원의 프리셋 처리
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:정력 = MIN(MAXBASE:LOCAL:정력, BASE:LOCAL:정력 + MAXBASE:LOCAL:정력 * BASE:LOCAL:체력 / MAXBASE:LOCAL:체력)
	
	;은밀 중인 캐릭터가 있으면 플래그 OFF
	CALL SET_SNEAK(0,LOCAL,1)
	;CFLAG:LOCAL:은밀중 = 0
	
	;장난 중인 캐릭터가 있으면 플래그 OFF
	CALL SET_PRANK(0,LOCAL,1)
	;CFLAG:LOCAL:장난 = 0
	
NEXT

;연회의 개최 판정
SIF FLAG:3 && FLAG:연회개최플래그 == 0
	CALL ENKAI_SETTING

[SKIPSTART]
IF CFLAG:MASTER:육아인원수
	양육비 = CFLAG:MASTER:육아인원수 * 3000 / (1 + FLAG:생활능력무) ;원판은 인당 1000원
	IF MONEY <= 양육비
		PRINTFORML %마스터는% 아이의 양육비로써 \\{양육비} 만큼 지불해야 하지만..
		PRINTFORML 그런 돈은 없기에, 가진 만큼만 냈습니다
		MONEY = 0
	ELSE
		PRINTFORML %마스터는% 아이의 양육비로서 \\{양육비} 지불했다.
		MONEY -= 양육비
	ENDIF
ENDIF
[SKIPEND]

CALL RANK_POINT
PRINTW  

FLAG:0 = 0

;모브코의 세이브
IF TALENT:RANDOM_CHARANUM:스침 == 1 && GETBIT(TFLAG:일일일회, 9) && !TALENT:RANDOM_CHARANUM:창녀
	IF FORGET_MOBGIRL
		FORGET_MOBGIRL = 0
		GOTO FORGET_MOB2
	ENDIF
	IF TALENT:RANDOM_CHARANUM:사망
		PRINTFORML %마스터는% 오늘 죽인 %조사처리(CALLNAME:RANDOM_CHARANUM,"를")%……
	ELSE
		PRINTFORML %마스터는% 오늘 만난 %조사처리(CALLNAME:RANDOM_CHARANUM,"를")%……
	ENDIF
	CALL ASK_YN("잊는다","기억해 둔다")
	IF RESULT
		IF !TALENT:RANDOM_CHARANUM:사망
			VARSET TCVAR:RANDOM_CHARANUM:0, 0
			VARSET EX:RANDOM_CHARANUM:0, 0
			VARSET TEQUIP:RANDOM_CHARANUM:0, 0
			VARSET PALAM:RANDOM_CHARANUM:0, 0
			CALL InitializeBeforeTrain(RANDOM_CHARANUM)
			CFLAG:RANDOM_CHARANUM:900 ++
			CALL SAVE_MOBGIRL
			SIF RESULT == -1
				GOTO FORGET_MOB
		ENDIF
		PRINTFORMW %마스터는% %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% \@ TALENT:RANDOM_CHARANUM:풍속양 ? 다시 즐기고 싶다고 # 다시 만났으면 좋겠다고 \@ 생각했다
		IF TALENT:RANDOM_CHARANUM:사망
			PRINTFORML 만날수 있다면 말이지
		ENDIF
	ELSE
		$FORGET_MOB
		PRINTFORMW %마스터는% %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% 깨끗히 잊었다
	ENDIF
ENDIF
$FORGET_MOB2
IF TALENT:RANDOM_CHARANUM:스침 == 2 && GETBIT(TFLAG:일일일회, 9)
	CALL MOBGIRL_PROFILE2()
	VARSET TCVAR:RANDOM_CHARANUM:0, 0
	VARSET EX:RANDOM_CHARANUM:0, 0
	VARSET TEQUIP:RANDOM_CHARANUM:0, 0
	VARSET PALAM:RANDOM_CHARANUM:0, 0
	CALL InitializeBeforeTrain(RANDOM_CHARANUM)
	SAVECHARA TOSTR(CFLAG:RANDOM_CHARANUM:모브코세이브슬롯), CSTR:RANDOM_CHARANUM:모브코프로필1, RANDOM_CHARANUM
	[IF DEBUG]
		PRINTFORMW %CALLNAME:RANDOM_CHARANUM%의 정보를 갱신했습니다
	[ENDIF]
ENDIF
IF FLAG:우유통 >= 20
	PRINTFORML 착유기 속 가득 찬 모유 탱크로부터 모유를 꺼냈다
	LOCAL = FLAG:우유통/20
	ITEM:우유 += LOCAL
	FLAG:우유통 -= LOCAL * 20
ENDIF
IF FLAG:식재료구독 && GET_DAY() == "월"
	CALL 야채배달()
ENDIF

BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;청결도
LOCAL:2 = EX:MASTER:오늘의청소회수 / 50
PRINTFORML 오늘 청소한 쓰레기의 양 {EX:MASTER:오늘의청소회수}
PRINTFORML 청소로 얻은 보너스 \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【대청소 보너스】
	LOCAL:2 *= 2
ENDIF
;거점의 주인의 호감도와 신뢰도가 상승
LOCAL:3 = MIN(10, EX:MASTER:오늘의청소회수 / 2000)
IF LOCAL:3
	PRINTFORML 청소를 열심히 한 것으로,  %CALLNAME:거점_주인%의 평가가 올라갔다
	CFLAG:거점_주인:호감도 += LOCAL:3
	CFLAG:거점_주인:신뢰도 += LOCAL:3/2
	IF LOCAL:3 / 3 > 0
		PRINTFORML 청소를 열심히 하니、추가로 쌀을 {LOCAL:3 / 3}개 받았다
		ITEM:쌀 += LOCAL:3 / 3
	ENDIF
ENDIF
PRINTL  
;심부름 보수
LOCAL:4 = TCVAR:MASTER:도운양/5

SIF LOCAL:4 > 0
	PRINTFORML 업무 도우미로 얻은 보너스 \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;호감도 상승
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 호감도 상승 보너스 ＋{LOCAL:5} 카리스마
	MONEY:2 += LOCAL:5
	PRINTFORML 현재 카리스마：%세자리구분(MONEY:2)%　카리스마 보유 상한：%세자리구분(CM_LIMIT)%
ENDIF
FLAG:12 = 0

PRINTW  

@연모취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연모
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_연모
	RETURNF 0
SIF CFLAG:ARG:신뢰도 < REQUIRED_TRUST_연모
	RETURNF 0
SIF ABL:ARG:친밀 < REQUIRED_AFFECTION_연모
	RETURNF 0
SIF MARK:ARG:반발각인
	RETURNF 0
SIF TALENT:ARG:애욕 && !FLAG:함락노선변화
	RETURNF 0
RETURNF 1

@사모취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:애욕
	RETURNF 0
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:사모
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_사모
	RETURNF 0
SIF CFLAG:ARG:신뢰도 < REQUIRED_TRUST_사모
	RETURNF 0
SIF ABL:ARG:친밀 < REQUIRED_AFFECTION_사모
	RETURNF 0
SIF TALENT:ARG:섹프 && !FLAG:함락노선변화
	RETURNF 0
RETURNF 1

@애욕취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:애욕
	RETURNF 0
SIF !TALENT:ARG:섹프
	RETURNF 0
SIF CFLAG:ARG:호감도 < REQUIRED_FAVOR_애욕
	RETURNF 0
SIF ABL:ARG:욕망 < REQUIRED_DESIRE_애욕
	RETURNF 0
SIF (EXP:ARG:절정경험 - EXP:ARG:무자각절정경험) < REQUIRED_ECSTACY_애욕
	RETURNF 0
SIF (ABL:ARG:욕망 - ABL:ARG:친밀) < 1
	RETURNF 0
RETURNF 1

@섹프취득조건(ARG)
#FUNCTION
SIF TALENT:ARG:연인
	RETURNF 0
SIF TALENT:ARG:연모
	RETURNF 0
SIF TALENT:ARG:섹프 == 2
	RETURNF 0
SIF CFLAG:ARG:호감도 <= REQUIRED_FAVOR_섹프
	RETURNF 0
SIF ABL:ARG:욕망 < REQUIRED_DESIRE_섹프
	RETURNF 0
SIF (EXP:ARG:절정경험 - EXP:ARG:무자각절정경험) < REQUIRED_ECSTACY_섹프
	RETURNF 0
SIF (ABL:ARG:욕망 - ABL:ARG:친밀) < 0
	RETURNF 0
RETURNF 1
