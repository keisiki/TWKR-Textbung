;FileName_EVENTCOMEND.ERB -------------------------- Rev1. 00
;턴 종료의 처리
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@EVENTCOMEND
#DIM 장비부위
#DIMS 아이의이름
;일동작의 에러 체크
;FOR LOCAL, 0, CHARANUM
;	SIF CFLAG:LOCAL:326 == 0 && CFLAG:LOCAL:행동 == 5
;		PRINTFORMW ### COM_END 326ERROR %CALLNAME:LOCAL% ### CHARA_NO:{LOCAL}
;	SIF CFLAG:LOCAL:현재위치 == 0
;		PRINTFORMW ### COM_END LOCATION ERROR %CALLNAME:LOCAL% 의 현재위치가 이상합니다 ### CHARA_NO:{LOCAL}
;NEXT
;턴 종료 공통 처리
CALL TURN_RESET

SIF FLAG:연회개최플래그 > 0
	CALL ENKAI_SINKOU

SIF TFLAG:102 < 2 && TARGET && SHIRAHU(TARGET) && !FLAG:70
	CALL 봇슈트
IF BASE:MASTER:술기운 >= MAXBASE:MASTER:술기운
	;AT_HOME 보는 것만으로도 좋을지도 모른다
	IF !AT_HOME(MASTER) && GET_MAPID(CFLAG:MASTER:현재위치) != MAIN_MAP
		CALL 데이트종료타임업처리
	ELSE
		PRINTFORMW 한계까지 계속 마신 %마스터는%, 그대로 딱과 넘어져 버렸다…
	ENDIF
	GOTO OYASUMI
ENDIF
IF (BASE:MASTER:체력 <= 500 && !TCVAR:TARGET:마음대로해) || BASE:MASTER:체력 <= 0
	PRINT (체력이 한계에 와 있습니다)
	;밀어넘어뜨려지고 안이라면 분기
	IF TFLAG:102 == 3 && BASE:MASTER:체력 > 0 && !TCVAR:TARGET:마음대로해
		PRINTL
		PRINTL [0]마음대로해 주는 [1]해방받는다
		INPUT
		IF !RESULT
			TCVAR:TARGET:마음대로해 = 1
			BASE:MASTER:체력 -= 250
			GOTO SUKINISITE
		ENDIF
	ENDIF
	PRINTW 　하루를 종료합니다
	IF BASE:MASTER:체력 <= 0 && !CHK_DATENOW(CFLAG:MASTER:데이트중)
		PRINTFORMW %마스터는% 체력을 다 써 버려 실신해 버렸다…
		FLAG:기절중단 = 100 + CFLAG:MASTER:현재위치 / 100
	ENDIF
	;밀어넘어뜨려지고 안이라면 TARGET에 기학 경험
	IF TFLAG:102 == 3
		IF BASE:MASTER:체력 <= 0
			EXP:가학쾌락경험 += 10
			PRINTL 
			PRINTFORMW 가학쾌락경험 +10(%CALLNAME%)
			PRINTL 
		ELSE
			EXP:가학쾌락경험 += 2
			PRINTL 
			PRINTFORMW 가학쾌락경험 +2(%CALLNAME%)
			PRINTL 
		ENDIF
	ENDIF
	$OYASUMI
	CALL SET_SLEEP(1, MASTER, 0)
	CFLAG:MASTER:취침시간 = DAY * 1440 + TIME
	;외박 할 수 있는 상태라면 그대로 잔다(연모 전제)
	IF CFLAG:MASTER:현재위치 == OMANEKIBEYA() && !TCVAR:(CFLAG:MASTER:초대):연속숙박금지
		IF CFLAG:(CFLAG:MASTER:초대):데이트중
			PRINTFORMW 오늘은 %CALLNAME:(CFLAG:MASTER:초대)%가 묵게 해 주는 것 같다
			FLAG:기절중단 = 0
		ENDIF
	ELSEIF TARGET > 0 && TALENT:TARGET:연모 && BASE:TARGET:체력 >= 500 && !CFLAG:TARGET:수면 && !CFLAG:MASTER:동침중 && ((CFLAG:TARGET:동행 && CHK_DATENOW(CFLAG:MASTER:데이트중)) || !CHK_DATENOW(CFLAG:MASTER:데이트중))
		IF CHK_DATENOW(CFLAG:MASTER:데이트중)
			PRINTFORMW %CALLNAME:TARGET%에 의지하고 %마스터는% %GET_MAPNAME(MAIN_MAP)%까지 돌아왔다
		ENDIF
		IF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
			PRINTFORMW %타겟이% %마스터를%사실로 옮겨 주었다…
			CFLAG:MASTER:현재위치 = CFLAG:MASTER:311
			CALL KOJO_MESSAGE_SEND("EVENT", 3, TARGET, 6,0)
		ELSE
			PRINTFORMW %타겟이% %마스터를%\@ INROOM(CFLAG:MASTER:현재위치) == 2 ?  침대 # 이불 \@로 옮겨 주었다…
		ENDIF
		CFLAG:MASTER:데이트중 = MAIN_MAP
		FLAG:기절중단 = 0
	ELSEIF CFLAG:MASTER:현재위치 == 난쟁이벌레통
		CFLAG:MASTER:현재위치 = 툇마루
		PRINTFORMW 어느 새인가 %마스터는% 툇마루에 널려 있었다…
	ELSEIF CHK_DATENOW(CFLAG:MASTER:데이트중)
		PRINTFORMW %마스터는% 마지막 힘을 떨쳐 짜, %GET_MAPNAME(MAIN_MAP)%까지 돌아왔다
		FLAG:기절중단 = 100 + CFLAG:MASTER:현재위치 / 100
		CFLAG:MASTER:현재위치 = ENTRANCE
		PRINTFORMW 
	ELSE
		PRINTFORMW %마스터는% 휘청거리는 발걸음으로,사실로 돌아왔다
		CFLAG:MASTER:현재위치 = CFLAG:MASTER:311
		FLAG:기절중단 = 0
	ENDIF
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:출금||TALENT:LOCAL:사망
			CONTINUE
		CALL ENDUFUFU(LOCAL)
		CALL SET_PRANK(0, LOCAL)
		CFLAG:LOCAL:데이트중 = MAIN_MAP
		CFLAG:LOCAL:동행 = 0
		FLAG:데이트상대 = 0
		TFLAG:데이트전호감도 = 0
		SIF !BATHCHECK(CFLAG:LOCAL:현재위치) && TEQUIP:LOCAL:목욕탕플레이
			TEQUIP:LOCAL:목욕탕플레이 = 0
	NEXT
;	수치 변수가 소수점 이하를 잘라 버리는 특성을 악용 한 것이 이쪽, 다른 변수를 사용하지 않는 분 이쪽 쪽이 좋은 것인지도?
	CFLAG:MASTER:데이트중 = CFLAG:MASTER:현재위치 / 100
	TFLAG:106 = 0
	BEGIN AFTERTRAIN
ENDIF
IF TARGET && TFLAG:키스마크 == TARGET && TFLAG:붕대 && !RAND:2
	PRINTFORMW %CALLNAME:MASTER%의 목덜미를 가리는 것을 알아차린%타겟은%, 붕대를 인가 했다
	TFLAG:붕대 = 0
	BASE:분노 += 500
ENDIF
$SUKINISITE
IF TARGET && CFLAG:언짢음 && !FLAG:70
	IF TALENT:TARGET:담력 < 0
		PRINTFORMW %타겟을% 울려 버렸다…
		CALL KOJO_MESSAGE_SEND("EVENT", 29, TARGET, 2,0)
	ELSE
		PRINTFORMW %타겟을% 화나게 해 버렸다…
		CALL KOJO_MESSAGE_SEND("EVENT", 29, TARGET, 1,0)
	ENDIF
	
	IF !TCVAR:억지로 && FLAG:모리야복권데이트상대 != TARGET && !FLAG:개인실입장
		IF CFLAG:감금됨&&CFLAG:현재위치 == 1445
		ELSE
		FOR LOCAL, 0, CHARANUM
			CALL ENDUFUFU(LOCAL)
		NEXT
		TFLAG:106 = 0
		ENDIF
	ENDIF
ENDIF

IF FLAG:70 == 1
	IF BASE:MASTER:TSP <= 0
		PRINTFORML (TSP가 없어졌습니다)
		[SKIPSTART]
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:현재위치 == CFLAG:PLAYER:현재위치
				FOR 장비부위, 0,23
					SIF (!수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(200 + 장비부위)) || (수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(240 + 장비부위))
						LOCAL:1 ++
					SIF 장비부위 == 6 && CFLAG:LOCAL:노팬티 && ((!수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(200 + 장비부위)) || (수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(240 + 장비부위)))
						LOCAL:1 --
				NEXT
				IF LOCAL:1
					EQUIP:LOCAL:0 = 1
					IF CFLAG:LOCAL:시간정지착의 == 1
						PRINTFORMW %마스터는% %CALLNAME:LOCAL%를\@ CFLAG:LOCAL:노팬티 ?  팬티 이외를 #  \@원래 모습에 되돌렸다!
						CFLAG:LOCAL:시간정지착의 = 0
					ELSE
						PRINTFORMW %마스터는% %CALLNAME:LOCAL%에\@ CFLAG:LOCAL:노팬티 ?  팬티 이외의#  \@옷을 입혔다
					ENDIF
				ENDIF
			ENDIF
		NEXT
		[SKIPEND]
		IF CFLAG:MASTER:현재위치 != OMANEKIBEYA()
			PRINTFORMW %마스터는% 당황해 그 자리를 떨어졌다
			TFLAG:운반 = 0
			CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
			FOR LOCAL:10, 0, CHARANUM - 1
				LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
				SIF CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
					CONTINUE
				IF EX:LOCAL:C절정 + EX:LOCAL:V절정 + EX:LOCAL:A절정 + EX:LOCAL:B절정 + EX:LOCAL:M절정 > 30
					PRINTFORMW 돌연 격렬한 절정에 습격당한 %조사처리(CALLNAME:LOCAL,"는")%, 단 교성을 주어 그 자리에 거치거나 붐비어 버렸다
					CFLAG:LOCAL:333 += 20
				ELSEIF EX:LOCAL:C절정 + EX:LOCAL:V절정 + EX:LOCAL:A절정 + EX:LOCAL:B절정 + EX:LOCAL:M절정 > 0
					PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")%, 수수께끼의 쾌감에 깜짝과 몸을 경직시켰다
				ELSEIF CFLAG:LOCAL:291 == 1
					PRINTFORMW 알지 않는 사이에 팬티를 탈의한 %조사처리(CALLNAME:LOCAL,"는")% 침착하고 없을 것 같다
				ENDIF
				SIF EX:LOCAL:처녀상실체크 == 1
					PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 고간의 둔통에 얼굴을 찡그리고 있는…
			NEXT
			IF !AT_HOME(MASTER)
				PRINTFORML %GET_MAPNAME(MAIN_MAP)%까지 전력 질주 해 버렸다…
				BASE:MASTER:체력 -= TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:데이트중) * 5
				BASE:MASTER:기력 -= TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:데이트중) * 5
				BASE:MASTER:체력 = MAX(BASE:MASTER:체력, 0)
				BASE:MASTER:기력 = MAX(BASE:MASTER:기력, 0)
				TIME += TIME_REQUIRED(MAIN_MAP, CFLAG:MASTER:데이트중) / 5
			ENDIF
			IF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
				CFLAG:MASTER:현재위치 = CFLAG:MASTER:초기위치
			ELSE
				CFLAG:MASTER:현재위치 = 피난처
			ENDIF
			BASE:MASTER:TSP = 0
			CALL AFTER_AFFAIR
			FOR LOCAL, 0, CHARANUM
				IF CFLAG:LOCAL:우후후 == 1 && LOCAL != 0
					;우후후 종료 말호출
					CALL KOJO_MESSAGE_SEND("EVENT", 15, LOCAL, 2,0)
				ENDIF
				CALL ENDUFUFU(LOCAL)
				CFLAG:LOCAL:데이트중 = MAIN_MAP
			NEXT
			FLAG:데이트상대 = 0
			TFLAG:데이트전호감도 = 0
			TFLAG:106 = 0
		ELSE
			CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
			FOR LOCAL:10, 0, CHARANUM - 1
				LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
				IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
					IF EX:LOCAL:C절정 + EX:LOCAL:V절정 + EX:LOCAL:A절정 + EX:LOCAL:B절정 + EX:LOCAL:M절정 > 30
						PRINTFORMW 돌연 격렬한 절정에 습격당한 %조사처리(CALLNAME:LOCAL,"는")%, 단 교성을 주어 그 자리에 거치거나 붐비어 버렸다
						CFLAG:LOCAL:333 += 20
					ELSEIF EX:LOCAL:C절정 + EX:LOCAL:V절정 + EX:LOCAL:A절정 + EX:LOCAL:B절정 + EX:LOCAL:M절정 > 0
						PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")%, 수수께끼의 쾌감에 깜짝과 몸을 경직시켰다
					ELSEIF CFLAG:LOCAL:291 == 1
						PRINTFORMW 알지 않는 사이에 팬티를 탈의한 %조사처리(CALLNAME:LOCAL,"는")% 침착하고 없을 것 같다
					ENDIF
					SIF EX:LOCAL:처녀상실체크 == 1
						PRINTFORMW %조사처리(CALLNAME:LOCAL,"는")% 고간의 둔통에 얼굴을 찡그리고 있는…
				ENDIF
				CALL ENDUFUFU(LOCAL)
			NEXT
			CFLAG:MASTER:우후후 = 0
		ENDIF
		FLAG:70 = 0
		RESETBGCOLOR
		BASE:MASTER:TSP = 0
	ENDIF
	SIF CFLAG:MASTER:우후후 && BASE:MASTER:발기 <= 900 && BASE:MASTER:정력 >= 500
		BASE:MASTER:발기 += 10
ENDIF
IF TARGET && BASE:체력 < 500
	IF FLAG:70 == 1 && CFLAG:MASTER:현재위치 != OMANEKIBEYA()
		IF !CFLAG:쇠약
			PRINTFORML (%CALLNAME:TARGET%의 체력이 한계에 와 있습니다)
			PRINTFORMW %마스터는% 그 자리를 떨어져, 시간정지를 해제했다
			CFLAG:쇠약 = 360
			FLAG:70 = 0
			FLAG:데이트상대 = 0
			TFLAG:데이트전호감도 = 0
			RESETBGCOLOR
			IF AT_HOME(MASTER)
				CFLAG:MASTER:데이트중 = MAIN_MAP
				IF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
					CFLAG:MASTER:현재위치 = CFLAG:MASTER:초기위치
				ELSE
					CFLAG:MASTER:현재위치 = 피난처
				ENDIF
			ELSE
				IF STR:(6000 + CFLAG:MASTER:현재위치 + 10) == ""
					CFLAG:MASTER:현재위치 -= 10
				ELSE
					CFLAG:MASTER:현재위치 += 10
				ENDIF
			ENDIF
		ENDIF
		CALL CHARA_SLEEP, TARGET, 0
		FOR LOCAL, 0, CHARANUM
			CALL ENDUFUFU(LOCAL)
			CALL SET_PRANK(0, LOCAL, 1)
			SIF LOCAL
				CFLAG:LOCAL:데이트중 = MAIN_MAP
			CFLAG:LOCAL:동행 = 0
		NEXT
		FLAG:데이트상대 = 0
		TFLAG:데이트전호감도 = 0
		TFLAG:106 = 0
	ELSE
		IF !CFLAG:쇠약
			PRINTFORMW (%CALLNAME:TARGET%의 체력이 한계에 와 있습니다)
			CFLAG:쇠약 = 360
			FLAG:70 = 0
			RESETBGCOLOR
		ENDIF
		CALL CHARA_SLEEP, TARGET, 0
		FOR LOCAL, 0, CHARANUM
			CALL ENDUFUFU(LOCAL)
			CALL SET_PRANK(0, LOCAL, 1)
			SIF LOCAL
				CFLAG:LOCAL:데이트중 = MAIN_MAP
			CFLAG:LOCAL:동행 = 0
		NEXT
		FLAG:데이트상대 = 0
		TFLAG:데이트전호감도 = 0
		TFLAG:106 = 0
	ENDIF
ENDIF

IF FLAG:자식묘사
	FOR LOCAL, 1, CHARANUM
		SIF !TALENT:LOCAL:육아중
			CONTINUE
		SIF CFLAG:LOCAL:현재위치 != CFLAG:MASTER:현재위치
			CONTINUE
		아이의이름 = %CHILDNAME:LOCAL:(TALENT:LOCAL:육아중)%
		;욕구 불만도 억제와 유아기의 공복 판정과 기상 판정
		IF TCVAR:LOCAL:아이공복도 != -1
			SIF TCVAR:LOCAL:아이공복카운트
				TCVAR:LOCAL:아이공복도 += (TIME:0 - TCVAR:LOCAL:아이공복카운트)
			TCVAR:LOCAL:아이공복카운트 = TIME:0
			SIF CFLAG:LOCAL:욕구불만도 > 300
			CFLAG:LOCAL:욕구불만도 = 300
		ELSEIF TCVAR:LOCAL:아이공복도 == -1 && TIME:0 - TCVAR:LOCAL:아이공복카운트 > 120 && !RAND:2 && !CFLAG:MASTER:우후후 && CFLAG:LOCAL:출산경과일 < CHILD_어휘
			PRINTFORMDL %아이의이름%는 눈을 떴다
			TCVAR:LOCAL:아이공복도 = 120
			TCVAR:LOCAL:아이공복카운트 = TIME:0
			SIF CFLAG:LOCAL:욕구불만도 > 300
			CFLAG:LOCAL:욕구불만도 = 300
		ENDIF
		;통학
		IF GO_SCHOOL(LOCAL)
			IF !CHK_DATENOW(CFLAG:LOCAL:데이트중) && INRANGE(TIME, 420, 540) && TCVAR:LOCAL:아이공복도 != -1 && !CFLAG:MASTER:우후후
				PRINTFORMDL %아이의이름%는 서당으로 향했습니다
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_등하교")
				TCVAR:LOCAL:아이공복도 = -1
			;우후후중 돌아오지 않는 공기가 읽을 수 있는 아이
			ELSEIF !CHK_DATENOW(CFLAG:LOCAL:데이트중) && INRANGE(TIME, 840, 1020) && TCVAR:LOCAL:아이공복도 == -1 && !CFLAG:MASTER:우후후
				PRINTFORMDL %아이의이름%가 서당으로부터 돌아왔습니다
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 1, 0, "CHILD_RAISING_등하교")
				TCVAR:LOCAL:아이공복도 = 0
			ELSEIF INRANGE(TIME, 540, 840)
				TCVAR:LOCAL:아이공복도 = -1
			ELSEIF INRANGE(TIME, 1020, 1260)
				TCVAR:LOCAL:아이공복도 = 0
			ENDIF
		ENDIF
		;이 이후는 통학/수면중에는 발생하지 않는다
		SIF TCVAR:LOCAL:아이공복도 == -1
			CONTINUE
		;우후후가 중단하는 것 같은 이벤트도 만들어야할 것인가?
		SIF CFLAG:MASTER:우후후
			CONTINUE
		;신부가 자기도 하고 있으면(자) 아이도 무반응은의도
		SIF !SHIRAHU(LOCAL)
			CONTINUE
		;유아기
		IF CFLAG:LOCAL:출산경과일 < CHILD_어휘
			IF BATHROOM(CFLAG:현재위치) && !RAND:2
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_BATH", "유아기")
			ELSEIF TIME:0 <= CFLAG:LOCAL:취침시간 && TCVAR:LOCAL:아이공복도 >= 120 && CFLAG:LOCAL:출산경과일 < CHILD_젖떼기
					TCVAR:LOCAL:아이공복도 = 0
					EXP:LOCAL:분유경험 ++
					PRINTFORMDL %조사처리(CALLNAME:LOCAL,"는")%%아이의이름%에 수유를 하고 있다
					CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_식사", "수유")
			ELSEIF TIME:0 <= CFLAG:LOCAL:취침시간 && TCVAR:LOCAL:아이공복도 >= 120 && GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617)
					TCVAR:LOCAL:아이공복도 = 0
					PRINTFORMDL %조사처리(CALLNAME:LOCAL,"는")%%아이의이름%에 이유식을 주고 있다
					CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_식사", "이유식")
			ELSEIF TCVAR:LOCAL:아이공복도 < 120 && !RAND:6
				PRINTFORMDL %아이의이름%는 새근새근 자고 있다……
				TCVAR:LOCAL:아이공복도 = -1
					CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_SLEEPING", "유아기")
			ELSEIF CFLAG:LOCAL:출산경과일 >= CHILD_완구 && !RAND:5
				PRINTFORMDL %아이의이름%는 %마스터가% 만든 완구로 놀고 있다
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_TOY", "유아기")
			ELSEIF !RAND:3
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_OTHER", "유아기")
			ENDIF
		;유소년기
		ELSEIF INRANGE(CFLAG:LOCAL:출산경과일, CHILD_어휘, CHILD_자립전 -1)
			IF 아이의수면시간(LOCAL)
				PRINTFORMDW %아이의이름%는 침실로 향하고 있었던……
				TCVAR:LOCAL:아이공복도 = -1
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_SLEEPING", "유소년기")
			ELSEIF BATHROOM(CFLAG:현재위치) && !RAND:2
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_BATH", "유소년기")
			ELSEIF GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617)
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_식사", "유소년기")
			ELSEIF !RAND:6
				PRINTFORMDL %아이의이름%는 %마스터가% 만든 완구로 놀고 있다
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_TOY", "유소년기")
			ELSEIF !RAND:3
				TCVAR:LOCAL:아이공복도 = 0
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_OTHER", "유소년기")
			ENDIF
		;자립전
		ELSE
			;아이의 자립일이 아니면 잔다
			IF 아이의수면시간(LOCAL) && CFLAG:LOCAL:출산경과일 != CHILD_자립
				PRINTFORMDW %아이의이름%는 침실로 향하고 있었던……
				TCVAR:LOCAL:아이공복도 = -1
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_SLEEPING", "자립전")
			ELSEIF BATHROOM(CFLAG:현재위치) && !RAND:2
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_BATH", "자립전")
			ELSEIF GROUPMATCH(SELECTCOM, 414, 415, 610, 615, 617)
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_식사", "자립전")
			ELSEIF CFLAG:LOCAL:현재위치 == CFLAG:LOCAL:초기위치 && !RAND:8
				PRINTFORMDL %아이의이름%는 %CALLNAME:MASTER%에 만들어 준 나무의  완구를 추억 깊은 듯이 바라보고 있다……
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_TOY", "자립전")
			ELSEIF !RAND:5
				TCVAR:LOCAL:아이공복도 = 0
				CALL KOJO_MESSAGE_SEND("EVENT", 0, LOCAL, 0, 0, "CHILD_RAISING_OTHER", "자립전")
			ENDIF
		ENDIF
	NEXT
	RESETCOLOR
ENDIF

IF GET_EXISTMOB(MAIN_MAP)
	FOR LOCAL, MINROOM(), MAXROOM
		IF ROOMDATA:(LOCAL % 100) & 장소_모브
			YOGORE:LOCAL += TIME_PROGRESS(TIME:1) * MOB_YOGORE(MAIN_MAP)
			SIF CFLAG:MASTER:현재위치 == LOCAL && AT_HOME(MASTER)
				TRYCALLFORM MOB_TURNENDEVENT_{MAIN_MAP}
		ENDIF
	NEXT
ENDIF
IF !FLAG:70 && TARGET
	IF TALENT:TARGET:동물귀 && CFLAG:TARGET:900 == 7 && !TCVAR:TARGET:발정 && PALAM:TARGET:욕정 >= PALAMLV:5 && !TALENT:TARGET:임신
		PRINTFORML %타겟은% 뺨을 상기시켜, 숨을 할딱이며 있다…
		PRINTFORML 위험일에 극도로 흥분한 %타겟은% [발정]해 버린 것 같다
		TCVAR:TARGET:발정 = 1
	ENDIF
ENDIF

FOR LOCAL, 0, CHARANUM
	;IF !CFLAG:LOCAL:우후후
		FOR LOCAL:2, 101,109
			SIF TEQUIP:LOCAL:(LOCAL:2) != TEQUIP:LOCAL:(LOCAL:2 + 10)
				TEQUIP:LOCAL:(LOCAL:2) = 0
		NEXT
	;ENDIF
	SIF CFLAG:LOCAL:현재위치 == SUKIMA()
		CONTINUE
	;의상의 갈아입음 수선은@SHOW_STATUS에 이동
	[SKIPSTART]
	LOCAL:1 = 0
	FOR 장비부위, 0, 23
		SIF (!수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(200 + 장비부위)) || (수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(240 + 장비부위))
			LOCAL:1 ++
		SIF 장비부위 == 6 && CFLAG:LOCAL:노팬티 && ((!수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(200 + 장비부위)) || (수면시간(LOCAL) && EQUIP:LOCAL:장비부위 != CFLAG:LOCAL:(240 + 장비부위)))
			LOCAL:1 --
	NEXT
	IF !CFLAG:LOCAL:우후후 && CFLAG:LOCAL:현재위치 != BATHROOM(CFLAG:LOCAL:현재위치) && LOCAL:1
		IF 수면시간(LOCAL)
			;파자마 세트
			CALL CTRL_CLOTHES_SET(LOCAL, "현재 의상의 변경_파자마")
			CFLAG:LOCAL:파자마 = 1
		ELSE
			;평상복 세트
			CALL CTRL_CLOTHES_SET(LOCAL, "현재 의상의 변경_평상복")
			CFLAG:LOCAL:파자마 = 0
		ENDIF
		SIF !수면시간(LOCAL) && CFLAG:LOCAL:면간발각
			CFLAG:LOCAL:면간발각 = 0
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
	ENDIF
	[SKIPEND]
	;일단 CLOTHES_SETTING_TRAIN는 해 손해는 없을 것이다
		SIF !수면시간(LOCAL) && CFLAG:LOCAL:면간발각
			CFLAG:LOCAL:면간발각 = 0
		CALL CLOTHES_SETTING_TRAIN(LOCAL)
NEXT
;강제적으로 WAIT가 추가되는 것을 회피하기 위해서 TWAIT를 가르친다
;이것으로 입력 회수가 1회줄어든다
IF CFLAG:MASTER:현재위치 == 99
	SETCOLOR C_RED
	PRINTFORMW ### COM_END LOCATION ERROR!%마스터는% 존재하지 않아야 할 장소에 있다!###
	PRINTW 강제적으로 하루를 종료합니다. 로그를 취해 스레에 버그 보고를 부탁합니다.
	RESETCOLOR
	CFLAG:MASTER:현재위치 = CFLAG:MASTER:311
	CALL SET_SLEEP(1, MASTER, 0)
	TFLAG:106 = 0
	BEGIN AFTERTRAIN
ENDIF
	;CFLAG:MASTER:취침시간 = DAY * 1440 + TIME
SIF SELECTCOM == 400
	TWAIT 100,0

@ENKAI_SINKOU
;연회의 참가인수를 세트
FLAG:35 = 0
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:20 == 1 || CFLAG:LOCAL:20 == 2
		SIF CFLAG:LOCAL:현재위치 == TFLAG:연회장
			FLAG:35 ++
	ENDIF
NEXT
;날씨가 나쁘면 중지가 되는 연회도 있다
IF FLAG:연회개최플래그 > 0 && TIME:5 >= 4 && FLAG:36 > 0 && DAY >= FLAG:34 && TIME >= FLAG:33
	IF CFLAG:MASTER:현재위치 == TFLAG:연회장
		PRINTL  
		PRINTW 유감스럽지만, 날씨가 나쁘기 때문에 연회는 중지가 되었습니다
	ELSE
		PRINTL  
		PRINTW 유감스럽지만, 악천후 때문에 연회는 중지가 된 것 같습니다
	ENDIF
	CALL ENKAI_STOP
;연회의 도중에 내리기 시작했을 경우
ELSEIF FLAG:연회개최플래그 >= 98 && TIME:5 >= 4 && FLAG:36 > 0
	IF CFLAG:MASTER:현재위치 == TFLAG:연회장
		PRINTL  
		PRINTW 유감스럽지만, 날씨가 나쁘기 때문에 연회는 중지가 되었습니다
	ELSE
		PRINTL  
		PRINTW 유감스럽지만, 악천후 때문에 연회는 중지가 된 것 같습니다
	ENDIF
	CALL ENKAI_STOP
;연회 개시
ELSEIF FLAG:연회개최플래그 > 0 && FLAG:연회개최플래그 != 98 && FLAG:연회개최플래그 != 99 && DAY >= FLAG:34 && TIME >= FLAG:33
	IF CFLAG:MASTER:현재위치 == TFLAG:연회장
		IF FLAG:35 < 2
			PRINTL  
			PRINTW 조용한 분위기안, 연회가 시작되었습니다…
			PRINTL  
		ELSEIF FLAG:35 >= 8
			PRINTL  
			PRINTW 떠들썩한 분위기안, 연회가 시작되었습니다…
			PRINTL  
		ELSE
			PRINTL  
			PRINTW 부드러운 분위기안, 연회가 시작되었습니다…
			PRINTL  
		ENDIF
		FLAG:연회개최플래그 = 99
	ELSE
		FLAG:연회개최플래그 = 98
	ENDIF
ELSEIF FLAG:연회개최플래그 > 0 && FLAG:연회개최플래그 != 99 && DAY >= FLAG:34 && TIME >= FLAG:33
	IF CFLAG:MASTER:현재위치 == TFLAG:연회장
		PRINTL  
		PRINTW 이미 연회는 시작되어 있는 것 같습니다…
		PRINTL  
		FLAG:연회개최플래그 = 99
	ENDIF
	;주최자측은 아직 연회준비 플래그 인 채의 모아 두어 플래그를 억지로 진행한다
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:20 == 2
			CALL ENKAI_WORK(LOCAL)
	NEXT
ENDIF
