@CHARA_DIARY_COM(C_ID)
#DIM DCOUNT
#DIM PAGENUM
#DIM C_ID
#DIM CLOSE
RESULT = 0
PAGENUM = 0
CLOSE = 0
;コマンドで呼び出されたとき
CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, 0, 0, "EXIST")
IF !RESULT
	PRINTFORMW 아직 아무것도 적혀있지 않은 것 같다…
	RETURN
ENDIF

FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF GROUPMATCH(DIARY:C_ID:DCOUNT, 1, 3)
		PAGENUM ++
NEXT
IF PAGENUM == 0
	PRINTFORMW 아직 아무것도 적혀있지 않은 것 같다…
	RETURN
ENDIF
FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF !PAGESET:C_ID:DCOUNT
		BREAK
	CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, PAGESET:C_ID:DCOUNT, DCOUNT, "TEXT", "コマンド")
	DRAWLINE
	;未読(3)だったら既読(1)にする
	SIF DIARY:C_ID:(PAGESET:C_ID:DCOUNT) == 3
		DIARY:C_ID:(PAGESET:C_ID:DCOUNT) = 1
	IF PAGENUM > 1
		CALL ASK_M("다음 페이지 ", 1, " 5 페이지 앞", DCOUNT + 5 <= PAGENUM, "10 페이지 앞", DCOUNT + 10 <= PAGENUM, "앞 페이지", DCOUNT > 1, "일기장을 덮는다", 1)
		IF RESULT == 0
			CONTINUE
		ELSEIF RESULT == 1
			DCOUNT += 4
		ELSEIF RESULT == 2
			DCOUNT += 9
		ELSEIF RESULT == 3
			DCOUNT += -2
		ELSEIF RESULT == 4
			CLOSE = 1
			BREAK
		ENDIF
	ENDIF
NEXT
IF !CLOSE
	PRINTFORMW 일기가 여기서 끝났다… 
ELSE
	PRINTFORMW %마스터는% 읽는 것을 멈췄다
ENDIF

@CHARA_DIARY_AFTERTRAIN
#DIM CHARA
#DIM DCOUNT
#DIM PAGECOUNT

;毎日終わるとき
FOR CHARA, 1, CHARANUM 
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "EXIST")
	SIF !RESULT
		CONTINUE
	;PRINTFORML %CALLNAME:CHARA%の日記を確認
	TARGET = CHARA
	;日記の書き込み判定
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "BEFORE_CHECK")
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		;最初にヒットした未読(2)を表示
		IF DIARY:CHARA:DCOUNT == 2
			;ページセットしてから口上色を付けて表示
			CALL CHARA_DIARY_PAGESETTING(CHARA, DCOUNT)
			PAGECOUNT = RESULT
			CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, PAGESET:CHARA:PAGECOUNT, PAGECOUNT, "TEXT", "デイリー")
			PRINTL
			;既読フラグをつける
			DIARY:(NO:CHARA):DCOUNT = 1
			DRAWLINE
			BREAK
		;未読でない場合スキップ
		ELSE
			CONTINUE
		ENDIF
	NEXT
	;日記は毎日一回だけ。呼び出されなかった日記IDのフラグをリセットする
	;恋慕とか告白とか大事なイベントは順番を前にしたほうがいいです。
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		IF DIARY:CHARA:DCOUNT == 2
			DIARY:CHARA:DCOUNT = 0
		ENDIF
	NEXT
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "AFTER_CHECK")
NEXT


@MASTER_DIARY_INPUT
#DIMS 내용행,40
#DIM 행수
VARSET 내용행

행수 = 0
$INPUT_LOOP_DIARY

PRINTL
SETCOLOR C_YELLOW
PRINTFORML ・ %GET_MONTH()% {DAY:3,2,RIGHT}일 （%GET_DAY()%） %시각표시(TIME)%
FOR LOCAL,0,행수
	PRINTFORML 　　%내용행:LOCAL%
NEXT
RESETCOLOR
PRINTL
PRINTBUTTON "[98] - 앞 행을 삭제한다", "98"
PRINT 　　
PRINTBUTTON "[99] - 일기를 끝낸다", "99"
PRINTL

INPUTS

IF RESULTS == "98"
	IF 행수 > 0
		내용행:행수 = 
		행수 -= 1
		CLEARLINE 1
	ENDIF
	
	CLEARLINE (행수+5)
	GOTO INPUT_LOOP_DIARY
ELSEIF RESULTS == "99"
	LOCALS = 
	FOR LOCAL,0,행수-1
		LOCALS += @"%내용행:LOCAL%<br>"
	NEXT
	SIF 행수>0
		LOCALS += @"%내용행:(행수-1)%"
	
	RESULTS = %LOCALS%
	RETURN
ELSEIF 행수 == 39
	SETCOLOR C_YELLOW
	PRINTFORMW 라인수는 이미 상한
	RESETCOLOR
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (행수+5)
	GOTO INPUT_LOOP_DIARY
ELSE
	내용행:행수 = %RESULTS%
	
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (행수+4)
	행수 += 1
	GOTO INPUT_LOOP_DIARY
ENDIF


@MASTER_DIARY_WRITE,내용
#DIMS 내용

;上限超えたら2つ消す
IF STRCOUNT(SAVESTR:1,"//") >= 100
	SPLIT SAVESTR:1,"//",LOCALS
	SAVESTR:1 =
	FOR LOCAL,0,99
		SAVESTR:1 = %LOCALS:LOCAL%%SAVESTR:1%
	NEXT
ENDIF
SAVESTR:1 = ・ %GET_MONTH()% {DAY:3,2,RIGHT}일 （%GET_DAY()%） %시각표시(TIME)%_L_%내용%//%SAVESTR:1%

@READ_MASTER_DIARY
#DIMS 표시내용,100
#DIMS 내용,2
#DIM PAGE

SPLIT SAVESTR:1,"//",표시내용
FOR LOCAL,0,100
	SIF 표시내용:LOCAL != ""
		PAGE ++
NEXT

PRINTFORML ---| %CALLNAME:MASTER%의 일기 |-------------------------------------------------------------------------------------
FOR LOCAL,0,100
	IF 표시내용:LOCAL != ""
		SPLIT 표시내용:LOCAL,"_L_",내용
		PRINTL
		PRINTFORML PAGE.{1+LOCAL}
		PRINTL
		HTML_PRINT @"%내용:0%<br><br>%내용:1%<br>"
		PRINTL
		IF PAGE > 1
			PRINTL --------------------------------------------------------------------------------------------------------------------
			CALL ASK_YN("다음 페이지 ","일기장을 덮는다")
			IF RESULT
				BREAK
			ENDIF
			CLEARLINE 3
		ENDIF
	ENDIF
NEXT
PRINTFORMW 일기는 여기서 끝난다… 


@SHARE_MASTER_DIARY
#DIMS 표시내용,100
#DIMS 내용,2
#DIM PAGE
#DIM READPAGE

SPLIT SAVESTR:1,"//",표시내용
FOR LOCAL,0,100
	SIF 표시내용:LOCAL != ""
		PAGE ++
NEXT

SIF PAGE >= 1
	READPAGE = RAND:PAGE

IF PAGE == 0
	CALL SPTALK,TARGET,,,@"「아직 아무것도 적혀있지 않다……」//%타겟은% 일기장을 덮었다."
	RETURN 0
ENDIF

PRINTFORML %타겟은% 책장을 넘기며 읽기 시작했다…
PRINTL
CALL PRINT_FACE,TARGET

IF 표시내용:READPAGE != ""
	SPLIT 표시내용:READPAGE,"_L_",내용
	PRINTL
	PRINTFORML PAGE.{1+READPAGE}
	PRINTL
	HTML_PRINT @"%내용:0%<br><br>%내용:1%<br>"
	PRINTL
	PRINTW
ENDIF
;===========================================================================
;日記に書き込む処理
;指定されたCHARAのDIARY番号(PAGENUM)を最後の空白ページにセット
;何ページに書いたかを返り値にする
;===========================================================================
@CHARA_DIARY_PAGESETTING(CHARA, PAGENUM)
#DIM CHARA
#DIM PAGENUM
FOR LOCAL, 1, MAX_DIARY_PAGE:CHARA:0 + 1
	IF !PAGESET:CHARA:LOCAL
		PAGESET:CHARA:LOCAL = PAGENUM
		RETURN LOCAL
	ENDIF
NEXT





;===========================================================================
;日記保存文件処理
;===========================================================================
@WRITE_MASTER_DIARY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"일기장[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[없음]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[그만둔다]", 99

$INPUT_LOOP_DIARY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1)
	CLEARLINE 1
	GOTO INPUT_LOOP_DIARY_FILE
ENDIF

RESULT = LOCAL

SAVETEXT SAVESTR:1, (1700+RESULT)
SIF !RESULT
	CALL COLORMESSAGE("보존에 실패했다",C_YELLOW,2,1)


@READ_MASTER_DIARY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"일기장[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[없음]", -1
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[그만둔다]", 99

$INPUT_LOOP_DIARY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1) || LOCAL == -1
	CLEARLINE 1
	GOTO INPUT_LOOP_DIARY_FILE
ENDIF

LOADTEXT (1700+RESULT)
SAVESTR:1 = %RESULTS%

@ASK_DIARY(ARGS)
PRINTFORML 이 일을 일기로 남기시겠습니까?
CALL ASK_YN
IF !RESULT
	CALL MASTER_DIARY_WRITE, @"%ARGS%"
ENDIF
