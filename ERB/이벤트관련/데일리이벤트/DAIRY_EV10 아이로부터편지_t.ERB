@DAILY_CONDITION_EV10
#DIM YOME
#DIM CHILDNUM
SIF RAND:20 > 0
	RETURN -1
CALL PickRandGrownChild
YOME = RESULT
CHILDNUM = RESULT:1
VARSET RESULT

SIF YOME <= 0
	RETURN -1

CALL DAILY_EV10(YOME, CHILDNUM)
RETURN 1

@DAILY_EV10(ARG, ARG:1)
;PRINTFORMW %CALLNAME:ARG%과의 아이의 %CHILDNAME:ARG:(ARG:1)%로부터 편지가 왔다
PRINTFORMW %조사처리(CHILDNAME:ARG:(ARG:1),"로")%부터 편지가 왔다
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:호감도 > CFLAG:ARG:호감도
		LOCAL:1 ++
NEXT

TARGET = ARG
;말전용의 편지 이벤트 분기(없으면 통상 이벤트)
;口上存在判定
RESULTS =
IF CFLAG:TARGET:구상셀렉터
	TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
ELSE
	TRYCALLFORM M_KOJO_K{NO:TARGET}
ENDIF
IF !RAND:2
	TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{ARG}_CHILD_DAILY_말편지(ARG:1)
	CATCH
		GOTO TEGAMI
	ENDCATCH
ELSE
	$TEGAMI
	IF LOCAL:1 >= 3
		TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{ARG}_CHILD_DAILY_공통편지(ARG:1, "바람둥이")
		CATCH
			PRINTFORMW 좀 더 %ARG를% 소중히 하라고 적혀있었다……
		ENDCATCH
	ELSEIF !RAND:5
		TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{ARG}_CHILD_DAILY_공통편지(ARG:1, "염치없는돈") ;염치없는돈
		;金銭を無遠慮にねだって得ようとすること、思慮なく金をせびることを意味する語。親に小遣いをたかることを指す場合が多い。 
		;용돈 달라는 뜻
		CATCH
			PRINTFORMW 용돈을 달라고 한다……
			;金の無心だ……
		ENDCATCH
		PRINTFORMW %마스터는% \@ MONEY > 45000? ￥45000을# 가진 돈 전부를\@ 보내기로 했다
		MONEY = MAX(MONEY - 45000,0)
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{ARG}_CHILD_DAILY_공통편지(ARG:1, "송금")
		CATCH
			PRINTFORMW %조사처리(CALLNAME:ARG,"와")% %마스터를% 신경쓰는 말과 송금\\30000이 들어가 있었다
		ENDCATCH
		MONEY += 30000
	ENDIF
ENDIF
TARGET = TARGET:1
RETURN 1
