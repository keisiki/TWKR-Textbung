@출산경과(ARG)
#DIM CHILDNUM
RESULTS =
;口上存在判定
IF CFLAG:TARGET:구상셀렉터
	TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
ELSE
	TRYCALLFORM M_KOJO_K{NO:TARGET}
ENDIF


SIF CFLAG:ARG:임신경과일수 > 50 ;일판은 100일
	CALL BIRTH(ARG)
SIF CFLAG:ARG:임신경과일수 > 1
	CALL PERCIEVE(ARG)
SIF CFLAG:ARG:임신경과일수 > 0
	CFLAG:ARG:임신경과일수 += 1
SIF CFLAG:ARG:출산경과일 > 0
	CFLAG:ARG:출산경과일 += 1

CHILDNUM = TALENT:ARG:육아중

;부모가 자식으로부터 정신적으로
IF CFLAG:ARG:출산경과일 > CHILD_자립
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{ARG}_CHILD_RAISING_자립
	CATCH
		PRINTFORMW %조사처리(CALLNAME:ARG,"과")% %CALLNAME:MASTER%의 아이의%조사처리(CHILDNAME:ARG:CHILDNUM,"는")% 무사하게 독립의 날을 맞이해 자립했다.
		PRINTFORML 
		PRINTFORML 가끔 씩은 얼굴을 보이러 오면 좋겠다
		PRINTFORML %CALLNAME:MASTER%의 소망이 입으로 되는 것은 없었다…….
		PRINTFORMW 
	ENDCATCH
	
	CFLAG:ARG:육아인원수 -= 1
	CFLAG:MASTER:육아인원수 -= 1
	;IF CFLAG:ARG:육아인원수 >= 0
	IF CFLAG:ARG:육아인원수 > 0
		;PRINTFORML 멀어지는 아이의 등을 보면서 아직 작은 다른 아이도 무사하게 기르려고,
		PRINTFORML 멀어지는 아이의 등을 지켜보면서,
		PRINTFORML 아직 작은 아이도 무사하게 기르고 자르려고,
		;PRINTFORML %CALLNAME:ARG%는 %마스터와% 조용하게 맹세했다.
		PRINTFORMW %CALLNAME:ARG%는 %CALLNAME:MASTER%의 손을 잡아, 조용하게 맹세했다.
	ELSE
		;PRINTFORML 멀어지는 아이의 등을 보면서 %CALLNAME:ARG%는 %CALLNAME:MASTER%곳들의 무사를 빌었다.
		PRINTFORMW 멀어지는 우리 아이의 등을 보면서 %CALLNAME:ARG%는 %마스터와% 아이의 무사를 빌었다.
	ENDIF
	;PRINTFORML 헤어질 때, 아이는 %마스터가% %CALLNAME:ARG%를 돌아보지 않고 다른 여자의 엉덩이를 쫓아다니고 있던 것을 입이 더럽게 매도해,
	;PRINTFORML 자신은 절대로 %마스터와% 같이는 안 된다고 토해 버려 떠나 갔다

	IF CFLAG:ARG:육아인원수 == 0
		TALENT:ARG:육아중 = 0
	ENDIF
	CFLAG:ARG:출산경과일 = 0
ENDIF
IF CFLAG:ARG:출산경과일 == CHILD_자립전
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_자립전
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%도 상당히 분별이 따라 왔다.
		PRINTFORML 키도 성장해, 이제(벌써)%CALLNAME:ARG%과 큰 차이 없다.
		PRINTFORMW 이 아이도 머지않아 집을 나와 버릴 것이다, 남겨진 시간을 소중하게 보내자…….
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_취학
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_취학
	CATCH
		PRINTFORML %CALLNAME:ARG%의 아이의%CHILDNAME:ARG:CHILDNUM%는 서당에 다니기 시작했다.
		PRINTFORML 아이의 성장은 빠른 것이다.
		PRINTFORML 부모 떨어지고의 날도 눈 깜짝할 사이에 와 버리는 것일까…….
		PRINTFORMW %마스터는% 조금 감상적으로 되었다.
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_바로잡기
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_예의범절
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%도 많이 지혜가 따라 와, 여러가지 행동을 취하게 되어,
		PRINTFORML 동시에 못된 장난이나 아진도 많아졌다.
		PRINTFORMW 이제 가르쳐에 임해서 성실하게 생각하지 않으면…….
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_어휘
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_어휘
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%의 어휘가 많아졌다.
		PRINTFORMW 말하는 내용도, 단편적으로 2개(살) 3개(살) 단어를 말할 뿐(만큼)이었던 것이 문장이 성립하게 되어 지고 있다.
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_말한다
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_말한다
	CATCH
		PRINTFORML %CALLNAME:ARG%가 대단한 기세로 뛰어들어 왔다.
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%가 %CALLNAME:ARG%를 불렀다는 것이다.
		PRINTFORML 서둘러 가 보면(자), 확실히%CALLNAME:ARG%의 얼굴을 봐 송곳이라고 부르고 있다.
		PRINTFORMW %마스터는% 자신도 불러 줄 수 있도록, 얼굴을 접근해 아버지와 연호했다.
	ENDCATCH
ELSEIF INRANGE(CFLAG:ARG:출산경과일, CHILD_말한다 - 3, CHILD_말한다 - 1)
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_말하기직전
	CATCH
		PRINTFORML 최근%CHILDNAME:ARG:CHILDNUM%는 잘 말하려고 하게 되었다.
		PRINTFORML 아직 의미가 있는 것은 이야기할 수 없지만, 갖고 싶은 것을 가리켜 만나는 만나는 말하거나와 분명하게 말을 사용하려 하고 있다.
		PRINTFORMW 머지않아 이야기할 수 있게 될 것이다…….
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_아장아장
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_아장아장
	CATCH
		PRINTFORML 아직 위태롭지만,%CHILDNAME:ARG:CHILDNUM%는 양 다리로 서 걸어 다니게 되었다.
		PRINTFORML 시야와 행동 반경이 퍼진 탓으로 다양한 것에 흥미를 나타내고 있다.
		PRINTFORMW %마스터는% 위험할 것 같은 것을%CHILDNAME:ARG:CHILDNUM%가 손이 미치지 않는 장소에 이동시켰다.
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_붙잡고서기
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_붙잡고서기
	CATCH
		PRINTFORML %CALLNAME:MASTER%까불며 떠들지 않아로%CHILDNAME:ARG:CHILDNUM%에 손가락을 잡게 하고 있다.
		PRINTFORMW 그리고 천천히 손가락을 들어 올려 잡혀 립 치노 연습을 시켰다.
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_완구
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_완구
	CATCH
		PRINTFORML %CHILDNAME:ARG:CHILDNUM%를 위해서(때문에)%마스터는% 나무를 깎아 간단한 완구를 만들고 있다.
		PRINTFORML 어차피 던지거나 갉아 먹거나 할테니까 단순하고 튼튼한 만들기가 좋을 것이다.
		PRINTFORMW 상처를 시키지 않게, %마스터는% 정성스럽게 단면을 닦았다.
	ENDCATCH
ELSEIF CFLAG:ARG:출산경과일 == CHILD_젖떼기
	DRAWLINE
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_젖떼기
	CATCH
		PRINTFORMW 만면의 웃는 얼굴로 이유식을 베어 무는 어린 아이의 모습에 %CALLNAME:MASTER%의 입술 가장자리가 느슨해진다.
	ENDCATCH
	PRINTFORML %CALLNAME:ARG%는 수유기간을 끝냈다.
	IF TALENT:ARG:모유체질 == 1 && EXP:ARG:분유경험 > 500 && EXP:ARG:출산경험 > 1
		PRINTFORML 그러나 수유의 역을 넘은 양의 모유를 계속 내 온%CALLNAME:ARG%의 유방은, 아직도 깊은 맛 풍부한 젖을 계속 만들고 있다……
		PRINTFORML %CALLNAME:ARG%는【모유체질】을 얻었다
		TALENT:ARG:모유체질 = 2
	ELSE
		PRINTFORMW 역할을 끝낸%CALLNAME:ARG%의 가슴은 작아져, 모유도 멈추었습니다.
		SIF TALENT:ARG:모유체질 == 1
			TALENT:ARG:모유체질 = 0
	ENDIF
	;가슴랭크업 플래그 끊고 있어, 지금 절벽이 아니면 작아진다. 통상 출산 이외의임신 종료도 고려하면(자) CALL로 호출하는 편이 좋겠지가…
	IF CFLAG:ARG:가슴랭크업 == 1 && TALENT:ARG:가슴사이즈 > -2
		TALENT:ARG:가슴사이즈 -= 1
		CFLAG:ARG:가슴랭크업 = 0
	ENDIF
ELSEIF CFLAG:ARG:출산경과일 == 20 && CFLAG:ARG:회복속도다운
	DRAWLINE
	PRINTFORMW 무사하게 산후의 발육을 끝내%CALLNAME:ARG%는 완전히 건강하게 되었다.
	;말체크
	TRYCCALLFORM M_KOJO_EVENT_K{ARG}_CHILD_RAISING_회복
	CATCH
		;체질 변화가 있으므로 생략 하지 않는다
	ENDCATCH
	MAXBASE:ARG:체력 += 500
	MAXBASE:ARG:기력 += 500
	SIF MAXBASE:ARG:체력 / 10 * 11 <= CSVBASE(ARG, GETNUM(BASE, "체력")) * 2
		MAXBASE:ARG:체력 = MAXBASE:ARG:체력 / 10 * 11
	SIF MAXBASE:ARG:기력 / 10 * 11 <= CSVBASE(ARG, GETNUM(BASE, "기력")) * 2
		MAXBASE:ARG:기력 = MAXBASE:ARG:기력 / 10 * 11
	TALENT:ARG:회복속도 = CFLAG:ARG:회복속도다운 - 2
	CFLAG:ARG:회복속도다운 = 0
ENDIF


;아이가 통학할까
@GO_SCHOOL(ARG)
#FUNCTION
SIF !INRANGE(DAY % 7, 1, 5) 
	RETURNF 0
SIF FESTIVAL() != ""
	RETURNF 0
SIF !TALENT:ARG:육아중
	RETURNF 0
;큰 비와 눈보라
SIF TIME:5 == 5
	RETURNF 0
SIF TIME:5 == 9
	RETURNF 0
SIF CFLAG:ARG:출산경과일 >= CHILD_취학
	RETURNF 1

;현재 육아중의아이의 이름을 표시
@SHOW_CHILDNAME(ARG)
#FUNCTIONS
RETURNF CHILDNAME:ARG:(TALENT:ARG:육아중)

;성장한 아이와 부모를 무작위로 주워 오는 함수
@PickRandGrownChild
#DIM YOME
#DIM CHILDNUM
SIF !CFLAG:MASTER:아이인원수
	RETURN -1
YOME = 0
CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
FOR LOCAL:10, 0, CHARANUM - 1
	LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
	IF CFLAG:LOCAL:아이인원수 - CFLAG:LOCAL:육아인원수 > 0
		YOME = LOCAL
		CHILDNUM = RAND:(CFLAG:LOCAL:아이인원수 - CFLAG:LOCAL:육아인원수) + 1
		BREAK
	ENDIF
NEXT
RETURN YOME, CHILDNUM