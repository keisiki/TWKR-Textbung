@CHK_HININ_LOVE(ARG)
#FUNCTION
LOCAL = 0
SIF TALENT:ARG:연인
	LOCAL += 300
;애정경험

LOCAL += EXP:ARG:애정경험 / 2

;호감도
SELECTCASE CFLAG:ARG:호감도
CASE 0 TO 499
	LOCAL -= 100
CASE 500 TO 999
	LOCAL += 30
CASE 1000 TO 2999
	LOCAL += 50
CASEELSE
	LOCAL += CFLAG:ARG:호감도 / 30
ENDSELECT

LOCAL += ABL:ARG:친밀 * 20
LOCAL += ABL:ARG:순종 * 7
LOCAL += ABL:ARG:봉사정신 * 7

RETURNF LOCAL

@CHK_HININ_REASON(ARG,ARGS)
#FUNCTION
#DIM 이성판정치
VARSET LOCAL

LOCAL:3 += GETPALAMLV(PALAM:욕정,15) * (10 + TALENT:ARG:쾌감응답 * 3)
LOCAL:3 += GETPALAMLV(PALAM:공순,15) * (10 - TALENT:ARG:태도 * 3)
LOCAL:3 += GETPALAMLV(PALAM:호의,15) * 10

LOCAL:5 += ABL:ARG:질내사정중독 * 15
LOCAL:5 += ABL:ARG:욕망 * (10 + TALENT:ARG:쾌감응답 * 2)
LOCAL:5 += MARK:ARG:쾌락각인 * 10
LOCAL:5 += MARK:ARG:괘씸각인 * 10
LOCAL:5 += MARK:ARG:고통각인 * 10
LOCAL:5 -= MARK:ARG:반발각인 * 100


이성판정치 = 800 - LOCAL:3 * LOCAL:5 / 300

SIF ESTRUS_CYCLE(ARG) == 150
	이성판정치 += 1000

IF TCVAR:ARG:발정 && TALENT:ARG:동물귀 && CFLAG:ARG:900 == 7
	이성판정치 -= 200
ELSEIF TCVAR:ARG:미약 || IsHeat(ARG)
	이성판정치 -= 50
ENDIF
SIF IsInsenceValid(ARG)
	이성판정치 -= 50

IF TALENT:ARG:츤데레
	IF TALENT:ARG:연모
		이성판정치 = 이성판정치 * 2 / 3
	ELSE
		이성판정치 = 이성판정치 * 3 / 2
	ENDIF
ENDIF

;신뢰도
IF CFLAG:ARG:신뢰도 < 100
	이성판정치 *= 5
ELSEIF CFLAG:ARG:신뢰도 < 300
	이성판정치 *= 5 / 2
ELSEIF CFLAG:ARG:신뢰도 < 500
	이성판정치 *= 5 / 3
ELSEIF CFLAG:ARG:신뢰도 < 1000
	이성판정치 *= 5 / 4
ENDIF

;카운터라면 여기서 소질 처리해 둔다
IF ARGS == "COUNTER"
	SIF TALENT:ARG:저항
		이성판정치 += 300
	IF TALENT:ARG:자존심 == -1
		이성판정치  = 이성판정치 * 9 / 10
	ELSEIF TALENT:ARG:자존심 == 1
		이성판정치 = 이성판정치 * 6 / 5
	ENDIF
	SIF OTOKOGIRAI(ARG)
		이성판정치 = 이성판정치 * 3 / 2
	SIF TALENT:ARG:정조 == 1
		이성판정치 = 이성판정치 * 6 / 5
	SIF TALENT:MASTER:수수께끼의매력
		이성판정치 = 이성판정치 * 9 / 10
	SIF TALENT:ARG:도착적
		이성판정치 = 이성판정치 * 9 / 10
	IF TALENT:ARG:일선을넘지않음
		SELECTCASE EXP:ARG:애정경험
			CASE IS >= 500
				이성판정치 = 이성판정치 / 2
			CASE IS >= 300
				이성판정치 = 이성판정치 * 4 / 3
			CASE IS >= 100
				이성판정치 = 이성판정치 * 3 / 2
			CASEELSE
				이성판정치 *= 2
		ENDSELECT
	ENDIF
	IF TALENT:ARG:츤데레
		IF TALENT:ARG:연모
			이성판정치 = 이성판정치 * 2 / 3
		ELSE
			이성판정치 = 이성판정치 * 3 / 2
		ENDIF
	ENDIF
ENDIF


이성판정치 = MAX(이성판정치,0)
RETURNF 이성판정치

@OGAMITAOSHI(ARG)
#DIM 이성판정치
#DIM 호의판정치
#DIM 호의표시
VARSET LOCAL


PRINTFORMW %CALLNAME:ARG%의 마음 속

호의판정치 = CHK_HININ_LOVE(ARG) 
CALL SHOW_LOVEPOWER(호의판정치)

WAIT
이성판정치 = CHK_HININ_REASON(ARG)

CALL SHOW_REASONWALL(이성판정치)
IF TALENT:MASTER:수수께끼의매력
	CALL COLORMESSAGE(@"%ARG는%, %CALLNAME:MASTER%의 수수께끼의 매력에 이성이 흔들리는 것을 느꼈다……",C_PINK,1,1)
	이성판정치 = 이성판정치 * 9 / 10
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF TALENT:ARG:자존심 == -1
	이성판정치  = 이성판정치 * 9 / 10
ELSEIF TALENT:ARG:자존심 == 1
	이성판정치 = 이성판정치 * 6 / 5
	CALL COLORMESSAGE(@"자존심이 높은 %ARG는%, 자신을 싸게 파는 짓은 안 하려고 생각하고 있다",C_PINK,1,1)
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF TALENT:ARG:정조 == 1
	이성판정치 = 이성판정치 * 6 / 5
	CALL COLORMESSAGE(@"정조 관념이 강한 %ARG는%, 거기까지 허락해도 좋은 걸까 하고 다시 생각했다……",C_PINK,1,1)
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF OTOKOGIRAI(ARG)
	이성판정치 = 이성판정치 * 3 / 2
	IF TALENT:ARG:성별기호 == 3
		CALL COLORMESSAGE(@"%ARG는% 다른 사람에 대해 일반적으로 기피 감정을 가지고 있다……",C_PINK,1,1)
	ELSEIF TALENT:ARG:성별기호 == 1
		CALL COLORMESSAGE(@"%ARG는% 남성에 대해 일반적으로 기피 감정을 가지고 있다……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF TALENT:ARG:도착적
	CALL COLORMESSAGE(@"%ARG는%, 흐트러진 관계에 도착적인 매력을 느끼고 있다……",C_PINK,1,1)
	이성판정치 = 이성판정치 * 8 / 10
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF TALENT:ARG:일선을넘지않음
	SELECTCASE EXP:ARG:애정경험
		CASE IS >= 500
			CALL COLORMESSAGE(@"%ARG는%, %CALLNAME:MASTER%에게는 이제 모든 것을 허락해주고 싶다……!",C_PINK,1,1)
			이성판정치 = 이성판정치 / 2
		CASE IS >= 300
			CALL COLORMESSAGE(@"%ARG는%, %CALLNAME:MASTER% 상대라고 해도 아직 약간 저항이 있는 것 같다……",C_PINK,1,1)
			이성판정치 = 이성판정치 * 6 / 5
		CASE IS >= 100
			CALL COLORMESSAGE(@"%ARG는%, 친한 상대라도 어느 정도의 거리는 두고 싶다……",C_PINK,1,1)
			이성판정치 = 이성판정치 * 3 / 2
		CASEELSE
			CALL COLORMESSAGE(@"%ARG는%, 다른 사람을 그렇게까지 받아들이고 싶어 하지 않는다……",C_PINK,1,1)
			이성판정치 *= 2
	ENDSELECT
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
IF CFLAG:ARG:종속도 > 0
	IF TALENT:ARG:당신의노예
		CALL COLORMESSAGE(@"노예인 %ARG는% %마스터의% 명령을 거역하기 힘들다……",C_PINK,1,1)
		이성판정치 = 1
	ELSE
		CALL COLORMESSAGE(@"%ARG는% 왜인지 굴복감을 느끼고 있다……",C_PINK,1,1)
		이성판정치 -= CFLAG:ARG:종속도
		SIF 이성판정치 < 0
			이성판정치 = 0
	ENDIF
ENDIF
IF TALENT:ARG:츤데레
	IF TALENT:ARG:연모
		이성판정치 = 이성판정치 * 2 / 3
		CALL COLORMESSAGE(@"%ARG는% 데레다!",C_PINK,1,1)
	ELSE
		이성판정치 = 이성판정치 * 3 / 2
		CALL COLORMESSAGE(@"%ARG는% 반감을 품고 있다……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(이성판정치)
ENDIF
WAIT
IF CHK_HININ_LOVE(ARG) > 이성판정치
	IF ESTRUS_CYCLE(ARG) != 150
		PRINTFORMW %CALLNAME:MASTER%의 부끄러움도 체면도 없는 간곡한 부탁에, %CALLNAME:ARG%도 결국 허락해 주었다
		CFLAG:ARG:생해금 = 1
		CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,4,0)
		PRINTW 위험일 이외에는 피임 없이 섹스해도 괜찮게 됐다!
	ELSE
		PRINTFORMW %CALLNAME:ARG%의 아이를 원한다, 라고 하자, %ARG는% 얼굴을 붉히며 고개를 끄덕였다
		CFLAG:ARG:생해금 = 2
		CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,5,0)
		PRINTFORMW %CALLNAME:ARG%에게서 아이 만들기 합의를 얻었다!
	ENDIF
ELSE
	PRINTFORML	%ARG는% 간곡히 거절했다
	CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,2,0)
	CALL ASK_YN("상대방의 의사를 존중한다","한 번 더 밀고 나간다")
	IF RESULT == 0
		TCVAR:ARG:삽입거절 = 1
		RETURN -1
	ELSE
	;말솜씨와 상대하기에 따라서는 밀어붙이면 어떻게든 된다
		LOCAL = (4 - TALENT:ARG:응답 - TALENT:ARG:담력 - TALENT:ARG:태도) * (ABL:MASTER:화술기능 + 3 * CFLAG:ARG:퇴짜)
		LOCAL:2 = 이성판정치 * (100 - LOCAL) / 100
		CALL SHOW_REASONWALL(LOCAL:2)
		IF CHK_HININ_LOVE(ARG) > LOCAL:2
			PRINTFORMW %마스터는% 세 치 혀로 어떻게든 %ARG를% 구슬렸다
			IF ESTRUS_CYCLE(ARG) != 150
				CFLAG:ARG:생해금 = 1
				CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,4,0)
				PRINTW 위험일 이외에는 피임 없이 섹스해도 괜찮게 됐다!
			ELSE
				CFLAG:ARG:생해금 = 2
				CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,5,0)
				PRINTFORMW %CALLNAME:ARG%에게서 아이 만들기 합의를 얻었다!
			ENDIF
		ELSEIF TCVAR:ARG:패배강간
			PRINTFORM %ARG는% 
			IF TALENT:ARG:연인 || TALENT:ARG:연모
				PRINTFORM 약간 서글픈 표정으로 %마스터를% 바라보다 마지못해
			ELSEIF MARK:ARG:반발각인 == 3
				PRINTFORM 눈물까지 흘리며 바들바들 떨다 마지못해
			ELSEIF TALENT:ARG:애욕 || TALENT:ARG:섹프 || TALENT:ARG:당신의노예
				PRINTFORM 한숨을 쉬며 마지못해
			ELSEIF TALENT:ARG:사모
				PRINTFORM 복잡한 표정으로 마지못해
			ELSE
				PRINTFORM 무언가 하고 싶은 말이 있는것 같아보이지만, 마지못해
			ENDIF
			PRINTFORML  고개를 끄덕였다
			TCVAR:ARG:임시생해금 = 1
		ELSE
			CALL KOJO_MESSAGE_SEND("EVENT",18,TARGET,3,0)
			PRINTFORMW %CALLNAME:ARG%에게 진심으로 혼났다…
			CFLAG:ARG:퇴짜 += 1
			CALL ENDUFUFU(ARG)
			CALL GET_ANGRY(ARG)
			MARK:ARG:반발각인 = MIN(MARK:ARG:반발각인 + 1, 3)
			TFLAG:106 = 0
			TCVAR:ARG:삽입거절 = 1
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SHOW_REASONWALL(이성판정치, UNIT = 100)
#DIM 이성판정치
#DIM UNIT

PRINTFORM 이성의 벽 %" " * STRLENS(CALLNAME:MASTER)%: 
SETCOLOR 255,255,0
LOCAL = MAX(이성판정치 / UNIT,1)
PRINTFORM %"★" * LOCAL%
RESETCOLOR
SIF 이성판정치 % UNIT > (UNIT / 2)
	PRINTL ★
PRINTW

@SHOW_LOVEPOWER(호의판정치, UNIT = 100, ARGS)
#DIM 호의판정치
#DIM 호의표시
#DIM UNIT
IF ARGS == ""
	PRINTFORM %CALLNAME:MASTER%에게의 호의: 
ELSE
	PRINTFORM %ARGS%
ENDIF
호의표시 = MAX(호의판정치 / UNIT,0)
;LOCALS:1 = %GETFONT()%
;SETFONT "Times New Roman"
LOCAL = GETCOLOR()
PRINTFORM %UNICODE(0x2764) * MAX((호의표시 / 10000 -1),0)%
RESETCOLOR
SETCOLOR 255,51,153
SIF 호의표시 >=10000
	PRINTFORM %UNICODE(0x2764) * (9999)%%UNICODE(0x2764)%

PRINTFORM %UNICODE(0x2764) * (호의표시 % 10000)%
SETCOLOR LOCAL

SIF 호의판정치 % UNIT > (UNIT / 2)
	PRINTFORML %UNICODE(0x2764)%
SETFONT LOCALS:1
PRINTW