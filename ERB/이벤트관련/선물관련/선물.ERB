;-------------------------------------------------
;선물을 생성하는 함수
;RESULT:0  GIFT_ID
;RESULT:1  값
;RESULTS   이름
;-------------------------------------------------
@CREATE_GIFT(AREA)
#DIM AREA
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
VARSET RESULT

CALL DRAW_GIFT_MAIN(AREA)
MAIN = RESULT:0
BASIC_PRICE = RESULT:1
GIFTNAME_MAIN = %RESULTS:0%

CALL DRAW_GIFT_ADJECT(AREA, RESULTS:1)
ADJECT = RESULT:0
MAGNIF_PRICE = RESULT:1
GIFTNAME_ADJECT = %RESULTS:0%

;가격설정
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;반값 세일이나 할인 요소 등
SIF FLAG:기념품점염가대매출 >= 100 && FLAG:기념품점염가대매출 % 100 == CFLAG:MASTER:데이트중
	TIMES RESULT:1, 0.50

;이름의 설정
RESULTS = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;ID의 설정
RESULT:0 = ADJECT * 1000 + MAIN
RETURN RESULT:0

;-------------------------------------------------
;キャラクターに依存した贈り物を生成する関数
;-------------------------------------------------
@CREATE_GIFT_FROM_CHARA(CHARA, SCORE_MIN, SCORE_MAX)
#DIM CHARA
#DIM SCORE_MIN
#DIM SCORE_MAX
#DIM GIFT_ID
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
#DIM SCORE
#DIMS GIFT_SENSE
VARSET RESULT
SIF SCORE_MAX <= SCORE_MIN
	SCORE_MAX = 1000

CALL DRAW_GIFT_MAIN(99)
MAIN = RESULT:0
BASIC_PRICE = RESULT:1
GIFTNAME_MAIN = %RESULTS:0%

CALL DRAW_GIFT_ADJECT(99, RESULTS:1)
ADJECT = RESULT:0
MAGNIF_PRICE = RESULT:1
GIFTNAME_ADJECT = %RESULTS:0%

;가격설정
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;이름의 설정
GIFTNAME = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;ID의 설정
GIFT_ID = ADJECT * 1000 + MAIN

;評価
CALL REVIEW_GIFT(CHARA, GIFT_ID)
SCORE = RESULT
GIFT_SENSE = %RESULTS%

IF INRANGE(RESULT, SCORE_MIN, SCORE_MAX)
	RESULT:1 = SCORE
	RESULTS = %GIFTNAME%
	RESULTS:1 = %GIFT_SENSE%
	RETURN GIFT_ID
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;선물의본체를뽑는함수
;-------------------------------------------------
@DRAW_GIFT_MAIN(AREA)
#DIM AREA
#DIM CATEGORY_MAIN
#DIM GIFT_MAIN
#DIM 카테고리상한
VARSET RESULT
카테고리상한 = 52
WHILE !RESULT
	CATEGORY_MAIN = RAND:카테고리상한 + 1
	GIFT_MAIN = CATEGORY_MAIN * 10 + RAND:10
	CALL GIFTDATA_MAIN(GIFT_MAIN, AREA)
WEND
RETURN GIFT_MAIN

;-------------------------------------------------
;선물의형용사를뽑는함수
;-------------------------------------------------
@DRAW_GIFT_ADJECT(AREA, SENSE)
#DIM AREA
#DIM CATEGORY_ADJECT
#DIM GIFT_ADJECT
#DIMS SENSE
#DIM 카테고리상한
VARSET RESULT
카테고리상한 = 21
WHILE !RESULT
	CATEGORY_ADJECT = RAND:카테고리상한 + 1
	GIFT_ADJECT = CATEGORY_ADJECT * 100 + RAND:100
	CALL GIFTDATA_ADJECT(GIFT_ADJECT, AREA, SENSE)
WEND
RETURN GIFT_ADJECT

;-------------------------------------------------
;선물을평가하는함수
;-------------------------------------------------
@REVIEW_GIFT(CHARA, GIFT_ID, ORIGINAL_SENSE, ORIGINAL_PRICE)
#DIM CHARA
#DIM GIFT_ID
#DIMS SENSE_CHARA_GOOD
#DIMS SENSE_CHARA_BAD
#DIMS SENSE_GIFT
#DIM SENSE_GOOD
#DIM SENSE_BAD
#DIM MAGNIF_PRICE
#DIM BASIC_PRICE
#DIMS ORIGINAL_SENSE
#DIM ORIGINAL_PRICE

IF ORIGINAL_SENSE != ""
	SENSE_GIFT = %ORIGINAL_SENSE%
	IF ORIGINAL_PRICE >= 10000
		SENSE_GIFT += "고급/"
	ELSEIF ORIGINAL_PRICE < 1500
		SENSE_GIFT += "싸구려/"
	ENDIF
ELSE
;SENSE추출
	SENSE_GIFT = 
	CALL GIFTDATA_ADJECT(GET_GIFTDATA(GIFT_ID, "형용사"))
	SENSE_GIFT += RESULTS:1
	MAGNIF_PRICE = RESULT:1

	CALL GIFTDATA_MAIN(GET_GIFTDATA(GIFT_ID, "본체"))
	SENSE_GIFT += RESULTS:1
	BASIC_PRICE = RESULT:1

	;정가를 보고 10,000이상이면고급、1,500미만이면싸구려
	IF BASIC_PRICE * MAGNIF_PRICE / 100 >= 10000
		SENSE_GIFT += "고급/"
	ELSEIF BASIC_PRICE * MAGNIF_PRICE / 100 < 1500
		SENSE_GIFT += "싸구려/"
	ENDIF
ENDIF


;캐릭터의 감성
SENSE_CHARA_GOOD = %GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함")%
SENSE_CHARA_BAD = %GET_STR(, "캐릭터데이터", CHARA, "감성：싫어함")%

;공통설정：좋아함
;［유치］는 캐릭터가 좋아함
SIF TALENT:CHARA:유치
	SENSE_CHARA_GOOD += "캐릭터/" * 3
;［요정］는 자연을 좋아함
SIF CHECK_CHARA(CHARA, "요정")
	SENSE_CHARA_GOOD += "자연/" * 3
;［츠쿠모가미］는 츠쿠모가미랑 도구를 좋아함
SIF CHECK_CHARA(CHARA, "츠쿠모가미")
	SENSE_CHARA_GOOD += "츠쿠모가미/도구/" * 3
;요괴는 요괴를 좋아함
SIF CHECK_CHARA(CHARA, "요괴")
	SENSE_CHARA_GOOD += "요괴/" * 3
;CSV의전투능력이 있다면 스펠카드를 좋아함
SIF CSVABL(CHARA, GETNUM(ABL, "전투능력")) > 0
	SENSE_CHARA_GOOD += "스펠카드/" * 3
;캐논볼은 모두가 탐내다
SENSE_CHARA_GOOD += "캐논볼/"
;섹프, 애욕은 로터를 좋아함
SIF TALENT:CHARA:섹프 || TALENT:CHARA:애욕
	SENSE_CHARA_BAD += "로터/"

;공통설정：싫어함
;조악한건 모두 싫어한다 
SIF 1
	SENSE_CHARA_BAD += "조악/" * 6
;요괴・유령은 주술을 좋아하지 않는다면 주술은 싫어한다
IF (CHECK_CHARA(CHARA, "요괴") || CHECK_CHARA(CHARA, "유령")) && !STRCOUNT(SENSE_CHARA_GOOD, "주술/")
	SENSE_CHARA_BAD += "주술/" * 3
	;법구를 좋아하지 않는다면 법구는 싫어함
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "법구/")
		SENSE_CHARA_BAD += "법구/" * 3
ENDIF
;요괴ではなく요술が好きでないなら요술は嫌い,もしくは주술が好きなら요술は嫌い
IF (!CHECK_CHARA(CHARA, "요괴") && !STRCOUNT(SENSE_CHARA_GOOD, "요술/")) || STRCOUNT(SENSE_CHARA_GOOD, "주술/")
	SENSE_CHARA_BAD += "요술/" * 3
	;거듭해서 뼈、피、독 또한 마찬가지
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "뼈/")
		SENSE_CHARA_BAD += "뼈/" * 3
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "피/")
		SENSE_CHARA_BAD += "피/" * 3
	SIF !STRCOUNT(SENSE_CHARA_GOOD, "독/")
		SENSE_CHARA_BAD += "독/" * 3
ENDIF
;술내성이 낮으면 술을 싫어한다
IF TALENT:CHARA:술내성 < -1
	SENSE_CHARA_BAD += "술/" * 3
ELSEIF TALENT:CHARA:술내성 < 0
	SENSE_CHARA_BAD += "술/"
ENDIF
;CSV의 전투능력이 없다면 스펠카드는 좀…
SIF CSVABL(CHARA, GETNUM(ABL, "전투능력")) <= 0
	SENSE_CHARA_BAD += "스펠카드/"
;로터는 섹프, 애욕이 아니면 싫어하고, 연모, 연인의 경우 경감
IF !TALENT:CHARA:섹프 && !TALENT:CHARA:애욕
	IF TALENT:CHARA:연인
		SENSE_CHARA_BAD += "로터/" * 2
	ELSEIF TALENT:CHARA:연모
		SENSE_CHARA_BAD += "로터/" * 3
	ELSE
		SENSE_CHARA_BAD += "로터/" * 6
	ENDIF
ENDIF

;평가득점
SENSE_GOOD = 300 + 100 * STR_MULTI_COUNT(SENSE_CHARA_GOOD, SENSE_GIFT)

;;무드에 의한 보정,※봉인했습니다
;IF BASE:CHARA:무드 >= MAXBASE:CHARA:무드
;	TIMES SENSE_GOOD, 1.00
;ELSEIF BASE:CHARA:무드 >= MAXBASE:CHARA:무드 / 2
;	TIMES SENSE_GOOD, 0.80
;ELSEIF BASE:CHARA:무드 >= MAXBASE:CHARA:무드 / 4
;	TIMES SENSE_GOOD, 0.60
;ELSE
;	TIMES SENSE_GOOD, 0.40
;ENDIF

;;자택위치에 의한 보정,※봉인했습니다
;SIF CFLAG:CHARA:데이트중  == CFLAG:CHARA:자택위치 / 100
;	TIMES SENSE_GOOD, 0.80

;축일에 의한 보정
SIF FESTIVAL() != ""
	TIMES SENSE_GOOD, 1.25

;평가감점
SENSE_BAD = 300 + 100 * STR_MULTI_COUNT(SENSE_CHARA_BAD, SENSE_GIFT)

;総평가
RESULT = MIN(SENSE_GOOD * 1000 / (SENSE_GOOD + SENSE_BAD), 999)
[IF_DEBUG]
;	PRINTFORML 좋아함：%SENSE_CHARA_GOOD%
;	PRINTFORML 싫어함：%SENSE_CHARA_BAD%
;	PRINTFORML 물건：%SENSE_GIFT%
;	PRINTFORML 득점：{SENSE_GOOD}
;	PRINTFORML 감점：{SENSE_BAD}
;	PRINTFORML 결과：{RESULT}
;	WAIT
[ENDIF]
RESULTS = %SENSE_GIFT%
RETURN RESULT


;-------------------------------------------------
;하루의 끝에 선물을 즐겨찾는 함수
;-------------------------------------------------
@GIFT_FAVORITE
#DIM CHARA
#DIM GIFT_ID
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIM GIFT_SCORE, 3
#DIMS GIFT_NAME
#DIMS SENSE
FOR CHARA, 1, CHARANUM
	SIF !TCVAR:CHARA:오늘의선물
		CONTINUE
	
	GIFT_ID = TCVAR:CHARA:오늘의선물
	GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "형용사")
	GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "본체")
	GIFT_SCORE:1 = GET_GIFTDATA(GIFT_ID, "득점")
	
	;선물데이터 참조
	CALL GIFTDATA_ADJECT(GIFT_ADJECT)
	LOCALS:1 = %RESULTS:0%
	SENSE = %RESULTS:1%
	CALL GIFTDATA_MAIN(GIFT_MAIN)
	SENSE += RESULTS:1
	LOCALS:2 = %RESULTS:0%
	CALL GIFTNAME(TCVAR:CHARA:오늘의선물, CHARA)
	GIFT_NAME = %RESULTS%
;	PRINTFORMW SCORE{GIFT_SCORE:1}
	
	
	PRINTL 
	PRINTFORM %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:MASTER%에게 받은 %GIFT_NAME%
	IF GIFT_SCORE:1 >= 700
		IF STRCOUNT(SENSE, "움켜쥠/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 소중하게 움켜쥐고 잠들었다……。
		ELSEIF STRCOUNT(SENSE, "품음/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 생각하며 끌어안았다……。
		ELSEIF STRCOUNT(SENSE, "몸에걸침/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 시험삼아 몸에 걸치고, 거울 앞에서 얼굴을 붉혔다……。
		ELSE
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 넋을 잃고 바라보다가 잠이 들었다……。
		ENDIF
		CALL KOJO_MESSAGE_SEND("GIFT", 2, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
		;현재의 선물과 비교해서 마음에 들게한다
		IF CFLAG:CHARA:받은선물
			GIFT_SCORE:2 = GET_GIFTDATA(CFLAG:CHARA:받은선물, "득점")
		ELSE
			GIFT_SCORE:2 = 0
		ENDIF
		IF GIFT_SCORE:1 >= GIFT_SCORE:2
			CFLAG:CHARA:받은선물 = TCVAR:CHARA:오늘의선물
			CALL COLORMESSAGE(@"%조사처리(GIFT_NAME,"는")% %CALLNAME:CHARA%의 마음에 들었다!", C_YELLOW, 2)
			CALL KOJO_MESSAGE_SEND("GIFT", 3, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
		ENDIF
	ELSEIF GIFT_SCORE:1 >= 400
		IF STRCOUNT(SENSE, "움켜쥠/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 선반 위에 놓았다……。
		ELSEIF STRCOUNT(SENSE, "품음/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 책상 위에 놓았다……。
		ELSEIF STRCOUNT(SENSE, "몸에걸침/")
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 옷장에 넣었다……。
		ELSE
			PRINTFORMW %조사만처리(GIFT_NAME,"를")% 일단 보관했다……。
		ENDIF
		CALL KOJO_MESSAGE_SEND("GIFT", 4, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
	ELSE
		PRINTFORMW %조사만처리(GIFT_NAME,"를")% 슬쩍 저당잡았다……。
		CALL KOJO_MESSAGE_SEND("GIFT", 5, CHARA, GIFT_ID, GIFT_SCORE:1, GIFT_NAME, SENSE)
	ENDIF
	PRINTL 
NEXT

;-------------------------------------------------
;Look안에서 받은선물을 표시하는 함수
;-------------------------------------------------
@LOOK_GIFT(CHARA)
#DIM CHARA
#DIM GIFT_NAME
#DIM GIFT_SCORE
;우후후중에는 표시하지않는다
SIF CFLAG:CHARA:우후후
	RETURN

;즐겨찾기
IF CFLAG:CHARA:받은선물
	PRINTL 
	PRINT 【마음에 듦】
	CALL COLORMESSAGE("★", C_YELLOW, 0)
	CALL GIFTNAME(CFLAG:CHARA:받은선물, CHARA)
	PRINTFORM %RESULTS%
	[IF_DEBUG]
		CALL PRINT_GIFTDATA(CFLAG:CHARA:받은선물, CHARA, "디버그")
	[ENDIF]
;마음에 들지 않는 경우
ELSEIF CFLAG:CHARA:선물스톡
	GIFT_SCORE = GET_GIFTDATA(CFLAG:CHARA:선물스톡, "득점")
	;400미만은 휴대하지 않는다
	IF GIFT_SCORE < 400
		RETURN
	ELSE
		;평가득점이 높을수록 휴대하기 쉽다
		SIF CFLAG:CHARA:오늘의운세 > GIFT_SCORE
			RETURN
	ENDIF
	PRINTL 
	PRINT 【 휴대 】
	CALL GIFTNAME(CFLAG:CHARA:선물스톡, CHARA)
	PRINTFORM %RESULTS%
ENDIF

;-------------------------------------------------
;선물 데이터를 표시하는 함수
;-------------------------------------------------
@PRINT_GIFTDATA(GIFT_ID, CHARA, ARGS)
#DIM GIFT_ID
#DIM CHARA
#DIM 횟수
#DIM GIFT_SCORE
#DIM 받은날
#DIM 산장소
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIMS GIFT_NAME
#DIM MAGNIF_PRICE
#DIM SELL_AREA_ADJECT
#DIM BASIC_PRICE
#DIM SELL_AREA_MAIN
#DIMS SENSE

횟수 = GET_GIFTDATA(GIFT_ID, "횟수")
GIFT_SCORE = GET_GIFTDATA(GIFT_ID, "득점")
받은날 = GET_GIFTDATA(GIFT_ID, "날짜")
산장소 = GET_GIFTDATA(GIFT_ID, "장소")
GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "형용사")
GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "본체")
CALL GIFTDATA_ADJECT(GIFT_ADJECT)
MAGNIF_PRICE = RESULT:1
SELL_AREA_ADJECT = RESULT:2
IF GIFT_ADJECT == 9999
	IF ARGS == "당신의"
		LOCALS:1 = %GIFT_GAVE_ORI_ADJECT:CHARA:(횟수 %100)%
	ELSE
		LOCALS:1 = %GIFT_GOT_ORI_ADJECT:CHARA:(횟수 %100)%
	ENDIF
ELSE
	LOCALS:1 = %RESULTS:0%
ENDIF
SENSE = %RESULTS:1%
CALL GIFTDATA_MAIN(GIFT_MAIN)
BASIC_PRICE = RESULT:1
SELL_AREA_MAIN = RESULT:2
IF GIFT_MAIN == 999
	IF ARGS == "당신의"
		LOCALS:2 = %GIFT_GAVE_ORI_MAIN:CHARA:(횟수 %100)%
	ELSE
		LOCALS:2 = %GIFT_GOT_ORI_MAIN:CHARA:(횟수 %100)%
	ENDIF
ELSE
	LOCALS:2 = %RESULTS:0%
ENDIF
IF ARGS == "당신의"
	CALL GIFTNAME(GIFT_ID, CHARA, 1)
	GIFT_NAME = %RESULTS%
ELSE
	CALL GIFTNAME(GIFT_ID, CHARA)
	GIFT_NAME = %RESULTS%
ENDIF
SENSE += RESULTS:1

IF ARGS == "당신의"
	PRINTFORM [{횟수, 2}] %GIFT_NAME, 36, LEFT%
	CALL REVIEW_STARS(GIFT_SCORE)
	PRINT 　
	CALL PRINT_DATE(받은날)
	IF 산장소 == 99
		PRINTFORM %조사처리(CALLNAME:CHARA,"로")%부터 직접 받은
	ELSE
	PRINTFORM %GET_MAPNAME(산장소)%에서 돌아오는 길에 \@ 횟수 == 1 ? 처음으로  # \@선물받았다.
	PRINTL 
	ENDIF
ELSEIF ARGS == "캐릭터의"
	IF CFLAG:CHARA:받은선물 == GIFT_ID
		CALL COLORMESSAGE("★", C_YELLOW, 0)
		PRINTFORM %GIFT_NAME, 34, LEFT%
	ELSE
		PRINTFORM %GIFT_NAME, 36, LEFT%
	ENDIF
	CALL REVIEW_STARS(GIFT_SCORE)
	PRINT 　
	CALL PRINT_DATE(받은날)
	;PRINTFORM %GIFTSHOP_NAME(산장소)%에서 \@ 횟수 == 1 ? 처음으로 # {횟수}번째로 \@ 사주었다.
	PRINTFORM \@ 산장소 == 99 ? 특별 주문하여 # %GIFTSHOP_NAME(산장소)%에서 \@\@ 횟수 == 1 ? 처음으로 # {횟수}번째로 \@사주었다.
	PRINTL 
ELSEIF ARGS == "디버그"
	PRINTL 
	CALL PRINT_DATE(받은날)
	;PRINTFORML %GIFTSHOP_NAME(산장소)%에서 %CALLNAME:CHARA%에{횟수}回目に買ってあげた%GIFT_NAME%
	PRINTFORM \@ 산장소 == 99 ? 특별 주문하여 # %GIFTSHOP_NAME(산장소)%에서 \@\@ 횟수 == 1 ? 처음으로 # {횟수}번째로 \@사주었다.
	PRINTFORML 정가는 {BASIC_PRICE} * {MAGNIF_PRICE}\% = \\{BASIC_PRICE * MAGNIF_PRICE / 100}
	PRINTFORML 평가득점은　{GIFT_SCORE}점
	PRINTFORM 이 형용사를 파는 곳은
	SIF GETBIT(SELL_AREA_ADJECT, 0)
		PRINT 　신사
	SIF GETBIT(SELL_AREA_ADJECT, 1)
		PRINT 　절　
	SIF GETBIT(SELL_AREA_ADJECT, 2)
		PRINT 　마을
	SIF GETBIT(SELL_AREA_ADJECT, 3)
		PRINT 　호수
	SIF GETBIT(SELL_AREA_ADJECT, 4)
		PRINT 　죽림
	SIF GETBIT(SELL_AREA_ADJECT, 5)
		PRINT 　숲　
	SIF GETBIT(SELL_AREA_ADJECT, 6)
		PRINT 　명계
	SIF GETBIT(SELL_AREA_ADJECT, 7)
		PRINT 　산록
	SIF GETBIT(SELL_AREA_ADJECT, 8)
		PRINT 　산정
	SIF GETBIT(SELL_AREA_ADJECT, 9)
		PRINT 　지저
	SIF GETBIT(SELL_AREA_ADJECT, 10)
		PRINT 　달　
	PRINTL 
	PRINTFORM 이 본체를 파는 곳은　
	SIF GETBIT(SELL_AREA_MAIN, 0)
		PRINT 　신사
	SIF GETBIT(SELL_AREA_MAIN, 1)
		PRINT 　절　
	SIF GETBIT(SELL_AREA_MAIN, 2)
		PRINT 　마을
	SIF GETBIT(SELL_AREA_MAIN, 3)
		PRINT 　호수
	SIF GETBIT(SELL_AREA_MAIN, 4)
		PRINT 　죽림
	SIF GETBIT(SELL_AREA_MAIN, 5)
		PRINT 　숲　
	SIF GETBIT(SELL_AREA_MAIN, 6)
		PRINT 　명계
	SIF GETBIT(SELL_AREA_MAIN, 7)
		PRINT 　산록
	SIF GETBIT(SELL_AREA_MAIN, 8)
		PRINT 　산정
	SIF GETBIT(SELL_AREA_MAIN, 9)
		PRINT 　지저
	SIF GETBIT(SELL_AREA_MAIN, 10)
		PRINT 　달　
	PRINTL 
	IF STRCOUNT(SENSE, "움켜쥠/")
		PRINTFORML 움켜쥘 수 있는 것이다
	ELSEIF STRCOUNT(SENSE, "품음/")
		PRINTFORML 껴안을 수 있는 것이다
	ELSEIF STRCOUNT(SENSE, "몸에걸침/")
		PRINTFORML 몸에 걸치는 것이다
	ELSE
		PRINTFORML 바라보는 것이다
	ENDIF
	PRINTFORML SENSE：%SENSE%
	RETURN
ENDIF
;-------------------------------------------------
;선물데이터를 추출하는 함수
;-------------------------------------------------
@GET_GIFTDATA(GIFT_ID, ARGS)
#FUNCTION
#DIM GIFT_ID
SELECTCASE ARGS
CASE "횟수"
	RETURNF GIFT_ID / 10000000000000000
CASE "득점"
	RETURNF GIFT_ID / 10000000000000 % 1000
CASE "날짜"
	RETURNF GIFT_ID / 1000000000 % 10000
CASE "장소"
	RETURNF GIFT_ID / 10000000 % 100
CASE "형용사"
	RETURNF GIFT_ID / 1000 % 10000
CASE "본체"
	RETURNF GIFT_ID % 1000
ENDSELECT

;-------------------------------------------------
;선물 이름을 출력하는 함수
;FUNCTIONS으로 하고싶었지만 CALL하지못하고 울다가 이모양으로…
;-------------------------------------------------
@GIFTNAME(GIFT_ID, CHARA, FROM)
#DIM CHARA
#DIM GIFT_ID
#DIM GIFT_ADJECT
#DIM GIFT_MAIN
#DIM FROM
#DIM 횟수
GIFT_ADJECT = GET_GIFTDATA(GIFT_ID, "형용사")
GIFT_MAIN = GET_GIFTDATA(GIFT_ID, "본체")
횟수 = GET_GIFTDATA(GIFT_ID, "횟수") % 100
IF GIFT_ADJECT == 9999
	IF FROM
		LOCALS:1 = %GIFT_GAVE_ORI_ADJECT:CHARA:횟수%
	ELSE
		LOCALS:1 = %GIFT_GOT_ORI_ADJECT:CHARA:횟수%
	ENDIF
ELSE
	CALL GIFTDATA_ADJECT(GIFT_ADJECT)
	LOCALS:1 = %RESULTS:0%
ENDIF
IF GIFT_MAIN == 999
	IF FROM
		LOCALS:2 = %GIFT_GAVE_ORI_MAIN:CHARA:횟수%
	ELSE
		LOCALS:2 = %GIFT_GOT_ORI_MAIN:CHARA:횟수%
	ENDIF
ELSE
	CALL GIFTDATA_MAIN(GIFT_MAIN)
	LOCALS:2 = %RESULTS:0%
ENDIF

RESULTS = %LOCALS:1%%LOCALS:2%
RETURN

;-------------------------------------------------
;인수에 지역을 나열하면 그 곳의 bit를 넣어 반환하는 함수Ver.2.0
;"전부"를 넣으면 전부 집어넣은 수치 (2^11-1)를 되돌린다
;"이외"를 넣으면 거꾸로 된다
;COMMON에 넣어도 좋을지도 모른다(어디에 쓸지 몰라）
;-------------------------------------------------
@AREA_SETBIT(ARGS)
#FUNCTION
VARSET RESULT
SIF STRCOUNT(ARGS, "전부")
	RETURNF 2047

SIF STRCOUNT(ARGS, "신사")
	SETBIT RESULT, 0
SIF STRCOUNT(ARGS, "절")
	SETBIT RESULT, 1
SIF STRCOUNT(ARGS, "마을")
	SETBIT RESULT, 2
SIF STRCOUNT(ARGS, "호수")
	SETBIT RESULT, 3
SIF STRCOUNT(ARGS, "죽림")
	SETBIT RESULT, 4
SIF STRCOUNT(ARGS, "숲")
	SETBIT RESULT, 5
SIF STRCOUNT(ARGS, "명계")
	SETBIT RESULT, 6
SIF STRCOUNT(ARGS, "산록")
	SETBIT RESULT, 7
SIF STRCOUNT(ARGS, "산정")
	SETBIT RESULT, 8
SIF STRCOUNT(ARGS, "지저")
	SETBIT RESULT, 9
SIF STRCOUNT(ARGS, "달")
	SETBIT RESULT, 10

IF STRCOUNT(ARGS, "이외")
	FOR LOCAL, 0, 11
		INVERTBIT RESULT, LOCAL
	NEXT
ENDIF
RETURNF RESULT

;-------------------------------------------------
;デート帰りの贈り物イベントが発生するかのチェック
;-------------------------------------------------
@GIFT_DATE_CHECK(CHARA)
#FUNCTION
#DIM CHARA
#DIM 上限
#DIM 発生率
;祭日でないなら上限80%
IF FESTIVAL() == ""
	上限 = 80
ELSE
	上限 = 100
ENDIF

;모브코
SIF TALENT:CHARA:스침
	RETURNF 0
;すでに渡している
SIF DAY == GET_GIFTDATA(CFLAG:CHARA:건네준선물, "날짜")
	RETURNF 0
;前回の贈り物から一週間以上経ってる(祭日を除く）
SIF DAY - GET_GIFTDATA(CFLAG:CHARA:건네준선물, "날짜") < 7 && FESTIVAL() == ""
	RETURNF 0
;반발각인,언짢음,분노
SIF MARK:CHARA:반발각인 || TALENT:CHARA:기분 < 0 || CFLAG:CHARA:빡침
	RETURNF 0
;무드1220未満
SIF BASE:CHARA:무드 < 1220
	RETURNF 0

;친밀5以降から친밀1につき5%上昇
発生率 = (ABL:CHARA:친밀 - 4) * 5
;祭日なら1.5倍
SIF FESTIVAL() != ""
	TIMES 発生率, 1.5
;연인なら1.5倍
SIF TALENT:CHARA:연인
	TIMES 発生率, 1.5
;発生率%で発生
SIF MIN(発生率, 上限) > RAND:100
	RETURNF 1
RETURNF 0

;-------------------------------------------------
;데이트 귀가시 선물 이벤트
;-------------------------------------------------
@GIFT_DATE_EVENT(CHARA)
#DIM CHARA
#DIM GIFT_ID
#DIMS GIFT_NAME
#DIM 渡した횟수
#DIM 点数
#DIM 本人の評価点
#DIMS GIFT_SENSE

PRINTL 
PRINTFORM 데이트에서 돌아오는 길, 
SELECTCASE CFLAG:CHARA:성격경향
;気弱系
CASE 1
	PRINTFORML %조사처리(CALLNAME:CHARA,"는")% 머뭇거리는 모습으로 품 속에서 무언가를 꺼내어, 조심스럽게 %CALLNAME:MASTER%에게 내밀어 왔다…
;強気系
CASE 2
	PRINTFORML %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:MASTER%의 얼굴을 보고, 생각났다는 듯이 무언가를 꺼내면서 자랑스럽게 그것을 건네주었다…
;素直系
CASE 3
	PRINTFORML %조사처리(CALLNAME:CHARA,"는")% 물건을 조심스럽게 꺼내면서 %CALLNAME:MASTER%에게 준다고 말하면서 그것을 건네주었다…
;真가면目系
CASE 4
	PRINTFORML %조사처리(CALLNAME:CHARA,"는")% %CALLNAME:MASTER%에게 주고 싶은 물건이 있다고 말하면서, 상자를 꺼내어 정중하게 내밀었다…
;その他
CASEELSE
	PRINTFORML %조사처리(CALLNAME:CHARA,"는")% 주고 싶은 것이 있다고 하면서 무언가를 꺼내어, %CALLNAME:MASTER%에게 내밀어 왔다…
ENDSELECT
PRINTW 

;점수는 필요없지만 일단
点数 = 0

;贈り物生成
CALL CREATE_GIFT_FROM_CHARA(CHARA, 701, 1000)
GIFT_ID = RESULT
GIFT_NAME = %RESULTS%
渡した횟수 = GET_GIFTDATA(CFLAG:CHARA:건네준선물, "횟수") + 1
本人の評価点 = RESULT:1
;ID의 설정
GIFT_ID = 渡した횟수 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + CFLAG:MASTER:데이트중 * 10000000 + GIFT_ID
GIFT_GAVE:CHARA:(渡した횟수 % 100) = GIFT_ID
CFLAG:CHARA:건네준선물 = GIFT_ID
CALL KOJO_MESSAGE_SEND("GIFT", 1, CHARA, GIFT_ID, 本人の評価点, GIFT_NAME, GIFT_SENSE)
PRINTFORMW 【%GIFT_NAME%】을(를) 받았다！

;-------------------------------------------------
;기념품점の이름を返す関数
;-------------------------------------------------
@GIFTSHOP_NAME(산장소, コマンド)
#FUNCTIONS
#DIM 산장소
#DIM コマンド
SELECTCASE 산장소
CASE 5
	RETURNF "향림당"
CASEELSE
	IF コマンド
		RETURNF "기념품점"
	ELSE
		RETURNF GET_MAPNAME(산장소)
	ENDIF
ENDSELECT

;-------------------------------------------------
;점수에 따른 ★를 표시하는 함수
;평가방법이 특수하기 때문에 표시 개수도 변칙적
;-------------------------------------------------
@REVIEW_STARS(SCORE)
#DIM SCORE
#DIM STAR
#DIM STARCOLOR
SELECTCASE SCORE
CASE IS >= 800
	STAR = 5
CASE IS >= 700
	STAR = 4
CASE IS >= 500
	STAR = 3
CASE IS >= 400
	STAR = 2
CASEELSE
	STAR = 1
ENDSELECT
FOR LOCAL, 1, 6
	IF LOCAL <= STAR
		IF STAR >= 4
			SETCOLOR C_ORANGE
		ELSEIF STAR <= 1
			SETCOLOR C_AQUA
		ELSE
			SETCOLOR C_YELLOW
		ENDIF
		PRINT ★
	ELSE
		SETCOLOR C_GRAY
		PRINT ☆
	ENDIF
NEXT
RESETCOLOR

;-------------------------------------------------
;SHOPから呼ばれる表示関数
;-------------------------------------------------
@SHOW_COLLECTION_GIFT
PRINTFORML ☆받은선물☆
FOR LOCAL, 1, CHARANUM
	IF !CFLAG:LOCAL:출금 && !TALENT:LOCAL:스침
		SIF !GIFT_GAVE:LOCAL:1
			SETCOLOR C_GRAY
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL, 15, LEFT%
		RESETCOLOR
	ENDIF
NEXT
PRINTL 
DRAWLINE
PRINTFORML [999] - 돌아간다
INPUT

IF RESULT == 999
		RETURN
ELSEIF INRANGE(RESULT, 1, CHARANUM) && !CFLAG:RESULT:출금 && !TALENT:RESULT:스침
	CALL SHOW_GIFT_GAVE(RESULT)
ENDIF
RESTART

;-------------------------------------------------
;받은선물을 표시하는 함수
;-------------------------------------------------
@SHOW_GIFT_GAVE(CHARA)
#DIM CHARA
#DIM SCORE
#DIM GIFT_NUM
#DIM LINE
#DIM FAVO
PRINTFORML 　☆%CALLNAME:CHARA%에게서 받은 선물　　　　　　　　　●선택하시면 리뷰가 가능합니다
FOR LOCAL, 0, 100
	;フラグ0番が100を兼ねているのでこの形に
	SIF GIFT_GAVE:CHARA:((LOCAL + 1) % 100)
		CALL PRINT_GIFTDATA(GIFT_GAVE:CHARA:((LOCAL + 1) % 100), CHARA, "당신의")
	IF !GIFT_GAVE:CHARA:((LOCAL + 1) % 100)
		LINE = LOCAL
		BREAK
	ENDIF
NEXT
DRAWLINE
CALL GIFTNAME(ITEM:마음에드는아이템, CHARA, 1)
PRINTFORML ☆현재 마음에 드는 선물：%RESULTS%
CALL GIFTNAME(FAVO, CHARA, 1)
PRINTFORML [996] - 이전캐릭터
PRINTFORML [997] - 다음캐릭터
PRINTFORML [999] - 돌아간다　　　　　　　　　　　　　　 \@ FAVO ? [998] %조사처리(RESULTS,"을")% 마음에 드는 것으로 한다 # \@
INPUT
IF RESULT == 999
	FAVO = 0
	RETURN
ELSEIF RESULT == 998
	ITEM:마음에드는아이템 = FAVO
ELSEIF RESULT == 997
	FOR LOCAL, CHARA + 1, CHARANUM
		IF GIFT_GAVE:LOCAL:1
			CHARA = LOCAL
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
	FOR LOCAL, 1, CHARA + 1
		IF GIFT_GAVE:LOCAL:1
			CHARA = LOCAL
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
ELSEIF RESULT == 996
	FOR LOCAL, 1, CHARA
		LOCAL:1 = CHARA - LOCAL
		IF GIFT_GAVE:(LOCAL:1):1
			CHARA = LOCAL:1
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
	FOR LOCAL, 1, CHARANUM
		LOCAL:1 = CHARANUM - LOCAL
		IF GIFT_GAVE:(LOCAL:1):1
			CHARA = LOCAL:1
			CLEARLINE 7 + LINE
			RESTART
		ENDIF
	NEXT
ELSEIF INRANGE(RESULT, 0, 100) && GIFT_GAVE:CHARA:RESULT
	GIFT_NUM = RESULT
	;별의 수가 변칙적이어서
	SELECTCASE GET_GIFTDATA(GIFT_GAVE:CHARA:GIFT_NUM, "득점")
	CASE IS >= 800
		SCORE = 0
	CASE IS >= 700
		SCORE = 800
	CASE IS >= 500
		SCORE = 700
	CASE IS >= 400
		SCORE = 500
	CASEELSE
		SCORE = 400
	ENDSELECT
	GIFT_GAVE:CHARA:GIFT_NUM -= (GET_GIFTDATA(GIFT_GAVE:CHARA:GIFT_NUM, "득점") * 10000000000000)
	GIFT_GAVE:CHARA:GIFT_NUM += (SCORE * 10000000000000)
	FAVO = GIFT_GAVE:CHARA:GIFT_NUM
ENDIF
CLEARLINE 7 + LINE
RESTART

;-------------------------------------------------
;キャラ→あなたの贈り物オーダーメイドする関数
;◆使い方◆
;CHARA    あなたに渡すキャラの番号
;ARGS:1   形容詞名
;ARGS:2   本体名
;
;口上等のイベントで使われることを想定した贈り物をオーダーメイド生成して、あなたの貰った贈り物に登録する関数です
;形容詞と本体名を設定して呼び出すと、指定の贈り物がキャラからあなたへ渡されます
;あなたの貰った贈り物はシステム上では完全にフレーバーなので何を渡しても自由です
;【레트로한＋사진】等、既存のものでも良いですし
;【%CALLNAME:CHARA%モデルの＋오나홀】みたいなオリジナルアイテムも生成可能です
;口上内の専用フラグを使えば連動させることもできます
;ARGSに"ランダム"を入れると、ランダムなものが選ばれます
;関数を呼んだ時点であなたの貰った贈り物として登録されます
;地の文等はありません
;RESULTS:0に贈り物の名前(ARGS:1＋ARGS:2）が入ります
;GIFT_IDに登録される日付は現在のDAY、場所は99(特注品）になります
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_CHARA(CHARA, ARGS:1, ARGS:2)
#DIM CHARA
#DIM MAIN
#DIM ADJECT
#DIM BASIC_PRICE
#DIM MAGNIF_PRICE
#DIMS GIFTNAME_MAIN
#DIMS GIFTNAME_ADJECT
#DIMS SENSE
#DIMS GIFTNAME
#DIM 渡した回数
#DIM 点数
#DIM GIFT_ID
SENSE = 
;渡した回数の設定
渡した回数 = GET_GIFTDATA(CFLAG:CHARA:건네준선물, "횟수") + 1

MAIN = 0
IF ARGS:2 == "ランダム"
	CALL DRAW_GIFT_MAIN(99)
	MAIN = RESULT
	BASIC_PRICE = RESULT:1
	GIFTNAME_MAIN = %RESULTS:0%
	SENSE += RESULTS:1
ELSE
	FOR LOCAL, 11, 550
		CALL GIFTDATA_MAIN(LOCAL)
		IF RESULTS == ARGS:2
			MAIN = LOCAL
			BREAK
		ENDIF
	NEXT
	IF MAIN
		CALL GIFTDATA_MAIN(MAIN)
		BASIC_PRICE = RESULT:1
		GIFTNAME_MAIN = %RESULTS:0%
		SENSE += RESULTS:1
	ELSE
		MAIN = 999
		BASIC_PRICE = 10000
		GIFTNAME_MAIN = %ARGS:2%
		SENSE += ARGS:2 + "オリジナル/"
		GIFT_GAVE_ORI_MAIN:CHARA:(渡した回数 % 100) = %GIFTNAME_MAIN%
	ENDIF
ENDIF

ADJECT = 0
IF ARGS:1 == "ランダム"
	CALL DRAW_GIFT_ADJECT(99)
	ADJECT = RESULT
	MAGNIF_PRICE = RESULT:1
	GIFTNAME_ADJECT = %RESULTS:0%
	SENSE += RESULTS:1
ELSE
	FOR LOCAL, 101, 2200
		CALL GIFTDATA_ADJECT(LOCAL)
		IF RESULTS == ARGS:1
			ADJECT = LOCAL
			BREAK
		ENDIF
	NEXT
	IF ADJECT
		CALL GIFTDATA_ADJECT(ADJECT)
		MAGNIF_PRICE = RESULT:1
		GIFTNAME_ADJECT = %RESULTS:0%
		SENSE += RESULTS:1
	ELSE
		ADJECT = 9999
		MAGNIF_PRICE = 300
		GIFTNAME_ADJECT = %ARGS:1%
		SENSE += ARGS:1 + "オリジナル/"
		GIFT_GAVE_ORI_ADJECT:CHARA:(渡した回数 % 100) = %GIFTNAME_ADJECT%
	ENDIF
ENDIF

;値段の設定(意味はない）
RESULT:1 = BASIC_PRICE * MAGNIF_PRICE / 100
;名前の設定
GIFTNAME = %GIFTNAME_ADJECT%%GIFTNAME_MAIN%
;IDの設定
GIFT_ID = ADJECT * 1000 + MAIN
;RESULTS設定
RESULTS:0 = %GIFTNAME%
RESULTS:1 = %SENSE%
;便宜上の点数
点数 = 0
;IDの設定
GIFT_ID = 渡した回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + 99 * 10000000 + GIFT_ID
;渡す処理
GIFT_GAVE:CHARA:(渡した回数 % 100) = GIFT_ID
CFLAG:CHARA:건네준선물 = GIFT_ID
[IF_DEBUG]
	CALL PRINT_GIFTDATA(GIFT_ID, CHARA, "デバッグ")
	PRINTFORMW 【%GIFTNAME%】を貰った！
[ENDIF]

;-------------------------------------------------
;あなた→キャラの贈り物オーダーメイドする関数
;◆使い方◆
;CHARA       あなたが渡すキャラの番号
;RETURN      1=あげた,-1=キャンセルした
;
;口上等のイベントで使われることを想定した贈り物をオーダーメイド生成してプレゼントする関数です
;指定したキャラへあなたがその場で贈り物を特注してプレゼントします
;※その日のうちにすでに土産屋で贈り物を買っていると重複してしまうので、必ず発生条件で"TCVAR:오늘의선물"を弾いてください
;全ての本体＆形容詞から自由に組み合わせることができます
;さらに【外国産の＋自動車】みたいなオリジナルアイテムも生成可能です
;その場合は各種特徴も同時に設定します
;※オリジナル扱いで既存の名称を使うと偽物認定されます(安価で指輪とか作ったり）
;ただし高得点を取ると携帯するので(自動車等だと）違和感が出ることを留意してください
;呼んだ時点で今日の贈り物として登録され、土産屋と同じ様に一日の終わりで評価点に応じてお気に入りになったりします
;当然ですが소지금を消費します(定価です）
;オリジナル本体は\10000、オリジナル形容詞は300%の倍率がかかります
;地の文等はありません
;RESULTS:0に贈り物の名前(形容詞＋本体名称）が入ります
;RESULT:1に点数(1～999）が入ります
;GIFT_IDに登録される日付は現在のDAY、場所は99(特注品）になります
;オリジナル品はデバッグ表示するとSENSEがズレてたりしますが、評価時にはちゃんと機能してます
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_MASTER(CHARA)
#DIM CHARA
#DIM START,20
#DIM ページ
#DIM 最後のページ
#DIM 本体
#DIM 形容詞
#DIMS 本体名称
#DIMS 形容詞名称
#DIMS 本体センス
#DIMS 形容詞センス
#DIMS GIFT_NAME
#DIMS SENSE
#DIM 全体価格
#DIM GIFT_ID
#DIM 点数
#DIM 貰った回数
#DIM 渡した回数
IF TCVAR:CHARA:오늘의선물
	PRINTFORMW ※注意：この関数を呼ぶ際は"TCVAR:오늘의선물"が有る場合は弾いてください
	RETURN
ENDIF
;▼本体部分
;各ページの頭番を設定する
IF !START
	VARSET LOCAL
	FOR LOCAL, 11, 550
		CALL GIFTDATA_MAIN(LOCAL)
		SIF RESULTS == ""
			CONTINUE
		LOCAL:1 ++
		IF LOCAL:1 == 21
			LOCAL:2 ++
			START:(LOCAL:2) = LOCAL
			LOCAL:1 = 1
		ENDIF
		LOCAL:3 = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = LOCAL:2 + 1
	START:最後のページ = LOCAL:3 + 1
ENDIF
VARSET LOCAL
PRINTFORML \@ CHARA ? %CALLNAME:CHARA%에게 건네줄 # \@ 선물을 고르자 (소지금：%금액표시(MONEY)%)
DRAWLINE
FOR LOCAL, START, START:ページ
	CALL GIFTDATA_MAIN(LOCAL)
	SIF RESULTS == ""
		CONTINUE
	PRINTFORML [{LOCAL, 3}] %RESULTS, 18, LEFT%%금액표시(RESULT:1), 9%
	LOCAL:1 ++
NEXT
WHILE LOCAL:1 < 20
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [999] 다음페이지
PRINTL [998] 이전페이지
PRINTFORML [997] 오리지널 (%금액표시(30000)%)
PRINTL [  0] 역시 그만둔다
INPUT
;그만둔다
IF !RESULT
	VARSET START
	RETURN -1
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;オリジナル
ELSEIF RESULT == 997
	PRINTFORML 선물(본체부분) 의 이름을 입력해주세요
	PRINTL [ 0] 역시 그만둔다
	INPUTS
	IF RESULTS == "0"
		RESTART
	ELSE
		本体 = 999
		本体名称 = %RESULTS%
		CALL GIFT_ORDER_MADE_ORIGINAL_DESIGN("本体", 本体名称)
		本体センス = %RESULTS%
		;違法チェック
		FOR LOCAL, 11, 550
			CALL GIFTDATA_MAIN(LOCAL)
			IF RESULTS == 本体名称
				本体センス += "조악/조악/조악/"
				BREAK
			ENDIF
		NEXT
	ENDIF
;選択
ELSEIF INRANGE(RESULT, 11, 550)
	LOCAL = RESULT
	CALL GIFTDATA_MAIN(RESULT)
	IF RESULTS != ""
		本体 = LOCAL
		本体名称 = %RESULTS%
		本体センス = %RESULTS:1%
	ENDIF
ENDIF
IF !本体
	CLEARLINE 28
	RESTART
ENDIF
;▼形容詞部分
CALL GIFT_ORDER_MADE_FROM_MASTER_2(CHARA, 本体, 本体名称)
SIF RESULT == -1
	RESTART
VARSET START
形容詞 = RESULT
全体価格 = RESULT:1
形容詞名称 = %RESULTS%
形容詞センス = %RESULTS:1%
GIFT_NAME = %形容詞名称 + 本体名称%
SENSE = %本体センス + 形容詞センス%
GIFT_ID = 形容詞 * 1000 + 本体
DRAWLINE
PRINTFORML 【%GIFT_NAME%】%금액표시(全体価格)%
PRINTL 
IF MONEY < 全体価格
	CALL COLORMESSAGE(@"소지금이 부족합니다!", C_ORANGE, 1)
	CALL COLORMESSAGE(@"처음으로 돌아가겠습니다", C_ORANGE, 1)
	FORCEWAIT
	RESTART
ELSE
	PRINTFORML 이걸로 결정하겠습니다. 괜찮으시겠어요?
	PRINTL [1] 결정
	PRINTL [0] 다시한다
	INPUT
	SIF !RESULT
		RESTART
ENDIF
MONEY -= 全体価格
貰った回数 = GET_GIFTDATA(CFLAG:CHARA:선물스톡, "回数") + 1
渡した回数 = CFLAG:MASTER:건네준선물 / 1000 + 1
CALL REVIEW_GIFT(CHARA, GIFT_ID, SENSE, 全体価格)
点数 = RESULT
GIFT_ID = 貰った回数 * 10000000000000000 + 点数 * 10000000000000 + DAY * 1000000000 + 99 * 10000000 + GIFT_ID
;ストックにGIFT_IDを保存
CFLAG:CHARA:선물스톡 = GIFT_ID
;ストレージにも保存
GIFT_GOT:CHARA:(貰った回数 % 100) = CFLAG:CHARA:선물스톡
;CHARAはID,MASTERは渡したキャラを保存
TCVAR:CHARA:오늘의선물 = CFLAG:CHARA:선물스톡
TCVAR:MASTER:오늘의선물 = CHARA
;MASTERは渡した回数と渡したキャラを保存
CFLAG:MASTER:건네준선물 = 渡した回数 * 1000 + CHARA
;オリジナル名称を設定
GIFT_GOT_ORI_ADJECT:CHARA:(貰った回数 % 100) = %形容詞名称%
GIFT_GOT_ORI_MAIN:CHARA:(貰った回数 % 100) = %本体名称%
;点数
RESULT:1 = 点数
;이름
RESULTS = %GIFT_NAME%
[IF_DEBUG]
	CALL PRINT_GIFTDATA(GIFT_ID, CHARA, "デバッグ")
	PRINTFORMW 【%GIFT_NAME%】をプレゼントした
[ENDIF]
RETURN 1

;-------------------------------------------------
;あなた→キャラの贈り物オーダーメイドする関数の形容詞部分
;@GIFT_ORDER_MADE_FROM_MASTERの内部関数
;-------------------------------------------------
@GIFT_ORDER_MADE_FROM_MASTER_2(CHARA, 本体, 本体名称)
#DIM CHARA
#DIM START,50
#DIM ページ
#DIM 最後のページ
#DIM 本体
#DIM 形容詞
#DIMS 本体名称
#DIMS 形容詞名称
#DIM 本体価格
#DIM 全体価格
#DIMS 形容詞センス
;各ページの頭番を設定する
IF !START
	VARSET LOCAL
	FOR LOCAL, 101, 2200
		CALL GIFTDATA_ADJECT(LOCAL)
		SIF RESULTS == ""
			CONTINUE
		LOCAL:1 ++
		IF LOCAL:1 == 21
			LOCAL:2 ++
			START:(LOCAL:2) = LOCAL
			LOCAL:1 = 1
		ENDIF
		LOCAL:3 = LOCAL
	NEXT
	START = 1
	ページ = 1
	;最後のページと最後のページの頭番を設定
	最後のページ = LOCAL:2 + 1
	START:最後のページ = LOCAL:3 + 1
ENDIF
VARSET LOCAL
PRINTFORML \@ CHARA ? %CALLNAME:CHARA%에게 건네줄 # \@ 선물을 고르자 (소지금：%금액표시(MONEY)%)
CALL GIFTDATA_MAIN(本体)
本体価格 = RESULT:1
本体名称 = \@ 本体 == 999 ? %本体名称% # %RESULTS% \@
DRAWLINE
FOR LOCAL, START, START:ページ
	CALL GIFTDATA_ADJECT(LOCAL)
	SIF RESULTS == ""
		CONTINUE
	PRINTFORML [{LOCAL, 4}] %RESULTS + 本体名称, 38, LEFT%%금액표시(本体価格 * RESULT:1 / 100), 12%
	LOCAL:1 ++
NEXT
WHILE LOCAL:1 < 20
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [ 999] 다음페이지
PRINTL [ 998] 이전페이지
PRINTFORML [ 997] 오리지널 (%금액표시(本体価格 * 300 / 100)%)
PRINTL [   0] 돌아간다
INPUT
;戻る
IF !RESULT
	VARSET START
	RETURN -1
;次のページ
ELSEIF RESULT == 999
	ページ ++
	IF ページ > 最後のページ
		ページ = 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;前のページ
ELSEIF RESULT == 998
	ページ --
	IF ページ < 1
		ページ = 最後のページ
		START = START:(最後のページ - 1)
	ELSEIF ページ == 1
		START = 1
	ELSE
		START = START:(ページ - 1)
	ENDIF
;オリジナル
ELSEIF RESULT == 997
	PRINTFORML 선물(形容詞部分）의 명칭을 입력해 주세요
	PRINTL [ 0] 역시 그만둔다
	INPUTS
	IF RESULTS == "0"
		RESTART
	ELSE
		形容詞 = 9999
		形容詞名称 = %RESULTS%
		CALL GIFT_ORDER_MADE_ORIGINAL_DESIGN("形容詞", 形容詞名称 + 本体名称)
		形容詞センス = %RESULTS%
		全体価格 = 本体価格 * 300 / 100
		;違法チェック
		FOR LOCAL, 101, 2200
			CALL GIFTDATA_ADJECT(LOCAL)
			IF RESULTS == 形容詞名称
				形容詞センス += "조악/조악/조악/"
				BREAK
			ENDIF
		NEXT
	ENDIF
;選択
ELSEIF INRANGE(RESULT, 101, 2200)
	LOCAL = RESULT
	CALL GIFTDATA_ADJECT(RESULT)
	IF RESULTS != ""
		形容詞 = LOCAL
		形容詞名称 = %RESULTS%
		形容詞センス = %RESULTS:1%
		全体価格 = 本体価格 * RESULT:1 / 100
	ENDIF
ENDIF
IF !形容詞
	CLEARLINE 28
	RESTART
ENDIF
VARSET START
RESULT:1 = 全体価格
RESULTS = %形容詞名称%
RESULTS:1 = %形容詞センス%
RETURN 形容詞

;-------------------------------------------------
;贈り物オーダーメイドのSENSEを決める関数
;@GIFT_ORDER_MADE_FROM_MASTERの内部関数
;-------------------------------------------------
@GIFT_ORDER_MADE_ORIGINAL_DESIGN(ARGS, 名称)
#DIMS 名称
VARSET LOCALS
IF ARGS == "本体"
	PRINTFORML ◆【%名称%】에게 마음을 쏟는다면 어떻게 할까?
	PRINTL [ 1] 바라보는 것
	PRINTL [ 2] 쥐는 것
	PRINTL [ 3] 껴안는 것
	PRINTL [ 4] 몸에 걸치는 것
	INPUT
	IF RESULT == 2
		LOCALS:1 = 움켜쥠/
	ELSEIF RESULT == 3
		LOCALS:1 = 품음/
	ELSEIF RESULT == 4
		LOCALS:1 = 몸에걸침/
	ELSE
		LOCALS:1 = 바라봄/
	ENDIF
	PRINTFORML ◆【%名称%】는 어떤 것？
	PRINTL [ 1] 도구　　　　　　[ 2] 소형장식물　　　　[ 3] 장식품
	PRINTL [ 4] 인형　　　　　　[ 5] 도서　　　　　　　[ 6] 식기
	PRINTL [ 7] 악기　　　　　　[ 8] 날붙이　　　　　　[ 9] 탄환
	PRINTL [10] 미술품　　　　　[11] 생체품　　　
	INPUT
	IF RESULT == 2
		LOCALS:2 = 소형장식물/
	ELSEIF RESULT == 3
		LOCALS:2 = 장식/
	ELSEIF RESULT == 4
		LOCALS:2 = 인형/
	ELSEIF RESULT == 5
		LOCALS:2 = 도서/
	ELSEIF RESULT == 6
		LOCALS:2 = 식기/
	ELSEIF RESULT == 7
		LOCALS:2 = 악기/
	ELSEIF RESULT == 8
		LOCALS:2 = 날붙이/
	ELSEIF RESULT == 9
		LOCALS:2 = 탄환/
	ELSEIF RESULT == 10
		LOCALS:2 = 미술품/
	ELSEIF RESULT == 11
		LOCALS:2 = 생체품/
	ELSE
		LOCALS:2 = 도구/
	ENDIF
	PRINTFORML ◆【%名称%】는 더 자세히 말하자면 어떤 것？（임의）
	PRINTL [ 1] 부적　　　　　　[ 2] 법구　　　　　　[ 3] 유물
	PRINTL [ 4] 안경　　　　　　[ 5] 탈　　　　　　　[ 6] 시계
	PRINTL [ 7] 문구　　　　　　[ 8] 조명　　　　　　[ 9] 부채
	PRINTL [ 0] 어느 쪽도 아니다
	INPUT
	IF RESULT == 1
		LOCALS:3 = 부적/
	ELSEIF RESULT == 2
		LOCALS:3 = 법구/
	ELSEIF RESULT == 3
		LOCALS:3 = 유물/
	ELSEIF RESULT == 4
		LOCALS:3 = 안경/
	ELSEIF RESULT == 5
		LOCALS:3 = 탈/
	ELSEIF RESULT == 6
		LOCALS:3 = 시계/
	ELSEIF RESULT == 7
		LOCALS:3 = 문구/
	ELSEIF RESULT == 8
		LOCALS:3 = 조명/
	ELSEIF RESULT == 9
		LOCALS:3 = 부채/
	ENDIF
	RESULTS = %LOCALS:1 + LOCALS:2 + LOCALS:3%
ELSEIF ARGS == "形容詞"
	PRINTFORML ◆【%名称%】은(는) 무슨 색？
	PRINTL [ 1] 흰색　　　　　　[ 2] 흑색
	PRINTL [ 3] 빨간색　　　　　[ 4] 푸른색　　　　　[ 5] 초록색
	PRINTL [ 6] 노랑색　　　　　[ 7] 보라색　　　　　[ 8] 주홍색
	PRINTL [ 9] 핑크　　　　　　[10] 찻빛　　　　　　[11] 잿빛
	PRINTL [12] 금색　　　　　　[13] 은색　　　　　　[14] 무지개색
	INPUT
	IF RESULT == 2
		LOCALS:1 = 흑빛/흑빛/
	ELSEIF RESULT == 3
		LOCALS:1 = 빨강빛/빨강빛/
	ELSEIF RESULT == 4
		LOCALS:1 = 푸른빛/푸른빛/
	ELSEIF RESULT == 5
		LOCALS:1 = 초록빛/초록빛/
	ELSEIF RESULT == 6
		LOCALS:1 = 노랑빛/노랑빛/
	ELSEIF RESULT == 7
		LOCALS:1 = 보랏빛/보랏빛/
	ELSEIF RESULT == 8
		LOCALS:1 = 주홍빛/주홍빛/
	ELSEIF RESULT == 9
		LOCALS:1 = 핑크/핑크/
	ELSEIF RESULT == 10
		LOCALS:1 = 찻빛/찻빛/
	ELSEIF RESULT == 11
		LOCALS:1 = 잿빛/잿빛/
	ELSEIF RESULT == 12
		LOCALS:1 = 금빛/금빛/
	ELSEIF RESULT == 13
		LOCALS:1 = 은빛/은빛/
	ELSEIF RESULT == 14
		LOCALS:1 = 무지갯빛/무지갯빛/
	ELSE
		LOCALS:1 = 흰빛/흰빛/
	ENDIF
	PRINTFORML ◆【%名称%】는 어떤 인상？
	PRINTL [ 1] 귀여운
	PRINTL [ 2] 근사함
	PRINTL [ 3] 아름다운
	PRINTL [ 4] 무서움
	INPUT
	IF RESULT == 2
		LOCALS:2 = 근사함/근사함/근사함/
	ELSEIF RESULT == 3
		LOCALS:2 = 아름다운/아름다운/아름다운/
	ELSEIF RESULT == 4
		LOCALS:2 = 무서움/무서움/무서움/
	ELSE
		LOCALS:2 = 귀여운/귀여운/귀여운/
	ENDIF
	PRINTFORML ◆【%名称%】는 어떤 외형?（임의）
	PRINTL [ 1] 심플
	PRINTL [ 2] 기묘
	PRINTL [ 3] 예술적
	PRINTL [ 4] 멋쟁이
	PRINTL [ 0] 어느 쪽도 아니다
	INPUT
	IF RESULT == 1
		LOCALS:3 = 심플/심플/
	ELSEIF RESULT == 2
		LOCALS:3 = 기묘/기묘/
	ELSEIF RESULT == 3
		LOCALS:3 = 예술적/예술적/
	ELSEIF RESULT == 4
		LOCALS:3 = 멋쟁이/멋쟁이/
	ENDIF
	PRINTFORML ◆【%名称%】는 어떤 분위기?（임의）
	PRINTL [ 1] 수수　　　　　　[ 2] 화려
	PRINTL [ 3] 섬세　　　　　　[ 4] 힘찬
	PRINTL [ 5] 어린애같은　　　[ 6] 지적
	PRINTL [ 7] 불가사의　　　　[ 8] 엄숙한
	PRINTL [ 9] 장대　　　　　　[10] 매니악
	PRINTL [ 0] 어느 쪽도 아니다
	INPUT
	IF RESULT == 1
		LOCALS:4 = 시크/시크/
	ELSEIF RESULT == 2
		LOCALS:4 = 화려/화려/
	ELSEIF RESULT == 3
		LOCALS:4 = 섬세/섬세/
	ELSEIF RESULT == 4
		LOCALS:4 = 파워풀/파워풀/
	ELSEIF RESULT == 5
		LOCALS:4 = 로리타/로리타/
	ELSEIF RESULT == 6
		LOCALS:4 = 지적/지적/
	ELSEIF RESULT == 7
		LOCALS:4 = 불가사의/ 불가사의/
	ELSEIF RESULT == 8
		LOCALS:4 = 위엄/위엄/
	ELSEIF RESULT == 9
		LOCALS:4 = 장대/장대/
	ELSEIF RESULT == 10
		LOCALS:4 = 매니악/매니악/
	ENDIF
	RESULTS = %LOCALS:1 + LOCALS:2 + LOCALS:3 + LOCALS:4%
ENDIF

