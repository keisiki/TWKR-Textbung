@ENKAI_SETTING
;------------------------------------------------------------------------------------------------------
;연회의 개최 판정
;------------------------------------------------------------------------------------------------------
;1턴에 마시는 술의 양은 경과시간 * 10 보통은 30분이므로 300, 연회규모 1200이라면 2시간 정도의 연회가 된다.
;주최자는 연회의 준비와 정리 업무를 해야 한다
;하루에 복수의 연회를 여는 방법이 아니므로 주의 (사람들이 분산되어버려서 허전하기도 하고…)
;------------------------------------------------------------------------------------------------------
PRINTL  

;개최 판정은 연회 플래그가 세우지 않은 상태에서 밖에 행해지지 않는다 (버그 방지)
SIF FLAG:연회개최플래그
	RETURN
	
;메인 주최자가 출입 금지 되어 있다면 개최되지 않는다
;연회 구별은 98이상으로 설정하지 말 것
;2 환상향 송년회　메인 주최자: 없음　섣달 그믐날 일주일 전에 100% 발생하다
IF DAY:2 == 4 && DAY:3 >= 25 && DAY:3 <= 30
	;연회 이름
	EnkaiTitle = 환상향 송년회
	;연회 구별
	FLAG:연회개최플래그 = 2
	;연회 규모
	FLAG:31 = 6000
	;연회장
	FLAG:32 = 9
	;개시 시간
	FLAG:33 = 900
	;개시일
	FLAG:개시일 = 31 - DAY:3
	;우천 중지
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 송년회 시즌이 다가왔다
;달맞이 술잔치
ELSEIF DAY:2 == 3 && DAY:3 >= 1 && DAY:3 < 15
	;연회 이름
	EnkaiTitle = 달맞이 술잔치
	;연회 구별
	FLAG:연회개최플래그 = 3
	;연회 규모
	FLAG:31 = 2000 + RAND:1000
	;会場
	FLAG:32 = 409
	;개시 시간
	FLAG:33 = 1140
	;개시일
	FLAG:개시일 = 15 - DAY:3
	;우천 중지
	FLAG:36 = 1
	PRINTL  
	IF MAIN_MAP == 4
		PRINTFORMW 에이린에게 달맞이 술잔치에 권유되었다
	ELSE
		PRINTFORMW 영원정에서 달맞이 술잔치에 초대되었다
	ENDIF
;수확제
ELSEIF DAY:2 == 3 && DAY:3 >= 16 && DAY:3 < 27
	;연회 구별
	FLAG:연회개최플래그 = 4
	;연회 규모
	FLAG:31 = 2000 + RAND:3000
	;会場
	FLAG:32 = 202
	;개시 시간
	FLAG:33 = 540
	;개시일
	FLAG:개시일 = 27 - DAY:3
	;우천 중지
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 올해도 수확제의 계절이 다가왔다
; 레밀리아의 변덕스러운 파티　메인 주최자: 레밀리아　자택이 홍마관　토요일에 50%로 개최예고, 실시일은 일요일
ELSEIF CFLAG:16:출금 == 0 && DAY%7 == 6 && MAIN_MAP == 3 && RAND:50 <= 49
	;연회 이름
	EnkaiTitle = 레밀리아의 변덕스러운 파티
	;연회 구별
	FLAG:연회개최플래그 = 6
	;연회 규모
	FLAG:31 = 1500 + RAND:2500
	;연회장
	FLAG:32 = 324
	;개시 시간
	FLAG:33 = 1140
	;개시일
	FLAG:개시일 = 1
	;우천 중지
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %조사처리(CALLNAME:16,"는")% 파티를 하고 싶어진 것 같다
;SAMPLE 레이무의 변덕스러운 술잔치　메인 주최자: 레이무　토요일에 50%로 개최예고, 실시일은 일요일
ELSEIF CFLAG:1:출금 == 0 && DAY%7 == 6 && RAND:100 <= 49
	;연회 이름
	EnkaiTitle = 레이무의 변덕스러운 술잔치
	;연회 구별
	FLAG:연회개최플래그 = 1
	;연회 규모
	FLAG:31 = 1000 + RAND:1500
	;연회장
	IF RAND:5 <= 3
		FLAG:32 = 11
	ELSEIF RAND:4 == 0
		FLAG:32 = 2
	ELSEIF RAND:4 == 0
		FLAG:32 = 20
	ELSEIF RAND:4 == 0
		FLAG:32 = 21
	ELSE
		FLAG:32 = 218
	ENDIF
	;개시 시간
	FLAG:33 = 1360 - (FLAG:31 / 10)
	;개시일
	FLAG:개시일 = 1

	;우천 중지
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %조사처리(CALLNAME:1,"는")% 연회를 하고 싶어진 것 같다
;	ELSEIF DAY:3 >= 8 && DAY:3 <= 10
;		;연회 이름
;		EnkaiTitle = 밤샘 독경 라이브
;		;연회 구별
;		FLAG:연회개최플래그 = 5
;		;연회 규모
;		FLAG:31 = 2000 + RAND:30 * 40
;		;연회장
;		FLAG:32 = 104
;		;개시 시간
;		FLAG:33 = 1440 + 240 - (FLAG:31 /10) ;아침 4시까지 연회한다
;		;개시일
;		FLAG:개시일 = 10 - DAY:3
;		;우천 중지
;		FLAG:36 = 0
;		PRINTL  
;		PRINTFORMW 밤샘 독경 라이브의 시기가 다가왔다
ELSEIF DAY:2 == 2 && DAY:3 >= 5 && DAY:3 < 10 && !탄막놀이축제발동
	;연회 이름
	EnkaiTitle = 탄막놀이 축제
	;연회 구별
	FLAG:연회개최플래그 = 7
	;연회 규모
	FLAG:31 = 2400 + RAND:3300
	;会場
	FLAG:32 = 202
	;개시 시간
	FLAG:33 = 720
	;개시일
	FLAG:개시일 = 7
	;우천 중지
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 올해도 탄막놀이 축제를 한다
	탄막놀이축제참가완료 = 0
	탄막놀이축제발동 = 1
ENDIF
;현재의 참가 인원을 리셋
FLAG:참가인수 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
SIF FLAG:연회개최플래그
	CALL 연회개요
PRINTL  

@ENKAI_ENTRY
#DIM DYNAMIC HOST_LIST  , 캐릭터수상한;주최자 참가
#DIM DYNAMIC GUEST_LIST , 캐릭터수상한;손님 참가
#DIM DYNAMIC NG_LIST    , 캐릭터수상한;오지 않음
#DIM ResidentRate	;그 맵에 살고 있는 캐릭터의 참가 확률
#DIM VisitorRate	;그 맵에 살지 않는 캐릭터의 참가 확률
#DIM HL
;---   연회의 참가자 결정   -----------------------------------------------------------------------------
;CFLAG:연회참가 0=불참 1=참가 2=주최자 3=탈락자 4=안 마셔도 참가
;------------------------------------------------------------------------------------------------------
;플래그 초기화
CVARSET CFLAG, GETNUM(CFLAG, "연회참가"), 0, 1
SELECTCASE FLAG:연회개최플래그
	;1 레이무의 변덕스러운 술잔치
	CASE 1
		SELECTCASE FLAG:연회차입
			CASE 51
				FLAG:31 += RAND:1000
			CASE 52
				FLAG:31 += RAND:2000
		ENDSELECT
		;1:레이무, 2:루코토, 11:마리사
		HOST_LIST  = 1, 2, 11
		;10:스이카
		GUEST_LIST = 10
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		
		IF FLAG:31 >= 2500
		;규모가 크면 참가확률 큼
			ResidentRate = 16
			VisitorRate = 16
		ELSE
			ResidentRate = 9
			VisitorRate = 9
		ENDIF
	;2 환상향 송년회
	CASE 2
		;1:레이무, 2:루코토, 11:마리사
		HOST_LIST  = 1, 2, 11
		;10:스이카
		GUEST_LIST = 10
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		ResidentRate = 25
		VisitorRate = 25

	;달맞이 술잔치
	CASE 3
		;52:우동, 53테위, 72:에링　75:메디슨도 도와준다
		HOST_LIST  = 52, 53, 72, 75
		;62:구야
		GUEST_LIST = 62
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		ResidentRate = 33
		VisitorRate = 12
	;수확제
	CASE 4
		EnkaiTitle = 수확제
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:자택위치 / 100 == 2
				FOR HL, 0, 20
					SIF HOST_LIST:HL
						CONTINUE
					HOST_LIST:HL = LOCAL
				NEXT
			ENDIF
		NEXT
		;아키 자매는 반드시 온다
		GUEST_LIST = 77, 78
		;세이자는 참가하지 않는다
		NG_LIST    = 97
		ResidentRate = 0
		VisitorRate = 12
	;독경 라이브
;	CASE 5
;		;명련사 멤버는 일한다
;		HOST_LIST = 39, 40, 41, 55, 85, 86, 87, 88
;		;GUEST_LIST 없음
;		;하쿠레이 신사 조는 오지 않는다
;		;세이자도 오지 않는다
;		NG_LIST   = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 97
;		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
;		FOR LOCAL, 1, CHARANUM
;			IF CFLAG:LOCAL:연회참가
;				SIF CFLAG:LOCAL:연회참가 < 0
;					CFLAG:LOCAL:연회참가 = 0
;				CONTINUE
;			ENDIF
;			;다른 사람은 술을 마시지 않는다
;			SIF RAND:15 == 0
;				CFLAG:LOCAL:연회참가 = 9
;		NEXT
	;6 레밀리아의 변덕스러운 파티
	CASE 6
		;15:사쿠야, 58:메이링, 59:소악마는 일한다
		HOST_LIST  = 15, 58, 59
		;16:레밀리아, 50:플랑은 일하지 않는다
		GUEST_LIST = 16, 50
		;54:파츄리, 97:세이자는 참가하지 않는다
		NG_LIST    = 54, 97
		ResidentRate = 33
		IF FLAG:31 >= 2500
		;규모가 크면 참가확률 큼
			VisitorRate = 14
		ELSE
			VisitorRate = 12
		ENDIF
	;탄막축제
	CASE 7
		EnkaiTitle = 탄막놀이 축제
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:자택위치 / 100 == 2
				FOR HL, 0, 20
					SIF HOST_LIST:HL
						CONTINUE
					HOST_LIST:HL = LOCAL
				NEXT
			ENDIF
		NEXT
		;아키 자매는 반드시 온다
		GUEST_LIST = 1, 11, 64, 77, 78
		;세이자는 참가하지 않는다
		;NG_LIST    = 97
		ResidentRate = 40
		VisitorRate = 50
	CASEELSE
		RETURN
ENDSELECT
CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)


;리스트를 이용한 연회 캐릭터 프리셋 함수
@ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)
#DIM REF HOST_LIST  ;주최자 참가
#DIM REF GUEST_LIST ;손님 참가
#DIM REF NG_LIST    ;오지 않음
#DIM L_NUM
#DIM L_ID
#DIM ResidentRate
#DIM VisitorRate
#DIM VenueMap

VenueMap = FLAG:32 / 100

L_NUM = FINDELEMENT(HOST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(HOST_LIST:L_ID):연회참가 = 2
	NEXT
ENDIF
L_NUM = FINDELEMENT(GUEST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(GUEST_LIST:L_ID):연회참가 = 1
	NEXT
ENDIF
;설정한 것을 명확화하기 위해서 -1로 합니다
L_NUM = FINDELEMENT(NG_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(NG_LIST:L_ID):연회참가 = -1
	NEXT
ENDIF

FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:연회참가
		SIF CFLAG:LOCAL:연회참가 < 0
			CFLAG:LOCAL:연회참가 = 0
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:자택위치 / 100 == VenueMap
		SIF RAND:100 < ResidentRate
			CFLAG:LOCAL:연회참가 = 1
	ELSE
		SIF RAND:100 < VisitorRate
			CFLAG:LOCAL:연회참가 = 1
	ENDIF
NEXT

@ENKAI_WORK(ARG)
;연회의 행동내용을 입력한다
;NOSAMES:모든 인수가 다르면 1, 하나라도 일치하면 0을 돌려준다
IF FLAG:연회개최플래그 > 0 && (NOSAMES(CFLAG:ARG:직종, 48, 49, 50, 51) || CFLAG:ARG:행동 == 0)
	;연회 시간이 되면 주최 측이 준비된 일의 양에 따라 움직이기 시작한다
	IF TIME >= FLAG:개시시간 - (FLAG:연회규모 / 30) - 15 && TIME < FLAG:33 && CFLAG:ARG:연회참가 == 2
		CALL ENKAI_PREPARE(ARG)
		RETURN 1
	;놀러 올 뿐인 사람은 시간이 되면 놀러 온다
	ELSEIF DAY == FLAG:개시일 && TIME >= FLAG:33 && CFLAG:ARG:동행 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	;이미 시작된 경우
	ELSEIF 연회개최중판정() && CFLAG:ARG:동행 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	ENDIF
ELSEIF FLAG:연회개최플래그 > 0
	RETURN 1
ENDIF
RETURN 0


@ENKAI_PREPARE(ARG)
;주최자의 준비 업무
IF CFLAG:ARG:직종 != 연회준비
	MAXBASE:ARG:일량 = FLAG:31 / 3
	;최대값, 2400
	SIF MAXBASE:ARG:일량 >= 2400
		MAXBASE:ARG:일량 = 2400
	BASE:ARG:일량 = MAXBASE:ARG:일량
	CFLAG:ARG:직종 = 연회준비
	CFLAG:ARG:직장 = TFLAG:연회장
	TCVAR:ARG:315 = 0
	CFLAG:ARG:행동 = 5
ENDIF
RETURN 1

@ENKAI_START(ARG)
;각자 마시는 속도를 달리하므로, 잔치의 규모에 따라 업무량이 변화
IF CFLAG:ARG:직종 != ENKAI_SANKA && TCVAR:ARG:만취 == 0
	MAXBASE:ARG:일량 = FLAG:31
	;최대값, 통상7200(반나절)・술고래라면 제한이 없다
	SIF MAXBASE:ARG:일량 >= 7200 && TALENT:ARG:술내성 != 3
		MAXBASE:ARG:일량 = 7200
	MAXBASE:ARG:일량 += RAND:500
	IF TALENT:ARG:술내성 == -1
		TIMES MAXBASE:ARG:일량, 0.75
	ELSEIF TALENT:ARG:술내성 == 1
		TIMES MAXBASE:ARG:일량, 1.25
	ELSEIF TALENT:ARG:술내성 == 2
		TIMES MAXBASE:ARG:일량, 1.5
	ELSEIF TALENT:ARG:술내성 == 3
		TIMES MAXBASE:ARG:일량, 2.0
	ENDIF
	BASE:ARG:일량 = MAXBASE:ARG:일량
	;업무내용・연회에 참가
	CFLAG:ARG:직종 = ENKAI_SANKA
	CFLAG:ARG:직장 = TFLAG:연회장
	CFLAG:ARG:현재위치 = TFLAG:연회장
	TCVAR:ARG:315 = 0
	CFLAG:ARG:행동 = 5
	IF CFLAG:ARG:연회참가 == 2
		;주최자는 정리 예정을 세트(업무량은 최대 1200)
		CFLAG:ARG:정리일량 = MIN(FLAG:연회규모 / 6, 1200)
		CFLAG:ARG:정리장소 = FLAG:연회회장
	ENDIF
ENDIF

RETURN 1


@ENKAI_KATAZUKE(ARG)
;주최자의 정리 업무
MAXBASE:ARG:일량 = CFLAG:ARG:정리일량
BASE:ARG:일량 = MAXBASE:ARG:일량
;업무내용・연회정리
CFLAG:ARG:직종 = 50
CFLAG:ARG:직장 = GET_MAP_REPLACEMENT(CFLAG:ARG:정리장소)
TCVAR:ARG:도울수없다 = 0
CFLAG:ARG:행동 = 5
RETURN 1


@ENKAI_DRUNK(ARG)
;고주망태가 되면 다시 돌아온다
IF BASE:ARG:술기운 > (MAXBASE:ARG:술기운 / 10) * 8
	RETURN 1
ENDIF

;술을 마시지 않는다면 취하지 않는다
IF TALENT:ARG:술내성 == -2 || CFLAG:ARG:임신자각상태 == 1
	CFLAG:ARG:직종 = 0
	CFLAG:ARG:연회참가 = 4
	IF CFLAG:MASTER:현재위치 == CFLAG:ARG:현재위치
		PRINTFORMW %ARG는% 이미 충분히 즐긴 것 같다
	ENDIF
ELSE
	CFLAG:ARG:직종 = 0
	CFLAG:ARG:연회참가 = 3
;	PRINTFORMW %ARG는% 얼근히 취한 기분인 것 같다
	IF CFLAG:MASTER:현재위치 == CFLAG:ARG:현재위치
		PRINTFORMW %ARG는% 이미 충분히 즐긴 것 같다
	ENDIF
ENDIF
RETURN 1


@ENKAI_END
;会場となった場所は荒れ果てる、拠点外でも対象地点の汚れ増加させるためTFLAG:宴会場ではない
SIF YOGORE:(FLAG:32) < 300
	YOGORE:(FLAG:32) += 1000
CALL ENKAI_STOP()

@ENKAI_STOP
;악천후 등으로 인한 중지
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:직종 = 0
	MAXBASE:LOCAL:일량 = 0
	BASE:LOCAL:일량 = 0
	CFLAG:LOCAL:연회참가 = 0
	CFLAG:LOCAL:행동 = 0
NEXT
FLAG:연회개최플래그 = 0
FLAG:31 = 0
FLAG:32 = 0
TFLAG:연회장 = 0
FLAG:33 = 0
FLAG:개시일 = 0
FLAG:참가인수 = 0
FLAG:36 = 0
FLAG:연회차입 = 0
;연회회장표시 = 
TFLAG:연회상태 = 0
RETURN 0

@연회예정표시
PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}에 %연회장표시()%에서 %EnkaiTitle%


@SET_ENKAI_LOCATIONDISPLAY
연회회장표시 = 

@연회장표시
#FUNCTIONS
LOCALS = 연회장표시 에러
IF FLAG:연회개최플래그
	IF IN_HOME(FLAG:32)
		LOCALS = %NAME_FROM_PLACE(FLAG:32)%
	ELSE
		LOCALS = %GET_MAPNAME(FLAG:32/100)%의 %GET_PLACENAME(FLAG:32)%
	ENDIF
ENDIF
RETURNF LOCALS

@연회명표시
#FUNCTIONS
SELECTCASE FLAG:연회개최플래그
	CASE 1
		LOCALS = 레이무의 변덕 술잔치
	CASE 2
		LOCALS = 환상향 나이 잊고 회
	CASE 3
		LOCALS = 달을 보면서 술을 마심
	CASE 4
		LOCALS = 수확제
	CASE 5
		LOCALS = 밤안내 독경 라이브
	CASE 6
		LOCALS = 레밀리아의 변덕인 파티
	CASE 7
		LOCALS = 탄막놀이 축제
ENDSELECT
RETURNF LOCALS

@연회개요
PRINTFORML \@ FLAG:개시일 == 1? 내일# {FLAG:개시일}일 뒤\@, {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}에 %연회장표시()%에서, 「%EnkaiTitle%」%조사만처리(EnkaiTitle,"를")% 연다고 한다

PRINT 들리는 이야기에 따르면
IF FLAG:31 >= 5000
	PRINTW ……이건 장기전이 될 것 같다
ELSEIF FLAG:31 >= 2500
	PRINTW , 진수성찬을 푸짐하게 준비하고 있는 것 같다
ELSEIF FLAG:31 >= 1500
	PRINTW , 친목을 다지는 데는 딱 좋을 것 같다
ELSE
	PRINTW , 조그만한 모임을 예정하고 있는 것 같다
ENDIF
PRINTFORMW \@ FLAG:개시일 == 1? 갑작스러운 행사지만, 참가해 볼까……# 참가하려면 예정을 비워 두자……\@
SIF FLAG:36 > 0
	PRINTFORMW 또한, 비가 내리면 취소 된다고 한다……
FLAG:개시일 += DAY


@연회참가인원수재계산
#DIM 참가상태
;宴会の参加人数をセット
FLAG:참가인수 = 0
;FOR LOCAL,0,CHARANUM
;	IF (CFLAG:LOCAL:宴会参加 == 1 || CFLAG:LOCAL:宴会参加 == 2) && !TCVAR:LOCAL:泥酔
;		SIF CFLAG:LOCAL:現在位置 == TFLAG:宴会場
;			FLAG:参加人数 ++
;	ENDIF
;NEXT
LOCAL = 0
;参加状態 = 1	;参加者
WHILE 1
;	LOCAL = FINDCHARA(CFLAG:宴会参加, 参加状態, LOCAL + 1)
	LOCAL = FINDCHARA(CFLAG:현재위치, TFLAG:연회장, LOCAL + 1)
	IF LOCAL < 0
		;IF 参加状態 == 1
		;	LOCAL = 0
		;	参加状態 = 2	;主催者
		;ELSE
		;	BREAK
		;ENDIF
		BREAK
	ELSE
;		SIF CFLAG:LOCAL:現在位置 == TFLAG:宴会場 && !TCVAR:LOCAL:泥酔
		;泥酔していない参加者および主催者をカウント
		SIF (CFLAG:LOCAL:연회참가 == 1 || CFLAG:LOCAL:연회참가 == 2) && !TCVAR:LOCAL:만취
			FLAG:참가인수 ++
	ENDIF
WEND

@연회개최중판정
#FUNCTION
;RETURNF FLAG:宴会開催フラグ == 98 || FLAG:宴会開催フラグ == 99
RETURNF TFLAG:연회상태 == 98 || TFLAG:연회상태 == 99

@연회장소재판정(ARG)
#FUNCTION
RETURNF 연회개최중판정() && CFLAG:ARG:현재위치 == TFLAG:연회장

@연회개시일판정()
#FUNCTION
RETURNF 연회개최중판정() == 0 && DAY >= FLAG:개시일
