@외출이벤트
#DIM GO_OUT_EVENT, GO_OUT_MAX_NO
;외출이벤트는 일일일회
SIF GETBIT(TFLAG:일일일회, 11)
	RETURN
VARSET GO_OUT_EVENT, -1
CALLF FUNC_FISHER_YATES_SHAFFLE(GO_OUT_EVENT, GO_OUT_MAX_NO)
FOR COUNT, 0, GO_OUT_MAX_NO
	RESULT = 0
	TRYCALLFORM GO_OUT_CONDITION_EV{GO_OUT_EVENT:COUNT}
	IF RESULT
		SETBIT TFLAG:일일일회, 11
		BREAK
	ENDIF
NEXT
RETURN

@PICK_RUNINTO(MAP_ID)
#DIM C_ID
#DIM MAP_ID
SIF GETBIT(TFLAG:고유조우이벤트, MAP_ID)
	RETURN
SETBIT TFLAG:고유조우이벤트, MAP_ID
CALL FISHER_YATES_SHAFFLE(RANDOM_CHARANUM)
;ランダムで50人ピックアップ
FOR COUNT, 0, 50
	C_ID = SHAFFLE_ARRAY:COUNT
	;出禁/拠点住まい、あなたを弾く
	SIF CFLAG:C_ID:출금 || CFLAG:C_ID:신사거주 || !C_ID
		CONTINUE
	;訪問予定/仕事/宴会参加キャラを弾く
	SIF CFLAG:C_ID:거점내방 || 업무일체크(C_ID) || CFLAG:C_ID:연회참가
		CONTINUE
	;活動時間外/衰弱中キャラ/もう会った子を弾く
	SIF !VISIT(C_ID) || CFLAG:C_ID:쇠약 || !TCVAR:C_ID:오늘만났는지
		CONTINUE
	;自宅があるかよく行く地域じゃないと駄目
	SIF CFLAG:C_ID:자택위치 / 100 != MAP_ID && CFLAG:C_ID:자주가는지역 != MAP_ID
		CONTINUE
	;ここまで通ったキャラをチェック
	RESULT = 0
	TRYCALLFORM RUN_INTO_{C_ID}(MAP_ID)
	SIF RESULT == -1
		CONTINUE
	;その子を出現させる処理
	CFLAG:C_ID:만난위치 = MAP_ID * 100 + 10
	CFLAG:C_ID:현재위치 = CFLAG:C_ID:만난위치
	CFLAG:C_ID:마지막에만난날 = DAY
	TCVAR:C_ID:오늘만났는지 = 1
	IF RESULT == 0
		PRINTFORMW %조사처리(CALLNAME:C_ID,"와")% 마주쳤다
		CALL KOJO_MESSAGE_SEND("EVENT",1,C_ID,9,0)
	ENDIF
	BREAK
NEXT
