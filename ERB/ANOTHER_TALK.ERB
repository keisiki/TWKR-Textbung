;FileName_ANOTHER_TALK.ERB ------------------------- Rev1. 01
;TARGET 이외 멤버 회화
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@ANOTHERTARGET_TALK
#DIM TALKER,2
#DIM HATAKE
VARSET LOCAL
VARSET HATAKE
;DEBUGPRINTL 【ANOTHERTARGET_TALK】CALL ANOTHERTARGET_TALK

;CHARA_ARRAY fault 대책
SIF CHARANUM > 캐릭터수상한
	RETURN 0

;말하는 사람의 결정 처리
CALL ANOTHER_TALK_SET_TALKER(TALKER)
HATAKE = INT_NOUMIN(FLAG:지주, "장소 ID")

DEBUGPRINTFORML 【ANOTHERTARGET_TALK】회화자 1 = {TALKER:0} 회화자 2 = {TALKER:1}
IF (TCVAR:TARGET:일개시 - TIME:0 <= 30 && TCVAR:TARGET:일개시 - TIME:0 > 0) && !FLAG:70
	CALL MESSAGE_TALK(12)
ELSEIF CFLAG:TARGET:수면
	CALL MESSAGE_TALK(3)
ELSEIF GETBIT(FLAG:잠복,0)
	CALL MESSAGE_TALK(16)
ELSEIF TFLAG:묘사중인아이
	CALL MESSAGE_TALK(14)
ELSEIF ((CFLAG:TARGET:취침시간 - TIME:0 <= 30 && CFLAG:TARGET:취침시간 - TIME:0 > 0) || (TCVAR:수면제강도 >= 2 && !RAND:3)) && !FLAG:70
	CALL MESSAGE_TALK(13)
ELSEIF TALKER:0 && TALKER:1 && (!FLAG:70 || CFLAG:TARGET:344 == 1)
	CALL MESSAGE_TALK(2, TALKER:0, TALKER:1)
ELSEIF FLAG:연회개최플래그 >= 98 && CFLAG:MASTER:현재위치 == TFLAG:연회장
	CALL MESSAGE_TALK(1)
ELSEIF TALENT:MASTER:형상 == 4 && TIME > 1140 && CFLAG:MASTER:데이트중 && (CFLAG:MASTER:현재위치 % 100) % 10 && CFLAG:MASTER:현재위치 != OMANEKIBEYA()
	CALL MESSAGE_TALK(5,0,0)
ELSEIF CFLAG:MASTER:현재위치 == GET_MAP_REPLACEMENT(HATAKE)
	CALL MESSAGE_TALK(8)
;畑拡張実装までは暫定的に畑0で決め打ち
ELSEIF 텃밭가능(CFLAG:MASTER:현재위치) && HATAKE_NAME:0 != "" && PLANT_TYPE > 0
	;가정 채소밭 정경묘사
	CALL MESSAGE_TALK(10)
ELSEIF 빨래너는곳가능(CFLAG:MASTER:현재위치) && TIME:5 < 2
	CALL MESSAGE_TALK(11)
ELSE
	RESULT = 0
	IF CFLAG:MASTER:현재위치 /100 == 15
		TRYCALLFORM MAP_UNIQUEMESSAGE_15
	ELSE
		TRYCALLFORM MAP_UNIQUEMESSAGE_{MAIN_MAP}
	ENDIF
	;어느 것에도 걸리지 않으면 날씨
	SIF OUTROOF(CFLAG:MASTER:현재위치) && !RESULT
		CALL MESSAGE_TALK(4)
	;SIF+코멘트 행은 불안정을 일으키므로 조정
	;SIF OUTROOF(CFLAG:MASTER:현재위치) && !RESULT
	;어느 것에도 걸리지 않으면 날씨
	;	CALL MESSAGE_TALK(4)
ENDIF
RETURN 0



;말하는 사람의 결정 처리
;건네받은 TALKER를 직접 고쳐 쓴다
@ANOTHER_TALK_SET_TALKER(TALKER)
#DIM REF     TALKER
#DIM DYNAMIC CHARA, 캐릭터수상한
#DIM DYNAMIC CNT_CHARA
VARSET TALKER
;대상 캐릭터 선정
FOR LOCAL, 1, GET_TARGETNUM() + 1
	;MASTER의 상대를 하고 있으면(자) 스킵
	SIF TARGET:LOCAL == TARGET:0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):현재위치 != CFLAG:MASTER:현재위치
		CONTINUE
	;대상 캐릭터 ID취득
	CHARA:(CNT_CHARA) = TARGET:LOCAL
	CNT_CHARA ++
NEXT
SIF CNT_CHARA < 2
	RETURN

LOCAL = RAND:(CNT_CHARA)
TALKER:0 = CHARA:LOCAL
ARRAYREMOVE CHARA, LOCAL, 1
CNT_CHARA --

;대상 캐릭터 사이의 궁합 체크
FOR LOCAL, 0, CNT_CHARA
	;궁합이 좋은 캐릭터가 동실인가
	SIF !RELATION:(TALKER:0):(CHARA:LOCAL)
		CONTINUE
	;궁합을 봐 회화자 2이상이라면 회화자를 갱신
	SIF TALKER:1 && RELATION:(TALKER:0):(TALKER:1) >= RELATION:(TALKER:0):(CHARA:LOCAL)
		CONTINUE
	TALKER:1 = CHARA:LOCAL
NEXT



;FileName_ANOTHER_TALK.ERB ------------------------- Rev1. 03
;Info범위내 메시지 표시 처리
;CALL		USER
;ARG		0:메시지 NO, 1:회화자 1, 2:회화자 2(멤버 회화시만)
;RETURN		VOID
;COMMENT	CASE의 번호가 젊을 정도 출력 우선도가 높다
;-----------------------------------------------------------
@MESSAGE_TALK(ARG, ARG:1, ARG:2)
#DIMS 회장
#DIMS 아이의이름
#DIMS 子供の行動
#DIM CHARA
#DIM CHILDNUM
#DIM 同室
;기능 체크
;SIF !GETBIT(FLAG:3, 2)
	;RETURN 0
PRINTFORM ◆
SELECTCASE ARG
	CASE 1
		IF CHK_DATENOW(CFLAG:MASTER:데이트중) && CFLAG:MASTER:현재위치 != OMANEKIBEYA()
			회장 = %DATENAME_PLACE()%
		ELSE
			회장 = %NAME_FROM_PLACE(TFLAG:연회장)%
		ENDIF
		;참가자가 없으면 표시되지 않는다
		IF FLAG:35 > 0
			;연회의 규모에 응해 분기
			IF FLAG:31 <= 1500 || FLAG:35 <= 3
				PRINTFORML %회장%에선 {FLAG:35}명 정도의 조촐한 연회가 열리고 있다……
			ELSE
				PRINTFORML %회장%에선 {FLAG:35}명 정도의 연회가 열리고 있다……
			ENDIF
			;날을 넘었을 경우
			IF DAY > FLAG:개시일
				;경과시간에 응해 분기
				IF TIME >= (FLAG:33 + 240) - 1440 * (DAY - FLAG:개시일)
					IF FLAG:35 <= 3
						PRINTFORML 이제 남은 사람들은 일부의 강자들뿐인 모양이다……
					ELSE
						PRINTFORML 탈락자가 뜨문뜨문 생긴 모양이다……
					ENDIF
				ELSEIF TIME >= (FLAG:33 + 180) - 1440 * (DAY - FLAG:개시일)
					PRINTFORML 꽤 취한 모양이다……
				ELSEIF TIME >= (FLAG:33 + 60) - 1440 * (DAY - FLAG:개시일)
					PRINTFORML 웃음소리와 소란이 끊이지 않으며 달아오르고 있다……
				;아직 시작되었던 바로 직후
				ELSE
					PRINTFORML 상당히 활기찬 모양이다……
				ENDIF
			ELSE
				;경과시간에 응해 분기
				IF TIME >= (FLAG:33 + 240)
					IF FLAG:35 <= 3
						PRINTFORML 이제 남은 사람들은 일부의 강자들뿐인 모양이다……
					ELSE
						PRINTFORML 탈락자가 뜨문뜨문 생긴 모양이다……
					ENDIF
				ELSEIF TIME >= (FLAG:33 + 180)
					PRINTFORML 꽤 취한 모양이다……
				ELSEIF TIME >= (FLAG:33 + 60)
					PRINTFORML 웃음소리와 소란이 끊이지 않으며 달아오르고 있다……
				;아직 시작되었던 바로 직후
				ELSE
					PRINTFORML 상당히 활기찬 모양이다……
				ENDIF
			ENDIF
		ELSE
			PRINTFORML 강자들도 덧없이 스러지고
			PRINTFORML %NAME_FROM_PLACE(TFLAG:연회장)%는 연회 때문에 꽤 난장판이 되었다……
		ENDIF
	CASE 2
		;대화 표시
		;어느 쪽도 궁합 높은
		IF RELATION:(ARG:1):(ARG:2) > 100 && RELATION:(ARG:2):(ARG:1) > 100
			IF TALENT:(ARG:1):연모 && TALENT:(ARG:2):연모
				PRINTFORML %CALLNAME:(ARG:1)%와 %CALLNAME:(ARG:2)%는 넋 나간 표정으로 이쪽을 바라보고 있다
			ELSEIF TALENT:(ARG:1):연모 || TALENT:(ARG:2):연모
				PRINTFORML \@ TALENT:(ARG:1):연모 ?  %CALLNAME:(ARG:1)%는 %CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%는 %CALLNAME:(ARG:1)% \@와 담소하면서 뜨거운 눈길을 보내고 있다
			ELSE
				IF RAND:2
					PRINTFORML \@ CFLAG:(ARG:1):310 > CFLAG:(ARG:2):310 ?  %CALLNAME:(ARG:1)%는 %CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%는 %CALLNAME:(ARG:1)% \@의 머리를 상냥하게 어루만지고 있다
				ELSE
					PRINTFORML %CALLNAME:(ARG:1)%와 %CALLNAME:(ARG:2)%는 즐겁게 담소하고 있다
				ENDIF
			ENDIF
		;어느 쪽인지가 궁합 높은
		ELSEIF (RELATION:(ARG:1):(ARG:2) > 100 && RELATION:(ARG:2):(ARG:1) < 100) || (RELATION:(ARG:1):(ARG:2) < 100 && RELATION:(ARG:2):(ARG:1) > 100)
			IF TALENT:(ARG:1):연모 && TALENT:(ARG:2):연모
				PRINTFORML %CALLNAME:(ARG:1)%와 %CALLNAME:(ARG:2)%는 대화를 나누면서도 뜨거운 눈길을 이쪽에 보내고 있다
			ELSEIF TALENT:(ARG:1):연모 || TALENT:(ARG:2):연모
				PRINTFORML \@ TALENT:(ARG:1):연모 ?  %CALLNAME:(ARG:1)%는 %CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%는 %CALLNAME:(ARG:1)% \@와 대화하면서 이쪽을 훔쳐보고 있다
			ELSE
				PRINTFORM \@ RELATION:(ARG:1):(ARG:2) > 100 ?  %CALLNAME:(ARG:1)%의 말에 %CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%의 말에 %CALLNAME:(ARG:1)% \@는 
				PRINTDATA
					DATAFORM 불쾌한 표정을 짓고 있다…
					DATAFORM 딴청을 피우고 있다…
					DATAFORM 대충 맞장구 치고 있다…
				ENDDATA
			ENDIF
		;어느쪽이나 궁합 낮은
		ELSEIF RELATION:(ARG:1):(ARG:2) < 100 && RELATION:(ARG:2):(ARG:1) < 100
			IF TALENT:(ARG:1):연모 && TALENT:(ARG:2):연모
				PRINTFORML %CALLNAME:(ARG:1)%와 %CALLNAME:(ARG:2)%는 말다툼하면서 이쪽에 미소를 보내고 있다
			ELSEIF TALENT:(ARG:1):연모 || TALENT:(ARG:2):연모
				PRINTFORML \@ TALENT:(ARG:1):연모 ?  %CALLNAME:(ARG:1)%는 %CALLNAME:(ARG:2)% # %CALLNAME:(ARG:2)%는 %CALLNAME:(ARG:1)% \@의 이야기를 들은 체 만 체 하며 손을 흔들었다
			ELSE
				PRINTFORM %CALLNAME:(ARG:1)%와 %CALLNAME:(ARG:2)%는 
				PRINTDATA
					DATAFORM 뭣 때문인지 말다툼하고 있다…
					DATAFORM 입술을 구기고 있다…
					DATAFORM 싸움을 시작했다…
				ENDDATA
			ENDIF
		ENDIF
		PRINTL
	CASE 3
		PRINTFORML %타겟은% 깊이 잠들어 있다…
	CASE 4
		PRINTS "   "
		IF TIME:7
			SELECTCASE TIME:7
				;무지개
				CASE 1
					IF FLAG:70
						PRINTL 하늘에 흑백의 무지개가 걸려있다…
					ELSE
						PRINTL 하늘에 무지개가 걸려있다…
					ENDIF
				;다이아몬드 더스트
				CASE 2
					PRINTL 눈의 결정이 태양빛에 반짝반짝 빛나고 있다…… 숨결이 하얗다…
				;유성
				CASE 3
					IF FLAG:70
						PRINTL 밤하늘에 유성이 머물러 있다…
					ELSE
						PRINTL 밤하늘에 때때로 유성이 떨어지고 있다…
					ENDIF
				;유성군
				CASE 4
					IF FLAG:70
						PRINTL 밤하늘에 유성우가 머물러 있다…
					ELSE
						PRINTL 밤하늘을 모두 메울 만큼 유성이 하염없이 떨어지고 있다…
					ENDIF
			ENDSELECT
		ELSE
			SELECTCASE TIME:5
				;태양
				CASE 1
					IF TIME:2 >= 2 && TIME:2 <= 4
						IF DAY:2 == 2
							PRINTL 구름 한 점 없는 푸른 하늘에서 태양이 가차없이 내리쬐고 있다…
						ELSE
							PRINTL 하늘은 맑고 화창하게 개여 있다
						ENDIF
					ENDIF
				;우
				CASE 4
					IF FLAG:70
						PRINTL 빗방울이 정지해 있다…
					ELSE
						PRINTL 부슬부슬 비가 내리고 있다…
					ENDIF
				;큰 비
				CASE 5
					IF FLAG:70
						PRINTL 빗방울이 정지해 있다…
					ELSE
						PRINTL 비는 거세게 쏟아지고 있다…
					ENDIF
				;설
				CASE 8
					IF FLAG:70
						PRINTL 눈송이가 정지해 있다…
					ELSE
						PRINTL 눈이 천천히 춤추듯 내리고 있다… 숨결이 하얗다…
					ENDIF
				;눈보라
				CASE 9
					IF FLAG:70
						PRINTL 눈송이가 정지해 있다…
					ELSE
						PRINTL 얼어붙을 듯한 눈보라가 휘몰아치고 있다… 뜨거운 숨결마저도 모조리 하얘질 것만 같다…
					ENDIF
				;진눈깨비
				CASE 12
					IF FLAG:70
						PRINTL 진눈깨비가 정지해 있다…
					ELSE
						PRINTL 차가운 진눈깨비가 내리고 있다… 숨결이 하얗다…
					ENDIF
				;우박
				CASE 13
					IF FLAG:70
						PRINTL 우박 알갱이마저 정지해 있다…
					ELSE
						PRINTL 굵은 우박이 쏟아지고 있다… 숨결이 하얗다…
					ENDIF
			ENDSELECT
		ENDIF
		PRINTL
	CASE 5
		PRINTFORML 밤길이 어두워도 %CALLNAME:MASTER%의 빛나는 페니스만 있으면 든든하다
	CASE 6
		IF !CHARA_HOLIDAY(67)
			;수업중
			IF TIME >= 480 && TIME <= 720 || TIME >= 780
				IF !FLAG:70
					PRINTFORML 서당 아이들은 %CALLNAME:TARGET%의\@ 충전율(TARGET, 3) >= 10 ?  %SHOW_BOTTOM(TARGET, 1)% 안이 어떻게 된지도 모른 채# \@ 수업을 받고 있다…
				ELSEIF !CFLAG:MASTER:우후후
					PRINTFORML 서당 아이들은 정지해 있다…
				ELSE
					PRINTFORML %타겟은% 서당 아이들 앞에서 덮쳐지고 있다…
				ENDIF
			ELSEIF TIME >= 720 && TIME <= 780 && !FLAG:70
				PRINTFORML 서당 아이들은 %타겟과% 식사하고 있다…
			ENDIF
		ENDIF
	CASE 7
		IF CFLAG:MASTER:현재위치 == 210
			PRINTFORML %CALLNAME:(ARG:1)% 주변이 인산인해를 이루고 있다
			IF 충전율(ARG:1, 3) >= 10
				IF !RAND:10
					PRINTFORML %마스터는% 그녀의 표정이 살짝 굳은 것을 확인했다. 새어나온 것일까?
				ELSE
					PRINTFORML 구경꾼들이 알 노릇은 없지만 그녀의 배에는 지금도 %CALLNAME:MASTER%의 정액이 \@ 충전율(ARG:1, 3) >= 30 ? 듬뿍# \@ 고여있는 것이다.
				ENDIF
			ENDIF
		ELSE
			IF FLAG:70
			ELSE
				PRINTL 광장 쪽이 소란스럽다
			ENDIF
		ENDIF
	CASE 8
		PRINTFORML %CALLNAME:(FLAG:지주)%의 %STR_NOUMIN(FLAG:지주, "밭의 종류")%가 있다
		SELECTCASE FLAG:밭일성과
			CASE IS < 20
				PRINTFORML %STR_NOUMIN(FLAG:지주, "작물")%는 깜깜무소식이라고 해도 될 만큼 자라지 않았다
			CASE 20 TO 60
				PRINTFORML %STR_NOUMIN(FLAG:지주, "작물")%는 별로 자라지 않았다
			CASE 61 TO 100
				PRINTFORML %STR_NOUMIN(FLAG:지주, "작물")%는 그럭저럭 잘 자라고 있다
			CASEELSE
				PRINTFORML %STR_NOUMIN(FLAG:지주, "작물")%가 풍성하게 여물었다
		ENDSELECT
	CASE 9
		;말표시
		PRINTS "   "
		CALL KOJO_MESSAGE_SEND("INFO")
	CASE 10
		;가정 채소밭 정경묘사
		PRINTFORM %HATAKE_NAME:0%
		IF PLANT_DRY > 3 || PLANT_GROW > 10
			PRINTL 는 시들고 있다
		ELSE
			SELECTCASE PLANT_GROW
				CASE 10
					IF HATAKE_NAME:0 == "로터"
						PRINTFORM 는 가지가 휘어져라 여물어 진동음을 내고 있다
					ELSE
						PRINT 는 무르익어 수확할 순간을 기다리고 있다
					ENDIF
				CASE 8,9
					PRINT 가 여물기 시작하고 다
				CASE 6,7
					PRINT 의 꽃이 피어 있다
				CASE 4,5
					PRINT 의 잎이 우거져 있다
				CASEELSE
					PRINT 의 묘목은 순조롭게 자라고 있다
			ENDSELECT
			IF PLANT_DRY == 2
				PRINT 　흙이 마르기 시작하고 있다
			ELSEIF PLANT_DRY == 0
				SETCOLOR C_AQUA
				PRINT 　밭은 충분히 수분을 흡수한 상태다
				RESETCOLOR
			ENDIF
			IF PLANT_FOOD
				SETCOLOR C_CREAM
				IF ITEM:세방울의천수
					PRINT 　밭에 곤신의 축복이 느껴진다
				ELSE
					PRINT 　밭은 거름이 뿌려져 있다
				ENDIF
				RESETCOLOR
			ENDIF
			PRINTL
		ENDIF
	CASE 11
		PRINTFORML 빨래를 말리고 있다
		;PRINTFORML 그 옆에는 탄자쿠가 달린 조릿대가 있다
	CASE 12
		PRINTFORML 슬슬 %타겟이% 일할 시간이다
	CASE 13
		PRINT 졸음이 오는지 
		IF CFLAG:TARGET:우후후
			PRINTFORML %타겟은% 쾌감에 완전히 몰두하지 못한 모습이다
		ELSE
			PRINTFORML %타겟은% 하품을 꾹 참고 있다
		ENDIF
	CASE 14
		;자립후
		IF TFLAG:묘사중인아이 > 1000
			CHARA = TFLAG:묘사중인아이 % 1000
			CHILDNUM = TFLAG:묘사중인아이 / 1000
		;육아중
		ELSE
			CHARA = TFLAG:묘사중인아이
			CHILDNUM = TALENT:CHARA:육아중
		ENDIF
		아이의이름 = %CHILDNAME:CHARA:CHILDNUM%
		子供の行動 = %CHILD_ACT:CHARA:CHILDNUM%
		SELECTCASE 子供の行動
			CASE "睡眠"
				;幼少期以降は例外として自室外だと表示されず、フラグをオフにする（寝室に帰る描写があるため）
				IF CFLAG:CHARA:출산경과일 >= CHILD_어휘 && !自室自宅判定(CFLAG:MASTER:현재위치, CHARA)
					TFLAG:묘사중인아이 = 0
				ELSE
					PRINTFORM %조사처리(아이의이름,"은")%すやすやと眠っている
				ENDIF
			CASE "起床"
				IF CFLAG:CHARA:출산경과일 < CHILD_어휘
					PRINTFORM %조사처리(아이의이름,"은")%お腹を空かせている
				ELSE
					PRINTDATA
						DATAFORM %조사처리(아이의이름,"은")%あくびをしている
						DATAFORM %조사처리(아이의이름,"은")%眠たそうにしている
						DATAFORM %조사처리(아이의이름,"은")%背伸びしている
						DATAFORM %조사처리(아이의이름,"은")%目をこすっている
						DATAFORM %조사처리(아이의이름,"은")%寝ぼけている
					ENDDATA
				ENDIF
			CASE "入浴"
				IF CFLAG:CHARA:출산경과일 < CHILD_어휘
					PRINTFORM %조사처리(아이의이름,"은")%お風呂に入れてもらってる
				ELSE
					PRINTDATA
						DATAFORM %조사처리(아이의이름,"은")%十かぞえている
						DATAFORM %조사처리(아이의이름,"은")%湯船ではしゃいでいる
						DATAFORM %조사처리(아이의이름,"은")%泡を作って遊んでいる
						DATAFORM %조사처리(아이의이름,"은")%潜って遊んでいる
						DATAFORM %조사처리(아이의이름,"은")%頭を洗ってもらっている
						DATAFORM %조사처리(아이의이름,"은")%身体を洗ってもらっている
					ENDDATA
				ENDIF
			CASE "식사"
				IF CFLAG:CHARA:출산경과일 < CHILD_젖떼기
					PRINTFORM %조사처리(아이의이름,"은")%おっぱいを吸っている
				ELSEIF CFLAG:CHARA:출산경과일 < CHILD_어휘
					PRINTFORM %조사처리(아이의이름,"은")%젖떼기食を口に咥えている
				ELSE
					SELECTCASE TIME
					CASE 300 TO 540 
						PRINTFORM %조사처리(아이의이름,"은")%朝ごはんを食べている
					CASE 660 TO 900
						PRINTFORM %조사처리(아이의이름,"은")%お昼ごはんを食べている
					CASE 1020 TO 1200
						PRINTFORM %조사처리(아이의이름,"은")%夕飯を食べている
					CASE 1200 TO 1440
						PRINTFORM %조사처리(아이의이름,"은")%夜ごはんを食べている
					CASE 0 TO 180
						PRINTFORM %조사처리(아이의이름,"은")%夜食を食べている
					CASEELSE
						PRINTFORM %조사처리(아이의이름,"은")%ごはんを食べている
					ENDSELECT
				ENDIF
			CASE "玩具"
				IF CFLAG:CHARA:출산경과일 < CHILD_자립전
					PRINTFORM %조사처리(아이의이름,"은")%玩具で遊んでいる
				ELSE
					PRINTFORM %조사처리(아이의이름,"은")%玩具を見つめている
				ENDIF
			CASE "お絵かき"
				PRINTFORM %조사처리(아이의이름,"은")%お絵かきをしている
			CASE "遊び"
				PRINTDATA
					DATAFORM %조사처리(아이의이름,"은")%元気に走り回っている
					DATAFORM %조사처리(아이의이름,"은")%弾幕ごっこの真似事をしている
					DATAFORM %조사처리(아이의이름,"은")%飛び跳ねている
					DATAFORM %조사처리(아이의이름,"은")%転がっている
					DATAFORM %조사처리(아이의이름,"은")%駆け回っている
					DATAFORM %조사처리(아이의이름,"은")%踊り回っている
					DATAFORM %조사처리(아이의이름,"은")%チャンバラごっこをしている
					DATAFORM %조사처리(아이의이름,"은")%はしゃぎ回っている
					DATAFORM %조사처리(아이의이름,"은")%ケンケンパしている
					DATAFORM %조사처리(아이의이름,"은")%縄跳びをしている
				ENDDATA
			CASE "勉強"
				PRINTFORM %조사처리(아이의이름,"은")%勉強している
			CASE "読書"
				PRINTFORM %조사처리(아이의이름,"은")%黙々と本を読み耽っている
			CASE "料理"
				PRINTDATA
					DATAFORM %조사처리(아이의이름,"은")%たどたどしく料理している
					DATAFORM %조사처리(아이의이름,"은")%ぎこちなく料理をしている
					DATAFORM %조사처리(아이의이름,"은")%レシピとにらめっこしている
					DATAFORM %조사처리(아이의이름,"은")%慎重に食材を刻んでいる
					DATAFORM %조사처리(아이의이름,"은")%火加減を念入りに見ている
					DATAFORM %조사처리(아이의이름,"은")%目を凝らして計量している
				ENDDATA
			CASE "絵描き"
				PRINTDATA
					DATAFORM %조사처리(아이의이름,"은")%構想を練っている
					DATAFORM %조사처리(아이의이름,"은")%昔描いた絵を取り出した
					DATAFORM %조사처리(아이의이름,"은")%新しい紙を取り出した
					DATAFORM %조사처리(아이의이름,"은")%気ままに絵を描いている
					DATAFORM %조사처리(아이의이름,"은")%熱心に絵を描いている
					DATAFORM %조사처리(아이의이름,"은")%夢中になって絵を描いている
					DATAFORM %조사처리(아이의이름,"은")%鼻歌を歌いながら絵を描いている
					DATAFORM %조사처리(아이의이름,"은")%こちらを見ながら絵を描いている
					DATAFORM %조사처리(아이의이름,"은")%色を塗っている
				ENDDATA
			CASE "歌唱"
				PRINTDATA
					DATAFORM %조사처리(아이의이름,"은")%元気に歌っている
					DATAFORM %조사처리(아이의이름,"은")%気ままに歌っている
					DATAFORM %조사처리(아이의이름,"은")%なりきって歌っている
					DATAFORM %조사처리(아이의이름,"은")%うっとりして歌っている
					DATAFORM %조사처리(아이의이름,"은")%堂々と歌っている
					DATAFORM %조사처리(아이의이름,"은")%リズムに乗っている
					DATAFORM %조사처리(아이의이름,"은")%踊っている
					DATAFORM %조사처리(아이의이름,"은")%楽器を弾く真似をしている
					DATAFORM %조사처리(아이의이름,"은")%ポーズを決めている
				ENDDATA
			CASE "掃除"
				PRINTFORM %조사처리(아이의이름,"은")%部屋の掃除をしている
			CASE "支度"
				PRINTFORM %조사처리(아이의이름,"은")% 서당에 갈 채비를 하고 있다
			CASE "登校"
				PRINTFORM %조사처리(아이의이름,"가")%登校していく……
			CASE "授業"
				PRINTFORM %조사처리(아이의이름,"은")%授業を受けている
			CASE "帰宅"
				;寺子屋で見送った場合は表示されず、フラグをオフにする
				IF  !自室自宅判定(CFLAG:MASTER:현재위치, CHARA) && CFLAG:MASTER:현재위치 != CFLAG:CHARA:현재위치
					TFLAG:묘사중인아이 = 0
				ELSE
					PRINTFORM %조사처리(아이의이름,"은")%カバンを降ろしている
				ENDIF
			CASE "仕事"
				PRINTFORM %조사처리(아이의이름,"은")%仕事に励んでいる
			CASE "散歩"
				PRINTDATA
					DATAFORM %조사처리(아이의이름,"은")%誰かと話している
					DATAFORM %조사처리(아이의이름,"은")%異性の人物と並んでいる
					DATAFORM %조사처리(아이의이름,"은")%佇んでいる
					DATAFORM %조사처리(아이의이름,"은")%お茶をすすっている
					DATAFORM %조사처리(아이의이름,"은")%あくびをしている
					DATAFORM %조사처리(아이의이름,"은")%手帳に何かを書いている
					DATAFORM %조사처리(아이의이름,"은")%辺りを見回している
					DATAFORM %조사처리(아이의이름,"은")% 졸린 눈치다
					DATAFORM %조사처리(아이의이름,"은")%こちらの様子を伺っている
				ENDDATA
			;その他の行動,ここでCHILD_ACTを決めちゃう
			CASEELSE
				IF CFLAG:MASTER:현재위치 == CFLAG:CHARA:현재위치
					同室 = 1
				ELSE
					同室 = 0
				ENDIF
				;描写の抽選と固定
				IF LOCAL:1 != TIME || LOCAL:2 != 同室
					IF CFLAG:CHARA:출산경과일 < CHILD_어휘
						LOCAL = RAND:16
					ELSEIF CFLAG:CHARA:출산경과일 < CHILD_자립전
						IF 同室
							LOCAL = RAND:24
						ELSE
							LOCAL = RAND:14
						ENDIF
					ELSE
						IF 同室
							LOCAL = RAND:26
						ELSE
							LOCAL = RAND:11
						ENDIF
					ENDIF
					LOCAL:1 = TIME
					LOCAL:2 = 同室
				ENDIF
				IF CFLAG:CHARA:출산경과일 < CHILD_어휘
					SELECTCASE LOCAL
					CASE 1, 2, 3, 4
						PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:CHARA%에 안겨 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_抱かれる
					CASE 5, 6
						PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:CHARA%의 품속에서 졸린 눈치다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_眠たそう
					CASE 7, 8
						PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:CHARA%의 품속에서 얌전히 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_大人しい
					CASE 9, 10, 11, 12
						PRINTFORM %CALLNAME:CHARA%에 안긴 %조사처리(아이의이름,"가")% 이쪽을 보고 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_こちらを見る
					CASE 13
						PRINTFORM %CALLNAME:CHARA%에 안긴 %조사처리(아이의이름,"은")% 뭔가를 호소하고 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_何かを訴える
					CASE 14
						PRINTFORM %조사처리(아이의이름,"은")% 뭔가를 눈으로 좇고 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_何かを目で追う
					CASEELSE
						PRINTFORM %CALLNAME:CHARA%은 %아이의이름%를 어르고 있다
						CHILD_ACT:CHARA:CHILDNUM = 幼児期_あやされる
					ENDSELECT
				ELSEIF CFLAG:CHARA:출산경과일 < CHILD_자립전
					IF 同室
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %조사처리(CALLNAME:CHARA,"과")% %조사처리(아이의이름,"은")% 즐겁게 이야기를 나누고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 3, 4
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%에게 뭔가를 가르치고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_教わる
						CASE 5, 6
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%에게 이야기를 하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 7, 8
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%의 행실을 바로잡고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_しつけられる
						CASE 9
							PRINTFORM %조사처리(아이의이름,"은")% %조사처리(CALLNAME:CHARA,"과")% 놀고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_親子遊び
						CASE 10
							PRINTFORM %조사처리(아이의이름,"은")% %조사처리(CALLNAME:CHARA,"과")% 열심히 이야기를 나누고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_会話
						CASE 11
							PRINTFORM %조사처리(아이의이름,"은")% 혼자서 놀고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_一人遊び
						CASE 12
							PRINTFORM %조사처리(아이의이름,"은")% 얌전히 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_大人しい
						CASE 13
							PRINTFORM %조사처리(아이의이름,"은")% 골똘히 생각하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_考え事
						CASE 14
							PRINTFORM %조사처리(아이의이름,"은")% 졸린 눈치다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_眠そう
						CASE 15
							PRINTFORM %조사처리(아이의이름,"은")% 까불거리고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_はしゃぐ
						CASE 16
							PRINTFORM %조사처리(아이의이름,"가")% 쫄랑쫄랑 돌아다니고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うろちょろ
						CASE 17
							PRINTFORM %조사처리(아이의이름,"은")% 좀이 쑤신 눈치다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うずうず
						CASE 18
							PRINTFORM %조사처리(아이의이름,"은")% 이쪽을 빤히 쳐다보고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_見つめる
						CASE 19, 20
							PRINTFORM %조사처리(아이의이름,"은")% %마스터와% 놀려 하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_遊ぼう
						CASE 21, 22
							PRINTFORM %조사처리(아이의이름,"가")% 더듬더듬 만져댄다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_ぺたぺた
						CASEELSE
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:MASTER%에 놀아주길 바라는 모양이다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_かまって
						ENDSELECT
					;単独
					ELSE
						SELECTCASE LOCAL
						CASE 1
							PRINTFORM %조사처리(아이의이름,"은")% 혼자서 놀고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_一人遊び
						CASE 2
							PRINTFORM %조사처리(아이의이름,"은")% 大人しくしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_大人しい
						CASE 3
							PRINTFORM %조사처리(아이의이름,"은")% 골똘히 생각하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_考え事
						CASE 4
							PRINTFORM %조사처리(아이의이름,"은")% 졸린 눈치다
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_眠そう
						CASE 5
							PRINTFORM %조사처리(아이의이름,"은")% はしゃいでいる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_はしゃぐ
						CASE 6
							PRINTFORM %조사처리(아이의이름,"가")%うろちょろしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うろちょろ
						CASE 7
							PRINTFORM %조사처리(아이의이름,"은")% うずうずしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_うずうず
						CASE 8
							PRINTFORM %조사처리(아이의이름,"은")% こちらをじっと見つめている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_見つめる
						CASE 9, 10
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:MASTER%と遊ぼうとしている
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_遊ぼう
						CASE 11, 12
							PRINTFORM %조사처리(아이의이름,"가")%ぺたぺたと触ってくる
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_ぺたぺた
						CASEELSE
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:MASTER%にかまってほしいようだ
							CHILD_ACT:CHARA:CHILDNUM = 幼少期_かまって
						ENDSELECT
					ENDIF
				ELSE
					IF 同室
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %조사처리(CALLNAME:CHARA,"과")% %조사처리(아이의이름,"은")% 즐겁게 이야기를 나누고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_会話
						CASE 3, 4
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%에게 뭔가를 가르치고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_教わる
						CASE 5, 6
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%에게 이야기를 하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_会話
						CASE 7
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% %아이의이름%의 행실을 바로잡고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_しつけられる
						CASE 8
							PRINTFORM %조사처리(CALLNAME:CHARA,"은")% 이쪽을 보면서 %아이의이름%와 소곤소곤 이야기를 나누고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_ひそひそ話
						CASE 9
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:TARGET%에게 불평을 주워섬기고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_文句
						CASE 10
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:TARGET%에게 주장하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_主張
						CASE 11
							PRINTFORM %조사처리(아이의이름,"은")% %조사처리(CALLNAME:CHARA,"과")% 함께 웃고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_笑い合う
						CASE 12, 13
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:TARGET%에게 가르침을 청하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_教えて
						CASE 14, 15
							PRINTFORM %조사처리(아이의이름,"은")% %CALLNAME:TARGET%의 손을 거들고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_手伝い
						CASE 16, 17
							PRINTFORM %조사처리(아이의이름,"은")% 혼자서 놀고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_一人遊び
						CASE 18
							PRINTFORM %조사처리(아이의이름,"은")% 골똘히 생각하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_考え事
						CASE 19
							PRINTFORM %조사처리(아이의이름,"은")% 졸린 눈치다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_眠そう
						CASE 20, 21, 22, 23
							PRINTFORM %조사처리(아이의이름,"은")% %마스터와% 대화하고 싶은 모양이다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_話したい
						CASE 24
							PRINTFORM %조사처리(아이의이름,"은")% %마스터를% 신경 쓰고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_気にする
						CASE 25
							PRINTFORM %조사처리(아이의이름,"은")% %마스터를% 사양하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_遠慮
						CASEELSE
							PRINTFORM %조사처리(아이의이름,"가")% 날카로운 눈매로 이쪽을 보고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_睨む
						ENDSELECT
					;単独
					ELSE
						SELECTCASE LOCAL
						CASE 1, 2
							PRINTFORM %조사처리(아이의이름,"은")% 혼자서 놀고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_一人遊び
						CASE 3, 4
							PRINTFORM %조사처리(아이의이름,"은")% 골똘히 생각하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_考え事
						CASE 5
							PRINTFORM %조사처리(아이의이름,"은")% 졸린 눈치다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_眠そう
						CASE 6, 7, 8, 9
							PRINTFORM %조사처리(아이의이름,"은")% %마스터와% 대화하고 싶은 모양이다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_話したい
						CASE 10
							PRINTFORM %조사처리(아이의이름,"은")% %마스터를% 신경 쓰고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_気にする
						CASE 11
							PRINTFORM %조사처리(아이의이름,"은")% %마스터를% 사양하고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_遠慮
						CASEELSE
							PRINTFORM %조사처리(아이의이름,"가")% 날카로운 눈매로 이쪽을 보고 있다
							CHILD_ACT:CHARA:CHILDNUM = 自立前_睨む
						ENDSELECT
					ENDIF
				ENDIF
		ENDSELECT
		PRINTL 
	CASE 15
		IF !CHARA_HOLIDAY(30) && BETWEENTIME(540, 719)
			IF !FLAG:70
				IF RAND:2
					PRINTFORML %CALLNAME:30%は\@ 충전율(30,3) >= 10 ? %SHOW_BOTTOM(TARGET,1)%の中をぐしょぐしょにしながら、# \@%TEXTR("厳かに/粛々と/しめやかに")%裁判を進めている…
				ELSE
					PRINTFORML \@ 충전율(30,3) >= 30 ? たっぷりと# \@\@ 충전율(30,3) >= 10 ? お腹に精液を溜めた # \@%CALLNAME:30%は%TEXTR("神妙な顔つき/厳粛な雰囲気の中/")%で裁判を進めている…
				ENDIF
			ELSEIF !CFLAG:MASTER:우후후
				PRINTFORML 裁判は停止中だ…
			ELSE
				PRINTFORML %CALLNAME:TARGET%は死者の魂の前で襲われている…
			ENDIF
		ENDIF
	CASE 16
		CALL 잠복묘사
	;落とし穴描写
	CASE 17
		;穴掘り実行中の妖精が居るか見る
		同室 = 0
		LOCAL = 5, 6, 7, 14, 111
		FOR COUNT, 0, 5
			SIF CFLAG:MASTER:현재위치 != CFLAG:(LOCAL:COUNT):현재위치
				CONTINUE
			SIF Activity_Type:(LOCAL:COUNT) != 16 || !Activity_Duration:(LOCAL:COUNT)
				CONTINUE
			同室 ++
		NEXT
		;あなたが落ちた
		IF FA16_HOLE:1 == 2 && FA16_HOLE:2 == TIME
			IF 同室
				PRINTFORML イタズラ好きの妖精\@ 同室 > 1 ? 達 # \@がこちらを指差して大笑いしている…
			ELSEIF GET_TARGETNUM() > 0
				PRINTFORM 近くにいた%CALLNAME:TARGET%が%CALLNAME:MASTER%を見て
				SELECTCASE CFLAG:TARGET:성격경향
					CASE 1, 5
						PRINTFORML クスクスと笑っている…
					CASE 2
						PRINTFORML 鼻で笑っている…
					CASE 3
						PRINTFORML \@ TARGET == 57 && !TALENT:연모 ? ひょっとこの面を揺らし # 楽しそうに笑っ \@ている…
					CASE 4
						PRINTFORML こらえきれずに吹き出している…
				ENDSELECT
			ELSE
				PRINTFORML 落とし穴は妖精達のイタズラに違いない…他にもいくつかあるようだ
			ENDIF
		;穴掘り中
		ELSEIF 同室
			PRINTFORML イタズラ好きの妖精\@ 同室 > 1 ? 達 # \@が何かやっている…
		ELSE
			PRINTDATA
				DATAFORM 地面が不自然に盛り上がっている…
				DATAFORM 土を掘り返した跡がわずかに残っている…
				DATAFORM 周りより妙に\@ DAY:2 == 4 ? 雪の積もった # 落ち葉の多い \@場所がある…
			ENDDATA
			PRINTDATAL
				DATAFORM 面白いものが見られそうだ
				DATAFORM 何かが起きそうだ
			ENDDATA
		ENDIF
ENDSELECT
;아이의 묘사가 없다면 플래그를 끄고, 정경묘사에서 불린 건 제외
SIF ARG != 14 && !GROUPMATCH(ARG, 1, 6, 15, 7)
	TFLAG:묘사중인아이 = 0
;-----------------------------------------------------------------------------------------------------------------
@정경묘사
#DIM KROOM_DESC
VARSET LOCAL
;口上内私室描写をチェック
KROOM_DESC = 0
IF CFLAG:MASTER:초대
	KROOM_DESC = CFLAG:MASTER:초대
ELSEIF PRIVATEROOM:(CFLAG:MASTER:현재위치 % 100)
	KROOM_DESC = PRIVATEROOM:(CFLAG:MASTER:현재위치 % 100)
ENDIF
IF KROOM_DESC
	TRYCCALLFORM K{KROOM_DESC}_ROOM_DESCRIPTION
	;	PRINTFORML あったよ
		RETURN
	CATCH
	;	PRINTFORML ないよ
	;関数がないなら続行
	ENDCATCH
ENDIF
;自由行動：落とし穴の描写
SIF CFLAG:MASTER:현재위치 == FA16_HOLE:0
	CALL MESSAGE_TALK(17)
;부근에서 연회를 하고 있다면 떠들썩한 모습이 보인다
IF FLAG:연회개최플래그 >= 98 && CAN_MOVE(CFLAG:MASTER:현재위치, TFLAG:연회장) >= 2
	CALL MESSAGE_TALK(1)
;케이네 선생님의 서당 정경 문장
ELSEIF CFLAG:MASTER:현재위치 == 240 && CFLAG:67:행동 == 5
		CALL MESSAGE_TALK(6)
;에이키님의 裁判風景描写文章
ELSEIF ((CFLAG:MASTER:현재위치 == 640 && MAIN_MAP != 6) || (CFLAG:MASTER:현재위치 == 612 && MAIN_MAP == 6)) && CFLAG:30:행동 == 5
		CALL MESSAGE_TALK(15)
	;ELSEIF CFLAG:MASTER:우후후 && GET_TARGETNUM() > 1
	;	CALL EASY_TARGET_CHANGER
ELSEIF GROUPMATCH(CFLAG:MASTER:현재위치, 210,220,230,250,270) 
	FOR LOCAL, 1, CHARANUM
		IF CFLAG:LOCAL:행동 == 5 && CFLAG:LOCAL:현재위치 == 광장
			LOCAL:1 ++
			LOCAL:2 = LOCAL
		ENDIF
	NEXT
	SIF LOCAL:1
		CALL MESSAGE_TALK(7, LOCAL:2)
ENDIF
