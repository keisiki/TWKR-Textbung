;FileName_TRACHECK.ERB ----------------------------- Rev1. 00
;처녀상실시의 처리
;CALL		USER
;ARG		0:캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@LOST_VIRGIN(ARG)
#DIM DYNAMIC 상실자No
#DIMS DYNAMIC 상실자명
#DIM DYNAMIC 취득자No
#DIMS DYNAMIC 취득자명
#DIMS 수식어
;-------------------------------------------------
;처녀상실의 체크
;-------------------------------------------------
IF TALENT:ARG:처녀 == 1 && EXP_UP(1,ARG) > 0 && !TFLAG:2 && GETBIT(TALENT:ARG:성별,0) ;まずＶがないと！みんな有るけど…念の為。

	수식어 = ""
	IF ARG == MASTER
		CFLAG:ARG:처녀상실상대 = TARGET
		상실자No = MASTER
		상실자명 = %CALLNAME:MASTER%
		취득자No = MASTER_POSE(SET_P,SET_V)
		;P 이외로 상실했을 경우, MASTER_POSE(SET_P,SET_V)는 0이 되어, 결과적으로 취득자명까지 당신이 되어 버린다
		;따라서 취득자No=0이라면 TARGET를 대입
		SIF !취득자No
			취득자No = TARGET
		취득자명 = %CALLNAME:취득자No%
	ELSE
		CFLAG:ARG:처녀상실상대 = PLAYER
		상실자명 = %CALLNAME:ARG%
		상실자No = ARG
		취득자명 = %CALLNAME:MASTER%
		취득자No = MASTER
	ENDIF
	IF ARG != MASTER
		;정조 관념
		IF TALENT:ARG:정조 > 0
			IF !TALENT:ARG:연모
				TIMES SOURCE:ARG:사랑 , 0.60
				TIMES SOURCE:ARG:반감 , 100.00
			ENDIF
		;정조무관심
		ELSEIF TALENT:ARG:정조 < 0
			TIMES SOURCE:ARG:사랑 , 0.60
			TIMES SOURCE:ARG:반감 , 0.70
		ELSEIF TALENT:ARG:무지
			TIMES SOURCE:ARG:사랑 , 0.30
			TIMES SOURCE:ARG:반감 , 0.9
			SOURCE:ARG:공포 += 3000
		ENDIF
		IF CFLAG:ARG:우후후 == 2
			TIMES SOURCE:ARG:사랑 , 1.30
			TIMES SOURCE:ARG:반감 , 0.35
		ENDIF
	ENDIF
	SETCOLOR 247,171,166
	PRINTFORMW 【처녀상실】
	IF TEQUIP:ARG:50 == MASTER && GROUPMATCH(TALENT:MASTER:형상,5,6)
		PRINTFORMW %취득자명%의 흉악한 자지가, %상실자명%의 순결을 가차 없이 찢었다……
		TIMES SOURCE:ARG:고통, 3.00
		TIMES SOURCE:ARG:공포, 3.00
	ENDIF
	IF FLAG:70
		IF ARG == MASTER
			PRINTFORMW %조사처리(취득자명,"는")% 의식이 없는 채, \@ TALENT:ARG:스침 == 1 ?  만난지 얼마 안된# \@ %상실자명%의 처녀를 빼앗겼다……
		ELSE
			PRINTFORMW %조사처리(상실자명,"는")% 의식이 없는 채, \@ TALENT:ARG:스침 == 1 ?  만난지 얼마 안된# \@ %취득자명%에게 처녀를 빼앗겨 버렸다……
		ENDIF
	ELSE
		;함락 소질에 의한 처리
		;MASTER 상실
		IF ARG == MASTER
			IF TCVAR:TARGET:만취
				PRINTFORMW 혼탁 한 채의 %조사처리(취득자명,"는")%, %상실자명%의 처녀를 빼앗았다……
			;수면간, 네가 파파가 되어!
			ELSEIF CFLAG:TARGET:수면
				PRINTFORMW 잠들어 있는 %조사처리(취득자명,"는")%, %상실자명%에 억지로 처녀를 빼앗겨 버렸다
			;무지(쇼타)로 처녀상실이라든지 최고……아니, 재고
			ELSEIF TALENT:TARGET:무지
				;IF GROUPMATCH(TALENT:MASTER:형상, 5, 6)
				;무지한 상대의 자지가 들어가있는 경우
				IF TEQUIP:MASTER:50 == 취득자No
					PRINTFORMW 성 지식이 부족한 %조사처리(취득자명,"는")% 무엇을 하셨는지 모른채, \@ TALENT:취득자No:2 & 2 ? 강렬한 쾌감과 # \@ %조사처리(상실자명,"이")% 흘리는 피에 무서워하고 있다……
				;자지 이외
				ELSE
					PRINTFORMW 파과혈을 본 %조사처리(취득자명,"는")%, 자신이 %상실자명%에게 상처를 입혀 버렸다고 생각해 갈팡잘팡하고 있다
				ENDIF
				PRINTFORMW %조사처리(상실자명,"는")% 파과의 아픔을 참으면서 %조사처리(취득자명,"을")% 안심시켰다
			;소질 연인
			ELSEIF IS_연인(TARGET) || TALENT:TARGET:연모 > 1 ;KR판 오리지날 추가연인 지원용 IS_연인
				PRINTFORMW %조사처리(취득자명,"는")% 황홀해 하는 표정으로, %상실자명%의 은밀한 곳으로부터 흘러넘치는 선혈을 응시하고 있다……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:사랑 , 3.00
				TIMES SOURCE:TARGET:반감 , 0.10
			;연모
			ELSEIF TALENT:TARGET:연모
				PRINTFORMW %조사처리(취득자명,"는")% 기쁜듯한 미소를 띠며, 처녀를 바친 %조사처리(상실자명,"을")% 응시하고 있다……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:사랑 , 2.00
				TIMES SOURCE:TARGET:반감 , 0.30
				EXP:TARGET:애정경험 += 30
				CFLAG:ARG:처녀상실대상 |= 상실_연모
			ELSEIF TALENT:TARGET:사모
				IF TCVAR:TARGET:억지로 == 1
					PRINTFORMW %조사처리(취득자명,"는")% 쾌감에 당황하면서, %상실자명%에게 눈물 젖은 눈동자을 향하여 온다……
					TIMES SOURCE:TARGET:사랑 , 0.80
					TIMES SOURCE:TARGET:반감 , 1.50
					CFLAG:ARG:처녀상실대상 |= 상실_억지로
				ELSEIF TALENT:TARGET:정조 >= 0
					PRINTFORMW %조사처리(취득자명,"는")% 곤혹하면서, 어딘지 모르게 기쁜듯이 %조사처리(상실자명,"을")% 응시하고 있다……
					TIMES SOURCE:TARGET:사랑 , 1.30
					TIMES SOURCE:TARGET:반감 , 0.50
				ENDIF
				CFLAG:ARG:처녀상실대상 |= 상실_사모
			ELSE
				IF TCVAR:TARGET:억지로 == 1
					TIMES SOURCE:TARGET:사랑 , 0.10
					TIMES SOURCE:TARGET:반감 , 3.00
					PRINTFORMW %조사처리(취득자명,"는")%, 분노와 당혹감을 보이며 %조사처리(상실자명,"을")% 노려보았다……
					CFLAG:ARG:처녀상실대상 |= 상실_억지로
				ELSE
					PRINTFORMW %조사처리(취득자명,"는")% 무엇인가 말하고 싶어 하는 것 같은 표정을 지으며, %조사처리(상실자명,"과")%의 결합부를 응시하고 있다……
				ENDIF
			ENDIF
		;MASTER가 TARGET의 질에 삽입하고 있다
		ELSEIF TEQUIP:ARG:50 == MASTER
			IF TCVAR:ARG:만취
				PRINTFORMW %조사처리(상실자명,"는")% 혼탁 한 의식 속에서 처녀를 빼앗겨 버렸다……
			;수면간 좋은 아이는 자고 있는 동안에 마마로 해 버리자∼
			ELSEIF CFLAG:ARG:수면
				PRINTFORMW %상실자명%의 처음은 자고 있는 동안에 빼앗겨 버렸다……
			ELSEIF TALENT:ARG:무지
				PRINTFORMW 성 지식이 부족한 %조사처리(상실자명,"는")% 무엇을 하셨는지 모른채, 처녀상실의 아픔에 당황해 무서워하고 있다……
				PRINTFORMW %조사처리(취득자명,"는")% %조사처리(상실자명,"을")% 안심시켰다
			;친애 또한 상대가 주인(현재 친애가 없기 때문에, 연인으로 대용
			ELSEIF TALENT:ARG:연인 || TALENT:ARG:연모 > 1
				PRINTFORMW %조사처리(상실자명,"는")% %조사처리(취득자명,"아")% 처음의 상대인 일에 이 이상 없는 행복을 느끼고 있는 것 같다……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:사랑 , 3.00
				TIMES SOURCE:ARG:반감 , 0.10
				 CFLAG:ARG:처녀상실대상 |= 상실_연모
			;연모 또한 상대가 주인
			ELSEIF TALENT:ARG:연모
				PRINTFORMW %조사처리(상실자명,"는")% %취득자명%에게 처녀를 바칠 수 있었던 일에 행복을 느끼고 있는 것 같다……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:사랑 , 2.00
				TIMES SOURCE:ARG:반감 , 0.30
				EXP:ARG:애정경험 += 30
				CFLAG:ARG:처녀상실대상 |= 상실_연모
			ELSEIF TALENT:ARG:사모
				IF TCVAR:ARG:억지로 == 1
					PRINTFORMW 호의를 가지고 있던 상대에게 배신당한 %조사처리(상실자명,"는")%, 굵은 눈물을 흘리기 시작했다……
					TIMES SOURCE:ARG:사랑 , 0.80
					TIMES SOURCE:ARG:반감 , 1.50
					CFLAG:ARG:처녀상실대상 |= 상실_억지로
				ELSEIF TALENT:ARG:정조 >= 0
					PRINTFORMW 호의를 가지고 있었다고는 해도, %조사처리(상실자명,"는")% %취득자명%에게 정조까지도 허락해버린 것에 당혹해 하고 있다……
					TIMES SOURCE:ARG:사랑 , 1.30
					TIMES SOURCE:ARG:반감 , 0.80
				ENDIF
				CFLAG:ARG:처녀상실대상 |= 상실_사모
			ELSE
				IF TCVAR:ARG:억지로 == 1
					TIMES SOURCE:ARG:사랑 , 0.10
					TIMES SOURCE:ARG:반감 , 3.00
					IF CFLAG:ARG:패배자
						PRINTFORMW %조사처리(상실자명,"는")% 패배의 댓가로서, %취득자명%에게 처녀를 빼앗겨 버렸다……
					ELSE
						PRINTFORMW %상실자명%의 저항도 허무하게, %취득자명%에게 처녀를 빼앗겨 버렸다……
					ENDIF
					CFLAG:ARG:처녀상실대상 |= 상실_억지로
					IF TALENT:ARG:스침 == 1
						CSTR:ARG:모브코프로필2 = %REPLACE(@"%CSTR:ARG:모브코프로필2%","당해 지금은 얌전하다","당해, 처녀를 빼앗겼다")%
						CSTR:ARG:모브코프로필2 = %REPLACE(@"%CSTR:ARG:모브코프로필2%","애널처녀를 빼앗겼다","처녀를 빼앗겼다")%
					ENDIF
				ELSEIF TALENT:ARG:스침 == 1
					PRINTFORMW 비처로부터 흘러넘치는 피를 본 %조사처리(상실자명,"는")%, 만난지 얼마 되지 않은 %취득자명%에게 처녀를 바쳐 버린 것을 실감해 조금 후회했다……
				ELSE
					PRINTFORMW %조사처리(상실자명,"는")% 흘러가는 대로 %취득자명%에게 처녀를 빼앗겨 버렸다……
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	IF CSTR:ARG:처녀상실이력 == ""
		IF GROUPMATCH(TCVAR:상실자No:카운터행동, 80, 83, 85)
			CALL SET_HISTORY_LOST_V(상실자No, 취득자No, "페니스")
		ELSEIF TCVAR:취득자No:카운터행동 == 57
			CALL SET_HISTORY_LOST_V(상실자No, 취득자No, "손가락")
		ELSEIF SELECTCOM == 44
			CALL SET_HISTORY_LOST_V(상실자No, 취득자No, "바이브")
		ELSE
			CALL SET_HISTORY_LOST_V(상실자No, 취득자No, "페니스")
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
;-------------------------------------------------
;재생 처녀상실의 체크
;-------------------------------------------------
IF TALENT:ARG:처녀 == 2 && EXP_UP(1,ARG)
	TCVAR:ARG:파과 = 2
	;비디오촬영
	IF TEQUIP:28
		;성교
		IF TEQUIP:ARG:50
			LOCALS = LV1/
		ELSEIF TEQUIP:ARG:13
			LOCALS = LV3/
		;그 외
		ELSE
			LOCALS = LV4/
		ENDIF
		TSTR:1 += LOCALS
	ENDIF
	PRINTW 재생 처녀상실
ENDIF
;-------------------------------------------------
;무자각처녀상실의 체크
;-------------------------------------------------
IF TALENT:ARG:처녀 == -1 && EXP_UP(1,ARG) && CFLAG:ARG:292 == 1 && !FLAG:70 && !TCVAR:ARG:만취 && !CFLAG:MASTER:장난
	TCVAR:ARG:파과 = -1
	CFLAG:ARG:292 = 0
	PRINTW 처녀상실?
	SETCOLOR 247,171,166
	PRINTFORMW %ARG는% 고통인것 같은 고통을 느끼지 않아 곤혹스러워하고 있다…
	RESETCOLOR
ENDIF

@동정키스미경험상실(ARG)
#DIM 시추에이션
;시추에이션	 0:통상
;					 1:데이트 돌아오는 길
;					 2:딸딸이들킴
;					 4:페니스에
;					 8:자발적으로
;					16:무자각
;					32:매춘
시추에이션 = 0
VARSET LOCALS
FONTBOLD
SETCOLOR 255,100,255
IF !TALENT:ARG:비동정 && TALENT:ARG:성별 & 2
	IF TCVAR:ARG:동정상실 == 1
		PRINTFORMW 동정상실(%CALLNAME:ARG%)
		TALENT:ARG:비동정 = 1
	ENDIF
ENDIF

;키스미경험 상실
IF ARG == MASTER && TALENT:ARG:키스미경험 && GROUPMATCH(SELECTCOM,2,20,312)
	시추에이션 |= 8
	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		CALL SET_FIRSTKISS(MASTER, ARG, 시추에이션)
	ELSEIF GROUPMATCH(SELECTCOM,2) && TALENT:TARGET:성별 & 2
		시추에이션 |= 4
		CALL SET_FIRSTKISS(ARG, TARGET, 시추에이션)
	ENDIF
ELSEIF TALENT:MASTER:키스미경험 && GROUPMATCH(TCVAR:ARG:카운터행동,30,39,60,71,91)
	IF GROUPMATCH(TCVAR:ARG:카운터행동,30,39,60)
		CALL SET_FIRSTKISS(MASTER, ARG, 시추에이션)
	ELSEIF GROUPMATCH(TCVAR:ARG:카운터행동,71,91) && TALENT:ARG:성별 & 2
		시추에이션 |= 4
		CALL SET_FIRSTKISS(MASTER, ARG, 시추에이션)
	ENDIF
ENDIF
IF TALENT:ARG:키스미경험 && (TCVAR:ARG:FK상실 || GROUPMATCH(TCVAR:ARG:카운터행동,30,39,60,51))
	;무자각
	SIF CFLAG:ARG:수면 || FLAG:70
		시추에이션 |= 16
	;자발적으로
	SIF GROUPMATCH(TCVAR:ARG:카운터행동,30,39,60,51)
		시추에이션 |= 8
	;페니스에
	SIF TCVAR:ARG:FK상실 == 2 || GROUPMATCH(TCVAR:ARG:카운터행동,51)
		시추에이션 |= 4

	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		;이마 쪽을 제외한다
		CALL SET_FIRSTKISS(ARG, MASTER, 시추에이션)
	ELSEIF GROUPMATCH(SELECTCOM,81,88,140,337) || GROUPMATCH(TCVAR:ARG:카운터행동,30,60,51)
		CALL SET_FIRSTKISS(ARG, MASTER, 시추에이션)
	ELSEIF TCVAR:ARG:카운터행동 == 39
		CALL SET_FIRSTKISS(ARG, MASTER, 시추에이션, "엿과 함께")
	ENDIF
ENDIF

;애널 처녀상실
IF CFLAG:ARG:애널처녀상실대상 & 상실_시간정지 || !CFLAG:ARG:애널처녀상실기념일
	IF TCVAR:ARG:A상실발생 
		;시간정지중에 경험이 끝난 상태
		IF !FLAG:70 && ARG && CFLAG:ARG:애널처녀상실대상 & 상실_시간정지
			SIF FIRSTTIME("A파과",0)
				PRINTFORMW 애널 처녀상실? (%CALLNAME:ARG%)
			TCVAR:ARG:A파과 = -1
		;완전하게 처음
		ELSEIF !CFLAG:ARG:애널처녀상실기념일
			PRINTFORMW 애널 처녀상실(%CALLNAME:ARG%)
			IF CSTR:ARG:모브코프로필2 != ""
				CSTR:ARG:모브코프로필2 = %REPLACE(@"%CSTR:ARG:모브코프로필2%","당해 지금은 얌전하다","당해 애널처녀를 빼앗겼다")%
			ENDIF
			IF SELECTCOM == 45
				LOCALS = 애널바이브
			ELSEIF SELECTCOM == 46
				LOCALS = 애널비즈
			ELSE
				LOCALS = 페니스
			ENDIF
			IF !FLAG:70
				TCVAR:ARG:A파과 = 1
			ELSE
				TCVAR:ARG:A파과 = -2
			ENDIF
			IF ARG == TARGET
				CALL SET_HISTORY_LOST_A(ARG, PLAYER, @"%LOCALS%")
			ELSE
				CALL SET_HISTORY_LOST_A(ARG, TARGET, @"%LOCALS%")
			ENDIF
			
		ENDIF
	ENDIF
ENDIF
FONTREGULAR
RESETCOLOR

@LOST_VIRGIN_STOP(ARG)
SIF !SHIRAHU(ARG)
	RETURN
IF CFLAG:ARG:파과캔슬구상있음
	;말측으로부터 간섭 가능하게
	CALL KOJO_MESSAGE_SEND("LOST_VIRGIN_STOP", 0, ARG)
	IF RESULT == 1
		RETURN -1
	ELSE
		RETURN 0
	ENDIF
	RESETCOLOR
ELSEIF !TALENT:ARG:무지 && !TALENT:ARG:연모 && !TALENT:ARG:애욕 && !TALENT:ARG:연인 && !TCVAR:ARG:기다리지않다 && !CFLAG:장난
;정조 관념 소유 or정조 보통 또한 사모 없음
	IF TALENT:ARG:정조 > 0 || (!TALENT:ARG:정조 && !TALENT:ARG:사모)
		PRINTFORMW %ARG는% 그건 기다렸으면 좋겠다고 말하기 시작했다
		IF (EXP:ARG:60 + EXP:ARG:61 + EXP:ARG:62 + EXP:ARG:63 + EXP:ARG:64) == 0
			IF TALENT:ARG:정조 > 0
				PRINTFORMW 과연 몸가짐이 단단하다……
			ELSE
				PRINTFORMW 의외로 가드가 단단하다……
			ENDIF
		ELSE
			PRINTFORMW 귀찮은 아가씨다……
		ENDIF
		CALL ASK_YN("포기한다","차린 밥상을 먹지 않는 건")
		IF RESULT == 1
			IF TALENT:ARG:정조 > 0 && ABL:ARG:전투능력 > ABL:MASTER:전투능력 && !TCVAR:ARG:억지로
				PRINTFORMW %마스터는% 억지로 강요했지만, %ARG가% 너무나 격렬하게 저항해 포기했다
					RETURN 1
			ELSEIF TALENT:ARG:정조 > 0
				PRINTFORMW %ARG는% 눈에 눈물을 머금어 %마스터를% 거절했다
				PRINTL [0] 진심으로 싫을 것 같은 것으로 포기한다
				PRINTL [1] 해 버린다
				INPUT
				IF !RESULT
					PRINTFORMW %ARG는% 안심하고 있다…
					PRINTFORMW %마스터는% 조금 상처받았다
					RETURN -1
				ELSE
					PRINTFORMW %마스터는% 싫어하는 %ARG를% 덮쳤다…
					TCVAR:ARG:기다리지않다 = 1
					TCVAR:ARG:억지로 = 1
					RETURN 0
				ENDIF
			ELSE
				PRINTFORMW %마스터는% 억지로 밀어붙였다…
				TCVAR:ARG:기다리지않다 = 1
				RETURN 0
			ENDIF
		ELSE
			PRINTFORMW %ARG는% 안심하고 있다…
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SET_FIRSTKISS(ARG, 키스상대, 시추에이션, 수식어)
#DIM 키스상대
;시추에이션	 0:통상
;					 1:데이트 돌아오는 길
;					 2:딸딸이들킴
;					 4:페니스에
;					 8:자발적으로
;					16:무자각
;					32:매춘
#DIM 억지로시츄
#DIM 시추에이션
#DIMS 수식어
#DIM 스스로

VARSET LOCALS
스스로 = 0
억지로시츄 = (TCVAR:ARG:억지로 && !(TALENT:ARG:애욕 || TALENT:ARG:섹프 || TALENT:ARG:연모 || TALENT:ARG:사모))
;억지로状態で、キス合意はつかない。억지로だから！！！唇を許すわけ無いでしょ！！　ただし、陥落素質があればギリギリセーフ。

;キス経験済み
IF TALENT:ARG:키스미경험 == 0
	RETURN
;무자각 키스 경험 완료
ELSEIF TALENT:ARG:키스미경험 < 0
	;무자각 키스끼리는 중복되지 않는다
	SIF 시추에이션 & 16
		RETURN
ENDIF

;FLAG:FK판정에 따라서는 돌아간다
SIF !(FLAG:FK판정 & 1) && 시추에이션 & 16
	RETURN
SIF !(FLAG:FK판정 & 2) && 시추에이션 & 4
	RETURN


IF ARG == 키스상대
	IF ARG == MASTER
		키스상대 = TARGET
	ELSE
		키스상대 = MASTER
	ENDIF
ENDIF

SETCOLOR 247,171,166
PRINTFORM 키스미경험 상실
SIF 시추에이션 & 16
	PRINT ?
PRINTFORMW (%CALLNAME:ARG%)
RESETCOLOR

CFLAG:ARG:첫키스상실기념일 = DAY
TALENT:ARG:키스미경험 = 0
CFLAG:ARG:첫키스상실상대 = 키스상대

;대상과 수식어 결정
IF TALENT:ARG:사모
	스스로 = 1
	LOCALS:0 = 마음에 두고 있었던
ELSEIF ARG == MASTER && IS_연인(키스상대) ;KR판 오리지날 추가연인수 지원용 IS_연인
	스스로 = 1
	LOCALS:0 = 사랑하는
ELSEIF TALENT:ARG:연인 || TALENT:ARG:연모
	스스로 = 1
	LOCALS:0 = 사랑하는
ENDIF
IF 시추에이션 & 4
	LOCALS:1 = 의 페니스
	CFLAG:ARG:첫키스상실대상 = 1
ENDIF
SIF 시추에이션 & 8
	스스로 = 1
IF 시추에이션 & 16
	IF FLAG:70
		LOCALS:2 = 시간정지중에
	ELSEIF CFLAG:ARG:수면
		LOCALS:2 = 자고 있는 동안에
	ELSE
		LOCALS:2 = 자신도 모르는 사이에
	ENDIF
	CFLAG:ARG:첫키스상실대상 = 2
	TALENT:ARG:키스미경험 = -1
ENDIF
SIF TALENT:ARG:무지 && 시추에이션 & 4
	LOCALS:2 = %LOCALS:2% 그 의미도 알지 못한 채

SIF 억지로시츄
	LOCALS:2 = %LOCALS:2% 억지로

;장소 결정
IF 시추에이션 & 1
	CSTR:ARG:첫키스위치 = 데이트하고 돌아오는 길
	SIF ARG != MASTER
		스스로 = 1
ELSEIF 시추에이션 & 2
	IF ARG != MASTER
		CSTR:ARG:첫키스위치 = 자기 방
	ELSE
		CSTR:ARG:첫키스위치 = %CALLNAME:ARG% 댁
	ENDIF
ELSE
	IF CHK_DATENOW(CFLAG:ARG:데이트중) && CFLAG:ARG:현재위치 != OMANEKIBEYA()
		CSTR:ARG:첫키스위치 = %DATENAME_SPOT()%
	ELSEIF CHK_DATENOW(CFLAG:MASTER:데이트중) && CFLAG:MASTER:현재위치 != OMANEKIBEYA()
		CSTR:ARG:첫키스위치 = %DATENAME_PLACE()%의 %DATENAME_SPOT()%
	ELSE
		CSTR:ARG:첫키스위치 = %NAME_FROM_PLACE(CFLAG:ARG:현재위치)%
	ENDIF
ENDIF

;동사 결정
IF 수식어 == "엿과 함께"
	LOCALS:1 += "와"
	LOCALS:3 = 주고 받았다
ELSEIF 스스로
	LOCALS:1 += "에게"
	LOCALS:3 = 바쳤다
ELSEIF 시추에이션 & 32
	LOCALS:1 += "에게"
	LOCALS:3 = 빼앗아 주었다
ELSE
	LOCALS:1 += "에게"
	LOCALS:3 = 빼앗겼다
ENDIF

; 개인 임의수정, 띄어쓰기 추가
;---- EDIT START ---------------------------------
SIF LOCALS:0 != ""
	LOCALS:0 += " "
SIF LOCALS:1 != "" && 수식어 == "엿과 함께"
	LOCALS:1 += " "
SIF LOCALS:2 != ""
	LOCALS:2 += " "
;---- EDIT END -----------------------------------

IF CSTR:ARG:첫키스상실이력 == ""
	CSTR:ARG:첫키스상실이력 = %PRINT_DATE_F(DAY)%, %CSTR:ARG:첫키스위치%에서 %LOCALS:2%%LOCALS:0%%CALLNAME:키스상대%%LOCALS:1%%수식어% 첫키스를 %LOCALS:3%
ELSE
	CSTR:ARG:첫키스상실이력 = %PRINT_DATE_F(DAY)%, %CSTR:ARG:첫키스위치%에서 %LOCALS:2%%LOCALS:0%%CALLNAME:키스상대%%LOCALS:1%%수식어% 첫키스를 %LOCALS:3%\BR%CSTR:ARG:첫키스상실이력%
ENDIF
;기정사실도 붙여 둔다
SIF ARG != MASTER && !(시추에이션 & 16) && !억지로시츄
	SETBIT CFLAG:ARG:기정사실, 0
;애정경험도 여기서 들어갈 수 있기로 한다
SIF ARG != MASTER && !(시추에이션 & 16) && !억지로시츄
	EXP:ARG:애정경험 += 5

;===================================================================================
;이력 캐릭터라인 설정용 함수군
;대상이 미기입이라면"페니스"가 대입됩니다.
;또 그 경우 포함 대상이 페니스"라면 상대가 나지 않은 경우 자동으로 페니스밴드가 됩니다.
;장소가 미기입이라고 자동으로 위치를 취득합니다.
;시간정지중이라고 만취하고 있는이라고는 자동으로 대입됩니다.
;각종 플래그 처리도 함수측에서 설정합니다.
;즉 호출해 옆에서 분기 들어갈 수 있을 필요가 거의 없어졌습니다.
;ARG가 당신의 페니스로 처녀상실 했다면 극단적인 이야기 CALL SET_HISTORY_LOST_V(ARG)만으로 OK.
;잘되었네. 굉장하다.
;===================================================================================

@SET_HISTORY_LOST_A(ARG, 상대, 대상, 장소, 수식어)
#DIM 상대
#DIMS 대상
#DIMS 장소
#DIMS 수식어

CFLAG:ARG:애널처녀상실기념일 = DAY
CFLAG:ARG:애널처녀상실상대 = 상대

CSTR:ARG:애널처녀상실이력 = %HISTORY_LOST_TEXT(ARG, 상대, 대상, 장소, 수식어,"항문 처녀")%

@SET_HISTORY_LOST_V(ARG, 상대, 대상, 장소, 수식어)
#DIM 상대
#DIMS 대상
#DIMS 장소
#DIMS 수식어

;비처녀화, 처녀식 플래그, 상실 기념일등도 함수측에서 설정
SIF ARG != MASTER
	FLAG:처녀식 ++
CFLAG:ARG:처녀상실기념일 = DAY
TALENT:ARG:처녀 = 0
TCVAR:ARG:파과 = 1
CFLAG:ARG:처녀상실상대 = 상대
CSTR:ARG:처녀상실이력 = %HISTORY_LOST_TEXT(ARG, 상대, 대상, 장소, 수식어, "처녀")%

@HISTORY_LOST_TEXT(ARG, 상대, 대상, 장소, 수식어, 상실한것)
#FUNCTIONS
#DIM 상대
#DIMS 대상
#DIMS 장소
#DIMS 수식어
#DIMS 상실한것
#DIMS 상황
#DIM 스스로

VARSET LOCALS
스스로 = 0
IF ARG == MASTER || (CFLAG:MASTER:우후후 != 2 && ARG != MASTER && ARG != TARGET)
	LOCALS:0 = 
ELSEIF TALENT:ARG:사모
	스스로 = 1
	LOCALS:0 = 마음에 두고 있던 
ELSEIF TALENT:ARG:연인 || TALENT:ARG:연모
	스스로 = 1
	LOCALS:0 = 사랑하는 
ELSEIF !CFLAG:ARG:안면 && ARG != MASTER
	LOCALS:0 = 보지 않고 모르는 
ELSEIF TALENT:ARG:스침 == 1
	LOCALS:0 = 초대면의 
ENDIF

SIF !SHIRAHU(ARG)
	스스로 = 0
SIF 대상 == ""
	대상 = 페니스

IF FLAG:70
	상황 = 시간정지중에 
ELSEIF TCVAR:ARG:만취
	상황 = 만취하고 있을 때 
ELSEIF CFLAG:ARG:수면
	상황 = 자고 있는 동안에 
ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:우후후 != 2
	상황 = %CALLNAME:MASTER%의 소망대로 
ELSE
	상황 =
ENDIF

SIF ARG == MASTER && TALENT:상대:무지
	상황 += "아무것도 모르는 "

IF 대상 == "페니스"
	IF TALENT:상대:2 & 2
		LOCALS:1 = %CALLNAME:상대%의 %TINKO_SIMPLE(상대)%에
	ELSE
		LOCALS:1 = %CALLNAME:상대%에 페니스밴드로
	ENDIF
ELSEIF 대상 == "손가락"
	LOCALS:1 = %CALLNAME:상대%의 손가락에
ELSE
	LOCALS:1 = %CALLNAME:상대%에 %대상%로
ENDIF
SIF 장소 == ""
	장소 = %PLACE_NAME(ARG)%
IF 수식어 == ""  && SHIRAHU(ARG)
	IF TCVAR:ARG:억지로 && CFLAG:ARG:패배자
		수식어 = 패배의 댓가로서
		스스로 = 0
	ELSEIF TCVAR:ARG:억지로
		수식어 = 힘으로
		스스로 = 0
	ELSEIF TALENT:ARG:무지
		수식어 = 아무것도 모르는 채
		스스로 = 0
	ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:우후후 != 2
		수식어 = 불만을 느끼면서
	ELSEIF TALENT:ARG:연인
		수식어 = 행복감 속에서
	ELSEIF CFLAG:ARG:우후후 == 2
		수식어 = 스스로
		스스로 = 1
	ENDIF
ENDIF

IF 스스로
	LOCALS:3 = 바쳤다
ELSEIF ARG == MASTER || 수식어 == "치료라고 칭하며"
	LOCALS:3 = 주었다
ELSEIF 수식어 == "돈을 내고"
	LOCALS:3 = 빼앗아 주었다
ELSE
	LOCALS:3 = 빼앗겼다
ENDIF

LOCALS:10 = %PRINT_DATE_F(DAY)%, %상황%%장소%에서 %LOCALS:0%%LOCALS:1%%수식어% %상실한것%를 %LOCALS:3%

RETURNF LOCALS:10

;당신의 경우 이벤트로 CSV로 정의되어 있지 않은 상대와 하는 일도 있을 수 있으므로보다 자유도의 높은 것을
@SET_FIRSTKISS_MASTER(ARGS)
IF TALENT:MASTER:키스미경험
	CALL COLORMESSAGE(@"키스미경험 상실(%CALLNAME:MASTER%)", C_D_PINK, 2)
	CFLAG:MASTER:첫키스상실기념일 = DAY
	TALENT:MASTER:키스미경험 = 0
	CSTR:MASTER:첫키스상실이력 = %PRINT_DATE_F(DAY)%, %ARGS%
ENDIF
