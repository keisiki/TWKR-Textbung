;삽입 계속의 처리
@EQUIP_INCERT_V(ARG)
#DIM 간역

간역 = TEQUIP:ARG:50
;-------------------------------------------------
;행위자
;-------------------------------------------------
TCVAR:ARG:116 = 간역
;TCVAR:2 사정장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라티오 5=파이즈리 6=가랑이 7=풋잡 8=체표 9=애널 봉사 10=도구 11=촉수 12=A고문 13=V 꾸짖고)
;TCVAR:4 사정쾌감강도

CSTR:간역:10 = @"/{ARG}/"
TCVAR:간역:사정장소 = 1

CSTR:ARG:10 += @"{TEQUIP:ARG:50}/"
SIF TCVAR:ARG:사정장소 == 0 || TCVAR:ARG:사정장소 >= 8
	TCVAR:ARG:사정장소 = 13


IF ARG == MASTER
	PRINTFORML ＜%CALLNAME:간역%로부터 V인서트＞

	TCVAR:간역:사정쾌감강도 = 850
	TCVAR:ARG:사정쾌감강도 = 500 + (100 * ABL:ARG:V감각)
	SIF TALENT:간역:헐렁보지
		TCVAR:ARG:사정쾌감강도 /= 10
	DOWNBASE:간역:체력 += 15
	DOWNBASE:간역:기력 += 20
	DOWNBASE:ARG:체력 += 10
	DOWNBASE:ARG:기력 += 20
	SOURCE:ARG:쾌V += 200
	SOURCE:간역:쾌C += 200
	SOURCE:간역:사랑 += 25
	SOURCE:간역:성행동 += 250
	SOURCE:간역:달성 += 25
	SOURCE:간역:정복 += 500
	SOURCE:간역:일탈 += 50

ELSE
	PRINTFORML ＜%CALLNAME:ARG%에 V인서트＞

	TCVAR:간역:사정쾌감강도 = 700 + TEQUIP:ARG:자궁 * 100
	TCVAR:ARG:사정쾌감강도 = 750
	SIF TALENT:MASTER:헐렁보지
		TCVAR:ARG:사정쾌감강도 /= 10
	DOWNBASE:간역:체력 += 15
	DOWNBASE:간역:기력 += 20
	;시간정지중
	IF FLAG:70
		SOURCE:ARG:쾌V += 50
		CALL 삽입계속V고통처리(ARG)

	ELSE
		DOWNBASE:ARG:체력 += 10
		DOWNBASE:ARG:기력 += 20
		SIF TEQUIP:ARG:체위 == 6
			SOURCE:ARG:고통 += 300
		SIF TALENT:간역:형상 == 5
			SOURCE:ARG:고통 += 200
		SOURCE:ARG:쾌V += 50
		SOURCE:ARG:사랑 += 55
		SOURCE:ARG:노출 += 35
		SIF TCVAR:ARG:억지로
			SOURCE:ARG:반감 += 1500
		;체위 보너스
		IF TEQUIP:ARG:체위 == CFLAG:ARG:좋아하는체위
			SOURCE:ARG:사랑 += 90
			SOURCE:ARG:복종 += 30
		ENDIF
		CALL 삽입계속V고통처리(ARG)
	ENDIF

	IF CFLAG:ARG:우후후 == 2
		SOURCE:ARG:쾌V += 60
		SOURCE:ARG:사랑 += 100
		TCVAR:간역:사정쾌감강도 += 150
	ENDIF
ENDIF

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
EXP:간역:삽입경험 ++
EXP:ARG:V경험 ++
SIF !FLAG:70 && !CFLAG:ARG:장난
EXP:ARG:V성교경험 ++
SIF TEQUIP:ARG:체위 == 6
	EXP:ARG:V확장경험 ++
SIF FLAG:70 == 1
	EXP:MASTER:시간경험 += 1
IF CFLAG:MASTER:장난 == 2
	EXP:MASTER:수면간경험 += 1
	EXP:ARG:수면간경험 += 1
ENDIF
RETURN 1


@EQUIP_INCERT_A(ARG)
#DIM 간역

간역 = TEQUIP:ARG:51
;-------------------------------------------------
;행위자
;-------------------------------------------------
TCVAR:ARG:116 = 간역
;TCVAR:0 사정 대상 플래그(비트 0=TARGET 1=PLAYER 2=MASTER 3=ASSI 4=촉수 5=그 외)
;TCVAR:1 예비 사정 대상 플래그(비트를 사용하고 싶지 않은 인용, CFLAG:100으로 번역됩니다)
;	(1=TARGET 2=PLAYER 3=MASTER 4=ASSI 5=TARGET+ASSI 6=MASTER+ASSI -1=촉수 -2=그 외)
;TCVAR:2 사정장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라티오 5=파이즈리 6=가랑이 7=풋잡 8=체표 9=애널 봉사 10=도구 11=촉수 12=A고문 13=V 꾸짖고)
;TCVAR:4 사정쾌감강도
CSTR:간역:10 = @"/{ARG}/"
TCVAR:간역:사정장소 = 2

CSTR:ARG:10 += @"{TEQUIP:ARG:51}/"
SIF TCVAR:ARG:사정장소 == 0 || TCVAR:ARG:사정장소 >= 8
	TCVAR:ARG:사정장소 = 12


IF ARG == MASTER
	PRINTFORML ＜%CALLNAME:간역%로부터 A인서트＞

	TCVAR:간역:사정쾌감강도 = 850
	TCVAR:ARG:사정쾌감강도 = 500 + (100 * ABL:ARG:A감각)
	SIF TALENT:간역:헐렁항문
		TCVAR:ARG:사정쾌감강도 /= 10
	DOWNBASE:간역:체력 += 15
	DOWNBASE:간역:기력 += 20
	DOWNBASE:ARG:체력 += 20
	DOWNBASE:ARG:기력 += 20
	SOURCE:ARG:쾌A += 200
	SOURCE:간역:쾌C += 200
	SOURCE:간역:사랑 += 25
	SOURCE:간역:성행동 += 250
	SOURCE:간역:달성 += 25
	SOURCE:간역:정복 += 500
	SOURCE:간역:일탈 += 50

ELSE
	PRINTFORML ＜%CALLNAME:ARG%에 A인서트＞
	TCVAR:간역:사정쾌감강도 = 900
	TCVAR:ARG:사정쾌감강도 = 750
	SIF TALENT:MASTER:헐렁항문
		TCVAR:ARG:사정쾌감강도 /= 10
	DOWNBASE:간역:체력 += 15
	DOWNBASE:간역:기력 += 20
	;시간정지중
	IF FLAG:70
		SOURCE:ARG:쾌A += 200
		CALL 삽입계속A고통처리(ARG)

	ELSE
		DOWNBASE:ARG:체력 += 20
		DOWNBASE:ARG:기력 += 20
		SIF TALENT:간역:형상 == 5
			SOURCE:ARG:고통 += 300
		SOURCE:ARG:쾌A += 50
		SOURCE:ARG:사랑 += 30
		SOURCE:ARG:노출 += 30
		SOURCE:ARG:일탈 += 100
		SIF TEQUIP:ARG:체위 == 6
			SOURCE:ARG:고통 += 300
		;체위 보너스
		IF TEQUIP:ARG:체위 == CFLAG:ARG:좋아하는체위 - 5
			SOURCE:ARG:사랑 += 80
			SOURCE:ARG:복종 += 50
		ENDIF
		CALL 삽입계속A고통처리(ARG)
	ENDIF

	IF CFLAG:ARG:우후후 == 2
		SOURCE:ARG:쾌A += 50
		SOURCE:ARG:사랑 += 100
		TCVAR:간역:사정쾌감강도 += 150
	ENDIF
ENDIF
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
EXP:간역:삽입경험 ++
EXP:ARG:A경험 ++
SIF !FLAG:70 && !CFLAG:ARG:장난
	EXP:ARG:A성교경험 ++
SIF TEQUIP:ARG:체위 == 6
	EXP:ARG:A확장경험 ++
RETURN 1


@삽입계속V고통처리(ARG)
;EXP:V경험을 본다(처녀는 있을 수 없다)
LOCAL = 200
LOCAL:1 = 150
IF EXP:ARG:V경험 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:V경험 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:V경험 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:ARG:V경험 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:ARG:V경험 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:윤활을 본다
IF PALAM:ARG:윤활 < PALAMLV:1
	TIMES LOCAL, 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF PALAM:ARG:윤활 < PALAMLV:2
	TIMES LOCAL , 2.00
	TIMES LOCAL:1 , 2.50
ELSEIF PALAM:ARG:윤활 < PALAMLV:3
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:윤활 < PALAMLV:4
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:ARG:윤활 < PALAMLV:5
	TIMES LOCAL , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF PALAM:ARG:윤활 >= PALAMLV:5
	TIMES LOCAL , 0.02
	TIMES LOCAL:1 , 0.30
ENDIF

SOURCE:ARG:고통 += LOCAL

SIF !FLAG:70
	SOURCE:ARG:반감 += LOCAL:1


@삽입계속A고통처리(ARG)
LOCAL = 400
LOCAL:1 = 250

;EXP:A경험을 본다
IF EXP:ARG:A경험 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:ARG:A경험 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:ARG:A경험 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:ARG:A경험 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:A경험 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:A경험 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:윤활을 본다
IF PALAM:ARG:윤활 < PALAMLV:1
	TIMES LOCAL, 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF PALAM:ARG:윤활 < PALAMLV:2
	TIMES LOCAL , 2.00
	TIMES LOCAL:1 , 2.50
ELSEIF PALAM:ARG:윤활 < PALAMLV:3
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:윤활 < PALAMLV:4
	TIMES LOCAL , 0.50
	TIMES LOCAL:1 , 1.00
ELSEIF PALAM:ARG:윤활 < PALAMLV:5
	TIMES LOCAL , 0.30
	TIMES LOCAL:1 , 0.50
ELSEIF PALAM:ARG:윤활 >= PALAMLV:5
	TIMES LOCAL , 0.20
	TIMES LOCAL:1 , 0.30
ENDIF

SOURCE:ARG:고통 += LOCAL

SIF !FLAG:70 && !CFLAG:ARG:장난
	SOURCE:ARG:반감 += LOCAL:1


@삽입불가(ARG)
;-2 = 헐렁헐렁으로 기분 좋지 않은 -1 = 자극이 약한 1= 경험이 필요 2= 경험과 지식이 필요 3= 난쟁이오나홀
#FUNCTION
#DIM 당신의사이즈
#DIM 사이즈차이
IF ARG == PLAYER
	RETURNF 0
ENDIF

SELECTCASE TALENT:PLAYER:형상
	CASE 0,3,4
		당신의사이즈 = 0
	CASE 1
		당신의사이즈 = -1
	CASE 2
		당신의사이즈 = -2
	CASE 6
		당신의사이즈 = 1
	CASE 5,10
		당신의사이즈 = 2
ENDSELECT
사이즈차이 = (TALENT:PLAYER:체형 - TALENT:ARG:체형) * 2 + 당신의사이즈

SELECTCASE 사이즈차이
	CASE IS > 8
		RETURNF 3
	CASE 5 TO 7
		RETURNF 2
	CASE 3 TO 4
		RETURNF 1
	CASE -2 TO 2
		RETURNF 0
	CASE -3 TO -6
		RETURNF -1
	CASE IS < -6
		RETURNF -2
ENDSELECT
	