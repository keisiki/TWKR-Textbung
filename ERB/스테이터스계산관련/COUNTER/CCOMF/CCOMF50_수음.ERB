;*********************************************************************************************
;カウンターコマンド：수음
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF50(ARG)

	;몸의자세の変更
	;=============================
	CALL TOUCH_SET(SET_FROM_YUBI,1,ARG)
	TFLAG:MASTER의C사용중 ++

	;ソース計算および内部処理系統
	;=============================
	IF TEQUIP:PLAYER:0
		IF TEQUIP:PLAYER:0 & 1
			CALL DATUI_BOTTOM(ARG,1)
		ELSEIF TEQUIP:PLAYER:0 & 8
			CALL DATUI_BOTTOM(ARG,2)
		ENDIF
	ENDIF
	SOURCE:ARG:성행동 += 250
	SOURCE:ARG:유혹 += 500
	SOURCE:ARG:도발 += 100
	SOURCE:ARG:가학 += 100
	SOURCE:ARG:정복 += 200
	SIF !(TALENT:PLAYER:2 & 2)
		SOURCE:PLAYER:쾌C += 300
	;사정장소（1=질내 2=애널 3=수음 4=口淫 5=파이즈리 6=스마타 7=풋잡 8=体表 9=애널봉사 10=道具 11=触手 12=Ａ고문 13=Ｖ고문）
	CALL 사정대상등록(PLAYER, ARG, 3)
	;強度
	TCVAR:PLAYER:4 += 500
	EXP:ARG:수음경험 ++
	;봉사플래그
	TCVAR:ARG:108 = 1
	TCVAR:ARG:기학플래그 = 1
	CALL EVENT_COUNTER_POSE_69(ARG,2)

	;マスターカウンター制御
	;(ビット立ちで不可 0=엉덩이를 쓰다듬는다 1=껴안는다 2=키스한다 3=가슴애무 4=애널애무 5=클리애무 6=指挿入れ 7=치마를 넘긴다)
	;=============================
	;CALL 카운터삽입됨지속처리(ARG)

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE50(ARG)

	SIF TFLAG:62
		RETURN 0
	SIF ABL:ARG:손가락 < 1
		RETURN 0
	SIF TEQUIP:ARG:50 >= 0 || TEQUIP:ARG:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 && !(TALENT:MASTER:2 & 2)
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER50(커맨드번호, ARG, 기본치)
#DIM 커맨드번호
#DIM 기본치
#DIMS 카운터타입

	;카운터타입設定
	;=============================
	카운터타입 = 탈의 애무

	;実行判定値算出式
	;=============================
	LOCAL = 기본치 + 2 * TALENT:ARG:능숙한손가락 + ABL:ARG:손가락 + BASE:MASTER:발기 / 500

	;카운터타입別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:커맨드번호, 1)
	LOCAL *= COUNTER_TYPE(카운터타입, ARG)

RETURN LOCAL
