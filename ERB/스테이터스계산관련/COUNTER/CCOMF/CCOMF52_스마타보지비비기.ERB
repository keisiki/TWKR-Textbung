;*********************************************************************************************
;カウンターコマンド：스마타、보지 비비기
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOMF52(ARG)

	;몸의자세の変更
	;=============================
	IF !(TALENT:PLAYER:2 & 2)
		SOURCE:PLAYER:쾌C += 500
		CALL TOUCH_SET(SET_FROM_C,2,ARG)
	ELSE
		CALL TOUCH_SET(SET_FROM_C,1,ARG)
	ENDIF
	TFLAG:MASTER의C사용중 ++
	

	;ソース計算および内部処理系統
	;=============================
	SIF TEQUIP:PLAYER:0
		CALL DATUI_BOTTOM(ARG,0)
	SIF TEQUIP:ARG:0
		CALL DATUI_BOTTOM_T(ARG,0)
	SOURCE:ARG:쾌C += 150
	SOURCE:ARG:성행동 += 250
	SOURCE:ARG:유혹 += 700
	SOURCE:ARG:도발 += 200
	SOURCE:ARG:봉사 += 400
	SOURCE:ARG:강요 += 500
	;사정장소（1=질내 2=애널 3=수음 4=口淫 5=파이즈리 6=스마타 7=풋잡 8=体表 9=애널봉사 10=道具 11=触手 12=Ａ고문 13=Ｖ고문）
	CALL 사정대상등록(PLAYER, ARG, 6)
	;強度
	IF ABL:ARG:허리 < 1
		TCVAR:PLAYER:4 += 200
	ELSE
		TCVAR:PLAYER:4 += 800
	ENDIF
	;봉사플래그
	TCVAR:ARG:108 = 1
	TCVAR:ARG:기학플래그 = 1

	;マスターカウンター制御
	;(ビット立ちで不可 0=엉덩이를 쓰다듬는다 1=껴안는다 2=키스한다 3=가슴애무 4=애널애무 5=클리애무 6=指挿入れ 7=치마를 넘긴다)
	;=============================
	;CALL 카운터삽입됨지속처리(ARG)
	;複数人がありうるのでこれは…うーん？ ちょっとよくわからない
	SETBIT TCVAR:ARG:26 ,5
	SETBIT TCVAR:ARG:26 ,6

RETURN 1

;*********************************************************************************************
;カウンター実行可否出力関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ABLE52(ARG)

	SIF TFLAG:62
		RETURN 0
	;腰使いが無くても、十分な欲情があれば自分の快楽目的で実行する
	;無知な子も快Ｃが溜まると本能でこすりつけてくる
	IF ABL:ARG:허리 < 1
		IF PALAM:ARG:욕정 < PALAMLV:3
			RETURN 0
		ELSEIF TALENT:ARG:무지 && PALAM:ARG:쾌C < PALAMLV:3
			RETURN 0
		ENDIF
	ENDIF
	;윤활
	SIF PALAM:ARG:윤활 < 2000
		RETURN 0
	SIF TEQUIP:50 >= 0 || TEQUIP:51 >= 0
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0

RETURN 1

;*********************************************************************************************
;カウンター実行判定値算出関数
;ARG = 対象のキャラ番号
;*********************************************************************************************
@CCOM_ORDER52(커맨드번호, ARG, 기본치)
#DIM 커맨드번호
#DIM 기본치
#DIMS 카운터타입

	;카운터타입設定
	;=============================
	카운터타입 = 탈의애무

	;実行判定値算出式
	;=============================
	LOCAL = 기본치 + ABL:ARG:허리 + 2 * TALENT:ARG:젖기쉬움 + BASE:MASTER:발기 / 500

	;카운터타입別計算処理 実質実行判定処理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:커맨드번호, 1)
	LOCAL *= COUNTER_TYPE(카운터타입, ARG)

RETURN LOCAL
