;FileName_TRACHECK.ERB ----------------------------- Rev1. 00
;호감도 상승 계산 처리
;CALL		USER
;ARG		0:캐릭터 NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@FAVOR_CALC(ARG)
#DIM 분노상승치
#DIM 무드상승치
#DIM 신뢰도캡보정
#DIM 이성감소치
SIF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
	RETURN

;COMABLE 범위외 억제
SIF !TFLAG:102
	RETURN 0
;대상이 MASTER 이외의때 기동
SIF !ARG
	RETURN 0
;주인(당신)과 대상의 능력을 계산
;ABL:순종을 본다
TFLAG:97 += (ABL:ARG:순종 + ABL:ARG:친밀) / 2

;주인과 대상이 동성
IF HETEROSEX(TARGET, PLAYER)
	;남자라면 호모끼, 여자라면 레즈끼를 본다
	IF ABL:ARG:(HETEROSEX(TARGET, PLAYER) + 16) == 0
		TFLAG:97 -= 1
	ELSEIF ABL:ARG:(HETEROSEX(TARGET, PLAYER) + 16) < 5
		TFLAG:97 += 0
	ELSEIF ABL:ARG:(HETEROSEX(TARGET, PLAYER) + 16) < 9
		TFLAG:97 += 1
	ELSE
		TFLAG:97 += 2
	ENDIF
	;남자라면 BL중독, 여자라면 레즈중독을 본다
	IF ABL:ARG:(HETEROSEX(TARGET, PLAYER) + 31) > 9
		TFLAG:97 += 2
	ELSEIF ABL:ARG:(HETEROSEX(TARGET, PLAYER) + 31) > 4
		TFLAG:97 += 1
	ENDIF
	;양도
	SIF TALENT:ARG:성별기호 == -1
		TFLAG:97 += 1
ENDIF
;소질에 의한 증감
;반항적
SIF TALENT:ARG:태도 > 0
	TFLAG:97 -= 2
;자존심 높음
SIF TALENT:ARG:자존심 > 0
	TFLAG:97 -= 1
;솔직함
SIF TALENT:ARG:태도 < 0 && TFLAG:97 > 0
	TFLAG:97 += 1
;자제심
SIF TALENT:ARG:자제심
	TFLAG:97 -= 1
;감정결여
SIF TALENT:ARG:감정결여
	TFLAG:97 -= 1
;저항
SIF TALENT:ARG:저항
	TFLAG:97 -= 1
;헌신적
SIF TALENT:ARG:헌신적 && TFLAG:97 > 0
	TFLAG:97 += 1
;명랑끼리
SIF TALENT:ARG:명랑／음침 > 0 && TALENT:MASTER:명랑／음침 > 0
	TFLAG:97 += 1

;음침끼리
SIF TALENT:ARG:명랑／음침 < 0 && TALENT:MASTER:명랑／음침 < 0
	TFLAG:97 += 1

SIF TALENT:ARG:붕괴
	TFLAG:97 -= 2




;쾌감에 솔직
IF TALENT:ARG:쾌감응답 > 0
	TFLAG:97 += 1
;쾌감에 부정
ELSEIF TALENT:ARG:쾌감응답 < 0
	TFLAG:97 -= 1
ENDIF
;기분
TFLAG:97 += TALENT:ARG:15
;남성기피
IF OTOKOGIRAI(ARG)
	TFLAG:97 -= 3
	IF GETBIT (CFLAG:ARG:인계, 1)
		TFLAG:97 += 2
	ELSEIF GETBIT (CFLAG:ARG:인계, 0)
		TFLAG:97 += 1
	ENDIF
ENDIF
;연모
IF TALENT:ARG:연모
	TFLAG:97 += 2
;미약
ELSEIF TCVAR:ARG:미약
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;仙香玉兎
IF IsInsenceValid(ARG)
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;ガシングガーデン
SIF IsGardenValid(ARG)
	TFLAG:97 ++

SIF TALENT:ARG:동물귀 && TALENT:ARG:동물귀 == TFLAG:귀보정
	TFLAG:97 += 3
;사모
SIF TALENT:ARG:사모
	TFLAG:97 += 1
;주인의 용모
;TFLAG:97 += TALENT:MASTER:용모
;주인의 매력
SIF TALENT:MASTER:매력
	TFLAG:97 += 1
;주인의 매혹
SIF TALENT:MASTER:매혹
	TFLAG:97 += 1
;주인의 수수께끼의매력
SIF TALENT:MASTER:수수께끼의매력
	TFLAG:97 += 1
;주인의 인기
SIF TALENT:MASTER:인기
	TFLAG:97 += 1

IF GETBIT (CFLAG:ARG:인계, 1)
	TFLAG:97 += 2
ELSEIF GETBIT (CFLAG:ARG:인계, 0)
	TFLAG:97 += 1
ENDIF

LOCAL = 0
LOCAL:1 = 0
;각종 쾌감
;50상한, 30000으로+300 이 수치가 크면 SOURCE:ARG의 증가에 대해서 둔하게 된다
LOCAL += (100 - 30000 / (SOURCE:ARG:쾌C + SOURCE:ARG:쾌V + SOURCE:ARG:쾌A + SOURCE:ARG:쾌B + 300)) * (1 + TALENT:ARG:쾌감응답)
;사랑
LOCAL += (100 - 100000 / (SOURCE:ARG:사랑 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:사랑 + 1000))
;욕정
LOCAL += (20 - 20000 / (SOURCE:ARG:욕정 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:욕정 + 1000))
;달성감
LOCAL += (30 - 30000 / (SOURCE:ARG:달성 + 1000))
;복종
LOCAL += (20 - 20000 / (SOURCE:ARG:복종 + 1000))
;굴복
LOCAL += (20 - 20000 / (SOURCE:ARG:굴종 + 1000))
;노출
LOCAL += (30 - 30000 / (SOURCE:ARG:노출 + 1000)) * (ABL:ARG:노출벽 - 3) / 3

;고통
LOCAL += (50 - 20000 / (SOURCE:ARG:고통 + 400)) * (ABL:ARG:마조끼 - 3) / 3
;환락
LOCAL += (50 - 10000 / (SOURCE:ARG:환락 + 2000))
;정복
LOCAL += (30 - 90000 / (SOURCE:ARG:정복 + 3000))
;수동
LOCAL += (30 - 90000 / (SOURCE:ARG:수동 + 3000))
;공포
LOCAL -= (50 - 20000 / (SOURCE:ARG:공포 + 400)) * (ABL:ARG:순종 - 3) / 3
;불결
LOCAL -= (50 - 25000 / (SOURCE:ARG:불결 + 500)) * (2 - TALENT:ARG:악취내성) / 2
;울굴
LOCAL -= (50 - 15000 / (SOURCE:ARG:울굴 + 300))
;일탈
LOCAL -= (50 - 25000 / (SOURCE:ARG:일탈 + 500))
;반발
LOCAL -= (50 - 10000 / (SOURCE:ARG:반감 + 200))

;호감도 상승분에 계산치 추가
TFLAG:97 += LOCAL / 10

;일상시 ON
IF TFLAG:102 != 2
	;무드에 호감도 상승분 추가
	무드상승치 = MIN(ABL:ARG:친밀 + 1,10) * LOCAL / 3
	SIF FLAG:데이트상대 == ARG
		TFLAG:무드보너스 *= 2
	무드상승치 = GET_REVISION3(무드상승치, 50 + TFLAG:무드보너스, 100 + TFLAG:무드보너스, 200 + TFLAG:무드보너스)

	;억제 플래그가 서 있다
	IF TFLAG:무드상승억제
		무드상승치 /= 2
		SIF BASE:ARG:무드 > MAXBASE:ARG:무드 / 2
			무드상승치 = 0
	ENDIF
	;모브가 있으면(자) 상승량 반감
	SIF WITH_MOB()
		무드상승치 /= 2
	BASE:ARG:무드 += MIN(무드상승치, 200)
	BASE:ARG:무드 = MIN(BASE:ARG:무드 , MAXBASE:ARG:무드)
	;같은 방에 파루파루가 있는 상태로 다른 애와 무드를 올리면 분노가 모인다
	IF ARG != 60 && CFLAG:ARG:현재위치 == CFLAG:60:현재위치
		IF TALENT:60:연모
			BASE:60:분노 += MIN(ABL:60:친밀 + 1,10) * LOCAL / 3
		ELSEIF TALENT:60:사모
			BASE:60:분노 += MIN(ABL:60:친밀 + 1,10) * LOCAL / 5
		ELSEIF CFLAG:60:2 > 500
			BASE:60:분노 += MIN(ABL:60:친밀 + 1,10) * LOCAL / 8
		ENDIF
	ENDIF
	;이성을 쾌감도분감소
	이성감소치 = LOCAL:1 * (GETPALAMLV(PALAM:ARG:욕정, 10) + 1) / 10 * (CFLAG:ARG:욕구불만도 + 1000) / 2000
	IF TFLAG:이성깎기
		이성감소치 *= 4
		SIF CFLAG:ARG:몸목적
			이성감소치 *= 2
	ENDIF
	BASE:ARG:이성 = MAX(BASE:ARG:이성 - 이성감소치, 0 )
	분노상승치 = 0
	;일상 ON 또한 반감 소스유로 분노 가격인상승
	SIF SOURCE:ARG:반감 > 0 && TFLAG:102 == 1
		분노상승치 = SOURCE:ARG:반감 * 3 * (2000 - BASE:ARG:무드) * (500 + BASE:ARG:이성) / ((2000 + BASE:ARG:무드) * (500 + MAXBASE:ARG:이성))
	;바이브 팬티나 촉수 속옷을 입고 있는 때는 분노치가 상승하지 않는 분노 방지
	SIF SOURCE:ARG:반감 > 0 && TFLAG:102 == 1 && (EQUIP:ARG:하반신속옷2 == 28 || EQUIP:ARG:하반신속옷2 == 29 || EQUIP:ARG:하반신속옷2 == 30)
		분노상승치 = 0
	;정조대를 입고 있는 때는 분노상승치가 증가
	SIF SOURCE:ARG:반감 > 0 && TFLAG:102 == 1 && EQUIP:ARG:하반신속옷2 == 27
		분노상승치 = 분노상승치 * 4 / 3
	;기분으로 분노상승치가 변화
	IF TALENT:ARG:15 == 1
		분노상승치 = 분노상승치 * 7 / 10
	ELSEIF TALENT:ARG:15 == -1
		분노상승치 = 분노상승치 * 3 / 2
	ENDIF
	SIF TCVAR:ARG:수면제강도 == 1 && CFLAG:ARG:장난
		분노상승치 = 분노상승치 / 2
	SIF TCVAR:ARG:수면제강도 == 2 && CFLAG:ARG:장난
		분노상승치 = 분노상승치 / 5
	SIF TCVAR:ARG:수면제강도 == 3 && CFLAG:ARG:장난
		분노상승치 = 0
	BASE:ARG:분노 = 분노상승치
	;분노가 매우 기뻐해 분노 플래그 ON
	SIF BASE:ARG:분노 > MAXBASE:ARG:분노
		CALL GET_ANGRY(ARG)
	BASE:ARG:분노 = MIN(BASE:ARG:분노 , MAXBASE:ARG:분노 )
	;언짢음(반발각인취득시)은 항상 분노
	IF CFLAG:ARG:언짢음
		BASE:ARG:분노 = MAXBASE:ARG:분노
		CFLAG:ARG:빡침 = 1
	ENDIF
	;화내면(자) 눈을 뜬다
	IF CFLAG:ARG:빡침 && CFLAG:ARG:수면
		CALL CHARA_AWAKE(ARG, 1)
		IF CFLAG:ARG:현재위치 == CFLAG:MASTER:현재위치 && CFLAG:ARG:현재위치 == CFLAG:ARG:311
			IF IS_LOVER(ARG)
				IF CFLAG:ARG:쇠약
					PRINTFORML %ARG는% 내방자가 %CALLNAME:MASTER%인 것 확인하면(자) 지쳐 자 버렸다…
				ELSE
					PRINTFORML %ARG는% 내방자가 %CALLNAME:MASTER%인 것 확인하면(자) 안도의 표정을 보였다…
				ENDIF
			ELSE
				IF CFLAG:ARG:쇠약
					PRINTFORML %ARG는% 지쳐 자 버렸다…
				ELSE
					PRINTFORML %CALLNAME:ARG%에 내쫓아졌다…
				ENDIF
				CALL GETOUT(MASTER)
			ENDIF
		ENDIF
		;경고장 발행 체크
		SIF (CFLAG:ARG:빡침 == 1) && RAND:((ABL:ARG:친밀 + 1) * 3) == 0 && !CFLAG:ARG:수면 && FLAG:18 && !FLAG:개인실입장
			CALL CARD_GOT_CHECK(ARG)
	ENDIF
ENDIF
;반발각인 보정
TFLAG:97 -= (FLAG:4 + 1) * MARK:반발각인

;결과가 일정수미만의 경우
IF MARK:반발각인 >= 3 && TFLAG:97 < -3
	TFLAG:97 = -3
ELSEIF MARK:반발각인 == 2 && TFLAG:97 < -2
	TFLAG:97 = -2
ELSEIF MARK:반발각인 == 1 && TFLAG:97 < -1
	TFLAG:97 = -1
ELSEIF TFLAG:97 < 0
	TFLAG:97 = 0
ENDIF

;무관심의 경우, 연모가 없으면 호감도가 오르기 어렵다
IF TALENT:ARG:무관심 && !TALENT:ARG:연모
	IF TFLAG:97 > 1
		TFLAG:97 /= 2
	ELSEIF TFLAG:97 > 0
		TFLAG:97 = 1
	ENDIF
	SIF ABL:ARG:새드끼 >= 4
		TFLAG:97 += 1
ENDIF
;걸레라면 친밀 5로부터 올리기 어려워진다
IF TALENT:ARG:걸레
	TFLAG:97 += 2
	SIF ABL:ARG:친밀 >= 5
		TFLAG:97 /= 2
ENDIF
[SKIPSTART]
;불성실 때마다 밤패널티
SELECTCASE TCVAR:ARG:불성실도
	CASE IS <= 10
			TFLAG:97 ++
	CASE 11 TO 20
	CASE 21 TO 50
		TFLAG:97 -= 1
	CASE 51 TO 100
		TFLAG:97 -= 3
	CASE 101 TO 300
		TFLAG:97 -= 5
	CASEELSE
		TFLAG:97 -= 10
ENDSELECT
[SKIPEND]
;조수 조교중
SIF ASSIPLAY == 1
	TFLAG:97 = 0
;억지로
SIF TCVAR:ARG:억지로
	TFLAG:97 = MIN(0, TFLAG:97 * -3)
;대상이 자고 있으면(자) 상승치 0
SIF !SHIRAHU(ARG)
	TFLAG:97 = 0
;호감도가 신뢰도의 4배를 넘으면(자) 상승치가 저하 다만 데이트중은 예외
IF CFLAG:ARG:2 > 500 && CFLAG:ARG:호감도 > CFLAG:ARG:신뢰도 * 4
	신뢰도캡보정 = CFLAG:ARG:호감도
	SIF FLAG:데이트상대 == ARG
		신뢰도캡보정 /= 2
	IF TALENT:ARG:연모
		TFLAG:97 = GET_REVISION(CFLAG:ARG:신뢰도 * 40, TFLAG:97, 신뢰도캡보정)
	ELSEIF TALENT:ARG:사모
		TFLAG:97 = GET_REVISION(CFLAG:ARG:신뢰도 * 30, TFLAG:97, 신뢰도캡보정)
	ELSE
		TFLAG:97 = GET_REVISION(CFLAG:ARG:신뢰도 * 20, TFLAG:97, 신뢰도캡보정)
	ENDIF
ENDIF
;동시에 절정
SIF SYNCED_ORGASM(ARG)
	TFLAG:97 = TFLAG:97 * 3 / 2
;상대 주도 우후후
SIF CFLAG:우후후 == 2
	TFLAG:97 = TFLAG:97 * 6 / 5
;스침
SIF TALENT:ARG:스침 == 1
	CALL YUKIZURI_BONUS(ARG)
;호감도마이너스
SIF TFLAG:호감도마이너스 && TFLAG:97 > 0
	TFLAG:97 = TFLAG:97 * -1 *TFLAG:호감도마이너스
SIF TFLAG:호감도보너스 && TFLAG:97 > 0
	;TFLAG:97 = TFLAG:97 * TFLAG:호감도보너스
	TFLAG:97 = TFLAG:97 * (100 + TFLAG:호감도보너스) / 100

;호감도 상승이 낮은 경우, 거점 거주자이며 모브가 같은 장소에 있으면 보너스
SIF TFLAG:97 < 3 && WITH_MOB() && MAP_거주자(MAIN_MAP, ARG)
	TFLAG:97 ++
;커맨드 실패했을 경우 호감 도는 오르지 않는다
SIF TFLAG:98 < 0 || TFLAG:호감도안들어감
	TFLAG:97 = MIN(0, TFLAG:97)

SIF TFLAG:3 == 300 && TFLAG:193 < 0
	TFLAG:97 = 0

;만취간 들키고 때는 문답무용으로 호감도 대폭 저하
SIF TCVAR:ARG:만취간들킴
	TFLAG:97 -= MAX(30 - ABL:ARG:친밀, 0) * 3

;KR판 오리지날 호감신뢰상승률
TFLAG:97=TFLAG:97*(1+FLAG:호감신뢰상승률+TALENT:수수께끼의매력*2)

;호감 때마다 상승치를 가산
CFLAG:ARG:2 += TFLAG:97

;호감 도는 0 이하에도 100만 이상이나 되지 않는다
CFLAG:ARG:2 = LIMIT(CFLAG:ARG:2, 0, 1000000)
LOCAL:1 = 0
FOR LOCAL, 0,100
	SIF SOURCE:ARG:LOCAL
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	TFLAG:97 = 0

;호감도 표시
SIF TFLAG:97
	PRINTFORML \@(TFLAG:97 >= 0) ? 호감도＋{TFLAG:97} # 호감도-{-TFLAG:97}\@
;호감도상승치 합산
FLAG:12 += TFLAG:97

@YUKIZURI_BONUS(ARG)
TFLAG:97 *= 10
SIF CFLAG:ARG:몸목적
	TFLAG:97 *= 2
LOCAL = GETPALAMLV(PALAM:ARG:호의, 10) - 1
CALL YUKIZURI_BONUS_1(ARG, "친밀", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:습득,3)
CALL YUKIZURI_BONUS_1(ARG, "기교", LOCAL)

LOCAL = MAX(GETPALAMLV(PALAM:ARG:복종,3), GETPALAMLV(PALAM:ARG:굴복,3))
CALL YUKIZURI_BONUS_1(ARG, "순종", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:복종,3) - 1
CALL YUKIZURI_BONUS_1(ARG, "봉사정신", LOCAL)


@YUKIZURI_BONUS_1(C_ID, CHK_ABL, ARG)
#DIM C_ID
#DIMS CHK_ABL

SIF !ARG
	RETURN
SIF ABL:C_ID:CHK_ABL >= ARG
	RETURN
LOCAL = FINDELEMENT(ABLNAME, CHK_ABL)
CALL COLORMESSAGE(@"%조사처리(ABLNAME:LOCAL,"가")% {ARG} 올랐다", C_YELLOW,2,1)
ABL:C_ID:CHK_ABL = ARG
