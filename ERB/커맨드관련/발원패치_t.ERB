@GANKAKE_0(ARG, ARG:1)

IF CFLAG:ARG:신사거주 && ARG > 0
;	// 호감도 상승 //
	CFLAG:ARG:호감도 += 10 + RAND:20
	CFLAG:ARG:신뢰도 += 10 + RAND:20
ENDIF
FOR LOCAL, 1, CHARANUM
;	// 이사로 자택이 바뀌므로 자택 멤버도 발원함 플래그는 세우기로 한다 //
;	SIF CFLAG:LOCAL:신사거주 > 0
;		CONTINUE
	IF ARG == LOCAL
		CFLAG:LOCAL:다음날내방 = 1
	ELSE
		CFLAG:LOCAL:다음날내방 = 0
	ENDIF
NEXT

FLAG:발원내용 = ARG

@GANKAKE_1(ARG)
#FUNCTION
#DIM 버프가능개소수
#DIM 버프하는곳후보, 12
#DIM 실제버프하는곳

;버프하는곳후보:0~12에 소질번호를 할당해, 그때마다 버프가능개소수를 늘린다

VARSET 버프하는곳후보
버프가능개소수 = 0
실제버프하는곳 = 0

IF TALENT:ARG:사모 || IS_LOVER(ARG)
	IF TALENT:ARG:성적흥미 < 1
		버프하는곳후보:버프가능개소수 = 23
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:자기애 < 1
		버프하는곳후보:버프가능개소수 = 31
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:젖기쉬움 < 1
		버프하는곳후보:버프가능개소수 = 41
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:쾌감응답 < 1
		버프하는곳후보:버프가능개소수 = 70
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:도착적 < 1
		버프하는곳후보:버프가능개소수 = 80
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:새드 < 1
		버프하는곳후보:버프가능개소수 = 82
		버프가능개소수 ++
	ENDIF
	IF TALENT:ARG:마조 < 1
		버프하는곳후보:버프가능개소수 = 83
		버프가능개소수 ++
	ENDIF
ENDIF

FOR LOCAL,101,105
	IF TALENT:ARG:LOCAL < 1
		버프하는곳후보:버프가능개소수 = LOCAL
		버프가능개소수 ++
	ENDIF
NEXT
IF TALENT:ARG:106 < 1
	버프하는곳후보:버프가능개소수 = 106
	버프가능개소수 ++
ENDIF

SIF !버프가능개소수
	RETURNF 버프가능개소수

실제버프하는곳 = 버프하는곳후보:(RAND:버프가능개소수)
TALENT:ARG:실제버프하는곳 ++
TCVAR:TARGET:발원함보정 = 실제버프하는곳
TFLAG:193 = 실제버프하는곳

RETURNF 버프가능개소수

@SHOW_CHALALIST
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:출금 && !TALENT:LOCAL:스침 && !TALENT:LOCAL:사망
		PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL,15,LEFT%
NEXT

@IKENIE(ARG,ARG:1)
;ARG 대상
;ARG:1 소원(1=반발 해제 2=감도 일시 상승 3=요술망치 4=시간정지들킴 해제 5=천벌)
#DIM 총계
#DIM 종류수
#DIM 중복
#DIM 바친매수
#DIM 바치는매수
#DIM 필요매수
VARSET LOCAL
CALL CHECK_COLLECTION(ARG)
총계 = RESULT:0
종류수 = RESULT:1
중복 = 총계 - 종류수
바친매수 = 0
바치는매수 = 0
;KR판 오리지널 CASE 8,9,10,11,12,13 
SELECTCASE ARG:1
	CASE 1,8
		필요매수 = 25
	CASE 2
		필요매수 = 3
	CASE 3
		필요매수 = CFLAG:MASTER:마력회수 / 2 + 1
	CASE 4,6
		필요매수 = 1
	CASE 5,7
		필요매수 = 3
	CASE 9,10,11,12,13
		필요매수 = 5
ENDSELECT

PRINTL 머릿속에 목소리가 울린다…
PRINTW ((그 소망을 이루고 싶다면, 공물을 바쳐라…))
PRINTFORMW ((%CALLNAME:ARG%의 팬티를{필요매수}매 바쳐라……))
PRINTFORM 현재%CALLNAME:ARG%의 팬티를
IF !종류수
	PRINTL 한 장도 가지고 있지 않다
	RETURN -1
ENDIF
PRINTFORML 전부{총계}매{종류수}종, 겹치고 있는 것은{중복}매 가지고 있다
IF 총계 < 필요매수
	PRINTFORML 소망하고 있는 매수에{필요매수 - 총계}매 부족하다
	RETURN -1
ENDIF

LOCALS = 바친다
SIF 중복 < 필요매수
	LOCALS = 겹치지 않은 것도 사용하게 되지만 바친다
$LOOP
;KR판 오리지날 수정. 일판 4.760 에서는 LOCALS 처리를 잘못해두었기에 이를 수정.
CALL ASK_M("바치지 않는다",1,@"%LOCALS%",총계 >= 필요매수)
IF RESULT == 1
	FOR LOCAL, 0, MAXPANTS
		IF CFLAG:ARG:(팬티관리 + LOCAL) > 1
			바치는매수 = MIN(CFLAG:ARG:(팬티관리 + LOCAL) - 1, (필요매수 - 바친매수))
			CFLAG:ARG:(팬티관리 + LOCAL) -= 바치는매수
			바친매수 += 바치는매수
			SIF 바친매수 >= 필요매수
				BREAK
		ENDIF
	NEXT
	IF 바친매수 < 필요매수
		FOR LOCAL, 0, MAXPANTS
			바치는매수 = MIN(CFLAG:ARG:(팬티관리 + LOCAL), (필요매수 - 바친매수))
			CFLAG:ARG:(팬티관리 + LOCAL) -= 바치는매수
			바친매수 += 바치는매수
			FLAG:700 --
			SIF 바친매수 >= 필요매수
				BREAK
		NEXT
	ENDIF
	SELECTCASE ARG:1
		CASE 1
			MARK:ARG:반발각인 --
			TFLAG:193 = 30
		CASE 2
			FLAG:발원내용 = 999
		CASE 3
			CFLAG:MASTER:마력회수 = 1
			SIF (CFLAG:MASTER:요술망치 == 4 || CFLAG:MASTER:요술망치 == 5) && TALENT:MASTER:형상 == 2
				TALENT:MASTER:형상 = CFLAG:MASTER:382
		CASE 4
			CFLAG:ARG:시간정지들킴 = 0
		CASE 5
			FLAG:발원내용 = 204
			TFLAG:193 = 200
			FLAG:개운대문구입회수 = 1
			BASE:53:체력 = 1
			BASE:53:기력 = 0
		CASE 6
			FLAG:발원내용 = 206
			IF FLAG:자식기원 == TARGET
				CFLAG:자식보정 ++
			ELSE
				FOR LOCAL,1,CHARANUM
					CFLAG:LOCAL:자식보정 = 0
				NEXT
				CFLAG:자식보정 ++
			ENDIF
			SIF TALENT:임신소망
				CFLAG:자식보정 ++
			SIF TALENT:연인
				CFLAG:자식보정 ++
			FLAG:자식기원 = TARGET
		CASE 7
			ABL:ARG:새드끼 -= 1
			JUEL:ARG:우월 = 0
			EXP:ARG:가학쾌락경험 = MAX(EXP:ARG:가학쾌락경험 - 200, 0)
		;KR판 오리지널 CASE 8,9,10,11,12,13 
		CASE 8
			CFLAG:ARG:약점잡음=0
		CASE 9,10,11,12
			TALENT:ARG:((ARG:1)-8+100)++
		CASE 13
			TALENT:ARG:M감도++
		
	ENDSELECT
	PRINTFORML %마스터가% 비장의 콜렉션을 늘어놓고 빌자, 이 세상 아닌 불꽃이{필요매수}매의 팬티를 타다 남은것 하나 남기지 않고 다 태워 버렸다!
	FLAG:독신 += 필요매수
	IF ARG:1 == 7
		CALL COLORMESSAGE(@"%CALLNAME:ARG%의 새드끼가 내려갔다",C_YELLOW,2,1)
		CALL COLORMESSAGE(@"%CALLNAME:ARG%의 가학쾌락경험이 내려갔다",C_YELLOW,2,1)
		CALL COLORMESSAGE(@"%CALLNAME:ARG%의 우월의 구슬이 0이 되었다",C_YELLOW,2,1)
	ENDIF
	IF ARG:1 == 6
		PRINTFORML ((그 아가씨에게로의 정과 기원을 빠뜨리지 말것))
		PRINTFORMW ((자 그렇다면 머지않아 소원은 성취하자……))
	ELSEIF ARG:1 == 2 && !GANKAKE_1(ARG)
		PRINTW
		PRINTW …
		PRINTW ……
		PRINTW ………
		PRINTW …………
		PRINTFORML 팬티님도 %ARG는% 더 이상 야하게 할 수 없을 것 같다……
		RETURN -1
	ELSE
		PRINTW ((네 소원은 성취되었도다……))
	ENDIF
	PRINTFORML %마스터는% 팬티님에 대한 경신의 생각을 새로이 했다
	RETURN 0
ELSE
	RETURN -1
ENDIF

@PINK_WIND
SETCOLOR 255,100,255
FONTBOLD
PRINTFORMW 환상향에 분홍빛 바람이 불었다!
FOR LOCAL,1,CHARANUM
	IF !RAND:15
		PRINTFORML 분홍빛 바람을 받은 %조사처리(CALLNAME:LOCAL,"는")% 이상한 기분이 되었다…
		CFLAG:LOCAL:욕구불만도 += LIMIT((10 + RAND:20 + RAND:20 + RAND:20) * ABL:LOCAL:욕망, 200,1000)
	ENDIF
NEXT
RESETCOLOR
FONTREGULAR
PRINTW ((네 소원은 성취되었도다……))
PRINTFORML %마스터는% 팬티님에 대한 경신의 생각을 새로이 했다

@DOWSING_PANTS
#DIM CHK_SLOT
#DIM CHARA
#DIM HIT
PRINTFORMW %마스터는% 의뢰된 물건을 찾을 수 있기를 기도했다……
PRINTL 
HIT = 0
FOR CHK_SLOT, 1, 6
	SIF !GETBIT(CAN_DOWSING(), CHK_SLOT)
		CONTINUE
	CHARA = 0
	SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
	CASE "수색(인물)"
		CHARA = IRAI_SLOT_TO_PERSON(CHK_SLOT)
		;手持ち
		CALL CHECK_COLLECTION(CHARA, 1)
		IF RESULT
			CALL DOWSING_PANTS_MESSAGE(CHARA, 1)
			HIT = 1
		ELSE
			;コレクション
			CALL CHECK_COLLECTION(CHARA, 0)
			IF RESULT
				CALL DOWSING_PANTS_MESSAGE(CHARA, 0)
				HIT = 1
			ENDIF
		ENDIF
	CASE "수색(장소)"
		;自宅があるキャラを抽出
		FOR CHARA, 1, CHARANUM
			IF CFLAG:CHARA:자택위치 == GET_MAP_REPLACEMENT(IRAI_SLOT_TO_PLACE(CHK_SLOT))
				;手持ち
				CALL CHECK_COLLECTION(CHARA, 1)
				IF RESULT
					CALL DOWSING_PANTS_MESSAGE(CHARA, 1)
					HIT = 1
					BREAK
				ELSE
					;コレクション
					CALL CHECK_COLLECTION(CHARA, 0)
					IF RESULT
						CALL DOWSING_PANTS_MESSAGE(CHARA, 0)
						HIT = 1
						BREAK
					ENDIF
				ENDIF
			ENDIF
		NEXT
	CASEELSE
		CONTINUE
	ENDSELECT
NEXT
SIF !HIT
	PRINTFORMW ……그러나 기도는 이루어지지 않았다

@DOWSING_PANTS_MESSAGE(CHARA, HAND)
#DIM CHARA
#DIM HAND
#DIM 종류
#DIM 소유수
CALL FISHER_YATES_SHAFFLE(MAXPANTS)
SETCOLOR C_YELLOW
FOR LOCAL, 0, MAXPANTS
	종류 = SHAFFLE_ARRAY:LOCAL
	IF !HAND
		IF CFLAG:CHARA:(종류 + 팬티관리)
			PRINTFORMW 이럴수가! 보관하고 있었던 %CALLNAME:CHARA%의 %조사처리(PANTSNAME(종류,CHARA),"가")% 빛나기 시작했다!
			BREAK
		ENDIF
	ELSE
		IF PANTS_TEMP:CHARA:종류
			PRINTFORMW 이럴수가! 손에 들고 있었던 %CALLNAME:CHARA%의 %조사처리(PANTSNAME(종류,CHARA),"가")% 빛나기 시작했다!
			BREAK
		ENDIF
	ENDIF
NEXT
RESETCOLOR

