@클리캡유지조건(ARG)
#FUNCTION
IF TALENT:ARG:당신의노예
	RETURNF 1
ENDIF

IF TALENT:ARG:애욕||TALENT:ARG:섹프
	RETURNF 1
ENDIF

IF CFLAG:ARG:종속도 > 50 && (TALENT:ARG:연인||TALENT:ARG:연모)
	RETURNF 1
ENDIF

IF CFLAG:ARG:종속도 > 70 && (TALENT:ARG:사모)
	RETURNF 1
ENDIF

IF CFLAG:ARG:종속도 > 85
	RETURNF 1
ENDIF

RETURNF 0

;-------------------------------------------------
;클리캡
;도구 사용 커맨드, 레벨 2
;-------------------------------------------------
@COM42
;착탈의처리
TEQUIP:클리캡 = !TEQUIP:클리캡
IF !TEQUIP:클리캡
	TCVAR:클리캡유지 = 0
ELSEIF 클리캡유지조건(TARGET)
	PRINTFORML 계속 붙일까?
	CALL ASK_YN
	IF !RESULT
		TCVAR:클리캡유지 = 1
	ENDIF
ENDIF


;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;봉사 쾌락 플래그
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	;SOURCE:PLAYER:성행동 +=10
	SOURCE:쾌C += 200

ELSE

	DOWNBASE:체력 += 10
	DOWNBASE:기력 += 80
	;SOURCE:PLAYER:성행동 +=10

	SOURCE:쾌C += 200
	SOURCE:노출 += 20
	SOURCE:일탈 += 30
	SOURCE:반감 += 30


	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;억압, 저항 소유에는, 억울소스 추가
	SIF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 30
ENDIF
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET, PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET, PLAYER)
		EXP:(50 + HETEROSEX(TARGET, PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:도구사용경험 ++

RETURN 1
;-------------------------------------------------
;장착 계속의 처리
;-------------------------------------------------
@EQUIP_COM42(ARG)
SIF CFLAG:ARG:동실
PRINTL ＜클리캡 장비중＞

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:ARG:쾌C += 100

ELSE

	DOWNBASE:ARG:체력 += 5
	DOWNBASE:ARG:기력 += 20

	SOURCE:ARG:쾌C += 100
	SOURCE:ARG:노출 += 10
	SOURCE:ARG:일탈 += 15
	SOURCE:ARG:반감 += 15
ENDIF
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE42
;전동 클리캡 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(42)
	RETURN RESULT
;해제는 언제라도 가능
SIF TEQUIP:11
	RETURN 1
;아이템 을 가지고 있는인가, 아이템 없음 설정이 아니면 안 돼
SIF ITEM:2 == 0 && NOITEM == 0
	RETURN 0
;독인형이라면 항문이 붙어있지 않기 때문에 안돼
SIF TALENT:196 == 99
	RETURN 0

;하반신착의
SIF TEQUIP:0 > 1
	RETURN 0
;남자나 양성은 안 돼
SIF HAS_PENIS(TARGET)
	RETURN 0
;ASSI의 경우, 기교 3이상이 아니면 사용할 수 없다.
IF ASSIPLAY
	SIF ABL:ASSI:기교 < 3
		RETURN 0
ENDIF
;샤워중은 안 돼
SIF TEQUIP:32
	RETURN 0

RETURN 1

