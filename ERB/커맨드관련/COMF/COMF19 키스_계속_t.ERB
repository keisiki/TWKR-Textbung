;-------------------------------------------------
;키스(계속)
;애무계 커맨드, 레벨 1
;-------------------------------------------------
@COM19

IF TEQUIP:계속키스
	PRINTFORMW 입술을 떼어 놓으면(자), 타액이 두 명의 사이에 투명의 다리를 만들었다……
	TEQUIP:계속키스 = 0
	RETURN -1
ENDIF

CALL KISS_CANCEL
SIF RESULT == -1
	RETURN -1


;착탈의처리
TEQUIP:계속키스 = 1
CALL 몸사용하기("입",1)
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;봉사 쾌락 플래그
TCVAR:PLAYER:108 = 1
TCVAR:108 = 1

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:PLAYER:성행동=(ABL:PLAYER:혀 * 3 + 10)/20
	SOURCE:쾌M = ABL:PLAYER:혀 * 3 + 10
ELSE
	DOWNBASE:기력 += 30
	SOURCE:PLAYER:성행동=(ABL:PLAYER:혀 * 3 + 10)/20
	SOURCE:쾌M = ABL:PLAYER:혀 * 3 + 10
	SOURCE:PLAYER:쾌M = ABL:혀 * 3 + 10
	SOURCE:사랑 = 100 + GET_REVISION(EXP:애정경험, 200,100)
	SOURCE:성행동 = 50 + GET_REVISION(EXP:애정경험, 50,100)

	SOURCE:굴종 = 10
	SOURCE:일탈 = 10
	SOURCE:반감 = 10
	SOURCE:불결 = 30

	;호감 도를 본다
	IF CFLAG:2 <= 500
		SOURCE:사랑 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:사랑 += 300 + (CFLAG:2 - 500) / 6
	ELSE
		SOURCE:사랑 += 1000 + (CFLAG:2 - 5000) / 8
	ENDIF
	SIF SOURCE:사랑 < 0
		SOURCE:사랑 = 0
	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	CALL KISS_TALENTEFFECT
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 입⇔조교자의 입의 더러움이 이동
STAIN:구 |= STAIN:PLAYER:구
STAIN:PLAYER:구 |= STAIN:구
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
CALL KISS_EXP
SIF TALENT:연모 && !FLAG:70 && !CFLAG:장난
	EXP:애정경험 += 1
TCVAR:FK상실 = 1
RETURN 1

;-------------------------------------------------
;장착 계속의 처리
;-------------------------------------------------
@EQUIP_COM19(ARG)

PRINTL ＜키스중＞
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;KISS_TALENTEFFECT를 사용할까하고 생각했지만, 그렇다면 계속중에 실행한 커맨드에도 영향이 나오는 것에 눈치챘다

IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:PLAYER:성행동=(ABL:PLAYER:혀 * 3 + 5)/20
	SOURCE:쾌M = ABL:PLAYER:혀 * 3 + 5
ELSE

	DOWNBASE:기력 += 30
	DOWNBASE:PLAYER:기력 += 30
	SOURCE:PLAYER:성행동=((ABL:PLAYER:혀 - TALENT:PLAYER:고양이혀 + TALENT:PLAYER:혀놀림 + TALENT:키스마) * 3 + 5)/20
	SOURCE:쾌M = (ABL:PLAYER:혀 - TALENT:PLAYER:고양이혀 + TALENT:PLAYER:혀놀림 + TALENT:키스마) * 3 + 5
	SOURCE:PLAYER:쾌M = (ABL:혀 * 3 - TALENT:고양이혀 + TALENT:혀놀림 + TALENT:키스마) * 3 + 5
	SOURCE:사랑 = 100 - TALENT:PLAYER:고양이혀 * 10 + TALENT:PLAYER:혀놀림 * 20 + TALENT:키스마 * 20
	SOURCE:성행동 = 50 - TALENT:고양이혀* 10 + TALENT:혀놀림 * 20 + TALENT:키스마 * 20
	SOURCE:굴종 = 10
	SOURCE:일탈 = 10
	SOURCE:반감 = 10

	SOURCE:불결 = 30
	SIF TALENT:고양이혀
		SOURCE:가학 += 20
	SIF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 50
ENDIF
CALL KISS_EXP


RETURN 1

@MESSAGE_EQUIP_COM19

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE19
VARSET LOCAL
;키스 하는 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리

SIF GLOBAL_COMABLE(19)
	RETURN RESULT
;볼개그 사용중은 불가
SIF TEQUIP:볼개그
	RETURN 0

;TARGET와 키스중이라면 가능, 벌써 다른 누군가와 키스해줘 라면 안 돼
FOR LOCAL, 1, CHARANUM
	IF TEQUIP:LOCAL:계속키스
		IF LOCAL != TARGET
			RETURN 0
			BREAK
		ELSE
			RETURN 1
			BREAK
		ENDIF
	ENDIF
NEXT


;직전에 TARGET와 키스해줘 라면 가능
SIF PREVCOM == 20 && TARGET == TFLAG: 전턴의TARGET교대
	RETURN 1
;그 이외는 안 돼
RETURN 0

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
@KISS_EXP
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF FLAG:70 == 1 || CFLAG:장난
	SIF HETEROSEX(TARGET, PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 3
	EXP:PLAYER:펠라티오경험 ++
	EXP:PLAYER:키스경험 ++
ELSE
	CALL ADD_EXP_LESNIAN(3)
	EXP:키스경험 ++
	EXP:펠라티오경험 ++
	EXP:M경험 ++
	EXP:PLAYER:펠라티오경험 ++
	EXP:PLAYER:키스경험 ++
	SIF TALENT:TARGET:고양이혀
		EXP:가학쾌락경험 += 1
ENDIF

;-------------------------------------------------
;더러움이나 고양이혀에 의한 캔슬
;-------------------------------------------------
@KISS_CANCEL

IF TALENT:PLAYER:고양이혀 && ABL:PLAYER:혀 < 2
	PRINTFORML 고양이혀의 %조교자가% 서투르게 키스 하면(자) 구내염으로 해 버릴지도 모른다
	PRINTFORML 조금 더 technician가 되고 나서로 하자
	RETURN -1
ENDIF
IF TALENT:고양이혀 && ABL:혀 < 2
	PRINTFORML 고양이혀의 %CALLNAME:TARGET%에 혀를 넣을 수 있으면(자) 구내염이 될지 모른다
	PRINTFORML 조금 더 technician가 되어 주고 나서로 하자
	RETURN -1
ENDIF
IF TALENT:PLAYER:악취내성 < 2
	IF STAIN:구 & 더러운_정액 && ABL:PLAYER:정액중독 < 3
		PRINTFORML 정액 붙고 있는 것은 조금……
		RETURN -1
	ENDIF
	IF STAIN:구 & 더러운_애널
		PRINTFORML 애널 빤 뒤는 조금……
		RETURN -1
	ENDIF
	IF STAIN:구 & 더러운_페니스 && TALENT:PLAYER:악취내성 < 0
		PRINTFORML 섬세한 %조교자는% 페니스를 질천민구와 키스는 할 수 없다
		RETURN -1
	ENDIF
	IF TALENT:PLAYER:악취내성 == -2
		IF STAIN:구 & 더러운_파과피 || STAIN:구 & 더러운_애액
			PRINTFORML 결벽증의 %CALLNAME:PLAYER%에는, 그런 더러움은 견딜 수  없다
			RETURN -1
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;특수소스처리
;-------------------------------------------------
@KISS_TALENTEFFECT
;억압, 저항 소유에는, 억울소스 추가
SIF TALENT:자기애 < 0 || TALENT:저항
	SOURCE:울굴 += 50

;정상위인가 대면좌위라고 사랑과 성행동추가
IF TEQUIP:체위 == 1 || TEQUIP:체위 == 4
	TIMES SOURCE:사랑 , 1.30
	TIMES SOURCE:성행동 , 1.20
ENDIF
;혀를 사용하는 조교에서는, 조교자의 혀놀림에 의해, 쾌감의 소스 증가
IF TALENT:PLAYER:혀놀림
	TIMES SOURCE:사랑 , 1.50
	TIMES SOURCE:쾌M , 1.50
ENDIF
IF TALENT:혀놀림
	TIMES SOURCE:PLAYER:쾌M , 1.50
ENDIF
;고양이혀에서는 감소
IF TALENT:PLAYER:고양이혀
	TIMES SOURCE:사랑 , 0.75
	TIMES SOURCE:쾌M , 0.75
ENDIF
IF TALENT:고양이혀
	SOURCE:가학 += 50
	TIMES SOURCE:PLAYER:쾌M , 0.75
ENDIF
IF TALENT:키스마
	TIMES SOURCE:성행동 , 1.40
	TIMES SOURCE:사랑 , 1.40
	TIMES SOURCE:PLAYER:쾌M , 1.40
ENDIF