;-------------------------------------------------
;볼개그
;SM계 커맨드, 레벨 2
;-------------------------------------------------
@COM106
;착탈의처리
TEQUIP:볼개그 = !TEQUIP:볼개그
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 50

ELSE
	IF RAND:2 == 0
		CALL 종속도상승(TARGET, 1)
	ENDIF
	DOWNBASE:체력 += 0
	DOWNBASE:기력 += 50

	SOURCE:공포 = 500
	SOURCE:노출 = 200
	SOURCE:굴종 = 150
	SOURCE:일탈 = 100
	SOURCE:반감 = 300

	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;긴박경험이 높으면 소비 감소
	IF EXP:긴박경험 < EXPLV:3
		TIMES DOWNBASE:체력 , 1.00
		TIMES DOWNBASE:기력 , 1.00
	ELSEIF EXP:긴박경험 < EXPLV:4
		TIMES DOWNBASE:체력 , 0.90
		TIMES DOWNBASE:기력 , 0.90
	ELSE
		TIMES DOWNBASE:체력 , 0.70
		TIMES DOWNBASE:기력 , 0.70
	ENDIF

	;-------------------------------------------------
	;경험의 처리
	;-------------------------------------------------
	EXP:긴박경험 += 2
ENDIF
RETURN 1
;-------------------------------------------------
;장착 계속의 처리
;-------------------------------------------------
@EQUIP_COM106(ARG)
PRINTL ＜볼개그 장비중＞

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1

ELSE

	DOWNBASE:ARG:체력 += 0

	SOURCE:ARG:공포 += 250
	SOURCE:ARG:노출 += 150
	SOURCE:ARG:굴종 += 75
	SOURCE:ARG:일탈 += 50
	SOURCE:ARG:반감 += 150

	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;긴박경험이 높으면 소비 감소
	LOCAL = 25
	IF EXP:ARG:긴박경험 < EXPLV:3
		TIMES LOCAL , 1.00
	ELSEIF EXP:ARG:긴박경험 < EXPLV:4
		TIMES LOCAL , 0.90
	ELSE
		TIMES LOCAL , 0.70
	ENDIF
	DOWNBASE:ARG:기력 += LOCAL

	;-------------------------------------------------
	;경험의 처리
	;-------------------------------------------------
	EXP:ARG:긴박경험 ++
ENDIF
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE106
;볼개그 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(106)
	RETURN RESULT
;해제는 언제라도 가능
SIF TEQUIP:볼개그
	RETURN 1
;아이템 을 가지고 있는인가, 아이템 없음 설정이 아니면 안 돼
SIF ITEM:17 == 0 && NOITEM == 0
	RETURN 0
;새댁플레이중은 안 돼
SIF TEQUIP:33
	RETURN 0
RETURN 1

