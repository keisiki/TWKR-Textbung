;존부 함수
;F関数はTRYCALL系が使えないので通常関数で存否配列を作っちゃう
@EXIST_일반의뢰12
#LOCALSIZE 1
#LOCALSSIZE 1

;오브젝트 본체
;여기서 ARG로 건네받는 것은 CLIENT_ID입니다
@일반의뢰12(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "의뢰명"
	CALLF MAKE_STR(V_NAME, "혼내줘！")
CASE "의뢰내용 표시"
	CALLF MAKE_STR(V_NAME, @"토벌대상을 탄막승부로 이긴다")
CASE "의뢰 보수 표시"
	CALLF MAKE_STR(V_NAME, @"상금(시가)")
CASE "의뢰실행커맨드명"
	CALLF MAKE_STR(V_NAME, "혼내준다")
CASE "의뢰내용"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(RAND_HANTING(ARG), "토벌"))
CASE "의뢰 서브"
CASE "의뢰기한"
;発生時ではなく受託時に確定します
	CALLF MAKE_INT(V_NAME, 10)
CASE "의뢰 발생율"
;これは他の일반의뢰との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 2)
CASE "의뢰 발생 조건"
;CSVの전투능력が1以上
	SIF CSVABL(ARG, GETNUM(ABL, "전투능력")) >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰 수탁 가능 조건"
;あなたの전투능력が1以上
	SIF ABL:MASTER:전투능력 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰실행 가능 조건"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "의뢰 소멸 조건"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰인데리고나가기가능"
ENDSELECT



@IRAI_일반12(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  CHK_SLOT
#DIM  OPPONENT_REMAIN
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "의뢰 조건 표시"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "의뢰실행시"
;このCASEに限り、RESULTが意味を持ちます
;의뢰실행시のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "실행해 성공", "実行して失敗", "실행해 미해결"です
;独自処理を仕込みたい時は"실행해 미해결"で先延ばしするなりしてください
;
;
	;実行できないパターン
	;拠点では屋内だとダメ（原作だとめっちゃやってるけど）
	IF CANNOT_DANMAKU() == 1
		PRINTFORMW 옥외에서 도전하자…
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	;仕事中はダメ
	ELSEIF CANNOT_DANMAKU() == 2
		PRINTFORMW 바쁜 상황이다…
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	;あなたが疲労
	ELSEIF CANNOT_DANMAKU() == 3
		PRINTFORMW 지금은 피곤하다…
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	;対象が疲労
	ELSEIF CANNOT_DANMAKU() == 4
		PRINTFORMW 지금 도전하는 것은 공정하지 않다…
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	;既に挑んでる
	ELSEIF CANNOT_DANMAKU() == 5
		PRINTFORMW 오늘은 더 이상 도전할 수 없다…
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	ELSE
		;ほぼ弾幕勝負のコピペ
		SETCOLOR C_GREEN
		PRINTFORML ＜토벌전＞
		RESETCOLOR
		PRINTFORMW 승부 개시！！
		CALL DANMAKU_BATTLE(TARGET)
		OPPONENT_REMAIN = RESULT
		CALL KOJO_MESSAGE_SEND("DANMAKU", OPPONENT_REMAIN, TARGET, -1, -1, "戦闘後")
		PRINTL 
		TCVAR:TARGET:탄막승부불능 = 1
		SIF OPPONENT_REMAIN == 0
			CFLAG:TARGET:탄막승부승리 = 1
		DOWNBASE:기력 += 500
		DOWNBASE:MASTER:기력 += 500
		DOWNBASE:체력 += 500
		DOWNBASE:MASTER:체력 += 500
		SOURCE:환락 += (3000 + ABL:친밀 * 100) / (OPPONENT_REMAIN + 1)
		TFLAG:호감도보너스 = 1600 / (OPPONENT_REMAIN + 1)
		TFLAG:신뢰도관리 = 3 - LOCAL

		EXP:MASTER:전투경험 += 3

		TIME += 60

		SIF AT_HOME(MASTER)
			YOGORE:(CFLAG:MASTER:현재위치) = LIMIT(YOGORE:(CFLAG:MASTER:현재위치) + 3000, 0, 6000)
		IF OPPONENT_REMAIN == 0
			PRINTL 
			SETCOLOR C_YELLOW
			PRINTFORMW %타겟을% 혼내주었다！
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("실행해 성공")
		ELSE
			SETCOLOR C_GREEN
			PRINTFORML %CALLNAME:TARGET%의 토벌에 실패했다……
			PRINTFORMW ……이렇게 되면 후일 다시 시도할 수밖에 없다
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("실행해 미해결")
		ENDIF
	ENDIF
CASE "의뢰 성공시"
;성공 보고시の報酬など
	SELECTCASE ABL:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):전투능력
	CASE IS <= 1
		SOURCE:환락 = 1000
		CALL CHANGE_CFLAG(2, TARGET, 10 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 15 + RAND:5)
		CALL GET_SPELL_CARD_BONUS_YEN(1000, 2)
	CASE 2
		SOURCE:환락 = 1500
		CALL CHANGE_CFLAG(2, TARGET, 15 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 20 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(2000, 2)
	CASE 3
		SOURCE:환락 = 2000
		CALL CHANGE_CFLAG(2, TARGET, 20 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 25 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(5000, 2)
	CASE 4
		SOURCE:환락 = 2500
		CALL CHANGE_CFLAG(2, TARGET, 25 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 30 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(10000, 2)
	CASE 5
		SOURCE:환락 = 3000
		CALL CHANGE_CFLAG(2, TARGET, 30 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 35 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(15000, 2)
	CASEELSE
		SOURCE:환락 = 4000
		CALL CHANGE_CFLAG(2, TARGET, 40 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 40 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(30000, 2)
	ENDSELECT
CASE "의뢰 실패시"
;실패 보고시の処理
;過酷な依頼なので少しだけ
	PRINTFORML BoNus FAilEd...
	CALL CHANGE_CFLAG(2, TARGET, -3)
	CALL CHANGE_CFLAG(4, TARGET, -2)
ENDSELECT

;依頼での弾幕勝負ができない場合は真
@CANNOT_DANMAKU
#FUNCTION
;拠点では屋内だとダメ（原作だとめっちゃやってるけど）
IF AT_HOME(MASTER) && !OUTROOF(CFLAG:MASTER:현재위치) && !전투훈련가능(CFLAG:MASTER:현재위치)
	RETURNF 1
;仕事中はダメ
ELSEIF CFLAG:TARGET:행동 == 5
	RETURNF 2
;あなたが疲労
ELSEIF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
	RETURNF 3
;対象が疲労
ELSEIF BASE:TARGET:기력 < 500 || BASE:TARGET:체력 < 500
	RETURNF 4
;既に挑んでる
ELSEIF TCVAR:TARGET:탄막승부불능
	RETURNF 5
ENDIF
RETURNF 0
