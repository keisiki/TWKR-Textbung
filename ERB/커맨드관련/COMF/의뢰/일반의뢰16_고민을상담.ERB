;존부 함수
;F함수는 TRYCALL계를 사용할 수 없기 때문에 통상 함수로 존부 배열을 만들어버린다
@EXIST_일반의뢰16
#LOCALSIZE 1
#LOCALSSIZE 1

;오브젝트 본체
;ここでARGで渡されるのはCLIENT_IDです
@일반의뢰16(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "의뢰명"
	CALLF MAKE_STR(V_NAME, "고민거리를 들어줘！")
CASE "의뢰내용 표시"
	CALLF MAKE_STR(V_NAME, @"고민거리를 들어준다")
CASE "의뢰 보수 표시"
	CALLF MAKE_STR(V_NAME, @"야채나 과일 (해소된다면)")
CASE "의뢰실행커맨드명"
	CALLF MAKE_STR(V_NAME, "고민상담")
CASE "의뢰내용"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "그 외"))
CASE "의뢰 서브"
CASE "의뢰기한"
;発生時ではなく受託時に確定します
CASE "의뢰 발생율"
;これは他の일반의뢰との相対確率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 5)
CASE "의뢰 발생 조건"
	SIF ABL:MASTER:화술기능 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰 수탁 가능 조건"
	CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰실행 가능 조건"
	SIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失敗処理はCARRYOUTで直接書く
;SOURCEやITEM入手、消費が煩雑になるので
CASE "의뢰 소멸 조건"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰인데리고나가기가능"
ENDSELECT



@IRAI_일반16(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 正解の選択肢
#DIM 逆張りナシ
#DIM お悩みナンバー
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "의뢰 조건 표시"
;何か専用の表示情報を設けたい時用
;受託済みの依頼表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも無しで
;ここ以外の表示項目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが見栄えが良いです

CASE "의뢰실행시"
;このCASEに限り、RESULTが意味を持ちます
;依頼実行時のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な引数は"キャンセル", "実行して成功", "実行して失敗", "実行して未解決"です
;独自処理を仕込みたい時は"実行して未解決"で先延ばしするなりしてください
	VARSET RESULT
	VARSET LOCAL
	;口上に委託するか判定
	CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "구상위탁률")
	;LOCAL:1～に発生させない番号を保存
	FOR LOCAL, 1, 100
		LOCAL:LOCAL = RESULT:LOCAL
	NEXT
	;口上専用ルート
	IF RESULT:100 > RAND:100
		VARSET RESULT
		VARSET RESULTS
		CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "구상전용")
	;汎用ルート
	ELSE
		RESULT = 0
		WHILE !RESULT
			RESULT = 0
			;お悩みパターンを増やしたらここを調整(あえてちょっと多めにしてます）
			お悩みナンバー = RAND:26
			SIF LOCAL:お悩みナンバー
				CONTINUE
			CALL IRAI_一般16_お悩みデータベース(お悩みナンバー, TARGET)
		WEND
	ENDIF
	正解の選択肢 = RESULT:1
	逆張りナシ = RESULT:2
	DRAWLINE
	[IF_DEBUG]
		PRINTFORML ｺﾀｴﾊ No.{正解の選択肢} ﾀﾞﾖ
	[ENDIF]
	PRINTFORML ～%CALLNAME:TARGET%의 오늘의 고민～
	PRINTFORML 　　　『%RESULTS%』
	PRINTFORML [ 1] - %RESULTS:1%
	PRINTFORML [ 2] - %RESULTS:2%
	PRINTFORML [ 3] - %RESULTS:3%
	INPUT
	LOCAL = RESULT
	TIME += 10
	;ひとつだけハズレのパターン（"正解の選択肢"がハズレの選択肢）
	IF GROUPMATCH(正解の選択肢, 4, 5, 6)
		IF LOCAL == 正解の選択肢 - 3
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족하지 못했다…
			SOURCE:환락 = 50 + ABL:친밀 * 5
			SOURCE:반감 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("실행해 실패")
		ELSE
			CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족했다！
			TFLAG:193 = 1
			SOURCE:환락 = 500 + ABL:친밀 * 50
			EXP:MASTER:회화경험 += 5
			TFLAG:호감도보너스 = ABL:친밀 * 5
			TFLAG:신뢰도관리 = 2
			RETURN IRAI_CONST_RESULT("실행해 성공")
		ENDIF
	;全部正解のパターン
	ELSEIF 正解の選択肢 == 7
		CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족했다！
		TFLAG:193 = 1
		SOURCE:환락 = 500 + ABL:친밀 * 50
		EXP:MASTER:회화경험 += 5
		TFLAG:호감도보너스 = ABL:친밀 * 5
		TFLAG:신뢰도관리 = 2
		RETURN IRAI_CONST_RESULT("실행해 성공")
	;全部ハズレのパターン
	ELSEIF 正解の選択肢 == 8
		CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족하지 못했다…
		SOURCE:환락 = 50 + ABL:친밀 * 5
		SOURCE:반감 = 100
		TFLAG:193 = -1
		RETURN IRAI_CONST_RESULT("실행해 실패")
	ELSE
		;もし正解の選択肢がおかしい場合、警告を出して修正する
		IF !GROUPMATCH(正解の選択肢, 1, 2, 3)
			PRINTFORMW ※정답 선택지가 이상합니다（{正解の選択肢}）
			正解の選択肢 = RAND:3 + 1
		ENDIF
	;正解の選択肢を選ぶ
		IF LOCAL == 正解の選択肢
			;6分の5の確率でそのまま正解
			IF RAND:6 || 逆張りナシ
				CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족했다！
				TFLAG:193 = 1
				SOURCE:환락 = 500 + ABL:친밀 * 50
				EXP:MASTER:회화경험 += 5
				TFLAG:호감도보너스 = ABL:친밀 * 5
				TFLAG:신뢰도관리 = 2
				RETURN IRAI_CONST_RESULT("실행해 성공")
			;6分の1の確率で逆にハズレ
			ELSE
				CALL COLORMESSAGE(@"＜SAD…＞", C_P_PURPLE, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %타겟은% %CALLNAME:MASTER%의 뻔한 대답에 실망하고 있다…
				SOURCE:환락 = 100 + ABL:친밀 * 10
				SOURCE:반감 = 50
				EXP:MASTER:회화경험 += 1
				TFLAG:193 = -2
				RETURN IRAI_CONST_RESULT("실행해 실패")
			ENDIF
		;6分の1の確率で逆に正解
		ELSEIF !RAND:6 && !逆張りナシ
			CALL COLORMESSAGE(@"＜COOL!!＞", C_AQUA, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %타겟은% %CALLNAME:MASTER%의 의외의 대답에 두근거렸다！
			TFLAG:193 = 2
			SOURCE:환락 = 2000 + ABL:친밀 * 100
			EXP:MASTER:회화경험 += 10
			TFLAG:호감도보너스 = ABL:친밀 * 10
			TFLAG:신뢰도관리 = 5
			RETURN IRAI_CONST_RESULT("실행해 성공")
		;ハズレの選択肢を選ぶ
		ELSE
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %타겟은% %CALLNAME:MASTER%의 대답에 만족하지 못했다…
			SOURCE:환락 = 50 + ABL:친밀 * 5
			SOURCE:반감 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("실행해 실패")
		ENDIF
	ENDIF
CASE "의뢰 성공시"
;성공 보고시の報酬など
	IF TFLAG:192 == 2
		CALL CHANGE_CFLAG(4, TARGET, 10 + ABL:친밀 * 2)
		TALENT:기분 = MIN(TALENT:기분 + 1, 1)
		LOCAL:1 = 3
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, 5 + ABL:친밀)
		TALENT:기분 = MIN(TALENT:기분 + 1, 0)
		LOCAL:1 = 1
	ENDIF
	;野菜召喚の儀
	LOCALS = 
	WHILE LOCALS == ""
		LOCAL = RAND:40 + GETNUM(ITEM, "양배추")
		LOCALS = %ITEMNAME:LOCAL%
	WEND
	CALL GET_ITEM(LOCALS, LOCAL:1, 2)
CASE "의뢰 실패시"
;실패 보고시の処理
	IF TFLAG:192 == -2
		CALL CHANGE_CFLAG(4, TARGET, -5)
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, -1)
	ENDIF
ENDSELECT


;---------------------------------------
;お悩み相談のデータ
;ARG           お悩みナンバー
;RETURN        発生判定
;RESULT:1      正解の選択肢
;RESULT:2      逆張り正解／ハズレ判定の消去
;RESULTS:0     お悩み文章
;RESULTS:1～3  選択肢１～３
;---------------------------------------
@IRAI_一般16_お悩みデータベース(ARG, CHARA)
#DIM CHARA
VARSET RESULT
VARSET RESULTS
VARSET LOCAL
SELECTCASE ARG
CASE 1, 2
	;弾幕ごっこの悩み
	;もともとの戦闘能力がないなら除外
	SIF CSVABL(CHARA, GETNUM(ABL, "전투능력")) <= 0
		RETURN
	RESULTS:0 = 새 스펠카드는 어떤 것이 좋을%性格傾向語尾(CHARA, "까나？")%
	IF ARG % 2 == 0
		RESULTS:1 = 흩뿌리는 탄막
		RESULTS:2 = 발을 묶는 탄막
		RESULTS:3 = 유도탄막
		SELECTCASE CFLAG:CHARA:성격경향
		;気弱系
		CASE 1
			RESULT:1 = 3
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 3
		;その他
		CASEELSE
			RESULT:1 = 1
		ENDSELECT
	ELSE
		RESULTS:1 = 외형을 중시한 아름다운 탄막
		RESULTS:2 = 살의를 내뿜는 귀축 탄막
		RESULTS:3 = 현혹되는 듯한 불가사의한 탄막
		SELECTCASE CFLAG:CHARA:성격경향
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 2
		;真面目系
		CASE 4
			RESULT:1 = 1
		;その他
		CASEELSE
			RESULT:1 = 3
		ENDSELECT
	ENDIF
CASE 3, 4
	;晩酌の悩み
	;お酒が飲めないなら除外
	SIF TALENT:CHARA:술내성 <= 0
		RETURN
	IF ARG % 2 == 0
		RESULTS:0 = 오늘 저녁 반주는 뭐가 좋다고 %性格傾向語尾(CHARA, "생각하니？")%
		RESULTS:1 = 맥주
		RESULTS:2 = 와인
		RESULTS:3 = 일본주
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "요리：좋아하는 맛"), "일식")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "요리：좋아하는 맛"), "양식")
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:성격경향
			;気弱系
			CASE 1
				RESULT:1 = 2
			;強気系
			CASE 2
				RESULT:1 = 3
			;素直系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 2
			;その他
			CASEELSE
				RESULT:1 = 1
			ENDSELECT
		ENDIF
	ELSE
		RESULTS:0 = 오늘 저녁 반주는 %性格傾向語尾(CHARA, "어떻게 할까나？")%
		RESULTS:1 = 들이부을만한 양의 맥주로 거나하게 취해보자
		RESULTS:2 = 달짝지근한 리큐르로 얼근하게 취해보자
		RESULTS:3 = 오늘은 금주!
		IF TALENT:CHARA:술내성 > 2
			RESULT:1 = 1
		ELSEIF TALENT:CHARA:술내성 < 1
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:성격경향
			;気弱系
			CASE 1
				RESULT:1 = 3
			;強気系
			CASE 2
				RESULT:1 = 1
			;素直系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 3
			;その他
			CASEELSE
				RESULT:1 = 2
			ENDSELECT
		ENDIF
	ENDIF
CASE 5, 6
	;本の悩み
	;本を読まないようなら除外
	SIF ABL:CHARA:교양 <= 0
		RETURN
	RESULTS:0 = 새로운 책을 읽는다면 어떤 장르가 좋을%性格傾向語尾(CHARA, "까나？")%
	IF ARG % 2 == 0
		RESULTS:1 = 연애소설
		RESULTS:2 = 추리소설
		RESULTS:3 = SF소설
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(장대|불가사의|매니악|미래)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(위엄|지적|섬세|기묘|레트로)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(귀여운|팝|로리타)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 유행하는 라이트노벨
		RESULTS:2 = 엄숙한 순수 문학
		RESULTS:3 = 매니악한 기서
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(매니악|지적|기묘)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(위엄|레트로|시크)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(귀여운|팝|로리타)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ENDIF
CASE 7, 8
	;香料の悩み
	;条件なし
	RESULTS:0 = 방에 향을 둔다면 어느 것이 좋을%性格傾向語尾(CHARA, "까나？")%
	IF ARG % 2 == 0
		RESULTS:1 = 프레시 계열
		RESULTS:2 = 멜로우 계열
		RESULTS:3 = 딥 계열
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(요술|불가사의|위엄)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(예술적|멋쟁이|섬세)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(자연|심플)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 심플한 방향제
		RESULTS:2 = 아로마 오일
		RESULTS:3 = %CALLNAME:CHARA%의 냄새로 좋아
		IF TALENT:CHARA:악취내성 < 0
			RESULT:1 = 1
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")), "요수", "요조", "요충", "오니")
			RESULT:1 = 3
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")), "흡혈귀", "악마")
			RESULT:1 = 2
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")), "천인", "월인", "마계인")
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:요정
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:조합지식
			RESULT:1 = 2
		ELSE
			RESULT:1 = 1
		ENDIF
	ENDIF
CASE 9, 10
	;音楽の悩み
	;条件なし
	IF ARG % 2 == 0
		RESULTS:0 = 기분전환에 좋은 음악은 어느 쪽일%性格傾向語尾(CHARA, "까나？")%
		RESULTS:1 = 멋진 재즈
		RESULTS:2 = 매력적인 락
		RESULTS:3 = 차분한 클래식
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(근사함|팝|멋쟁이|불가사의)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(무서움|멋쟁이|파워풀|화려)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(아름다운|예술적|지적|위엄|장대)")
			RESULT:1 = 3
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:0 = 동방 노래 중에서는 어떤 것이 좋을%性格傾向語尾(CHARA, "까나？")%
		RESULTS:1 = 역시 보스곡！
		RESULTS:2 = 도중에 나오는 필드곡！
		RESULTS:3 = 구작곡도 좋아!！
		;めんどいけどちまちま
		IF GROUPMATCH(CHARA, [[미마]], [[리카]], [[메이라]], [[엘렌]], [[카나]], [[리카코]], [[치유리]], [[유메미]], [[루코토]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[쿠루미]], [[엘리]], [[무게츠]], [[겐게츠]], [[루이즈]], [[유키]], [[마이(구작)]], [[유메코]], [[신키]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[아큐]], [[유카]], [[렌코]], [[메리]], [[토요히메]], [[요리히메]], [[레이센]], [[토키코]], [[코스즈]], [[츠카사]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[대요정]], [[소악마]], [[릴리 W]], [[릴리 B]], [[테위]], [[시즈하]], [[모미지]], [[키스메]], [[토지코]])
			RESULT:1 = 2
		ELSEIF CSVABL(CHARA, GETNUM(ABL, "전투능력")) >= 5
			RESULT:1 = 1
		ELSE
			;3番にはならない
			RESULT:1 = RAND:2 + 1
		ENDIF
	ENDIF
CASE 11, 12
	;服の悩み
	;条件なし
	RESULTS:0 = 옷을 바꿔 입어 본다면 어떤 스타일이 좋을%性格傾向語尾(CHARA, "까나？")%
	IF ARG % 2 == 0
		RESULTS:1 = 귀여운 원피스
		RESULTS:2 = 쿨한 커터셔츠
		RESULTS:3 = 섹시한 캐미솔
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "귀여운")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "근사함")
			RESULT:1 = 2
		ELSE
			;무서움/美しい派
			RESULT:1 = 3
		ENDIF
	ELSE
		RESULTS:1 = 참신한 패션 코디
		RESULTS:2 = 차분한 캐주얼 코디
		RESULTS:3 = 지금 입은 복장이 좋아
		IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(예술적|팝|멋쟁이|화려)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(심플|시크|섬세|지적)")
			RESULT:1 = 2
		ELSE
			RESULT:1 = 3
		ENDIF
	ENDIF
CASE 13
	;髪型の悩み
	;条件なし
	RESULTS:0 = 머리를 자르려고 하는데 어떤 머리를 할%性格傾向語尾(CHARA, "까나？")%
	RESULTS:1 = 지금 하고 있는 머리가 좋아
	;めんどいけどちまちま
	IF GROUPMATCH(CHARA, [[코마치]], [[니토리]], [[서니]], [[키스메]], [[하타테]], [[코스즈]], [[치유리]])
		RESULTS:2 = 포니테일
	ELSE
		RESULTS:2 = 트윈테일
	ENDIF
	;参考『東方ロング化タグ』,ありがとうございます
	IF GROUPMATCH(CHARA, [[미마]], [[메이라]], [[엘렌]], [[리카코]], [[쿠루미]], [[엘리]], [[신키]])
		RESULTS:3 = 숏헤어
	ELSEIF GROUPMATCH(CHARA, [[메이링]], [[소악마]], [[파츄리]], [[릴리 W]], [[릴리 B]], [[유카리]], [[스이카]])
		RESULTS:3 = 숏헤어
	ELSEIF GROUPMATCH(CHARA, [[케이네]], [[우동게]], [[에이린]], [[카구야]], [[모코우]], [[사나에]], [[텐시]], [[유기]], [[오쿠]])
		RESULTS:3 = 숏헤어
	ELSEIF GROUPMATCH(CHARA, [[뱌쿠렌]], [[후토]], [[코코로]], [[카게로]], [[벤벤]], [[클라운피스]], [[순호]])
		RESULTS:3 = 숏헤어
	ELSEIF GROUPMATCH(CHARA, [[시온]], [[네무노]], [[아운]], [[나루미]], [[오키나]], [[스타]], [[토요히메]], [[요리히메]])
		RESULTS:3 = 숏헤어
	ELSE
		RESULTS:3 = 롱헤어
	ENDIF
	IF TALENT:CHARA:성적흥미 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:담력 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:자존심 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:수치심 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:명랑／음침 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:성적흥미 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:명랑／음침 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:수치심 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:응답 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:태도 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:응답 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:자기애 > 0
		RESULT:1 = 3
	ELSE
		;1番にはならない
		RESULT:1 = RAND:2 + 2
	ENDIF
CASE 14
	;参拝の悩み
	;所属している人らは除外
	IF GROUPMATCH(CHARA, [[레이무]], [[루코토]], [[카나]], [[미마]], [[스이카]], [[코코로]], [[신묘마루]], [[아운]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[나즈린]], [[코가사]], [[누에]], [[뱌쿠렌]], [[마미조]], [[이치린]], [[무라사]], [[쇼]], [[쿄코]], [[조온]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[사나에]], [[카나코]], [[스와코]])
		RETURN
	;月の人たちとわかさぎ姫
	ELSEIF GROUPMATCH(CHARA, [[요리히메]], [[토요히메]], [[레이센]], [[사구메]], [[와카사기히메]])
		RETURN
	ENDIF
	;ほぼ運ゲーです
	RESULTS:0 = 발원하러 간다면 어디가 좋을%性格傾向語尾(CHARA, "까나？")%
	RESULTS:1 = 하쿠레이 신사
	RESULTS:2 = 모리야 신사
	RESULTS:3 = 명련사
	;命蓮寺はヤダ！（3番にはならない）
	IF GROUPMATCH(CHARA, [[요시카]], [[청아]], [[토지코]], [[후토]], [[미코]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[오린]], [[오쿠]], [[파르시]], [[키스메]], [[야마메]], [[무라사]], [[쇼]], [[쿄코]], [[조온]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[사나에]], [[카나코]], [[스와코]])
		RESULT:1 = RAND:2 + 1
	ELSEIF TALENT:CHARA:요정
		RESULT:1 = RAND:2 + 1
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 15
	;ダイエットの悩み
	;条件なし
	RESULTS:0 = 뱃살이 걱정되는데、어떻게 해야 될%性格傾向語尾(CHARA, "까나？")%
	RESULTS:1 = 일상에서의 식이요법！
	RESULTS:2 = 건강한 운동！
	RESULTS:3 = 약 파워！
	IF TALENT:CHARA:대식가
		RESULT:1 = 1
	ELSEIF CSVABL(CHARA, GETNUM(ABL, "전투능력")) <= 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:조합지식
		RESULT:1 = 3
	ELSEIF ABL:CHARA:요리기능 >= 3
		RESULT:1 = 1
	ELSEIF ABL:CHARA:전투능력 <= 3
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:조제Lv >= 3
		RESULT:1 = 3
	ELSE
		SELECTCASE CFLAG:CHARA:성격경향
		;気弱系
		CASE 1
			RESULT:1 = 1
		;強気系
		CASE 2
			RESULT:1 = 2
		;素直系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 2
		;その他
		CASEELSE
			RESULT:1 = 2
		ENDSELECT
	ENDIF
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 16
	;菜園の悩み
	;冬は除外
	SIF DAY:2 == 4
		RETURN
	RESULTS:0 = 집에 있는 텃밭에서 무엇을 기르는 게 좋을%性格傾向語尾(CHARA, "까나？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;LOCAL:2に季節を入れる
		;答えはDAY:2
		IF LOCAL == RESULT:1
			LOCAL:2 = DAY:2
		;１つ目の間違い
		ELSEIF !LOCAL:3
			LOCAL:2 = DAY:2 + 1
			LOCAL:3 ++
		;２つ目の間違い
		ELSE
			;秋なら1つ前、それ以外なら2つ先の季節
			IF DAY:2 == 3
				LOCAL:2 = DAY:2 - 1
			ELSE
				LOCAL:2 = DAY:2 + 2
			ENDIF
		ENDIF
		;キャベツを基準に季節の作物を抽選
		LOCAL:1 = GETNUM(ITEM, "양배추") + (10 * LOCAL:2) - 10 + RAND:3
		RESULTS:LOCAL = %ITEMNAME:(LOCAL:1)%
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 17
	;菜園の悩み～花編～
	;条件なし
	RESULTS:0 = 화분에 무엇을 키우는 게 좋을%性格傾向語尾(CHARA, "까나？")%
	;答え＝DAY:2だと簡単すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;答えには正解の花
		IF LOCAL == RESULT:1
			;関数化してもいいけどするほどでもないかなと思い適当に
			SELECTCASE DAY:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("튤립/아네모네/제비꽃")
			CASE 2
				RESULTS:LOCAL '= TEXTR("나팔꽃/도라지/백합")
			CASE 3
				RESULTS:LOCAL '= TEXTR("코스모스/국화/달맞이꽃")
			CASE 4
				RESULTS:LOCAL '= TEXTR("데이지/수선화/스노우드롭")
			ENDSELECT
		ELSE
			;LOCAL:2に季節を入れる
			;１つ目の間違い
			IF !LOCAL:3
				LOCAL:2 = DAY:2 + 1
				LOCAL:3 ++
			;２つ目の間違い
			ELSE
				;秋なら1つ前、それ以外なら2つ先の季節
				IF DAY:2 == 3
					LOCAL:2 = DAY:2 - 1
				ELSE
					LOCAL:2 = DAY:2 + 2
				ENDIF
			ENDIF
			SELECTCASE LOCAL:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("튤립/아네모네/제비꽃")
			CASE 2
				RESULTS:LOCAL '= TEXTR("나팔꽃/도라지/백합")
			CASE 3
				RESULTS:LOCAL '= TEXTR("코스모스/국화/달맞이꽃")
			CASE 4
				RESULTS:LOCAL '= TEXTR("데이지/수선화/스노우드롭")
			ENDSELECT
		ENDIF
	NEXT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 18
	;休日の悩み
	;条件なし,仕事ないキャラもいるけど
	RESULTS:0 = 다음 휴일에는 뭐하고 보낼%性格傾向語尾(CHARA, "까나？")%
	RESULTS:1 = 밖에 나가서 산책
	RESULTS:2 = 시내에서 쇼핑하기
	RESULTS:3 = 집에서 뒹굴뒹굴
	SELECTCASE CFLAG:CHARA:성격경향
	;気弱系
	CASE 1
		RESULT:1 = 1
	;強気系
	CASE 2
		RESULT:1 = 1
	;素直系
	CASE 3
		RESULT:1 = 3
	;真面目系
	CASE 4
		RESULT:1 = 2
	;その他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
CASE 19
	;人気の悩み
	;条件なし
	RESULTS:0 = 최근 인기가 떨어지고 있다는 %性格傾向語尾(CHARA, "느낌이랄까나")%……
	RESULTS:1 = 절대 그렇지 않아！
	RESULTS:2 = 자기계발에 힘쓰자！
	RESULTS:3 = 이제는 벗을 수밖에 없다
	SELECTCASE CFLAG:CHARA:성격경향
	;気弱系
	CASE 1
		RESULT:1 = 2
	;強気系
	CASE 2
		RESULT:1 = 2
	;素直系
	CASE 3
		RESULT:1 = 1
	;真面目系
	CASE 4
		RESULT:1 = 2
	;その他
	CASEELSE
		;3番にはならない）
		RESULT:1 = RAND:2 + 1
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 20
	;集中力の悩み
	;条件なし
	RESULTS:0 = 요즘 어쩐지 기운이 없는 것 %性格傾向語尾(CHARA, "같으려나")%……
	RESULTS:1 = 제대로 자는 게 좋아
	RESULTS:2 = 기분 전환을 하자
	RESULTS:3 = 열정 주입 싸대기！！
	SELECTCASE CFLAG:CHARA:성격경향
	;気弱系
	CASE 1
		RESULT:1 = 3
	;強気系
	CASE 2
		RESULT:1 = 3
	;素直系
	CASE 3
		RESULT:1 = 2
	;真面目系
	CASE 4
		RESULT:1 = 1
	;その他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
	;この悩みは逆張りナシ
	RESULT:2 = 1
CASE 21
	;御三家の悩み
	RESULTS:0 = 스타팅 세 마리 중에 %性格傾向語尾(CHARA, "어떤 것으로 할까나？")%
	RESULTS:1 = 풀 타입
	RESULTS:2 = 불 타입
	RESULTS:3 = 물 타입
	IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(흙|식물|초록빛|벌레)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(불|해|빨강빛|주홍빛)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(물|얼음|푸른빛|물고기)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "자연")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "파워풀")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "지적")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASE 22
	;進化の悩み
	RESULTS:0 = 무슨 타입으로 진화%性格傾向語尾(CHARA, "시킬까나？")%
	RESULTS:1 = 물 타입
	RESULTS:2 = 전기 타입
	RESULTS:3 = 불 타입
	IF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(물|얼음|푸른빛|물고기)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(천둥|별|노랑빛)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(불|해|빨강빛|주홍빛)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(아름다운|지적)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(근사함|화려)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", CHARA, "감성：좋아함"), "(귀여운|파워풀)")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASEELSE
	;プレゼントの悩み,一番クイズっぽいので確率をちょっと上げてます
	;相性のいいキャラを探す
	CALL FISHER_YATES_SHAFFLE(CHARANUM)
	FOR LOCAL:1, 1, CHARANUM
		LOCAL = SHAFFLE_ARRAY:(LOCAL:1)
		IF RELATION:CHARA:LOCAL > 100
			LOCAL:2 = LOCAL
			BREAK
		ENDIF
	NEXT
	;いないなら除外
	SIF !LOCAL:2
		RETURN
	;友人キャラを隔離
	LOCAL:10 = LOCAL:2
	[IF_DEBUG]
		PRINTFORML %CALLNAME:(LOCAL:10)%
	[ENDIF]
	;答えを先に入れるパターン
	LOCAL = RAND:3 + 1
	;答えの選択肢に高得点を入れる,あえて701点ではなく700点から
	CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 700, 1000)
	LOCALS:LOCAL = %RESULTS%
	;ハズレの選択肢にダミーを入れる
	FOR LOCAL:1, 1, 4
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 0, 699)
		LOCALS:(LOCAL:1) = %RESULTS%
	NEXT
	;関数を呼んだのでこの時点ではRESULT/RESULTSがぐちゃってます
	RESULTS:0 = %CALLNAME:(LOCAL:10)%에게 선물한다면 어떤것이 좋을%性格傾向語尾(CHARA, "까나？")%
	RESULTS:1 = %LOCALS:1%
	RESULTS:2 = %LOCALS:2%
	RESULTS:3 = %LOCALS:3%
	RESULT:1 = LOCAL
	RESULT:2 = 0
[SKIPSTART]
CASE 
	RESULTS:0 = 
	RESULTS:1 = 
	RESULTS:2 = 
	RESULTS:3 = 
	RESULT:1 = 
[SKIPEND]
ENDSELECT
RETURN 1

;性格傾向によって汎用台詞の語尾を変える
@性格傾向語尾(ARG, ARGS)
#FUNCTIONS
;気弱系＆素直系＆その他系はそのまま返す
SIF !GROUPMATCH(CFLAG:ARG:성격경향, 2, 4)
	RETURNF ARGS
SELECTCASE ARGS
CASE "까나？"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "까？"
	;真面目系
	CASE 4
		RETURNF "까요？"
	ENDSELECT
CASE "생각하니？"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "생각해？"
	;真面目系
	CASE 4
		RETURNF "생각하시나요？"
	ENDSELECT
CASE "어떻게 할까나？"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "어떻게 할까？"
	;真面目系
	CASE 4
		RETURNF "어떻게 할까요？"
	ENDSELECT
CASE "어떤 것으로 할까나？"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "어떤 것으로 할까？"
	;真面目系
	CASE 4
		RETURNF "어떤 것으로 할까요？"
	ENDSELECT
CASE "시킬까나？"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "시킬까？"
	;真面目系
	CASE 4
		RETURNF "시킬까요？"
	ENDSELECT
CASE "느낌이랄까나"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "느낌이 들어"
	;真面目系
	CASE 4
		RETURNF "느낌이 들어요"
	ENDSELECT
CASE "같으려나"
	SELECTCASE CFLAG:ARG:성격경향
	;強気系
	CASE 2
		RETURNF "같네"
	;真面目系
	CASE 4
		RETURNF "같아요"
	ENDSELECT
ENDSELECT
