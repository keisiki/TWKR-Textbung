@고유의뢰30(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：설교 타임
CASE 101
	SELECTCASE O_DATA
	CASE "의뢰명"
		CALLF MAKE_STR(V_NAME, "설교 타임")
	CASE "의뢰내용 표시"
		CALLF MAKE_STR(V_NAME, "코마치에게 에이키의 편지를 전해준다")
	CASE "의뢰 보수 표시"
		CALLF MAKE_STR(V_NAME, @"\\1000")
	CASE "의뢰실행 커맨드 이름"
		CALLF MAKE_STR(V_NAME, "편지를 건네준다")
		
	CASE "의뢰내용"
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA([[코마치]], "전령(인물)"))
	CASE "의뢰 서브"
	CASE "의뢰기한"
	;발생시는 아니고 수탁시에 확정합니다
	CASE "의뢰 발생율"
	;이것은 다른 고유 의뢰와의 상대 확률 어째서 10%가 아닙니다
		CALLF MAKE_INT(V_NAME, 10)
	CASE "의뢰 발생 조건"
		SIF CAN_MEET([[코마치]])
			CALLF MAKE_INT(V_NAME, 1)
	CASE "의뢰 수탁 가능 조건"
	;수탁 가능은@COM_ABLE366도 확인
		;일의 요일이 아니다
		IF !YOUBI_MATCH(DAY, "월수금")
		;일의 시간대가 아니다
		ELSEIF !INRANGE(TIME / 60, 11, 15)
		;코마치가 외출 맵에서 삼도천이나 MAIN_MAP에서 주사위 강변에 들어가 있다
		ELSEIF (CFLAG:[[코마치]]:현재위치 == 삼도천 && MAIN_MAP != 6) || (CFLAG:[[코마치]]:현재위치 == 602 && MAIN_MAP == 6)
		;코마치が출산휴가중
		ELSEIF CFLAG:[[코마치]]:출산휴가중
		;상기의 조건을 빠졌을 경우, 땡땡이 인정으로 한다
		ELSE
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	CASE "의뢰실행 가능 조건"
	;실행 가능은@COM_ABLE367도 확인
		CALLF MAKE_INT(V_NAME, 1)
	;성공/실패 처리는 CARRYOUT로 직접 쓴다
	;SOURCE나 ITEM 입수 따위, 내용이 복잡해서
	CASE "의뢰 소멸 조건"
	;30%로 소실
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;고유 의뢰의 설정용
;이 함수는 실행/성공/실패시의
;보수와 페널티와 지문만을 취급합니다
;각 캐릭터의 메시지는 다른 장소에서
@IRAI_고유30(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "의뢰명", CLIENT_ID)
;개개의 의뢰명으로 분기
CASE "설교 타임"
	SELECTCASE SCENE
	CASE "의뢰 조건 표시"
	;무엇인가 전용의 표시 정보를 마련하고 싶을 시용
	;수탁 끝난 의뢰 표시로 사용됩니다
	;
	;PRINT계만으로 SOURCE라든지는 넣으면 안 돼, WAIT도 없음으로
	;여기 이외의표시 항목은
	;PRINTFORML 　　　　　：
	;되고 있으므로 스페이스를 가지런히 하는 편이 보기에 좋습니다
	
	CASE "의뢰실행시"
	;이 CASE에 한정해, RESULT가 의미를 가집니다
	;의뢰실행시의 RESULT에는 IRAI_CONST_RESULT()를 사용해 주세요
	;가능한 인수는"캔슬", "실행해 성공", "실행해 실패", "실행해 미해결"입니다
	;독자 처리를 가르치고 싶을 때는"실행해 미해결"로 유보하자 마자 해 주세요
		PRINTFORMW %마스터는% %CALLNAME:TARGET%에게 맡고 있던 편지를 전했다
		SIF DIARY:30:8 == 0
			CALL K30_WRITE_DIARY(8)
		SOURCE:공포 = 100
		SOURCE:굴종 = 500
		CALL CHANGE_CFLAG(4, TARGET, 3)
		RETURN IRAI_CONST_RESULT("실행해 성공")
	CASE "의뢰 성공시"
	;성공 보고시의 보수 등
		;시스템 메시지 다하고 호감도 변화
		;마지막 인수만 호감도가 변화
		CALL CHANGE_CFLAG(2, TARGET, 20)
		;시스템 메시지 다하고 신뢰도 변화
		;마지막 인수만 신뢰도가 변화
		CALL CHANGE_CFLAG(4, TARGET, 15)
		CALL GET_MONEY_YEN(1000, 2)
	CASE "의뢰 실패시"
	;실패 보고시의 처리
		;시스템 메시지 다하고 호감도 변화
		;마지막 인수만 호감도가 변화
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;시스템 메시지 다하고 신뢰도 변화
		;마지막 인수만 신뢰도가 변화
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
ENDSELECT
