;존부 함수
;F함수는 TRYCALL계를 사용할 수 없기 때문에 통상 함수로 존부 배열을 만들어버린다
@EXIST_일반의뢰6
#LOCALSIZE 1
#LOCALSSIZE 1

;오브젝트 본체
;여기서 ARG로 건네받는 것은 CLIENT_ID입니다
@일반의뢰6(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "의뢰명"
	CALLF MAKE_STR(V_NAME, "누군가 가르쳐 줘!")
CASE "의뢰내용 표시"
	CALLF MAKE_STR(V_NAME, @"%CALLNAME:ARG%의 의문에 답한다")
CASE "의뢰 보수 표시"
	CALLF MAKE_STR(V_NAME, @"%금액표시(500)%")
CASE "의뢰실행 커맨드 이름"
	IF IS_LIBRARY(CFLAG:MASTER:현재위치)
		CALLF MAKE_STR(V_NAME, "조사해본다")
	ELSE
		CALLF MAKE_STR(V_NAME, "가르쳐 준다")
	ENDIF
CASE "의뢰내용"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "그 외"))
CASE "의뢰 서브"
	CALLF MAKE_INT(V_NAME, (ABL:ARG:교양 + RAND(1, 10)) * 10)
CASE "의뢰기한"
;발생시는 아니고 수탁시에 확정합니다
CASE "의뢰 발생율"
;이것은 다른 일반의뢰와의 상대 확률 어째서 10%가 아닙니다
	CALLF MAKE_INT(V_NAME, 10)
CASE "의뢰 발생 조건"
	SIF ABL:ARG:교양 <= ABL:MASTER:교양
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰 수탁 가능 조건"
	CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰실행 가능 조건"
	;조사 것은 언제라도
	IF FLAG:70
	ELSEIF IS_LIBRARY(CFLAG:MASTER:현재위치)
		CALLF MAKE_INT(V_NAME, 1)
	;가르치는 것은 CLIENT를 TARGET로 할 필요가 있다
	ELSEIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;성공/실패 처리는 CARRYOUT로 직접 쓴다
;SOURCE나 ITEM 입수, 소비가 번잡하게 되므로
CASE "의뢰 소멸 조건"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "의뢰인 데리고 나가 가능"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_일반6(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  LIBRARY_ID
#DIMS LIBRARY_NAME
#DIM  CHK_SLOT
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "의뢰 조건 표시"
PRINTFORML 　　이해도：%IRAI_일반6_DIFFICULTY(CHK_SLOT)%
;무엇인가 전용의 표시 정보를 마련하고 싶을 시용
;수탁 끝난 의뢰 표시로 사용됩니다
;
;PRINT계만으로 SOURCE라든지는 넣으면 안 돼, WAIT도 없음으로
;여기 이외의표시 항목은
;PRINTFORML 　　　　　：
;되고 있으므로 스페이스를 가지런히 하는 편이 보기에 좋습니다

CASE "의뢰실행시"
;의뢰실행시에는 RESULT가 의미를 가집니다
;이 CASE에 한정해, RESULT가 의미를 가집니다
;의뢰실행시의 RESULT에는 IRAI_CONST_RESULT()를 사용해 주세요
;가능한 인수는"캔슬", "실행해 성공", "실행해 실패", "실행해 미해결"입니다
;독자 처리를 가르치고 싶을 때는"실행해 미해결"로 유보하자 마자 해 주세요
	LIBRARY_ID = IS_LIBRARY(CFLAG:MASTER:현재위치)
	IF LIBRARY_ID
		IF INRANGE(LIBRARY_ID, 1, 99)
			LIBRARY_NAME '= STR:LIBRARY_ID
		ELSE
			LIBRARY_NAME '= STR:(8000 + LIBRARY_ID)
		ENDIF
		IF TARGET == CLIENT_ID
			PRINTFORML %마스터는% %CALLNAME:TARGET%의 의문에 답하기 위해 %조사처리(LIBRARY_NAME,"를")% 방문했다
			PRINTFORML 여기라면 도움이 되는 자료가 발견될 것 같다
			CALL ASK_M("그만둔다", 1, "조사해본다", 1, "답변해 본다", 1)
			SELECTCASE RESULT
			CASE 0
				RETURN IRAI_CONST_RESULT("캔슬")
			CASE 1
				PRINTFORMW %마스터와% %타겟은% %LIBRARY_NAME%에서 조사를 했다
				EXP:MASTER:학습경험 ++
				EXP:TARGET:학습경험 ++
				IRAI_SUB:CHK_SLOT -= ABL:MASTER:교양 + ABL:TARGET:교양 + 5
				IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
				TIME += 30
			CASE 2
				GOTO ANSWER
			ENDSELECT
		ELSE
			PRINTFORML %마스터는% %LIBRARY_NAME%에서 조사를 했다
			EXP:MASTER:학습경험 ++
			IRAI_SUB:CHK_SLOT -= ABL:MASTER:교양 + 3
			IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
		ENDIF
		PRINTFORML 현재의 당신의 이해도
		PRINTFORMW 　%IRAI_일반6_DIFFICULTY(CHK_SLOT)%
		RETURN IRAI_CONST_RESULT("실행해 미해결")
	ELSE
		$ANSWER
		PRINTFORML %마스터는% %CALLNAME:TARGET%의 의문에 답했다
		IF IRAI_일반6_SUCCESS(CHK_SLOT)
			PRINTFORMW %CALLNAME:TARGET%의 의문은 풀렸다!
			SOURCE:환락 = 300
			EXP:MASTER:학습경험 += ABL:TARGET:교양 * 5
			EXP:TARGET:학습경험 += ABL:TARGET:교양 * 5
			IF ITEM:103
				EXP:MASTER:학습경험 ++
				EXP:TARGET:학습경험 ++
			ENDIF
			DOWNBASE:MASTER:체력 = 70
			DOWNBASE:MASTER:기력 = 100
			DOWNBASE:체력 = 50
			DOWNBASE:기력 = 70
			TIME += 30
			RETURN IRAI_CONST_RESULT("실행해 성공")
		ELSE
			PRINTFORMW %타겟은% 납득할 수 없었던 것 같다……
			RETURN IRAI_CONST_RESULT("실행해 실패")
		ENDIF
	ENDIF
CASE "의뢰 성공시"
;성공 보고시의 보수 등
	CALL CHANGE_CFLAG(2, TARGET, 50)
	CALL CHANGE_CFLAG(4, TARGET, 30)
	CALL KIGEN_CHANGE(TARGET,100,1)
	CALL GET_MONEY_YEN(500, 2)
CASE "의뢰 실패시"
;실패 보고시의 처리
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT

@IRAI_일반6_SUCCESS(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF RAND:MAX(1, IRAI_SUB:CHK_SLOT) <= (1 + ABL:MASTER:교양) * 20
	RETURNF 1


@IRAI_일반6_DIFFICULTY(CHK_SLOT)
#FUNCTIONS
#DIM CHK_SLOT
SIF IRAI_SUB:CHK_SLOT <= (1 + ABL:MASTER:교양) * 20
	RETURNF "완벽하게 이해시켜 보이겠다"
;난이도 전체로 차분을 퍼센트화한 것
SELECTCASE (IRAI_SUB:CHK_SLOT - ((1 + ABL:MASTER:교양) * 20)) * 100 / IRAI_SUB:CHK_SLOT
CASE 0, 1, 2, 3
	RETURNF "잘 알려줄 수 있을 것 같다"
CASE 4, 5, 6
	RETURNF "충분히 알려줄 수 있다"
CASE 7 TO 10
	RETURNF "대답할 수 있을 것이다"
CASE 11 TO 33
	RETURNF "이것으로 납득해 주지 않을까"
CASE 34 TO 50
	RETURNF "어떻게든 된다, 아마도"
CASE 51 TO 70
	RETURNF "어려운 질문이다"
CASEELSE
	RETURNF "전혀 짐작도 안간다"
ENDSELECT


