;-------------------------------------------------
;유두빨기
;애무계 커맨드, 레벨 1
;-------------------------------------------------
@COM10
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER

;봉사 쾌락
TCVAR:PLAYER:108 = 1
CALL 몸사용하기("입",1)
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:PLAYER:성행동 =1
	SOURCE:쾌B = ABL:PLAYER:혀 * 5

ELSE

	DOWNBASE:체력 += 5
	DOWNBASE:기력 += 50

	SOURCE:PLAYER:성행동 =2
	SOURCE:쾌B = 20
	SOURCE:사랑 = 30
	SOURCE:노출 = 20
	SOURCE:불결 = 30
	SOURCE:반감 = 30

	SOURCE:쾌B += ABL:PLAYER:혀 * 5
	SOURCE:사랑 += ABL:PLAYER:혀 * 5

	SIF ABL:친밀 >= 10
		SOURCE:사랑 += (ABL:친밀 * 20)

	;조교자의 TALENT:혀놀림을 본다
	SIF TALENT:PLAYER:혀놀림
		TIMES SOURCE:쾌B , 1.50

	;조교자의 TALENT:고양이혀를 본다
	SIF TALENT:PLAYER:고양이혀
		TIMES SOURCE:쾌B , 0.75
	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;억압, 저항 소유에는, 억울소스 추가
	SIF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 100
ENDIF
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 B⇔조교자의 입의 더러움이 이동
STAIN:5 |= STAIN:PLAYER:구
STAIN:PLAYER:구 |= STAIN:5
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET, PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET, PLAYER)
		EXP:(50 + HETEROSEX(TARGET, PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ENDIF
;조교자가 고양이혀 소유의 경우는, 조교자에게 가학쾌락경험을 추가
SIF TALENT:PLAYER:고양이혀
	EXP:PLAYER:가학쾌락경험 += 1
EXP:PLAYER:펠라티오경험 ++
RETURN 1


;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE10
;유두빨기 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(10)
	RETURN RESULT
;상반신착의
SIF (TEQUIP:1 & 2) && TEQUIP:3 == 0
	RETURN 0
;착유기 사용중은 불가
SIF TEQUIP:17
	RETURN 0
;유두캡 사용중은 불가
SIF TEQUIP:16
	RETURN 0
;유방 전극 장착중은 안 돼
SIF TEQUIP:24
	RETURN 0
SIF TEQUIP:체위 == 5 || TEQUIP:체위 == 2 || TEQUIP:체위 == 3
	RETURN 0

RETURN 1

