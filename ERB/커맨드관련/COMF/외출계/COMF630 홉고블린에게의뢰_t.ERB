;-------------------------------------------------
;홉고블린에의뢰
;외출 커맨드
;-------------------------------------------------
@COM630
PRINTFORML 홉고블린에게 뭔가 부탁하겠습니까?
CALL ASK_M("부탁하지 않는다", 1,"청소 대행",!TFLAG:출장청소,"물주기대행", ITEM:150)
SIF !RESULT
	RETURN -1
IF RESULT == 1
	PRINTFORML 홉고블린에게 %GET_MAPNAME(MAIN_MAP)%의 청소를 부탁하겠습니까?
	CALL PAY_YEN_OR_CM(3000 - (홍마관복종됨() * 350),10 - (홍마관복종됨()))
	SIF !RESULT
		RETURN -1
	PRINTFORML 출장 청소 서비스를 의뢰했습니다.
	TFLAG:출장청소 = 1
ELSEIF RESULT == 2
	PRINTFORML 홉고블린에게 텃밭의 물주기를 부탁하겠습니까? (이번 달까지)
	CALL PAY_YEN_OR_CM(15000 - (홍마관복종됨() * 1750),70 - (홍마관복종됨() * 5))
	SIF !RESULT
		RETURN -1
	PRINTFORML 물주기대행을 의뢰했습니다.
	FLAG:물주기대행 = 1
ENDIF
TIME += 5
RETURN 1

@COM_ABLE630
;일괄관리
SIF GLOBAL_COMABLE(630)
	RETURN RESULT
SIF MAIN_MAP == 3
	RETURN 0
SIF CFLAG:MASTER:현재위치 != 350
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

@HOBGOBLIN(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SIF ARG == 객실
		RETURNF 1
ENDIF
RETURNF 0

@홍마관복종됨
#FUNCTION
#DIM DYNAMIC FEEOFF
IF TALENT:[[레밀리아]]:당신의노예
	FEEOFF += 2
ENDIF
IF TALENT:[[사쿠야]]:당신의노예
	FEEOFF++
ENDIF
IF TALENT:[[파츄리]]:당신의노예
	FEEOFF++
ENDIF
IF TALENT:[[소악마]]:당신의노예
	FEEOFF++
ENDIF
IF TALENT:[[메이링]]:당신의노예
	FEEOFF++
ENDIF
IF TALENT:[[플랑]]:당신의노예
	FEEOFF += 2
ENDIF
RETURNF FEEOFF