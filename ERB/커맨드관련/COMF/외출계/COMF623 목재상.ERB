;목재상
@COM623
#DIM 제재판정
#DIM 판매가
#DIM 루프수
#DIM 결과보관
#DIM 후보번호
#DIM 후보개수
#DIM 매각개수
#DIM ProcessingCharge
루프수 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 영업시간 외 같다
	RETURN -1
ENDIF


;初回イベント
IF FLAG:마지막목재상방문일 == 0
	;初回イベント前は、통나무持ちでなければ店にも入れない
	IF ITEM:통나무 == 0
		PRINTFORMW 딱히 지금은 볼일이 없다... 팔 수 있는 목재를 베어와야한다...
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で통나무を入手してしまった場合の退避用
	SIF !FLAG:통나무종류
		FLAG:통나무종류 = MAX(RAND:12, 1)
	PRINTFORMW 「네, 감사합니다, 내가 여기 주인이다. 당신이 통나무를 팔고 싶은 사람인가?」
	PRINTFORMW 「그럼 물건 쪽을 살펴볼까?」
	PRINTFORMW 「………」
	PRINTFORMW 「허, 꽤 대단하군……」
	PRINTFORMW 「....응? 마음만 먹으면 매일이라도 가져올 수 있다고? 마을 밖이라면 많이 자라고 있으니까?」
	PRINTFORMW 「…………」
	PRINTFORMW 「……보통내기가 아니네, 당신」
	PRINTFORMW 「좋아. 벌목해 온 통나무, 우리 가게에서 매입하지.」
	PRINTFORMW 「물론 재료로서 가치 있는 것은 비싸고, 그렇지 않은 것은 싸게 매입할 거야.」
	PRINTFORMW 「가구든 뭐든 만들기 위한 제재가 필요하다면 가져온 통나무를 제재로 가공의뢰 해도 좋아. 나름대로 작업비는 받겠지만」
	PRINTFORMW 「그럼 이번 물건을 다시 한번 검사해 볼까?」
	;판매가はここで設定、基本的に中途キャンセルはしない
	판매가 = WOOD_VALUE(FLAG:통나무종류)
	SETCOLOR 0x00FF00
	PRINTFORMD 「
	PRINTFORM %WOOD_NAME(FLAG:통나무종류)%
	PRINTFORMD 로군……
	PRINTFORM %금액표시(판매가)%
	RESETCOLOR
	PRINTFORMW 에 매입하겠다」
	PRINTFORMW   소지금: %금액표시(MONEY)% → %금액표시(MONEY + 판매가)%
	PRINTFORMW 「이것으로 거래 종료. 그럼 다음 물건도 기다리지.」
	PRINTFORMW 「뭐, 요정이나 요괴가 나타나는 산림에서 나무를 베어 올 수 있는 나무꾼 같은 건 그리 많지 않으니까. 기대하고 있다고?」
	PRINTW 
	MONEY += 판매가
	ITEM:통나무 = 0
	FLAG:통나무종류 = 0
	루프수 = 1
ENDIF

$LOOP_SELECT

IF 루프수
	PRINTL 
	PRINTFORMW 「그럼, 뭐 다른 볼일이라도 있나?」
ELSE
	;平常来店
	PRINTL 
	IF DAY:0 - FLAG:마지막목재상방문일 > 28
		PRINTFORMW 「오, 뭐야. 틀림없이 벌목 일에서는 손을 씻은 줄 알았는데.」
	ELSEIF DAY:0 - FLAG:마지막목재상방문일 > 14
		PRINTFORMW 「오, 오랜만이네」
	ELSEIF FLAG:마지막목재상방문일 == DAY:0
		PRINTFORMW 「뭐야, 잊은 물건? 조급한 녀석이군」
	ELSE
		PRINTFORMW 「어어, 오늘은 무슨 일이야?」
	ENDIF
ENDIF

루프수 ++
FLAG:마지막목재상방문일 = DAY:0

PRINTFORML   소지금: %금액표시(MONEY)%
DRAWLINE

CALL ASK_M("통나무매각・제재", ITEM:통나무, "목재를 판다", ITEM:목재, "제재를 판다", 4, "거래 내용을 확인한다", 5, "가게를 나온다", 6)
결과보관 = RESULT
ProcessingCharge = LUMBERING(FLAG:통나무종류, "목재상")
CLEARLINE 12
SELECTCASE 결과보관
	;통나무を売る　제재してほしい
	CASE 0
		;依頼の報酬など、伐採以外で통나무を入手してしまった場合の退避用
		SIF !FLAG:통나무종류
			FLAG:통나무종류 = MAX(RAND:12, 1)
		SETCOLOR 0x00FF00
		PRINTFORML 「%WOOD_NAME(FLAG:통나무종류)%로군……」
		판매가 = WOOD_VALUE(FLAG:통나무종류)
		PRINTFORMD 「매입이라면 매입 가격은
		PRINTFORM %금액표시(판매가)%
		PRINTFORMD 、제재한다면 품삯은
		PRINTFORM %금액표시(ProcessingCharge)%
		RESETCOLOR
		PRINTFORMW 받아가겠다.」
		CALL ASK_M("매각", 1, "제재", MONEY > ProcessingCharge,"역시 그만둔다", 1)
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW  소지금: \\ → %금액표시(MONEY + 판매가)%
				PRINTFORMW 「매번 고맙네. 그럼, 다음 물건도 기다리지!」
				PRINTL 
				MONEY += 판매가
			CASE 1
				PRINTL 
				PRINTFORMW  소지금: %금액표시(MONEY)% → %금액표시(MONEY - ProcessingCharge)%
				PRINT 　
				CALL PAY_MONEY_YEN(ProcessingCharge, 2)
				PRINTL 
				PRINTFORMW 「그래. 그럼, 작업에 착수하도록 하지.」
				CALL LogRefine("목재상")
			CASE 2
				PRINTFORMW 「뭐야, 따로 뭐 도와줄 것이 있나?」
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
		ITEM:통나무 = 0
		FLAG:통나무종류 = 0
	;목재を売る
	CASE 1
		후보개수 = ITEM:목재
		후보번호 = 610
		PRINTFORM 「%ITEMNAME:후보번호%면 %금액표시(ITEMPRICE:후보번호 / 2)%로 매입하는데 어때?」
		CALL COM623_SELL(후보개수)
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				GOTO LOOP_SELECT
			CASE 1 TO 후보개수
				CLEARLINE 2
				매각개수 = RESULT
				PRINTFORML 「그러면 %ITEMNAME:후보번호%를 {매각개수}개、%금액표시((ITEMPRICE:(후보번호) / 2 * 매각개수))%으로 매입하도록 하지」
				PRINTFORMW  소지금: %금액표시(MONEY)% → %금액표시(MONEY + (ITEMPRICE:(후보번호) / 2 * 매각개수))%
				MONEY += (ITEMPRICE:(후보번호) / 2 * 매각개수)
				ITEM:후보번호 -= 매각개수
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
	;제재を売る
	CASE 2
		제재판정 = 0
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL
				BREAK
			제재판정 ++
		NEXT
		IF 제재판정 > 48
			PRINTFORMW 제재를 가지고 있지 않다
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
		PRINTFORMW 「제재를 가져왔는가? 어디 보자」
		DRAWLINE
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL == 0
				CONTINUE
			PRINTFORM 　
			SIF LOCAL < 10
				PRINTFORM  
			PRINTFORM [{LOCAL}] 
			PRINTFORM %WOOD_NAME(LOCAL), 16, LEFT%
			PRINTFORM 제재数： 
			SELECTCASE LOG_SALES:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			PRINTFORML {LOG_SALES:LOCAL}
		NEXT
		PRINTFORML 　 [0] 그만둔다

		$INPUT_LOOP03
		INPUT
		
		결과보관 = RESULT
		IF 결과보관 && LOG_SALES:결과보관
			판매가 = WOOD_VALUE(결과보관, "제재")
			후보개수 = LOG_SALES:결과보관
			SETCOLOR 0x00FF00
			PRINTFORMD 「
			PRINTFORM %WOOD_NAME(결과보관)%
			PRINTFORMD 의 제재라면
			PRINTFORM %금액표시(판매가)%
			RESETCOLOR
			PRINTFORML 에 매입하는데 어때?」
			CALL COM623_SELL(후보개수)
			SELECTCASE RESULT
				CASE 0
					PRINTL 
					GOTO LOOP_SELECT
				CASE 1 TO 후보개수
					CLEARLINE 2
					매각개수 = RESULT
					PRINTFORML 「그러면 %WOOD_NAME(결과보관)%의 제재를 {매각개수}본、%금액표시(판매가 * 매각개수)%으로 매입하도록 하지」
					PRINTFORMW  소지금: %금액표시(MONEY)% → %금액표시(MONEY + (판매가 * 매각개수))%
					MONEY += 판매가 * 매각개수
					LOG_SALES:결과보관 -= 매각개수
					PRINTL 
			ENDSELECT
		ELSEIF 결과보관 == 0
			PRINTL 
			GOTO LOOP_SELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP03
		ENDIF
	;取引内容を確認する
	CASE 3
		PRINTFORMW 「반입 통나무나 죽재, 제재를 매입 중이다」
		PRINTFORMW 「너무 많아서 주체할 수 없다면 짧은 목재류도 인수하겠다. 뭐, 두세 푼밖에 안 되지만」
		PRINTFORMW 「필요하면 통나무의 제재도 맡지. 단, 통나무의 종류나 크기에 따라 작업비를 지불받는다」
		PRINTFORMW 「참고로 여기서 제재한 것을 여기에 도매해도 손해밖에 안 나니까. 기부할 생각이라면 환영하지만」
		PRINTFORMW 「……뭐, 가구가 필요해? 그건 우리 영역이 아니네. 가구장이에게 가보게.」
		PRINTL 
	CASE 4
		PRINTFORMW 「어어, 그럼 이만」
		PRINTL 
		TIME += 루프수 * 5
		RETURN -1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;목재상
@COM_ABLE623
;一括管理
SIF GLOBAL_COMABLE(623)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:현재위치 != 상가거리
		RETURN 0
ELSE
	;西通り
	SIF CFLAG:MASTER:현재위치 != 206
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:수면
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;통나무・제재の매각価格を設定する関数
;何回か見る場合もあるので、その日の中では固定値出力
;ARG:0		FLAG:통나무종류
;Type		매각価格  省略時は통나무
;-------------------------------------------------
@WOOD_VALUE(ARG:0, Type)
#FUNCTION
#DIM Species
#DIM 판매가베이스
#DIM 입담보정
#DIMS Type

Species = ARG % 100

IF Type == "제재"
	판매가베이스 =WoodValue_Plank:Species
ELSE
	판매가베이스 = WoodValue_Log:Species
ENDIF

;日替わり変動
판매가베이스 += FLAG:일일이벤트 * 2

;입담보정  日付で乱数を固定
RANDOMIZE DAY:0
입담보정 = RAND:(1 + ABL:MASTER:화술기능 * 5 + ABL:MASTER:교양)

SELECTCASE 입담보정
	CASE 0 TO 10

	CASE 11 TO 20
		TIMES 판매가베이스, 1.05
	CASE 21 TO 30
		TIMES 판매가베이스, 1.07
	CASEELSE
		TIMES 판매가베이스, 1.10
ENDSELECT

;통나무の時のみ、巨木・大木補正
IF Type != "제재"
	IF ARG > 200
		판매가베이스 *= 5
	ELSEIF ARG > 100
		판매가베이스 *= 3
	ENDIF
ENDIF
RETURNF 판매가베이스


;-------------------------------------------------
;FLAG:통나무종류から、巨木・大木を無視して목재種番号を取り出す関数
;ARG…FLAG:통나무종류
;-------------------------------------------------
@WOOD_NUM(ARG)
#FUNCTION

RETURNF ARG % 100

;-------------------------------------------------
;제재コスト、제재数を割り出す関数
;複数回選べるので、固定値を出す
;ARG	FLAG:통나무종류
;Type	価格ないし제재数
;-------------------------------------------------
@LUMBERING(ARG, Craft, Type)
#FUNCTION
#DIM 비용베이스
#DIMS Craft
#DIMS Type

IF Type == "QTY"
	RETURNF 1 + (ARG / 100) * 2
ENDIF

비용베이스 = WOOD_VALUE(ARG)

SELECTCASE Craft
	CASE "목재상"
		RETURNF 비용베이스 / 2
	CASE "제재소"
		RETURNF 비용베이스 / 4
ENDSELECT

RETURNF 0

@COM623_SELL(후보개수)
#DIM 후보개수
SETCOLOR 0x777777
PRINTFORML 　몇 개 판매하겠습니까? (1～{후보개수}개 숫자를 입력、0으로 돌아간다)
RESETCOLOR
PRINTL 
PRINT 　
PRINTBUTTON "[0] 그만둔다", 0
PRINT     
PRINTBUTTON "[1] 1개", 1
PRINT     
IF 후보개수 >= 5
	PRINTBUTTON "[5] 5개", 5
	PRINT     
ENDIF
IF 후보개수 >= 10
	PRINTBUTTON "[10] 10개", 10
	PRINT    
ENDIF
INPUT
SIF RESULT > 후보개수
	RETURN 0
SIF RESULT <= 0
	RETURN 0
RETURN RESULT