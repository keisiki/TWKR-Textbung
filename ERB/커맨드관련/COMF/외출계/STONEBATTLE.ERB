@STONEBATTLE(difficulty, C_ID)
#DIM C_ID
#DIM FIRSTONE
#DIM SECONE, 3
#DIM LASTONE, 3
#DIM FIRSTONE_E
#DIM SECONE_E
#DIM LASTONE_E
#DIM STONE_YOSOKU
#DIM KUISHIBARI
#DIM Skill
#DIM OUTCOME	;0=双方失敗 1=あなた成功 2=相手成功 3=双方成功
#DIM PLAYER_TOTAL
#DIM ENEMY_TOTAL
#DIM difficulty

{
FIRSTONE = 1 + RAND:5 + (TALENT:MASTER:바늘질 * 2) + (TALENT:MASTER:능숙한손가락 * 2) + (TALENT:MASTER:채집Lv / 2 )
+ ( ABL:MASTER:청소기능 / 2 ) + (TALENT:MASTER:조제Lv / 2 ) + (TALENT:MASTER:돌쌓기랭커 * 2) 
}

;持ち込みアイテム
SIF STONE_ITEM:1 == 2
	FIRSTONE += TALENT:MASTER:돌쌓기랭커 + 1

Skill = ABL:MASTER:기교 + ABL:MASTER:손가락

SECONE:0 = RAND:(6 + Skill) + Skill + RAND:8 + 7
SECONE:1 = RAND:(4 + Skill) + Skill + RAND:5 + 4
SECONE:2 = RAND:(2 + Skill) + (Skill / 2) + RAND:2 + 2
LASTONE:0 = RAND:9 + 2
LASTONE:1 = RAND:7 + 4
LASTONE:2 = RAND:5 + 7
IF difficulty == 0
	FIRSTONE_E = RAND:4 + 1
	SECONE_E = RAND:4 + RAND:5 + 1
	LASTONE_E = RAND:5 + 1
ELSEIF difficulty == 1
	FIRSTONE_E = RAND:5 + 2
	SECONE_E = RAND:6 + RAND:6 + 2
	LASTONE_E = RAND:6 + 2
ELSEIF difficulty == 2
	FIRSTONE_E = RAND:6 + 3
	SECONE_E = RAND:6 + RAND:7 + 3
	LASTONE_E = RAND:7 + 3
ELSEIF difficulty == 3
	FIRSTONE_E = RAND:7 + 4
	SECONE_E = RAND:7 + RAND:7 + 4
	LASTONE_E = RAND:8 + 4
ELSEIF difficulty == 4
	FIRSTONE_E = RAND:8 + 5
	SECONE_E = RAND:7 + RAND:8 + 5
	LASTONE_E = RAND:9 + 5
ELSEIF difficulty == 5
	FIRSTONE_E = RAND:10 + 6
	SECONE_E = RAND:8 + RAND:8 + 6
	LASTONE_E = RAND:10 + 6
ELSEIF difficulty == 6
	LOCAL = RAND:3
	FIRSTONE_E = RAND:(10 + LOCAL) + 5 + LOCAL
	SELECTCASE RAND:3
		CASE 0
			SECONE_E = RAND:8 + RAND:8 + 5
		CASE 1
			SECONE_E = RAND:9 + RAND:9 + 6
		CASE 2
			SECONE_E = RAND:10 + RAND:10 + 7
	ENDSELECT
	LASTONE_E = RAND:5 + 8
ELSEIF difficulty == 7
	SELECTCASE RAND:3
		CASE 0
			FIRSTONE_E = RAND:10 + 6
		CASE 1
			FIRSTONE_E = RAND:11 + 7
		CASE 2
			FIRSTONE_E = RAND:12 + 8
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			SECONE_E = RAND:8 + RAND:8 + 6
		CASE 1
			SECONE_E = RAND:9 + RAND:9 + 7
		CASE 2
			SECONE_E = RAND:10 + RAND:10 + 8
	ENDSELECT
	LASTONE_E = RAND:6 + 9
ELSEIF difficulty == 8
	FIRSTONE_E = RAND:6 + 10
	SECONE_E = RAND:7 + RAND:7 + 10
	LASTONE_E = 20
ELSE 
	FIRSTONE_E = RAND:7 + 12
	SECONE_E = RAND:8 + RAND:8 + 12
	LASTONE_E = 22
ENDIF

;相手の積み石数予測
IF STONE_ITEM:1 == 4
	STONE_YOSOKU = SECONE_E
ELSE
	IF SECONE_E < 6
		STONE_YOSOKU = SECONE_E + RAND:3
	ELSE
		SELECTCASE 1
			CASE 0
				STONE_YOSOKU = SECONE_E - RAND:6
			CASEELSE
				STONE_YOSOKU = SECONE_E + RAND:6
		ENDSELECT
	ENDIF
ENDIF


DRAWLINE

PRINTFORMW 돌쌓기 승부가 시작됐다！
	SIF STONE_ITEM:1 == 2
		PRINTFORML 육체 강화 지장 발동！
	PRINTFORML 초반에 스타트 대시를 결정한 %마스터가% 현재 쌓은 돌의 수는 {FIRSTONE}개
PRINTFORML 한편 %조사처리(CALLNAME:C_ID,"는")% {FIRSTONE_E}개 쌓고있다…
IF STONE_ITEM:1 == 4
	SETCOLOR C_RED
	PRINTFORMW 너무 정직한 돌잡이 효과 발동!
	PRINTFORMW 사고가 일어나지 않는 한 상대는 여기서 {STONE_YOSOKU}개 쌓는다…！
ELSE
	PRINTFORMW 아마도 상대는 여기서 대략 {STONE_YOSOKU}개를 쌓을 것이다…
	RESETCOLOR
ENDIF
{
CALL ASK_M(@"급하게 쌓는다（돌탑을 무너뜨릴 리스크{12 + (ABL:욕망 * 2)}％＆돌을 쌓는 수 많음）",1,@"평범하게 쌓는다（돌탑을 무너뜨릴 리스크{10 + ABL:욕망}％）",1,
@"신중하게 쌓는다（돌탑을 무너뜨릴 리스크{7 + (ABL:욕망 / 2)}％＆돌을 쌓는 수 적음）",1, @"아무 것도 안 한다（돌탑을 무너뜨릴 리스크 0＆ 돌을 쌓는 수 0）",1)
}

IF RESULT == 0
	PRINTFORMW %마스터는% 다소의 리스크를 짊어지고서라도 빠르게 돌을 쌓아 올렸다！
	PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 6
			OUTCOME = 1
		CASE 7 TO 13
			OUTCOME = 0
		CASE 14 TO 18 + (ABL:욕망 * 2)
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
		ENDSELECT
ELSEIF RESULT == 1
PRINTFORMW %마스터는% 돌을 쌓았다
PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 6
			OUTCOME = 1
		CASE 7 TO 13
			OUTCOME = 0
		CASE 14 TO 16 + ABL:욕망
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
	ENDSELECT
ELSEIF RESULT == 2
	PRINTFORMW %마스터는% 조심스럽게 돌을 쌓았다
	PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 6
			OUTCOME = 1
		CASE 7 TO 13
			OUTCOME = 0
		CASE 13 TO 13 + (ABL:욕망 / 2)
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
	ENDSELECT
ELSEIF RESULT == 3
	PRINTFORMW %마스터는% 돌을 쌓지 않았다
	PRINTFORMW 
	PLAYER_TOTAL = FIRSTONE
	SELECTCASE RAND:100
		CASE 0 TO 13
			PRINTFORMW 당신은 아무것도 하지 않고 기다리고 있다…
			PRINTFORMW 아앗！ %조사처리(CALLNAME:C_ID,"는")% 끝나기 직전에 돌을 무너뜨렸다！
			PRINTFORMW 상대의 결과는{LASTONE_E}개……
			ENEMY_TOTAL = LASTONE_E
		CASEELSE
			IF STONE_ITEM:1 == 3 && RAND:9 == 0
				PRINTFORMW %마스터는% 아무것도 하지 않고 기다린다…
				SETCOLOR C_RED
				PRINTFORMW 제멋대로인 돌쌓기 효과 발동!
				RESETCOLOR
				PRINTFORMW %CALLNAME:C_ID%\@ OUTCOME % 2 == 1? 는 종료 직전에# \@돌을 무너뜨린 것 같다!
				PRINTFORMW 무너뜨린 것을 필사적으로 다시 쌓아 올린 상대의 결과는 {LASTONE_E}개……
				ENEMY_TOTAL = LASTONE_E
			ELSE
				PRINTFORMW %마스터는% 아무것도 하지 않고 기다리고 있다…
				PRINTFORMW 한숨 돌린 %마스터가% 옆을 보면…
				PRINTFORMW 상대는 차근차근 쌓아 올려 {SECONE_E}개 추가로 쌓아 올린 것 같다……
				ENEMY_TOTAL = FIRSTONE_E + SECONE_E
			ENDIF
	ENDSELECT
ENDIF

IF GROUPMATCH(RESULT, 0, 1, 2)
	IF OUTCOME % 2 == 1
		PRINTFORMW %마스터는% {SECONE:RESULT}개를 쌓아 올렸다！
		PLAYER_TOTAL = FIRSTONE + SECONE:RESULT
	ELSE
		PRINTFORMW 앗！
		PRINTFORMW %마스터는% 무심코 돌을 무너뜨리고 말았다！
		KUISHIBARI = RAND:CHARANUM
		;全キャラからランダムに判定して恋慕か恋人にヒットしたら食いしばりが発動する
		;仕様上全員を恋慕にしてるプレイヤーは絶対発動
		IF TALENT:(KUISHIBARI):연인 || TALENT:(KUISHIBARI):연모
			PRINTFORMW ……힘내！！！
			PRINTFORMW 
			PRINTFORMW %마스터는% 환청일지도 모르지만 %CALLNAME:KUISHIBARI%에게서 응원을 받은 기분이 들었다…
			PRINTFORMW %마스터는% 전력으로 다시 쌓았다！
			PRINTFORMW %마스터는% {FIRSTONE + SECONE:RESULT}개 쌓았다！
			PLAYER_TOTAL = FIRSTONE + SECONE:RESULT
		ELSEIF STONE_ITEM:1 == 1
			SELECTCASE RAND:5
				CASE 0
					SETCOLOR C_RED
					PRINTFORMW 작은 돌멩이 모자의 효과 발동！！
					PRINTFORMW 돌은 한 번 무너진 것 같았지만 보이지 않는 무언가에 의해 막혔다…！
					RESETCOLOR
					PRINTFORMW %마스터는% {FIRSTONE + SECONE:RESULT}개 쌓았다！
					PLAYER_TOTAL = FIRSTONE + SECONE:RESULT
				CASEELSE
					PRINTFORMW 서둘러 다시 쌓은 결과、{LASTONE:RESULT}개 쌓아 올렸다
					PLAYER_TOTAL = LASTONE:RESULT
			ENDSELECT
		ELSE
			PRINTFORMW 서둘러 다시 쌓은 결과 {LASTONE:RESULT}개 쌓아 올렸다
			PLAYER_TOTAL = LASTONE:RESULT
		ENDIF
	ENDIF
	IF OUTCOME >= 2
		IF STONE_ITEM:1 == 3
			SELECTCASE RAND:9
				CASE 0
					SETCOLOR C_RED
					PRINTFORMW 제멋대로인 돌쌓기 효과 발동！
					PRINTFORMW %CALLNAME:C_ID%\@ OUTCOME % 2 == 1? 는 종료 직전에 # \@돌을 무너뜨린 것 같다!
					RESETCOLOR
					PRINTFORMW 무너뜨린 것을 필사적으로 다시 쌓아 올린 상대의 결과는 {LASTONE_E}개……
					ENEMY_TOTAL = LASTONE_E
				CASEELSE
					PRINTFORMW 한편、상대는 착실히 쌓아 올려 {SECONE_E}개 추가로 쌓았나 보다……
					ENEMY_TOTAL = FIRSTONE_E + SECONE_E
			ENDSELECT
		ELSE
			PRINTFORMW 한편、상대는 착실히 쌓아 올려 {SECONE_E}개 추가로 쌓았나 보다……
			ENEMY_TOTAL = FIRSTONE_E + SECONE_E
		ENDIF
	ELSE
		PRINTFORMW 아앗！%CALLNAME:C_ID%\@ OUTCOME % 2 == 1? 은 종료 직전에 # 도\@ 돌을 무너뜨린 것 같다！
		PRINTFORMW 무너뜨린것을 필사적으로 다시 쌓은 상대의 결과는 {LASTONE_E}개……
		ENEMY_TOTAL = LASTONE_E
	ENDIF
ENDIF
CALL COLORMESSAGE(@"%CALLNAME:MASTER%：{PLAYER_TOTAL}　VS　%CALLNAME:C_ID%：{ENEMY_TOTAL}",C_YELLOW,2)
PRINTFORML 
IF PLAYER_TOTAL > ENEMY_TOTAL
	CALL COLORMESSAGE(@"YOU WIN!",C_YELLOW,1)
	CALL COLORMESSAGE(@"인생의 승리자！",C_YELLOW,1)
	PRINTFORMW
	;ラスボス特殊分岐
	IF difficulty == 8
		TRYCALLFORM StoneMessage_{C_ID}("lastbattle_w")
	ELSE
		TRYCALLFORM StoneMessage_{C_ID}("win")
	ENDIF
	RETURN 0
ELSEIF PLAYER_TOTAL == ENEMY_TOTAL
	PRINTFORML
	PRINTFORMW 무승부이므로 다시 한번 승부를 합니다
	JUMP STONEBATTLE(difficulty, C_ID)
ELSE
	CALL COLORMESSAGE(@"YOU LOSE!",C_YELLOW,1)
	CALL COLORMESSAGE(@"패배자！",C_YELLOW,1)
	PRINTFORMW
	;ラスボス特殊分岐
	IF difficulty == 8
		TRYCALLFORM StoneMessage_{C_ID}("lastbattle_l")
	ELSE
		TRYCALLFORM StoneMessage_{C_ID}("lose")
	ENDIF
	RETURN 1
ENDIF

RETURN 0


;-------------------------------------------------
;関数名:STONECombiDAY
;概　要:積み石コンテストのカップル部門の今日のテーマを表す文字列を取得する関数
;引　数:
;-------------------------------------------------
@STONECombiDAY(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "봄"
	CASE 2
		RETURNF "접시"
	CASE 3
		RETURNF "호러"
	CASE 4
		RETURNF "음악"
	CASE 5
		RETURNF "귀여움"
	CASE 6
		RETURNF "직물"
	CASE 7
		RETURNF "컬러풀"
	CASE 8
		RETURNF "여름"
	CASE 9
		RETURNF "대륙풍"
	CASE 10
		RETURNF "동물"
	CASE 11
		RETURNF "싸움"
	CASE 12
		RETURNF "미래"
	CASE 13
		RETURNF "가을"
	CASE 14
		RETURNF "레트로"
	CASE 15
		RETURNF "고급"
	CASE 16
		RETURNF "화려함"
	CASE 17
		RETURNF "일본식"
	CASE 18
		RETURNF "광석"
	CASE 19
		RETURNF "지성"
	CASE 20
		RETURNF "겨울"
	CASE 21
		RETURNF "카오스"
	CASE 22
		RETURNF "질서"
	CASE 23
		RETURNF "서양식"
	CASE 24
		RETURNF "천체"
	CASE 25
		RETURNF "물"
	CASE 26
		RETURNF "모리야"
	CASE 27
		RETURNF "마법사"
	CASE 28
		RETURNF "바니"
	CASE 29
		RETURNF "식물"
	CASE 30
		RETURNF "바람"
	CASE 31
		RETURNF "뭐든지"
	ENDSELECT

;-------------------------------------------------
;合作積み石を評価する関数
;-------------------------------------------------
@STONECombiBattle
#DIM CombiSkill
#DIM StoneState
#DIM CombiLove
#DIM StoneKoukando
#DIMS StoneGood
#DIMS StoneBad
#DIM StoneSense
#DIM CombiPoint
#DIM Stoneday

CombiSkill = 0
StoneState = 0
CombiLove = 0
StoneKoukando = 0
StoneGood = 0
StoneBad = 0
StoneSense = 0
CombiPoint = 0
Stoneday = 0

IF DAY:3 == 31
	PRINTFORMW 「오늘은 31일이니까 뭐든지의 날!」
	PRINTFORMW 「어떤 주제로 참여하시나요?」
	FOR LOCAL,1, 31
		PRINTFORMLC [{LOCAL}] - %STONECombiDAY(LOCAL)%
		SIF LOCAL % 5 == 0
			PRINTL
	NEXT
	$LOOP
	INPUT
	IF INRANGE(RESULT, 1, 30)
		Stoneday = RESULT
	ELSE
		GOTO LOOP
	ENDIF
ELSE
	Stoneday = DAY:3
ENDIF

CombiSkill = (ABL:MASTER:손가락 * 2) + ABL:MASTER:기교 + (ABL:TARGET:손가락 * 2) + ABL:TARGET:기교

{
StoneState = 30 + (TALENT:MASTER:바늘질 * 5) + (TALENT:MASTER:능숙한손가락 * 5) + TALENT:MASTER:채집Lv
+ ABL:MASTER:청소기능 + TALENT:MASTER:조제Lv + (TALENT:MASTER:돌쌓기랭커 * 2)
+ (TALENT:TARGET:바늘질 * 5) + (TALENT:TARGET:능숙한손가락 * 5) + TALENT:TARGET:채집Lv
+ ABL:TARGET:청소기능 + TALENT:TARGET:조제Lv + (TALENT:TARGET:돌쌓기랭커 * 2) + RAND:CombiSkill
+ CombiSkill - RAND:(ABL:TARGET:욕망) - RAND:(ABL:MASTER:욕망) 
}

IF CFLAG:TARGET:호감도 >= 100000
	StoneKoukando += 20
ELSEIF CFLAG:TARGET:호감도 >= 50000
	StoneKoukando += 18
ELSEIF CFLAG:TARGET:호감도 >= 30000
	StoneKoukando += 17
ELSEIF CFLAG:TARGET:호감도 >= 15000
	StoneKoukando += 16
ELSEIF CFLAG:TARGET:호감도 >= 12500
	StoneKoukando += 15
ELSEIF CFLAG:TARGET:호감도 >= 10000
	StoneKoukando += 14
ELSEIF CFLAG:TARGET:호감도 >= 8500
	StoneKoukando += 13
ELSEIF CFLAG:TARGET:호감도 >= 7000
	StoneKoukando += 12
ELSEIF CFLAG:TARGET:호감도 >= 5000
	StoneKoukando += 11
ELSEIF CFLAG:TARGET:호감도 >= 4000
	StoneKoukando += 10
ELSEIF CFLAG:TARGET:호감도 >= 3000
	StoneKoukando += 9
ELSEIF CFLAG:TARGET:호감도 >= 2500
	StoneKoukando += 8
ELSEIF CFLAG:TARGET:호감도 >= 2000
	StoneKoukando += 7
ELSEIF CFLAG:TARGET:호감도 >= 1500
	StoneKoukando += 5
ELSE
	StoneKoukando += 1
ENDIF

IF CFLAG:TARGET:호감도 >= 30000
	StoneKoukando += 20
ELSEIF CFLAG:TARGET:호감도 >= 10000
	StoneKoukando += 18
ELSEIF CFLAG:TARGET:호감도 >= 8000
	StoneKoukando += 17
ELSEIF CFLAG:TARGET:호감도 >= 5000
	StoneKoukando += 16
ELSEIF CFLAG:TARGET:호감도 >= 3500
	StoneKoukando += 15
ELSEIF CFLAG:TARGET:호감도 >= 2000
	StoneKoukando += 14
ELSEIF CFLAG:TARGET:호감도 >= 1000
	StoneKoukando += 13
ELSEIF CFLAG:TARGET:호감도 >= 850
	StoneKoukando += 12
ELSEIF CFLAG:TARGET:호감도 >= 700
	StoneKoukando += 11
ELSEIF CFLAG:TARGET:호감도 >= 600
	StoneKoukando += 10
ELSEIF CFLAG:TARGET:호감도 >= 500
	StoneKoukando += 9
ELSEIF CFLAG:TARGET:호감도 >= 400
	StoneKoukando += 8
ELSEIF CFLAG:TARGET:호감도 >= 300
	StoneKoukando += 7
ELSEIF CFLAG:TARGET:호감도 >= 200
	StoneKoukando += 5
ELSE
	StoneKoukando += 1
ENDIF

IF EXP:TARGET:데이트경험 >= 500
	StoneKoukando += 9
ELSEIF EXP:TARGET:데이트경험 >= 300
	StoneKoukando += 8
ELSEIF EXP:TARGET:데이트경험 >= 240
	StoneKoukando += 7
ELSEIF EXP:TARGET:데이트경험 >= 180
	StoneKoukando += 6
ELSEIF EXP:TARGET:데이트경험 >= 140
	StoneKoukando += 5
ELSEIF EXP:TARGET:데이트경험 >= 100
	StoneKoukando += 4
ELSEIF EXP:TARGET:데이트경험 >= 70
	StoneKoukando += 3
ELSEIF EXP:TARGET:데이트경험 >= 40
	StoneKoukando += 2
ELSEIF EXP:TARGET:데이트경험 >= 25
	StoneKoukando += 1
ELSE
ENDIF

IF EXP:TARGET:애정경험 >= 500
	StoneKoukando += 9
ELSEIF EXP:TARGET:애정경험 >= 300
	StoneKoukando += 8
ELSEIF EXP:TARGET:애정경험 >= 240
	StoneKoukando += 7
ELSEIF EXP:TARGET:애정경험 >= 180
	StoneKoukando += 6
ELSEIF EXP:TARGET:애정경험 >= 140
	StoneKoukando += 5
ELSEIF EXP:TARGET:애정경험 >= 100
	StoneKoukando += 4
ELSEIF EXP:TARGET:애정경험 >= 70
	StoneKoukando += 3
ELSEIF EXP:TARGET:애정경험 >= 40
	StoneKoukando += 2
ELSEIF EXP:TARGET:애정경험 >= 25
	StoneKoukando += 1
ELSE
ENDIF

IF CFLAG:TARGET:아이인원수 >= 3
	StoneKoukando += 6
ELSEIF CFLAG:TARGET:아이인원수 == 2
	StoneKoukando += 5
ELSEIF CFLAG:TARGET:아이인원수 == 1
	StoneKoukando += 4
ENDIF

{
CombiLove = (TALENT:TARGET:연모 * 15) + (TALENT:TARGET:사모 * 5) + (TALENT:TARGET:애욕 * 7)  + (TALENT:TARGET:섹프 * 3) 
+ ABL:TARGET:친밀 + RAND:(ABL:TARGET:친밀) + (TALENT:TARGET:기분 * 2) + (TALENT:TARGET:헌신적 * 5) + (CFLAG:TARGET:깨끗한교제 * 5)
- TALENT:TARGET:게으름 - TALENT:TARGET:츤데레 - (MARK:TARGET:반발각인 * 5) + RAND:20 - RAND:15 + StoneKoukando + 5
}

SELECTCASE Stoneday
	CASE 1
	;春
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(봄의|봄바람의|사계의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(벛꽃의|백합의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(핑크|노랑빛|무지갯빛|자연)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(여름의|가을의|겨울의|자연)")
			StoneSense -= 20

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(봄의|봄바람의|사계의)")
			StoneSense -= 30

	CASE 2
	;お皿を積もう
		StoneSense += ABL:MASTER:요리기능 + ABL:TARGET:요리기능
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(식기|접시|메이드의)")
			StoneSense += 25

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(식기|접시|메이드의)")
			StoneSense -= 25

	CASE 3
	;ホラー
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(어둠의|어둠|피의|무서움|독|독의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(요괴의|요마의|유령의|망령의|불가사의|기묘)")
			StoneSense += 20
		ENDIF

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(어둠의|어둠|피의|무서움|독|독의)")
			StoneSense -= 30

	CASE 4
	;音楽的
		StoneSense += ABL:MASTER:음악기능 + ABL:TARGET:음악기능
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(음악|악기)")
			StoneSense += 25
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(음악|악기)")
			StoneSense -= 25
		SIF FLAG:데이트상대 == 127
			StoneSense += 25
		
	CASE 5
	;メルヘン
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(귀여운|팝|로리타)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(핑크|하트의|꿈의)")
			StoneSense += 20
		ENDIF
		;KR판 주, カッコいい는 존재하지 않는 형용사명
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(무서움|カッコいい|피의)")
			StoneSense -= 20
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(귀여운|팝|로리타)")
			StoneSense -= 30

	CASE 6
	;布飾
		SIF TALENT:MASTER:바늘질
			StoneSense += 10
		SIF TALENT:TARGET:바늘질
			StoneSense += 10
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(장식|옷감)")
			StoneSense += 25
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(바늘|망토|예술적|리본|탈|인형|봉제인형|날개옷|멋쟁이)")
			StoneSense += 20
		ENDIF

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(장식|옷감)")
			StoneSense -= 20

	CASE 7
	;カラフル
	;KR판 주, 極色の는 존재하지 않는 형용사명
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(무지갯빛|무지개의|일곱빛깔의|極色の)")
			StoneSense += 45
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(빨강빛|주홍빛|노랑빛|푸른빛|초록빛|보랏빛|흰빛|흑빛|잿빛|금빛|은빛|찻빛|핑크)")
			StoneSense += 30
		ENDIF
			
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(무지갯빛|무지개의|일곱빛깔의|極色の)")
			StoneSense -= 45
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(빨강빛|주홍빛|노랑빛|푸른빛|초록빛|보랏빛|흰빛|흑빛|잿빛|금빛|은빛|찻빛|핑크)")
			StoneSense -= 30
		ENDIF

	CASE 8
	;夏
	;KR판 주, 夏風の는 존재하지 않는 형용사명
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(여름의|夏風の|사계의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(수박의|해바라기의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(빨강빛|푸른빛|초록빛|자연)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(봄의|가을의|겨울의|자연)")
			StoneSense -= 20
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(여름의|夏風の)")
			StoneSense -= 30

	CASE 9
	;大陸風
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(대륙풍)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(용의)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(일본풍|서양풍)")
			StoneSense -= 30
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(대륙풍)")
			StoneSense -= 30

	CASE 10
	;動物
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(동물)")
			StoneSense += 45
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(고양이의|개의|늑대의|여우의|수달의|양의|쥐의|토끼의|너구리의|말의|맥의)")
			StoneSense += 30
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(새|까마귀의|박쥐의|닭의|참새의|고래의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(거북의|개구리의|뱀의|벌레|물고기|개구리의|뱀의)")
			StoneSense += 10
		ENDIF

	CASE 11
	;戦い
		StoneSense += ABL:MASTER:전투능력 + ABL:TARGET:전투능력
		
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(날붙이|탄환|힘의)")
			StoneSense += 25
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(피의|무서움|독|사생)")
			StoneSense += 15
		ENDIF
		
		SIF !STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(날붙이|탄환)") && STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(피의|날붙이|탄환|독|힘의|사생)")
			StoneSense -= 25

	CASE 12
	;未来
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(미래|기계장치의|아티팩트)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(시간의|시공의)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(잃어버린|유물|레트로)")
			StoneSense -= 20
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(미래|기계장치의|아티팩트)")
			StoneSense -= 30


	CASE 13
	;秋
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(가을의|가을바람의|사계의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(단풍나무의|단풍의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(보랏빛|주홍빛|찻빛|자연)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(봄의|여름의|겨울의|자연)")
			StoneSense -= 20

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(가을의|가을바람의)")
			StoneSense -= 30

	CASE 14
	;レトロ
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(잃어버린|유물|레트로)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(시간의|시공의)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(미래|기계장치의|아티팩트)")
			StoneSense -= 20

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(잃어버린|유물|레트로)")
			StoneSense -= 30

	CASE 15
	;高級
	;KR판 주, 高級品, 芸術品는 존재하지 않는 형용사명
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(高級品|보석|芸術品|미술품|고급|브랜드의|고져스한)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(아름다운|기술|장식)")
			StoneSense += 20
		ENDIF

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(高級品|보석|芸術品|미술품|고급|브랜드의|고져스한)")
			StoneSense -= 30
			
		SIF FLAG:데이트상대 == 126
			StoneSense -= 30

	CASE 16
	;派手
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(장대|화려|고져스한)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(파워풀|무지갯빛)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(질서|심플|섬세|흰빛|흑빛|잿빛)")
			StoneSense -= 20
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(장대|화려)")
			StoneSense -= 30

	CASE 17
	;일본풍
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(일본풍)")
			StoneSense += 35

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(서양풍|대륙풍)")
			StoneSense -= 30
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(일본풍)")
			StoneSense -= 30

	CASE 18
	;鉱石
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(석제)")
			StoneSense += 40
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(광물|태양석의|보석|금빛|은빛|금속|비취의)")
			StoneSense += 30
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(심플)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(석제|광물)")
			StoneSense -= 30

	CASE 19
	;知性
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(지적)")
			StoneSense += 25
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(도서|기술|현자의|지식의)")
			StoneSense += 20
		ENDIF
		
		StoneSense += ABL:MASTER:교양 + ABL:TARGET:교양
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(지적|도서|기술|현자의|지식의)")
			StoneSense -= 40

	CASE 20
	;冬
		;KR판 주, 冬風の, 雪だるまの는 존재하지 않는 형용사명
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(겨울의|冬風の|사계의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(얼음의|흰눈의|雪だるまの|눈의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(흰빛|잿빛|흑빛|자연)")
			StoneSense += 10
		ENDIF
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(봄의|여름의|가을의|자연)")
			StoneSense -= 20
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(겨울의|冬風の|얼음의|흰눈의|雪だるまの|눈의)")
			StoneSense -= 30

	CASE 21
	;カオス
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(혼돈|기묘|불가사의|매니악|크레이지한|광기의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(파워풀|화려)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(질서|심플|섬세)")
			StoneSense -= 20
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(혼돈|기묘|불가사의|매니악|크레이지한|광기의)")
			StoneSense -= 30

	CASE 22
	;질서
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(질서|심플|섬세)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(위엄)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(혼돈|기묘|불가사의|매니악)")
			StoneSense -= 20
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(질서|심플|섬세)")
			StoneSense -= 30

	CASE 23
	;서양풍
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(서양풍)")
			StoneSense += 35

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(일본풍|대륙풍)")
			StoneSense -= 30
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(서양풍)")
			StoneSense -= 30

	CASE 24
	;天体
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(달|해|별|월광의|태양의|햇빛의|혜성의|별빛부스러기의|빛|칠성의|하늘색의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(해|태양의|햇빛의)")
			StoneSense += 25
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(조명)")
			StoneSense -= 20

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(달|해|별|월광의|태양의|햇빛의|혜성의|별빛부스러기의|빛|칠성의)")
			StoneSense -= 30

	CASE 25
	;물
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(물|물고기|닻의|물고기의|비의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(푸른빛|고래의|해파리의|인어의|심해어의|고대어류의)")
			StoneSense += 20
		ENDIF

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(독|불)")
			StoneSense -= 20

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(물|물고기|닻의|물고기의|비의)")
			StoneSense -= 30

	CASE 26
	;守矢
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(모리야의|기적의|개구리의|뱀의|바람|바람의|대지의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(신령|무녀의|주술)")
			StoneSense += 20
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(요괴의)")
			StoneSense -= 20
		
		SIF !STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(개구리의|뱀의)") && STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(개구리의|뱀의|모리야의|바람|바람의|대지의|기적의)")
			StoneSense -= 30

	CASE 27
	;마법사
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(마법의|마법|마녀의)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(마도서|비|도서|까마귀의)")
			StoneSense += 20
		ENDIF

		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(마법의|마법|마녀의)")
			StoneSense -= 30

	CASE 28
	;バニー
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(토끼의|동전|유리잔|메달|카드)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(달|월광의|만월의)")
			StoneSense += 20
		ENDIF
		
		SIF TALENT:TARGET:33 == 1
			StoneSense -= 10
			
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(달|월광의|만월의|토끼의|동전|유리잔|메달|카드)")
			StoneSense -= 30

	CASE 29
	;식물
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(식물|열매)")
			StoneSense += 35
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(벛꽃의|딸기의|수박의|벛꽃의|장미의|해바라기의|제비꽃의|백합의|국화꽃의|수국화의|은방울꽃의|단풍나무의|사과의|복숭아의|오렌지의|포도의|버찌의|오이의|네잎클로버의|단풍의|잎사귀의|자연)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(기계|기계장치의)")
			StoneSense -= 20
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(식물|열매)")
			StoneSense -= 30

	CASE 30
	;바람
		IF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(바람|바람의|푹신푹신한|질풍의|폭풍의|천계의|천둥|번개의)")
			StoneSense += 35
			;KR판 주, 蓮華蝶の는 존재하지 않는 형용사명
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(벌레|새|흑사나비의|蓮華蝶の|반딧불의|호랑나비의|까마귀의|텐구의|참새의)")
			StoneSense += 20
		ELSEIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(자연)")
			StoneSense += 10
		ENDIF
		
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：좋아함"), "(지저의)")
			StoneSense -= 30
		SIF STRCOUNT(GET_STR(, "캐릭터데이터", TARGET, "감성：싫어함"), "(바람|바람의|푹신푹신한|질풍의|폭풍의|천계의)")
			StoneSense -= 30
	ENDSELECT

DRAWLINE
PRINTFORMW 돌쌓기 경연대회 커플 부문이 시작되었다!
PRINTFORMW 이번 테마는- %STONECombiDAY(Stoneday)%다!……
IF StoneSense > 19
	PRINTFORMW %마스터는% 자신 있어 보이는 %CALLNAME:TARGET%와 함께 쌓기 시작했다…
ELSEIF StoneSense > 0
	PRINTFORMW %마스터는% 약간 자신 있어 보이는 %CALLNAME:TARGET%와 함께 쌓기 시작했다…
ELSEIF StoneSense < 0
	PRINTFORMW %마스터는% 자신 없어 보이는 %CALLNAME:TARGET%와 함께 쌓기 시작했다…
ELSE
	PRINTFORMW %마스터는% %CALLNAME:TARGET%와 함께 쌓기 시작했다…
ENDIF

;自信の有無との関係でこのタイミングでお題の基礎点挿入
;お題点の基礎点は35でランダムに入るのが35点
StoneSense += (35 + RAND:35)


PRINTFORMDL ………
PRINTFORMDL ……
PRINTFORMDW …
PRINTFORMW 「모두의 작품이 나온 것 같습니다! 그럼 심사위원 채점 결과를 발표하겠습니다!」
PRINTFORMW 「%CALLNAME:MASTER%＆%CALLNAME:(FLAG:데이트상대)%의 돌쌓기 기술을 나타내는 기술점수는……」
PRINTFORMW 「{StoneState}점!」
PRINTFORMW 「이어서 두 사람이 얼마나 협력했는지를 나타내는 협력점수는……」
PRINTFORMW 「{CombiLove}점!」
PRINTFORMW 「마지막으로! 두 작품이 테마에 맞는 상태인지를 나타내는 제목점수는……」
PRINTFORMW 「{StoneSense}점!」
PRINTFORMW 「이러하여 %CALLNAME:MASTER%＆%CALLNAME:(FLAG:데이트상대)%의 작품 종합 평균 점수는……！」


;評価得点
CombiPoint = ( StoneState + CombiLove + StoneSense ) / 3

;１００点を超えたら修正
SIF CombiPoint > 100
	CombiPoint = 100

CALL COLORMESSAGE(@"{CombiPoint}점!",C_YELLOW,2)

IF CombiPoint == 100
	PRINTFORMW 「대-단해요! 그 어떤 이의도 없는 깔끔한 우승입니다!!」
	PRINTFORMW 「100점이 넘더라도 100점인 거니까 이것이 최고득점이예요!」
	PRINTFORMW 「우승 상품을 받으세요!」
	MONEY += 20000
	SIF STONE_PLAYCOUNT:2 > 2
		ITEM:활석 += 1
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
	SOURCE:환락 = 3000
	SOURCE:사랑 = 2000
	STONE_PLAYCOUNT:1 += 10
	STONE_BATTLECOUNT:3 += 1
ELSEIF CombiPoint > 79
	PRINTFORMW 「대-단해요! 우승입니다!!」
	PRINTFORMW 「우승 상품을 받으세요!」
	MONEY += 20000
	SIF STONE_PLAYCOUNT:2 > 2
		ITEM:활석 += 1
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
	SOURCE:환락 = 2500
	SOURCE:사랑 = 1500
	STONE_PLAYCOUNT:1 += 7
	STONE_BATTLECOUNT:3 += 1
ELSEIF CombiPoint > 69
	PRINTFORMW 「아쉽다! 준우승이예요!!」
	PRINTFORMW 「준우승 상품으로 5000엔을 받으세요!」
	MONEY += 5000
	SOURCE:환락 = 1500
	SOURCE:사랑 = 800
ELSEIF CombiPoint > 59
	PRINTFORMW 「아쉽다! 3위 입상이었어요!!」
	SOURCE:환락 = 1000
	SOURCE:사랑 = 600
ELSE
	PRINTFORMW 「이런- …이번에는 입상하지 못한 것 같습니다……」
	SOURCE:환락 = 600
	SOURCE:사랑 = 300
ENDIF

IF STONE_COMBI_WIN:(Stoneday) < 80
	IF CombiPoint > 79
		STONE_COMBI_TROPHY:(Stoneday) = 1
	ELSE
	ENDIF
ENDIF

IF STONE_COMBI_WIN:(Stoneday) < CombiPoint
	STONE_COMBI_WIN:(Stoneday) = CombiPoint
	STONE_COMBI_COMBI:(Stoneday) = TARGET
ENDIF

IF STONE_COMBI_CHARA:TARGET:(Stoneday) < CombiPoint
	STONE_COMBI_CHARA:TARGET:(Stoneday) = CombiPoint
ENDIF

{
STONE_PLAYCOUNT:3 = STONE_COMBI_TROPHY:1 + STONE_COMBI_TROPHY:2 + STONE_COMBI_TROPHY:3 + STONE_COMBI_TROPHY:4 + STONE_COMBI_TROPHY:5
+ STONE_COMBI_TROPHY:6 + STONE_COMBI_TROPHY:7 + STONE_COMBI_TROPHY:8 + STONE_COMBI_TROPHY:9 + STONE_COMBI_TROPHY:10 + STONE_COMBI_TROPHY:11
+ STONE_COMBI_TROPHY:12 + STONE_COMBI_TROPHY:13 + STONE_COMBI_TROPHY:14 + STONE_COMBI_TROPHY:15 + STONE_COMBI_TROPHY:16 + STONE_COMBI_TROPHY:17
+ STONE_COMBI_TROPHY:18 + STONE_COMBI_TROPHY:19 + STONE_COMBI_TROPHY:20 + STONE_COMBI_TROPHY:21 + STONE_COMBI_TROPHY:22 + STONE_COMBI_TROPHY:23
+ STONE_COMBI_TROPHY:24 + STONE_COMBI_TROPHY:25 + STONE_COMBI_TROPHY:26 + STONE_COMBI_TROPHY:27 + STONE_COMBI_TROPHY:28 + STONE_COMBI_TROPHY:29
+ STONE_COMBI_TROPHY:30
}

PRINTFORML
IF STONE_PLAYCOUNT:3 == 30 && STONE_COMBI_TROPHY:31 == 0
	PRINTFORMW 「대단해요-!! 설마 모든 테마에서 우승하다니!!」
	PRINTFORMW 「이건… 약속대로 호화 상품을 줄 수밖에 없는 거죠!」
	STONE_COMBI_TROPHY:31 = 1
	PRINTFORMW 「먼저… 상금 10만엔입니다!」
	MONEY += 100000
	PRINTFORMW 「다음으로는 ……미라다의 돌? 이라는 잘 모르겠는 돌을 3개요!」
	ITEM:미라다의돌 += 3
	PRINTFORMW 「마지막으로… 이 책!『우리들만이 아는 비탄』! 우리 미즈코들의 부모님에 대한 생각이 가---득 담겨져 있습니다.」
	PRINTFORMW 「이것을 읽은 아버지와 어머니는 그 어느 때보다 태어난 우리 아이에게 상냥해질 수 있다는 평판입니다!」
	ITEM:우리들만이아는애탄 += 1
	IF !FLAG:임신스위치
		PRINTFORMW 「……어라? 미안해요! 아무래도 이 책 %CALLNAME:MASTER%에게는 의미가 없을 것 같습니다…」
		PRINTFORMW 「혜택을 받으려면 옵션에서 임신 스위치라는 것을 켜야 합니다.」
		PRINTFORMW 「…그렇습니다. 저는 의미를 모르겠지만…… 일단 책은 그대로 줄께요…」
	ELSE
		PRINTFORMW 「좋은 부모가 되길 바래요!!」
	ENDIF
	PRINTFORML
ELSEIF STONE_PLAYCOUNT:3 > 0
	PRINTFORML 「현재 %마스터는% {STONE_PLAYCOUNT:3}개의 테마에서 우승했습니다!」
	PRINTFORML 「총 서른개의 테마가 있으니까… 전 테마 제패를 목표로 하는거예요!」
	PRINTFORMW 「전 제패하면 호화 경품도 있어요!」
	PRINTFORML
ELSE
	PRINTFORML 「이번에는 우승을 못했지만... 사실 내일도 또 다른 주제로 대회를 하고 있습니다.」
	PRINTFORML 「총 30개의 테마가 있으니까요… 전 제패!」
	PRINTFORMW 「전 제패하면 호화 경품도 있어요!」
	PRINTFORML
ENDIF
	
PRINTFORML
PRINTFORMW 「그럼 다음에 또 참여해주세요!」
PRINTFORML
PRINTFORML (%타겟은% 돌쌓기 경연대회를 통해 손가락이 단련됨으로써 수음과 동등한 경험을 얻었습니다!)
EXP:MASTER:데이트경험 ++
EXP:TARGET:데이트경험 ++
EXP:TARGET:수음경험 += 1
STONE_PLAYCOUNT:1 += 5
STONE_BATTLECOUNT:2 += 1
SIF !TALENT:MASTER:돌쌓기랭커 == 6 && MONEY > 3000
	MONEY -= 3000
TIME += 40
RETURN 0
