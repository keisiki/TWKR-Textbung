@STONEBATTLE(difficulty, C_ID)
#DIM C_ID
#DIM FIRSTONE
#DIM SECONE, 3
#DIM LASTONE, 3
#DIM FIRSTONE_E
#DIM SECONE_E
#DIM LASTONE_E
#DIM Skill
#DIM OUTCOME	;0=双方失敗 1=あなた成功 2=상대成功 3=双方成功
#DIM PLAYER_TOTAL
#DIM ENEMY_TOTAL
#DIM difficulty
{
FIRSTONE = 1 + RAND:5 + (TALENT:MASTER:바늘질 * 2) + (TALENT:MASTER:능숙한손가락 * 2) + ( TALENT:MASTER:채집Lv / 2 )
+ ( ABL:MASTER:음악기능 / 2 ) + ( TALENT:MASTER:조제Lv / 2 ) + (TALENT:MASTER:돌쌓기랭커 * 2) 
}
Skill = ABL:MASTER:기교 + ABL:MASTER:손가락

SECONE:0 = RAND:(6 + Skill) + Skill + RAND:7 + 6
SECONE:1 = RAND:(4 + Skill) + Skill + RAND:5 + 4
SECONE:2 = RAND:(2 + Skill) + Skill + RAND:3 + 2
LASTONE:0 = RAND:9 + 2
LASTONE:1 = RAND:7 + 4
LASTONE:2 = RAND:5 + 7
IF difficulty == 0
	FIRSTONE_E = RAND:3
	SECONE_E = RAND:4 + RAND:5
	LASTONE_E = RAND:3
ELSEIF difficulty == 1
	FIRSTONE_E = RAND:5
	SECONE_E = RAND:5 + RAND:6
	LASTONE_E = RAND:5
ELSEIF difficulty == 2
	FIRSTONE_E = RAND:7 + 1
	SECONE_E = RAND:6 + RAND:6 + 1
	LASTONE_E = RAND:6
ELSEIF difficulty == 3
	FIRSTONE_E = RAND:7 + 2
	SECONE_E = RAND:6 + RAND:7 + 1
	LASTONE_E = RAND:7 + 1
ELSEIF difficulty == 4
	FIRSTONE_E = RAND:7 + 3
	SECONE_E = RAND:7 + RAND:7 + 2
	LASTONE_E = RAND:8 + 2
ELSEIF difficulty == 5
	FIRSTONE_E = RAND:9 + 3
	SECONE_E = RAND:7 + RAND:8 + 2
	LASTONE_E = RAND:9 + 2
ELSEIF difficulty == 6
	SELECTCASE RAND:3
		CASE 0
			FIRSTONE_E = RAND:9 + 4
		CASE 1
			FIRSTONE_E = RAND:9 + 5
		CASE 2
			FIRSTONE_E = RAND:10 + 5
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			SECONE_E = RAND:7 + RAND:8 + 3
		CASE 1
			SECONE_E = RAND:8 + RAND:8 + 3
		CASE 2
			SECONE_E = RAND:8 + RAND:9 + 4
	ENDSELECT
	LASTONE_E = RAND:3 + 8
ELSEIF difficulty == 7
	SELECTCASE RAND:3
		CASE 0
			FIRSTONE_E = RAND:9 + 6
		CASE 1
			FIRSTONE_E = RAND:9 + 7
		CASE 2
			FIRSTONE_E = RAND:10 + 7
	ENDSELECT
	SELECTCASE RAND:3
		CASE 0
			SECONE_E = RAND:7 + RAND:8 + 5
		CASE 1
			SECONE_E = RAND:8 + RAND:8 + 5
		CASE 2
			SECONE_E = RAND:8 + RAND:9 + 6
	ENDSELECT
	LASTONE_E = RAND:3 + 10
ELSEIF difficulty == 8
	FIRSTONE_E = RAND:5 + 10
	SECONE_E = RAND:7 + RAND:7 + 10
	LASTONE_E = 20
ELSE 
	FIRSTONE_E = RAND:6 + 12
	SECONE_E = RAND:8 + RAND:8 + 12
	LASTONE_E = 22
ENDIF

DRAWLINE
PRINTFORMW 돌쌓기 승부가 시작됐다！
PRINTFORML 초반에 스타트 대시를 결정한 당신이 현재 쌓은 돌의 수는 {FIRSTONE}개
PRINTFORMW 한편 %조사처리(CALLNAME:C_ID,"는")% {FIRSTONE_E}개 쌓고있다…
{
CALL ASK_M(@"급하게 쌓는다（돌탑을 무너뜨릴 리스크{20 + ABL:욕망}％＆돌을 쌓는 수 많음）",1,@"평범하게 쌓는다（돌탑을 무너뜨릴 리스크{15 + ABL:욕망}％）",1,
@"신중하게 쌓는다（돌탑을 무너뜨릴 리스크{8 + ABL:욕망}％＆돌을 쌓는 수 적음）",1, @"아무 것도 안 한다（돌탑을 무너뜨릴 리스크 0＆ 돌을 쌓는 수 0）",1)
}

IF RESULT == 0
	PRINTFORMW 당신은 다소의 리스크를 짊어지고서라도 빨리 돌을 쌓았다！
	PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 9
			OUTCOME = 1
		CASE 10 TO 19
			OUTCOME = 0
		CASE 20 TO 30 + ABL:욕망
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
		ENDSELECT
ELSEIF RESULT == 1
PRINTFORMW 당신은 돌을 쌓았다
PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 9
			OUTCOME = 1
		CASE 10 TO 19
			OUTCOME = 0
		CASE 20 TO 25 + ABL:욕망
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
	ENDSELECT
ELSEIF RESULT == 2
	PRINTFORMW 당신은 조심스럽게 돌을 쌓았다
	PRINTFORMW 
	SELECTCASE RAND:100
		CASE 0 TO 9
			OUTCOME = 1
		CASE 10 TO 17
			OUTCOME = 0
		CASE 18 TO 18 + ABL:욕망
			OUTCOME = 2
		CASEELSE
			OUTCOME = 3
	ENDSELECT
ELSEIF RESULT == 3
	PRINTFORMW 당신은 돌을 쌓지 않았다
	PRINTFORMW 
	PLAYER_TOTAL = FIRSTONE
	SELECTCASE RAND:100
		CASE 0 TO 19
			PRINTFORMW 당신은 아무것도 하지 않고 기다리고 있다…
			PRINTFORMW 아앗！ %조사처리(CALLNAME:C_ID,"는")% 끝나기 직전에 돌을 무너뜨렸다！
			PRINTFORMW 상대의 결과는{LASTONE_E}개……
			ENEMY_TOTAL = LASTONE_E
		CASEELSE
			PRINTFORMW 당신은 아무것도 하지 않고 기다리고 있다…
			PRINTFORMW 한숨 돌린 당신이 옆을 보면…
			PRINTFORMW 상대는 차근차근 쌓아올려 {SECONE_E}개 추가로 쌓아올린 것 같다……
			ENEMY_TOTAL = FIRSTONE_E + SECONE_E
	ENDSELECT
ENDIF
IF GROUPMATCH(RESULT, 0, 1, 2)
	IF OUTCOME % 2 == 1
		PRINTFORMW %마스터는% {SECONE:RESULT}개를 쌓아 올렸다！
		PLAYER_TOTAL = FIRSTONE + SECONE:RESULT
	ELSE
		PRINTFORMW 앗！
		PRINTFORMW %마스터는% 무심코 돌을 허물어뜨리고 말았다！
		PRINTFORMW 서둘러 다시 쌓은 결과 {LASTONE:RESULT}개 쌓았다
		PLAYER_TOTAL = LASTONE:RESULT
	ENDIF
	IF OUTCOME >= 2
		PRINTFORMW 한편、상대는 착실히 쌓고 {SECONE_E}개 추가로 쌓았나 보다……
		ENEMY_TOTAL = FIRSTONE_E + SECONE_E
	ELSE
		PRINTFORMW 아앗！%CALLNAME:C_ID%\@ OUTCOME % 2 == 1? 은 종료 직전에 # 도\@ 돌을 무너뜨린 것 같다！
		PRINTFORMW 무너뜨린것을 필사적으로 다시 쌓은 상대의 결과는 {LASTONE_E}개……
		ENEMY_TOTAL = LASTONE_E
	ENDIF
ENDIF
CALL COLORMESSAGE(@"%CALLNAME:MASTER%：{PLAYER_TOTAL}　VS　%CALLNAME:C_ID%：{ENEMY_TOTAL}",C_YELLOW,2)
PRINTFORML 
IF PLAYER_TOTAL > ENEMY_TOTAL
	CALL COLORMESSAGE(@"YOU WIN!",C_YELLOW,1)
	CALL COLORMESSAGE(@"인생의 승리자！",C_YELLOW,1)
	PRINTFORMW
	;ラスボス特殊分岐
	IF difficulty == 8
		TRYCALLFORM StoneMessage_{C_ID}("lastbattle_w")
	ELSE
		TRYCALLFORM StoneMessage_{C_ID}("win")
	ENDIF
	RETURN 0
ELSEIF PLAYER_TOTAL == ENEMY_TOTAL
	PRINTFORML
	PRINTFORMW 무승부이므로 다시 한번 승부를 합니다
	JUMP STONEBATTLE(difficulty, C_ID)
ELSE
	CALL COLORMESSAGE(@"YOU LOSE!",C_YELLOW,1)
	CALL COLORMESSAGE(@"패배자！",C_YELLOW,1)
	PRINTFORMW
	;ラスボス特殊分岐
	IF difficulty == 8
		TRYCALLFORM StoneMessage_{C_ID}("lastbattle_l")
	ELSE
		TRYCALLFORM StoneMessage_{C_ID}("lose")
	ENDIF
	RETURN 1
ENDIF

RETURN 0
		
;タッグバトル構文予定地
