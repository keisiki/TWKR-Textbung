;가구점
@COM625
#DIM 판매가
#DIM 루프수
#DIM 결과
#DIM 자재보관
#DIM 삭제행수
#DIM MaterialQTY
#DIMS DiscountedPrice

루프수 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 480, 1020) == 0
	PRINTFORMW 영업시간 외 같다
	RETURN -1
ENDIF

;初回イベント
IF FLAG:마지막가구점방문일 == 0
	PRINTL 
	PRINTFORMW 가구점이라고 적힌 간판이 눈에 띄어 처마를 지나 들어가 보았으나…
	PRINTFORMW 「……그거야, 그거는. 어엉?」
	PRINTFORMW 「응? 맞아, 여기는 가구점이야 뭐? 파는 거 없냐고?」
	PRINTFORMW 「바보야. 장롱이든 뭐든 주문을 받고 나서 만드는 거야. 그래서 지금은 한가하다는 소리지.」
	PRINTFORMW 「……그래서? 갖고 싶은게 뭔데, 너.」
	PRINTFORMW 「……뭐? 실물을 보고 결정하려고 했다고?」
	PRINTFORMW 「놀리는 거라면 돌아가라……라고 말하고 싶지만、좋았어. 어디, 저번에 작업했던 것의 기록지가――」
	PRINTFORMW 　남자가 스윽 내민 종이에는 의외로 꼼꼼해 보이는 글씨로 비용이 적혀 있었지만
	PRINTFORMW 　……우와, 비싸?!
	PRINTFORMW 「그야 내 솜씨는 일품이니까, 그렇게 싸게 주문 받을리가 없잖아.」
	PRINTFORMW 　그 부분을 어떻게든 안 될까요? 하고 물어보니…
	PRINTFORMW 「돈방석에 앉아서 여기부터 저기까지 뿌려준다면야 못할 것도 없다란 말도 있지.」; 속담 변형 같은데 영 모르겟음 의역함. 金に胡坐を掻いて、一から十までこっちにブン投げるんなら、思いっきり吹っ掛けてやるトコだが
	PRINTFORMW 「……글쎄, 재료가 될만한 '제재'를 준비해온다면, 그만큼 할인 해 줄수는 있지.」
	PRINTFORMW 　여차저차해서、최종 금액을 보여준다…… 뭐, 어떻게든 낼 수 있을 것 같은 금액에 도달한 것 같다.
	PRINTFORMW 「흥. 뭐, 준비되면 발주해줘. 최고의 명품으로 만들어 줄테니까.」
	PRINTL 
	루프수 = 1
ENDIF

SIF FLAG:작성중가구
	FLAG:가구작성잔여 = MAX(FLAG:가구작성잔여-((DAY:0 - FLAG:마지막가구점방문일)*2),0)

$LOOP_SELECT

IF 루프수
	PRINTL 
	IF FLAG:가구작성잔여
		PRINTFORMW 「비켜라 비켜, 나는 바쁘단 말이다!」
	ELSE
		PRINTFORMW 「뭐야, 아직 무슨 일 남았어?」
	ENDIF
ELSE
	;平常来店
	PRINTL 
	;가구発注中
	IF FLAG:가구작성잔여 >= 14
		PRINTFORMW 「뭐야, 너였냐. 성급하긴, 아직 도면 그린지 얼마 지나지도 않았어.」
	ELSEIF FLAG:가구작성잔여 >= 9
		PRINTFORMW 「뭐야, 너였냐. 주문했던 가구라면 아직 좀 걸릴거야.」
	ELSEIF FLAG:가구작성잔여 >= 5
		PRINTFORMW 「뭐야, 너였냐. 주문했던 가구라면 잘 만들고 있으니 걱정하지 마.」
	ELSEIF FLAG:가구작성잔여 >= 2
		PRINTFORMW 「오, 조금만 있으면 주문했던 가구, 완성된다고!」
	ELSEIF FLAG:작성중가구 && FLAG:가구작성잔여 == 0
		;完成日
		IF FLAG:작성중가구 == 902
			PRINTFORMW 「여어, 왔냐. 응? 목욕탕이 완성된 건 봤는데, 왜 일부러 가게까지 와야하냐고?」
			PRINTFORMW 「납품서 교환이라든가 여러가지가 있어. 이런거, 제대로 하지 않으면 뒤탈이 남으니까 싫거든.」
		ELSE
			PRINTFORMW 「여어, 기다리게 했네. 이것이 주문한 물건이다.」
		ENDIF
		IF FLAG:작성중가구 == 907
			PRINTFORMW 「이야…… 역시, 빗자루는 전문이 아니거든, 조금 수고스러웠어.」
		ELSEIF ITEM:(FLAG:작성중가구) == 2
			PRINTFORMW 「이야…… 간만에 이해되는 일이었다. 실로 장인으로서 뿌듯함을 느꼈어.」
		ELSEIF ITEM:(FLAG:작성중가구) == 1 
			PRINTFORMW 「흥, 신경 쓰이는 부분이 좀 있었지만…… 뭐, 이런거다.」
			PRINTFORMW 「제작자로서의 만족과 사용자로서의 만족은 서로 미묘하게 어긋나 있지만, 그것을 양립시키는 것이 장인이니까 말이야.」
		ELSE
			PRINTFORMW 「갖고 싶은 가구가 있으면 또 말을 걸어. 아, 지금의 가구에 불만이 있으면 그것도 알려줘.」
		ENDIF
		PRINTL 
		SETCOLOR 0x00FF00
		PRINTFORM %ITEMNAME:(FLAG:작성중가구)%
		IF ITEM:(FLAG:작성중가구) == 0
			PRINTFORMW 을 손에 넣었다!
		ELSE
			PRINTFORMW 가 개조되어 사용감이 좋아졌다!
		ENDIF
		RESETCOLOR
		PRINTL 
		ITEM:(FLAG:작성중가구) ++
		FLAG:작성중가구 = 0
	ELSE
		IF DAY:0 - FLAG:마지막가구점방문일 > 28
			PRINTFORMW 「뭐야, 너였냐. 드디어 주문인가?」
		ELSEIF FLAG:마지막가구점방문일 == DAY:0
			PRINTFORMW 「뭐야, 너한테 신경쓸 정도로……… 아아, 한가해. 잘못이네」
		ELSE
			PRINTFORMW 「뭐야, 너였냐. 가구가 필요해?」
		ENDIF
	ENDIF
ENDIF

루프수 ++
FLAG:마지막가구점방문일 = DAY:0

PRINTFORM 　　소지금: %TOSTR(MONEY,"#,###")%
IF FLAG:작성중가구
	PRINTFORML 　　%ITEMNAME:(FLAG:작성중가구)%을 작성중　납기：앞으로 {FLAG:가구작성잔여}일
ELSE
	PRINTL 
ENDIF
DRAWLINE

CALL ASK_M("가구を作ってほしい", !FLAG:작성중가구, "取引内容を確認する", 1, "店を出る", 2)

SELECTCASE RESULT
	;가구を作ってほしい
	CASE 0
		PRINTFORMW 「네가 필요할만한 가구를 만드려면……… 뭐 여기정도려나?」

		$INPUT_LOOP01
		;リストの呼び出し
		삭제행수 = 0
		FOR LOCAL, 902, 908
			삭제행수 ++
			IF ITEM:LOCAL == 3
				PRINTPLAINFORM 　[{LOCAL}]
				LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
				PRINTPLAINFORM %LOCALS, 21, LEFT%
			ELSE
				PRINTFORM 　
				PRINTFORM [{LOCAL}]
				IF ITEM:LOCAL == 0
					;未所持
					PRINTFORM %ITEMNAME:LOCAL, 21, LEFT%
				ELSE
					;所持
					LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
					PRINTFORM %LOCALS, 21, LEFT%
				ENDIF
			ENDIF
			CALL FURNITURE_EXPLANATION(LOCAL)
		NEXT
		PRINTFORML 　[999] 역시 그만둔다
		삭제행수 ++
		
		INPUT
		
		결과 = RESULT
		MaterialQTY = FURNITURE(결과, "軽減上限", ITEM:결과)
		SELECTCASE 결과
			CASE 902 TO 907
				;Lv3の時は選択できない
				IF ITEM:결과 == 3
					CLEARLINE 삭제행수 + 1
					GOTO INPUT_LOOP01
				ENDIF

				;注文가구の請求を作成
				DRAWLINE
				PRINTFORML %ITEMNAME:결과%の\@ ITEM:결과 == 0? 発注# 改修\@
				;所持製材量での値引きの表記
				;Lv1→2の時は半額
				IF ITEM:결과 == 1
					PRINTFORM  費用    ：%금액표시((ITEMPRICE:결과 / 2))% → 
				ELSE
					PRINTFORM  費用    ：%금액표시(ITEMPRICE:결과)%  → 
				ENDIF
				DiscountedPrice = %금액표시(FURNITURE(결과, 0, ITEM:결과))%
				
				IF FURNITURE(결과, 0, ITEM:결과) == FURNITURE(결과, "最低額", ITEM:결과)
					SETCOLOR C_RED
				ELSEIF ITEM:결과 == 1 && FURNITURE(결과, 0, ITEM:결과) < ITEMPRICE:결과 / 2
					SETCOLOR C_YELLOW
				ELSEIF FURNITURE(결과, 0, ITEM:결과) < ITEMPRICE:결과
					SETCOLOR C_YELLOW
				ENDIF
				PRINTFORML \\%DiscountedPrice% \@ FURNITURE(결과, 0, ITEM:결과) == FURNITURE(결과, "最低額", ITEM:결과)? 最安値！# \@
				RESETCOLOR

				;価格軽減に必要な資材量と現在所持量の比較
				PRINTFORM 　가격 할인 조건：%WOOD_NAME(FURNITURE(결과, "자재번호"))%의 제재
				PRINTFORM 를 {MaterialQTY}준비한다　
				SIF LOG_SALES:FURNITURE(결과, "자재번호") >= MaterialQTY
					SETCOLOR C_RED
				PRINTFORML （{LOG_SALES:FURNITURE(결과, "자재번호")}／{MaterialQTY}）
				RESETCOLOR
			CASE 999
				PRINTL 
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP01
		ENDSELECT
		PRINTL 
		IF ITEM:결과 == 0
			CALL ASK_M("この가구を作ってもらう", MONEY >= FURNITURE(결과, 0), "그만둔다", 1)
		ELSE
			CALL ASK_M("この가구を改修してもらう", MONEY >= FURNITURE(결과, 0, ITEM:결과) && ITEM:결과 <= 2, "그만둔다", 1)
		ENDIF
		
		IF RESULT
			CLEARLINE 9 + 삭제행수
			GOTO INPUT_LOOP01
		ELSE
			;費用の支払い
			PRINTFORMW  소지금: %금액표시(MONEY)% → %금액표시(MONEY - FURNITURE(결과, 0, ITEM:결과))%
			CALL PAY_MONEY_YEN(FURNITURE(결과, 0, ITEM:결과))

			;製材等の消費  軽減上限量以上に持っていれば上限量分減らし、上限に達していなければ全部消費
			;現在의 제재の量を退避してから計算
			자재보관 = LOG_SALES:FURNITURE(결과, "자재번호")
			LOG_SALES:FURNITURE(결과, "자재번호") = MAX(0, LOG_SALES:FURNITURE(결과, "자재번호") - MaterialQTY)
			;文章部分
			PRINTFORMW 　%WOOD_NAME(FURNITURE(결과, "자재번호"))%의 제재：{자재보관} → {LOG_SALES:FURNITURE(결과, "자재번호")}
			PRINTL 
			IF ITEM:결과
				PRINTFORMW 「뭐야, 이전 가구에 불만이 있는거야? 까다롭기는. {FURNITURE(결과, "期間", ITEM:결과)}일 뒤에 가게에 와라, 깜짝놀라게 해줄테니까!」
			ELSE
				PRINTFORMW 「으음, 계약 성립이다. 납기는 {FURNITURE(결과, "期間", ITEM:결과)}일 뒤다. 또 가게에 들러라.」
			ENDIF
			FLAG:가구작성잔여 = FURNITURE(결과, "期間", ITEM:결과)
			FLAG:작성중가구 = 결과
		ENDIF
	;取引内容を確認する
	CASE 1
		PRINTFORMW 「어? 또 확인하고 싶다고?」
		PRINTFORMW 「여기는 가구점이니까, 가구를 만드는 것이 당연하잖아.」
		PRINTFORMW 「재료의 제재를 준비해오면 할인해서 만들어 줄게」
		PRINTFORMW 「당연하지만 발주된 가구를 만들려면 나름대로 시간이 걸린다.」
		PRINTFORMW 「내친김에 그 기간동안은 주문한 가구 만드는데 전념할 테니까 다른 주문은 받지 않겠어.」
		PRINTFORMW 「아, 만약 만든 가구에 불만이 있다면 바로 말해. 돈은 받지만, 보다 좋은걸 목표로 하고 있으니까.」
		PRINTL 
	CASE 2
		PRINTFORMW 「냉큼 돌아가.」
		PRINTL 
		TIME += 루프수 * 5
		RETURN -1

ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;가구점
@COM_ABLE625
;一括管理
SIF GLOBAL_COMABLE(625)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:현재위치 != 주택가
		RETURN 0
ELSE
	;長屋前
	SIF CFLAG:MASTER:현재위치 != 214
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:수면
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;注文できる가구リストに付随する説明文
;ARG 가구のアイテム番号
;-------------------------------------------------
@FURNITURE_EXPLANATION(ARG)

IF ITEM:ARG
	PRINTFORM 設置場所：
	SELECTCASE ARG
		CASE 902
			LOCALS = %NAME_FROM_PLACE(NEAR_BATH(1))%
		CASE 904, 907
			LOCALS = %CALLNAME:MASTER%が持ち歩いている
		CASEELSE
			LOCALS = %CALLNAME:MASTER%の私室
	ENDSELECT
	PRINTFORMD %LOCALS, 26, LEFT%
ELSE
	PRINTFORM 　　　　　　　　　　　　　　　　　　
ENDIF

SIF ITEM:ARG == 0
	SETCOLOR C_GRAY

SELECTCASE ARG
	CASE 902
		PRINTFORM …설치한 장소에서의 목욕 체력 및 기력회복 증가
	CASE 903
		PRINTFORM …수면을 통한 체력·기력·정력회복 증가
	CASE 904
		PRINTFORM …요리의 완성도를 올린다
	CASE 905
		PRINTFORM …개인실에서의 공부나 조제의 효율을 올린다
	CASE 906
		PRINTFORM …개인실에서의 휴식 때 체력, 기력, 정력회복 증가
	CASE 907
		PRINTFORM …청소의 효율을 올린다
	CASEELSE
		PRINT 
ENDSELECT
IF ITEM:ARG == 3
	SETCOLOR C_ORANGE
	PRINTFORML ▲▲
ELSEIF ITEM:ARG == 2
	SETCOLOR C_YELLOW
	PRINTFORML ▲
ELSE
	PRINTL 
ENDIF
RESETCOLOR

;-------------------------------------------------
;가구注文で使用する各種数値をまとめた関数
;現状は902～907
;F_ID …가구のアイテム番号
;ARGS 0= 補正価格(省略時)  1= 資材による軽減上限数
;      2= 가구に対応する자재번호  3= 軽減最低額  
;      4= 発注後の作成期間   を返す
;EnhanceTime 0= 初発注(省略時)  1= 強化１  2= 強化２
;        主にITEM:가구番号を入れて利用
;-------------------------------------------------
@FURNITURE(F_ID, ARGS, EnhanceTime = 0)
#FUNCTION
#DIM F_ID
#DIM EnhanceTime
#DIM DiscountRate
#DIM MaxDiscount
#DIM 자재번호
#DIM 軽減上限
#DIM 最低額
#DIM 補正価格
#DIM 期間

;手持ち製材との調整価格を表記
SELECTCASE F_ID
	;ヒノキ風呂
	CASE 902
		자재번호 = 2;ヒノキ
		軽減上限 = 50
		最低額 = 250000
		期間 = 15
	;キリのベッド
	CASE 903
		자재번호 = 8;キリ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;イチョウのまな板
	CASE 904
		자재번호 = 6;イチョウ
		軽減上限 = 10
		最低額 = 50000
		期間 = 5
	;ケヤキの机椅子
	CASE 905
		자재번호 = 7;ケヤキ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;ナラのソファー
	CASE 906
		자재번호 = 9;ナラ
		軽減上限 = 30
		最低額 = 200000
		期間 = 10
	;カスタム竹箒
	CASE 907
		자재번호 = 12;竹
		軽減上限 = 30
		最低額 = 30000
		期間 = 10
ENDSELECT

;Lv1→Lv2時は半分  Lv2→Lv3時は最初と同じで
MaxDiscount = ITEMPRICE:F_ID - 最低額
IF EnhanceTime == 1
	軽減上限 /= 2
	最低額 /= 2
	期間 /= 2
	MaxDiscount /= 2
ENDIF

SIF ARGS == "자재번호"
	RETURNF 자재번호
SIF ARGS == "軽減上限"
	RETURNF 軽減上限
SIF ARGS == "最低額"
	RETURNF 最低額
SIF ARGS == "期間"
	RETURNF 期間
	
DiscountRate = 100 * MIN(LOG_SALES:자재번호, 軽減上限) / 軽減上限

IF EnhanceTime == 1
	補正価格 = (ITEMPRICE:F_ID / 2) - MaxDiscount * DiscountRate / 100
ELSE
	補正価格 = ITEMPRICE:F_ID - MaxDiscount * DiscountRate / 100
ENDIF
RETURNF 補正価格