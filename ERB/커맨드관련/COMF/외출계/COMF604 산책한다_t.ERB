;-------------------------------------------------
;산책한다
;외출 커맨드
;-------------------------------------------------
@COM604
#DIM LPCOUNT
#DIM 현재지점
#DIM P_ID
#DIM 소요시간
#DIM 시간보정
#DIM 시간정지점프
#DIM 특수워프
#DIM 전턴위치
#DIM 이동취소플래그
#DIM ANIMATION_SEED
;散策する
시간정지점프 = FLAG:시간정지 || (GETBIT(FLAG:순간이동, 0) && !FLAG:데이트상대)

ANIMATION_SEED = 0
;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++

DRAWLINEFORM ~

전턴위치 = CFLAG:MASTER:현재위치
이동취소플래그 = 0

IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "ODEKAKE")
ELSE
	CALL DRAW_MAP("ODEKAKE")
ENDIF

VARSET LOCAL
DRAWLINEFORM ~
PRINTL 어디로 향합니까?
현재지점 = CFLAG:MASTER:현재위치 % 100 / 10
FOR LPCOUNT, 1, 10
	P_ID = CFLAG:MASTER:데이트중 * 100 + LPCOUNT * 10
	SIF NAME_FROM_PLACE(P_ID) == ""
		CONTINUE
	IF LPCOUNT == 현재지점
		PRINTPLAINFORM [  0] - %NAME_FROM_PLACE(P_ID), 18, LEFT%
		CALL COLORMESSAGE(@"현위치　", C_GREEN, 3)
	ELSE
		시간보정 = 0
		IF 시간정지점프
			LOCALS = TSP%" "%
		ELSE
			;코마치의 능력
			SIF FLAG:데이트상대 == [[코마치]]
				시간보정 += -50
			;와카사기히메의 부담
			SIF FLAG:데이트상대 == [[와카사기히메]] && !GETBIT (FLAG:풀장, 0)
				시간보정 += 100
			;地点ごとのショートカット効果
			SIF SHORTCUT(CFLAG:MASTER:데이트중, -1)
				시간보정 += -50
			LOCALS = 분　
			ENDIF
		소요시간 = 산책소요시간(CFLAG:MASTER:현재위치, P_ID, 시간보정)
		PRINTFORM [{LPCOUNT, 3}] - %NAME_FROM_PLACE(P_ID), 18, LEFT%
		IF 시간보정 < 0
			SETCOLOR C_L_GREEN
		ELSEIF 시간보정 > 0
			SETCOLOR C_ROYAL_BLUE
		ENDIF
		PRINTFORM {소요시간, 3} %LOCALS%
		RESETCOLOR
	ENDIF
	LOCAL ++
	SIF LOCAL % 2 == 0
		PRINTL 
NEXT
SIF LOCAL % 2 == 1
	PRINTL 
;특수워프
특수워프 = ODEKAKEMAP_SETTING(CFLAG:MASTER:현재위치, "특수워프")
IF 특수워프
	소요시간 = 시간정지점프 ? 30 # 10
	PRINTFORML [{특수워프, 3}] - %NAME_FROM_PLACE(특수워프), 18, LEFT%{소요시간, 3} %LOCALS%
ENDIF
PRINTL [100] - 돌아간다　　　　　　　　　　　[ 99] - 다른 지역으로 이동
$INPUT_LOOP
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

IF INRANGE(RESULT, 1, 9)
	P_ID = CFLAG:MASTER:데이트중 * 100 + RESULT * 10
	;소요시간は消費TSPも込みになっているので注意
	소요시간 = 산책소요시간(CFLAG:MASTER:현재위치, P_ID, 시간보정)
ENDIF
;戻る
IF RESULT == 100 || RESULT == 0
	RETURN -1
;他の地域に移動
ELSEIF RESULT == 99
	DRAWLINE
	CALL OTHERREGIONS
	IF RESULT == 100
		RESTART
	ELSE
		RETURN 1
	ENDIF
;특수워프,10分/30TSP固定
ELSEIF 특수워프 && RESULT == 특수워프
	IF FLAG:시간정지 && BASE:MASTER:TSP < 30
		PRINTW TSP가 부족합니다
		RESTART
	ELSEIF 시간정지점프
		BASE:MASTER:TSP -= 30
	ELSE
		TIME += 10
	ENDIF
	PRINTFORML %마스터가% 공간의 왜곡에 발을 들여놓자
	PRINTFORMW 어느새 %NAME_FROM_PLACE(특수워프)%에 서있었다…
	P_ID = 특수워프
	CFLAG:MASTER:현재위치 = P_ID
	CFLAG:MASTER:데이트중 = GET_MAPID(P_ID)
	IF FLAG:데이트상대
		CFLAG:(FLAG:데이트상대):현재위치 = P_ID
		CFLAG:(FLAG:데이트상대):데이트중 = GET_MAPID(P_ID)
	ENDIF
	RETURN 1
ELSEIF RESULT == 1234
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
;散策移動
ELSEIF INRANGE(RESULT, 1, 9) && NAME_FROM_PLACE(P_ID) != ""
	;移動できるかチェック
	CALL SANSAKU_UNIQUEPLACE(P_ID)
	IF RESULT
		;P_IDへ移動
		P_ID = RESULT
		IF 전턴위치 == 310||전턴위치 == 320||전턴위치 == 330||전턴위치 == 340||전턴위치 == 910||전턴위치 == 920
			IF FLAG:시간정지 == 0
				IF (CFLAG:60:동행 == 0&&(P_ID == 930||P_ID == 940||P_ID == 950||P_ID == 960||P_ID == 970))&&!FLAG:지저통행권
					CALL 통행료뜯기(920)
					IF RESULT == 0
						;처리없음
					ELSE
						P_ID = 920
						IF 전턴위치 == 920
							이동취소플래그 = 1
						ENDIF
						LOCAL:1 = 1
					ENDIF
				ELSEIF (CFLAG:58:동행 == 0&&(P_ID == 350||P_ID == 360||P_ID == 370||P_ID == 380))&&!FLAG:홍마관출입증
					CALL 통행료뜯기(340)
					IF RESULT == 0
						;처리없음
					ELSE
						P_ID = 340
						IF 전턴위치 == 340
							이동취소플래그 = 1
						ENDIF
						LOCAL:1 = 1
					ENDIF
				ELSE
				ENDIF
				소요시간 = 산책소요시간(CFLAG:MASTER:현재위치, P_ID, 시간보정)
			ENDIF
		ENDIF
		CFLAG:MASTER:현재위치 = P_ID
		IF FLAG:데이트상대
			CFLAG:(FLAG:데이트상대):현재위치 = P_ID
			TIME += 소요시간
		ELSEIF FLAG:시간정지 && BASE:MASTER:TSP < 소요시간
			PRINTW TSP가 부족합니다
			RESTART
		ELSEIF 시간정지점프
			BASE:MASTER:TSP -= 소요시간
		ELSE
			TIME += 소요시간
		ENDIF
		RETURN 1
	ENDIF
ENDIF
;노파심에 한 번 더 꺼둔다
이동취소플래그 = 0
LOCAL:1 = 0



;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;산책한다
@COM_ABLE604
SIF TFLAG:망상중
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(604)
	RETURN RESULT
;데이트중 이외
SIF AT_HOME(MASTER)
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
;잠복モード中は不可
SIF GETBIT(FLAG:잠복,0)
	RETURN 0
;데이트 도중
SIF TFLAG:데이트도중
	RETURN 0
;망상중
SIF TFLAG:망상중 == 1
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;現在地→移動先の散策にかかる소요시간
;시간정지中は消費TSP
;二次元座標間の距離を算出してる,例えば12→64だったら5+2=7
;-----------------------------------------------------------
@산책소요시간(현위치ID, 이동처ID, 시간보정)
#FUNCTION
#DIM 현위치ID
#DIM 이동처ID
#DIM 시간보정
#DIM 현위치좌표
#DIM 이동처좌표
현위치좌표 = ODEKAKEMAP_SETTING(현위치ID, "좌표")
이동처좌표 = ODEKAKEMAP_SETTING(이동처ID, "좌표")
LOCAL = (ABS(현위치좌표 / 10 - 이동처좌표 / 10) + ABS(현위치좌표 % 10 - 이동처좌표 % 10)) * 2
LOCAL = LOCAL * (100 + MAX(시간보정, -50)) / 100
SIF FLAG:시간정지 || (GETBIT(FLAG:순간이동, 0) && !FLAG:데이트상대)
	LOCAL *= 3
RETURNF LOCAL

;-----------------------------------------------------------
;散策時に移動できるかを返す関数
;散策版MAP_CAN_MOVE, メッセージもここで表示する
;ARG     移動先の場所ID
;RETRUN  0=移動できない, 0以外=移動先
;-----------------------------------------------------------
@SANSAKU_UNIQUEPLACE(ARG)
SELECTCASE ARG
	CASE 요정의큰나무
		IF ABL:MASTER:교양 < 1
			PRINTFORMW 미숙한 %마스터로%는 찾아낼 수가 없다…
			RETURN 0
		ENDIF
	CASE 피안, 백옥루정원, 백옥루, 천계
		IF ABL:MASTER:교양 < 1 && ABL:(FLAG:데이트상대):교양 < 1
			PRINTFORMW 좀 더 교양을 갖추지 않으면 위험하다
			RETURN 0
		ENDIF
	CASE 달의도시, 와타츠키정, 옥토거리, 복숭아숲～모래사장, 고요의바다
		IF ABL:MASTER:교양 < 2 && ABL:(FLAG:데이트상대):교양 < 2
			PRINTFORMW 좀 더 교양을 갖추지 않으면 위험하다
			RETURN 0
		ENDIF
	;세이자 하우스의 전
	CASE 세이자은신처앞
		;본인은 들어갈 수 있는;연모가 있으면 들어갈 수 있어 줄 수 있는;전생에서 정사가 연모 or 연인이라면 장소는 안다
		IF FLAG:역극 == [[세이자]] || TALENT:[[세이자]]:연모 || GETBIT (CFLAG:[[세이자]]:인계, 0) || GETBIT (CFLAG:[[세이자]]:인계, 1)
		ELSE
			PRINTFORMW 어디에 있을지 모른다…
			RETURN 0
		ENDIF
	;카센하우스
	CASE 선인의저택
		;카센이 연모;본인은 들어갈 수 있다
		IF TALENT:[[카센]]:연모 || FLAG:역극 == [[카센]]
		;데이트상대가 텐시나 자신이 텐시
		ELSEIF FLAG:데이트상대 == [[텐시]] || FLAG:역극 == [[텐시]]
		;교양이 부족하면 가까스로 도착할 수 없다
		ELSEIF ABL:MASTER:교양 < 3
			PRINTFORMW 조금 전부터 저택과의 거리가 전혀 줄어들지 않는다……
			PRINTFORMW 같은 곳을 빙빙 돌고 있는 것 같다
			RETURN 0
		;그 이외는 전투력 3이상 필요
		ELSEIF ABL:MASTER:전투능력 < 3 && !FLAG:시간정지
			PRINTFORMW 뜰에 웅크리고 있던 무서운 호랑이가, %CALLNAME:MASTER%의 모습을 알아차리고 으르렁거렸다
			PRINTFORMW 다가가지 않는 편이 좋을 것 같다
			RETURN 0
		ENDIF
	CASE 영원정안채
		IF !FLAG:시간정지
			PRINTFORMW 무단으로 들어갔다
		ELSEIF !영원정침입허가()
			PRINTL 안쪽에 들어가려고 했지만, 입구에서 토끼들에게 제지당했다
			RETURN 영원정
		ENDIF
	CASE 혼숙여관, 러브호텔
		IF !FLAG:데이트상대
			PRINTW 혼자서 가도 어쩔 수 없다
			RETURN 0
		ELSEIF MONEY < 3000
			PRINTW 돈이 부족하다
			RETURN 0
		ENDIF
		SIF !GETBIT(CFLAG:(FLAG:데이트상대):기정사실 , 1)
			CALL 껴안고(FLAG:데이트상대)
		IF TCVAR:(FLAG:데이트상대):껴안고첫회 >= 6 && !TCVAR:(FLAG:데이트상대):만취
				PRINTFORMW %CALLNAME:(FLAG:데이트상대)%에 들어가는 것을 거부되어 버렸다
				SIF TCVAR:(FLAG:데이트상대):껴안고첫회 == 6
					PRINTFORMW 그러나 %CALLNAME:(FLAG:데이트상대)%는 꼭 싫지만은 않은 것 같다
			RETURN 0
		ELSE
			PRINTL \3000입니다. 괜찮겠습니까?
			CALL ASK_YN
			CFLAG:(FLAG:데이트상대):우후후 = 0
			CFLAG:MASTER:우후후 = 0
			IF RESULT == 0
				MONEY -= 3000
				IF TCVAR:(FLAG:데이트상대):만취
					PRINTFORMW %조사처리(CALLNAME:(FLAG:데이트상대),"가")% 몽롱해지고 있는 것을 기회로, %마스터는% 그녀를 %NAME_FROM_PLACE(ARG)%로 데리고 들어갔다
				ELSEIF CFLAG:(FLAG:데이트상대):패배자
					PRINTFORMW %마스터는% 싫어하는 %조사처리(CALLNAME:(FLAG:데이트상대),"를")% %NAME_FROM_PLACE(ARG)%로 끌어들였다
				ELSE
					PRINTFORMW %마스터는% %조사처리(CALLNAME:(FLAG:데이트상대),"와")% %NAME_FROM_PLACE(ARG)%에 들어갔다
				ENDIF
			ELSE
				RETURN 0
			ENDIF
		ENDIF
ENDSELECT

RETURN ARG
