;대본가게
@COM620
#DIM CHARISMA
#DIM ItemId
#DIM Borrow
#DIM DYNAMIC 사본구매
#DIM DYNAMIC C_책값
$C_LOOP
C_책값 = 3000
IF 사본구매
	C_책값 *= 10
ENDIF
DRAWLINE
IF !CHARISMA
	PRINTFORML 소지금:%금액표시(MONEY)%
ELSE
	PRINTFORML 소지카리스마:{MONEY:2}
ENDIF
PRINTFORML 「\@ !사본구매 ? 임차료 # 구매비 \@는 모두 ￦{C_책값} 입니다」
DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME:ItemId%
	IF ITEM:ItemId
		;---- EDIT START ---------------------------------
		SETCOLOR C_AQUA
		;---- EDIT END -----------------------------------
		PRINTFORML (%마스터가% \@ITEM:ItemId == 2 ? 가지고 # 빌리고\@ 있다) 반환까지 앞으로 {FLAG:대본반환기한-1}일)
		;---- EDIT START ---------------------------------
		RESETCOLOR
		SIF ITEM:ItemId == 1
			Borrow = ItemId
		;---- EDIT END -----------------------------------
	ELSEIF 0;GETBIT (FLAG:1011,LOCAL)
		PRINTL (대출중)
	ELSE
		PRINTL 　
	ENDIF
NEXT

PRINTFORML [99] \@ 사본구매 ? 대여 모드 # 사본 구매\@
IF !CHARISMA
	PRINTFORML [100] 카리스마 지불
ELSE
	PRINTFORML [100] 현금지불
ENDIF
DRAWLINE
PRINTL [0] 그만둔다

$LOOP
INPUT

IF RESULT == 100
	IF !CHARISMA
		LOCALS = 「카리스마로 지불해???」/「그러니까……그럼 10 카리스마? 로 괜찮아요……」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
		IF MONEY:2 < C_책값 / 300
			PRINTFORMW 카리스마가 부족하다
			GOTO C_LOOP
		ENDIF
	ELSE
		LOCALS = 「아, 돈으로 지불하는거네요」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
	ENDIF
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSEIF RESULT == 99
	사본구매 = !사본구매
	GOTO C_LOOP
ELSEIF RESULT == 0
	RETURN -1
ELSE
	IF (!CHARISMA && MONEY < C_책값) || (CHARISMA && MONEY:2 < C_책값/300)
		PRINTW 돈이 모자랐다…
		RETURN -1
	ENDIF
	IF ITEM:(100 + RESULT) == 2
		PRINTW 이미 산 책이다
		RETURN -1
	ENDIF
	;KR판 추가수정, 책 반납기능 추가
	IF INRANGE(RESULT, 1, BookTitles)
		RESULT:1 = RESULT
		IF GETBIT (FLAG:1011,RESULT:1)
			ItemId = RESULT:1 + 100
			IF ITEM:ItemId
				IF FLAG:대본반환기한 == 8
					LOCALS = 「그 책은 오늘 %마스터가% 빌린 책인데요……？」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
					RETURN -1
				ENDIF

				LOCALS = 「그 책은 지금 %마스터가% 빌리고 있는데요……」/「아니면 임대료를 내고 대출 기간을 1주일로 연장하실건가요?」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
				CALL ASK_YN
				IF !RESULT
					IF !CHARISMA
						MONEY -= C_책값
					ELSE
						MONEY:2 -= C_책값/300
					ENDIF
					FLAG:대본반환기한 = 8
					PRINTFORML %ITEMNAME:(100 + RESULT:1)%의 대출기한을 1주일로 연장했다
					LOCALS = 「감사합니다」/「그 책이 꽤나 마음에 드셨나 보네요」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
				ELSE
					RETURN -1
				ENDIF
			ELSE
				LOCALS = 「대출중입니다」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
				;KR판 추가수정, 대출중인 책을 선택시는 다시 처음으로
				;---- EDIT START ---------------------------------
				WAIT
				RESETCOLOR
				GOTO C_LOOP
				;---- EDIT END -----------------------------------
			ENDIF
			RETURN -1
		ELSE
			IF 사본구매
				IF !CHARISMA
					MONEY -= C_책값
				ELSE
					MONEY:2 -= C_책값/300
				ENDIF
				ITEM:(100 + RESULT) = 2
				PRINTFORML %조사처리(ITEMNAME:(100 + RESULT),"를")% 샀다
				LOCALS = 「구매해 주셔서 감사합니다」
				IF CFLAG:[[코스즈]]:현재위치 == CFLAG:PLAYER:현재위치
					CALL SPTALK, [[코스즈]], "笑顔", 0, @"%LOCALS%", 1
					SIF TARGET != [[코스즈]]
						RESETCOLOR
				ELSE
					CALL SPTALK, 0, "笑顔", 12, @"%LOCALS%", 1
				ENDIF
			ELSE
				FOR LOCAL,1, BookTitles + 1
					ItemId = LOCAL + 100
					IF ITEM:ItemId
						LOCALS = 「두 권 이상 대충은 안돼요」/「다시 임대료를 받겠지만 지금 빌린 책을 반납하고 그 책을 빌릴건가요?」
						CALL BOOK_RENTAL_SALESTALK(LOCALS)
						CALL ASK_YN
						IF !RESULT
							ITEM:ItemId = 0
							CLEARBIT FLAG:1011,LOCAL
						ELSE
							RETURN -1
						ENDIF
					ENDIF
				NEXT
				SETBIT FLAG:1011,RESULT
				IF !CHARISMA
					MONEY -= C_책값
				ELSE
					MONEY:2 -= C_책값/300
				ENDIF
				ITEM:(100 + RESULT:1) = 1
				FLAG:대본반환기한 = 8
				TFLAG:193 = 1
				PRINTFORML %조사처리(ITEMNAME:(100 + RESULT:1),"를")% 빌렸다
				LOCALS = 「감사합니다」/「대출 기간은 일주일간입니다」 
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
			ENDIF
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;대본소
@COM_ABLE620
;일괄관리
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;貸本屋ではない場所
SIF !BOOK_RENTAL(CFLAG:MASTER:현재위치)
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
SIF FLAG:영나암화재
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

;貸本屋を判定する関数
@BOOK_RENTAL(ARG)
#FUNCTION
;鈴奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
;KR판 추가수정, 매일 1/3의 확률로 대출중 전환 주석처리
; FOR LOCAL,1,BookTitles + 1
; 	SIF LOCAL == 6	;[그림연극]을 패스
; 		CONTINUE
; 	SIF !ITEM:(100 + LOCAL) && !RAND:3
; 		INVERTBIT FLAG:1011,LOCAL
; NEXT
SIF FLAG:대본반환기한
	FLAG:대본반환기한 -= 1
IF FLAG:대본반환기한 == 1
	FOR LOCAL,1,BookTitles + 1
		SIF ITEM:(100 + LOCAL) == 1
			ITEM:(100 + LOCAL) = 0
		CLEARBIT FLAG:1011,LOCAL
	NEXT
	PRINTL 반환 기한이 왔으므로, 빌리고 있던 책을 돌려주었습니다.
ENDIF

@BOOK_RENTAL_SALESTALK(ARGS)
IF CFLAG:[[코스즈]]:현재위치 == CFLAG:PLAYER:현재위치
	CALL SPTALK, [[코스즈]], "笑顔", 0, @"%ARGS%", 1
	SIF TARGET != [[코스즈]]
		RESETCOLOR
ELSE
	CALL SPTALK, 0, "笑顔", 12, @"%ARGS%", 1
ENDIF
