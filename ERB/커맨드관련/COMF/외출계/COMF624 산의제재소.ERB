;산의제재소
@COM624
#DIM 판매가
#DIM 루프수
#DIM 결과보관

루프수 = 0

;開店時間・閉店時間
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 영업시간 외 같다
	RETURN -1
ENDIF

;初回イベント
IF FLAG:마지막제재소방문일 == 0
	;初回イベント前は、통나무持ちでなければ店にも入れない
	IF ITEM:통나무 == 0
		PRINTFORMW 딱히 지금은 볼일이 없다... 팔리는 목재을 베어와야한다...
		RETURN -1
	ENDIF
	;依頼の報酬など、伐採以外で통나무を入手してしまった場合の退避用
	SIF !FLAG:통나무종류
		FLAG:통나무종류 = MAX(RAND:12, 1)
	
	PRINTFORMW 「처음뵙겠습니다. 동지」
	SIF FLAG:역극 == 0
		PRINTFORMW 「소문은 들어봤어~. 왠지 활동적이고 묘한 외래인이 있다고 하더라니」
	SIF ABL:[[니토리]]:친밀 >= 3
		PRINTFORMW 「신세를 지고 있는 동지들이 말하더라고. 좋은 카... 크흠흠, 착하고 신뢰할 수 있는 녀석이라고」 ; 원문은 [캇파の仲間も世話になってるみたいだね。良いカ……げふんげふん、善良で信頼のおけるヤツだって] 인데 해석이 안되네요. 의역했음.
	PRINTFORMW 「그래서, 무슨일이야？　헤에、통나무부터 제재해주길 원한다、라. 좋아, 그러면 공임비는 이렇게――」
	PRINTFORMW 스윽, 하고 캇파가 설명용 판자를 내밀었다……
	PRINTFORMW 이거, 마을 시세에 비해서 많이 싸지 않나?
	PRINTFORMW 「그건 그래. 공작기계에 의한 전자동 제재가 인력에 뒤질 리가 없잖아.」
	PRINTFORMW 「……그렇다고는 해도, 절의 건립이나 신사의 수복 등이 끝나서 주문이 적고, 모처럼 만들어둔 기계도 놀릴 수는 없으니까.」
	PRINTFORMW 「맞다 앞으로도 많이 도와준다면 오늘은 첫 서비스라는 의미에서 공짜로 해줄 수도 있는데?」
	PRINTFORMW …그거, 자기 기계를 자랑하고 싶을 뿐이잖아, 라는 말은 삼키고, 호의에 기대어 받아들이기로 했다.
	PRINTFORMW 「그럼 그렇지. 그러면 통나무를 여기다 세팅해줘」
	PRINTFORMW 「자, 스위치 온! 후후후, 으르렁거려라 우리 머신! 캇파의 기술력의 멋을 보라!」
	PRINTFORMW 묘하게도 흥이 오른 캇파는 제쳐두고서라도 공작기계는 치밀하고 고속으로 제재를 가공해 나갔다.
	PRINTFORMW …………
	PRINTFORMW ……
	CALL LogRefine("제재소")
	PRINTL 
	PRINTFORMW 「네, 완성! 어때!」
	PRINTFORMW 흥, 하고 멋진 표정을 짓는 캇파에게 솔직하게 감사를 표시해 두었다.
	PRINTFORMW 「헤헤, 보는 눈이 있어서 다행이야. 그럼 앞으로도 잘 부탁해~」
	PRINTL 
	루프수 = 1
ENDIF

$LOOP_SELECT

IF 루프수
	PRINTL 
	PRINTFORMW 「자, 다음 통나무를 갖다 줘」
ELSE
	;平常来店
	PRINTL 
	IF FLAG:마지막제재소방문일 == DAY:0
		PRINTFORMW 「어? 왜 그래?」
	ELSE
		PRINTFORM 「이야, 
		SELECTCASE TIME:0
			CASE 540 TO 600
				PRINTFORM 안녕, 좋은 아침. 
			CASE 601 TO 1020
				PRINTFORM 안녕, 좋은 날. 
			CASEELSE
				PRINTFORM 안녕, 좋은 밤.
		ENDSELECT
		PRINTFORM 동지. 
		IF DAY:0 - FLAG:마지막제재소방문일 > 28
			PRINTFORMW 한동안 얼굴을 못봐서 걱정했어」
		ELSEIF DAY:0 - FLAG:마지막제재소방문일 > 14
			PRINTFORMW 오랜만이구나.」
		ELSE
			PRINTFORMW 오늘은 무슨 일이야?」
		ENDIF
	ENDIF
ENDIF

루프수 ++
FLAG:마지막제재소방문일 = DAY:0

PRINTFORML 　　소지금: %TOSTR(MONEY,"#,###")%　소지카리스마: %TOSTR(MONEY:2,"#,###")%
DRAWLINE

CALL ASK_M("製材してほしい", ITEM:통나무, "取引内容を確認する", 1, "店を出る", 2)
CLEARLINE 7

SELECTCASE RESULT
	;製材してほしい
	CASE 0
		;依頼の報酬など、伐採以外で통나무を入手してしまった場合の退避用
		SIF !FLAG:통나무종류
			FLAG:통나무종류 = MAX(RAND:12, 1)

		SETCOLOR 0x00FF00
		PRINTFORMD 「
		PRINTFORM %WOOD_NAME(FLAG:통나무종류)%
		PRINTFORMD 의 제재네. 공임은
		PRINTFORM \\%TOSTR(LUMBERING(FLAG:통나무종류, "제재소"),"#,###")%
		PRINTFORMD 로 해낼 수 있어.아아,
		PRINTFORM %TOSTR(LUMBERING_CMCOST(FLAG:통나무종류),"#,###")%카리스마
		RESETCOLOR
		PRINTFORMW 인데 괜찮아？」
		CALL ASK_M("\\ 지불한다", MONEY >= LUMBERING(FLAG:통나무종류, "제재소"), "카리스마로", MONEY:2 >= LUMBERING_CMCOST(FLAG:통나무종류), "역시 그만둔다", 2)

		결과보관 = RESULT
		IF 결과보관 == 0 || 결과보관 == 1
			IF 결과보관 == 0
				PRINTFORMW 　소지금: \\%TOSTR(MONEY,"#,###")% → \\%TOSTR(MONEY - LUMBERING(FLAG:통나무종류, "제재소"),"#,###")%
				PRINT 　
				CALL PAY_MONEY_YEN(LUMBERING(FLAG:통나무종류, "제재소"), 2)
			ELSE
				PRINTFORMW 　소지카리스마: %TOSTR(MONEY:2,"#,###")% → %TOSTR(MONEY:2 - LUMBERING_CMCOST(FLAG:통나무종류),"#,###")%
				PRINT 　
				CALL PAY_MONEY_CM(LUMBERING_CMCOST(FLAG:통나무종류), 2)
			ENDIF
			PRINTL 
			PRINTDATAW
				DATAFORM 「어서옵셔. 그럼, 스위치 온! 후후후, 으르렁거려라 우리 머신! 캇파의 기술력을 봐라!」
				DATAFORM 「어서옵셔. 여기 통나무 세팅하고 나머지는 풀오토로 맡겨!」
				DATAFORM 「어서옵셔. 그럼, 팩토리 오토메이션의 진수를 보여드리죠!」
			ENDDATA
			CALL LogRefine("제재소")
			PRINTL 
		ELSEIF 결과보관 == 2
			PRINTFORMW 「음, 할인 교섭은 받지 않으니까~」
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
	;取引内容を確認する
	CASE 1
		PRINTFORMW 「통나무 제재 저렴하게 받고 있어~」
		PRINTFORMW 「통나무제재의 매입? 산에서는 그런 물자는 공급과다니까, 인간마을에서 도매하는 것이 좋은 값이 붙지 않을까?」
		PRINTFORMW 「가구 같은 거 못 만드냐고? 음. 우리들 질리기 쉬우니까, 그다지 그런 것에 적합하지 않아.」
		PRINTFORMW 「그런 정교한 일은 인간 장인들에게 부탁하는 것이 좋다고 생각해」
		PRINTFORMW 「우리가 만들면, 마음내키는대로, 필요없는 기능을 엉망진창으로 얹은 캇파가구가 되어버릴 것 같기도 하고 말야~」
		PRINTFORMW 「…그건 그것대로 관심이 있어? 헤헤, 뭐 조만간 봐~」
		PRINTL 
	CASE 2
		PRINTFORMW 「그럼 다음에 봐.」
		PRINTL 
		TIME += 루프수 * 5
		RETURN 1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;山の제재소
@COM_ABLE624
;一括管理
SIF GLOBAL_COMABLE(624)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:현재위치 != 현무의늪
		RETURN 0
ELSE
	;山麓が拠点の場合は、캇파のアジト
	SIF CFLAG:MASTER:현재위치 != 704
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:수면
	RETURN 0
;時間停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;製材コストをカリスマに変換する関数
;ARG …FLAG:통나무종류
;-------------------------------------------------
@LUMBERING_CMCOST(ARG)
#FUNCTION
#DIM 비용베이스

비용베이스 = LUMBERING(ARG, 1, 0)

;元値の幅がそんなに広くないので、一律で
RETURNF 비용베이스 / 300

;-------------------------------------------------
;製材結果の表示など
;-----------------------------------------------
@LogRefine(Craft)
#DIMS Craft
LOCAL = LUMBERING(FLAG:통나무종류, Craft, "QYT")
SETCOLOR 0x00FF00
PRINTFORMW 　%WOOD_NAME(FLAG:통나무종류, 1)%の製材: {LOG_SALES:WOOD_NUM(FLAG:통나무종류)} → {LOG_SALES:WOOD_NUM(FLAG:통나무종류) + LOCAL}
LOG_SALES:WOOD_NUM(FLAG:통나무종류) += LOCAL
ITEM:통나무 = 0
FLAG:통나무종류 = 0
RESETCOLOR