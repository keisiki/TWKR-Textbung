;-------------------------------------------------
;꽃집
;외출 커맨드
;-------------------------------------------------
@COM618
#DIM 買うもの
;購入条件設定
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:꽃집")
	IF RESULT
		CALL ITEMDATA(LOCAL, "SALES")
		ITEMSALES:LOCAL = RESULT
	ENDIF
NEXT
DRAWLINE
CALL LIST_ITEM("로터")
CALL LIST_ITEM("야채")
CALL LIST_ITEM("원예용품")
DRAWLINE
PRINTFORML 구입할 아이템을 선택해주세요 소지금:%금액표시(MONEY)%
DRAWLINE
CALL SHOW_SHOPPING("꽃집")
DRAWLINE
PRINT [999] - 돌아간다
INPUT
IF RESULT == 999
	TIME += 10
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "원예용품일식" ) && ITEMSALES:RESULT >= 0
	CALL ITEMDATA(RESULT, "구입")
	SIF RESULT
		RESTART
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "SHOP:꽃집")
	IF RESULT && ITEMSALES:買うもの >= 1 && ITEM:買うもの < 소지수제한
		CALL ITEMDATA(買うもの, "구입")
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF
TIME += 10

;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;꽃집
@COM_ABLE618
;일괄관리
SIF GLOBAL_COMABLE(618)
	RETURN RESULT

;꽃집ではない場所
SIF !FLOWER_SHOP(CFLAG:MASTER:현재위치)
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
RETURN 1

;꽃집を判定する関数
@FLOWER_SHOP(ARG)
#FUNCTION
;꽃집
SIF ARG == GET_MAP_REPLACEMENT(215)
	RETURNF 1
RETURNF 0
