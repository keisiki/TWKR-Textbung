;-------------------------------------------------
;식재료의 시장
;외출 커맨드
;-------------------------------------------------
@COM614
#DIM 구입품목
#DIM 가격
#DIM 구입상한
SIF !FLAG:일일이벤트 % 10
	PRINTL 오늘은 특매일 같다(표기 가격에서 50%를 뺍니다)
PRINTFORML 소지금:\\{MONEY}
CALL EX_SHOPPING("잡곡","피나 조. 가난한 사람의 음식", 48)
CALL EX_SHOPPING("묵은쌀","서민의 음식", 50)
CALL EX_SHOPPING("백미","부르주아의 음식", 47)
PRINTL [99] - 그만둔다
INPUT
SIF RESULT == 99
	RETURN -1
구입품목 = RESULT
가격 = ITEMPRICE:구입품목
SIF !FLAG:일일이벤트 % 10
	가격 /= 2

PRINTFORM 몇 개 사겠습니까?  1개　￥{가격}
IF CHK_DATENOW(CFLAG:TARGET:데이트중) && TARGET > 0
	구입상한 = ITEMSALES:구입품목 > 0 ?  MIN( ITEMSALES:구입품목, 10 ) # 10
ELSE
	구입상한 = ITEMSALES:구입품목 > 0 ?  MIN( ITEMSALES:구입품목, 5 ) # 5
ENDIF
PRINTFORML ({구입상한}개까지)
PRINTL [0] - 그만둔다
SIF 구입상한
	PRINT [1] - 1개　
SIF 구입상한 >= 5
	PRINT [5] - 5개　
SIF 구입상한 >= 10
	PRINT [10] - 10　
SIF NOSAMES(구입상한, 0, 1, 5, 10 ) 
	PRINTFORM [{구입상한}] - {구입상한}개　
$LOOP_INPUT
INPUT
IF RESULT < 1
	RETURN -1
ELSEIF RESULT > 5 && !(CHK_DATENOW(CFLAG:TARGET:데이트중) && TARGET > 0)
	PRINTL 혼자서는 그렇게 들 수 없다
	GOTO LOOP_INPUT
ELSEIF RESULT > 10
	PRINTL 그렇게 들 수 없다
	GOTO LOOP_INPUT
ELSEIF MONEY < 가격 * RESULT
	PRINTL 돈이 부족하다
	GOTO LOOP_INPUT
ELSEIF RESULT > 구입상한
	PRINTL 품절입니다
	GOTO LOOP_INPUT
ELSE
	MONEY -= 가격 * RESULT
	ITEM:구입품목 += RESULT
	ITEMSALES:구입품목 -= RESULT
	SIF ITEMSALES:구입품목 == 0
		ITEMSALES:구입품목 = -1	; 제고품절
	SETBIT TFLAG:일일일회, 2
	PRINTFORML {RESULT}개의 %ITEMNAME:구입품목%를 샀다
ENDIF

IF CHK_DATENOW(CFLAG:TARGET:데이트중) && TARGET > 0
	LOCAL = RAND:100
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 99
		LOCAL:1 = 99
	IF LOCAL <= 9
		;10%로 대성공
		EXP:MASTER:회화경험 ++
		;붉은실 KR판 고유기능
		CALL 붉은실발동
		EXP:MASTER:데이트경험 ++
		EXP:TARGET:데이트경험 ++
		TFLAG:193 = 1
	ELSEIF RESULT == 0
		;사지 않으면 실패
		TFLAG:193 = -1
	ELSE
		;나머지는 성공
		TFLAG:193 = 0
	ENDIF

	DOWNBASE:MASTER:기력 += 50
	DOWNBASE:MASTER:체력 += 30
	DOWNBASE:기력 += 30
	DOWNBASE:체력 += 20

	;고정으로 획득하는 소스
	SOURCE:환락 = 500
	SOURCE:사랑 = 200

	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:환락 += (ABL:순종 * 60)
		SOURCE:사랑 += (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 3
		SOURCE:환락 += 200 + (ABL:순종 * 60)
		SOURCE:사랑 += 150 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 5
		SOURCE:환락 += 400 + (ABL:순종 * 60)
		SOURCE:사랑 += 300 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 8
		SOURCE:환락 += 550 + (ABL:순종 * 75)
		SOURCE:사랑 += 450 + (ABL:친밀 * 45)
	ELSEIF ABL:순종 <= 11
		SOURCE:환락 += 800 + (ABL:순종 * 75)
		SOURCE:사랑 += 600 + (ABL:친밀 * 50)
	ELSE
		SOURCE:환락 += 1500 + (ABL:순종 * 30)
		SOURCE:사랑 += 1200 + (ABL:친밀 * 20)
	ENDIF

	;호감 도를 본다
	IF CFLAG:2 <= 500
		SOURCE:환락 += CFLAG:2
		SOURCE:사랑 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
		SOURCE:사랑 += 300 + (CFLAG:2 - 300) / 3
	ELSE
		SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
		SOURCE:사랑 += 1000 + (CFLAG:2 - 1000) / 5
	ENDIF
	SIF SOURCE:환락 < 0
		SOURCE:환락 = 0
	SIF SOURCE:사랑 < 0
		SOURCE:사랑 = 0


	SOURCE:수동 = 200 + 100 * ABL:순종
	SOURCE:정복 = 200 + 100 * ABL:새드끼

	IF TFLAG:193 == -1
		TIMES SOURCE:환락 , 0.10
		TIMES SOURCE:정복 , 0.50
		TIMES SOURCE:수동 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:환락 , 1.00
		TIMES SOURCE:정복 , 1.00
		TIMES SOURCE:수동 , 1.00
	ELSE
		TIMES SOURCE:환락 , 2.00
		TIMES SOURCE:정복 , 2.00
		TIMES SOURCE:수동 , 2.00
	ENDIF
	;KR판 붉은실 고유기능
	CALL 붉은실발동
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
ENDIF

TIME += 30
RETURN 1

;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;식재료의 시장
@COM_ABLE614
;일괄관리
SIF GLOBAL_COMABLE(614)
	RETURN RESULT
;물건을 살 수 없는 장소
SIF !KAIDASHI_SHOP(CFLAG:MASTER:현재위치)
	RETURN 0
;이제(벌써) 사고 있어
SIF GETBIT(TFLAG:일일일회, 2)
	RETURN 0
;수면중
SIF CFLAG:MASTER:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

;食材を買える場所を判定する関数
@KAIDASHI_SHOP(ARG)
#FUNCTION
;食料品店
SIF ARG == GET_MAP_REPLACEMENT(216)
	RETURNF 1
RETURNF 0
