;-------------------------------------------------
;식재료의 시장
;외출 커맨드
;-------------------------------------------------
@COM614
#DIM 구입품목
#DIM 할인
DRAWLINE
IF !FLAG:일일이벤트 % 10
	PRINTL 오늘은 특매일 같다!
	할인 = 60
ELSE
	할인 = 30
ENDIF
PRINTFORML 소지금:\\{MONEY}
DRAWLINE
CALL ITEM_SHOPPING(GETNUM(ITEM, "싸구려식재"), "色々な料理に使える間に合わせの食材", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "상급식재"), "食の質をワンランク上げるちょっといい食材", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "고급식재"), "質も高いが値段もお高いブルジョワの食材", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "감미재"), "デザート料理に使える甘い食材", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "짐승고기"), "肉料理全般に使える動物の肉", 할인)
CALL ITEM_SHOPPING(GETNUM(ITEM, "새고기"), "一部の肉料理に使える鳥類の肉", 할인)
DRAWLINE
PRINTL [999] 그만둔다
INPUT

IF RESULT == 999
	RETURN -1
ELSE
	구입품목 = RESULT
	CALL ITEMDATA(구입품목, "SHOP:식료품점")
	IF RESULT && ITEMSALES:구입품목 >= 1 && ITEM:구입품목 < 소지수제한
		CALL ITEMDATA(구입품목, "구입", 할인)
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF

;일일일회 구매한도는 철폐했습니다


IF CHK_DATENOW(CFLAG:TARGET:데이트중) && TARGET > 0
	LOCAL = RAND:100
	LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
	SIF LOCAL:1 > 99
		LOCAL:1 = 99
	IF LOCAL <= 9
		;10%로 대성공
		EXP:MASTER:회화경험 ++
		;붉은실 KR판 고유기능
		CALL 붉은실발동
		EXP:MASTER:데이트경험 ++
		EXP:TARGET:데이트경험 ++
		TFLAG:193 = 1
	ELSEIF RESULT == 0
		;사지 않으면 실패
		TFLAG:193 = -1
	ELSE
		;나머지는 성공
		TFLAG:193 = 0
	ENDIF

	DOWNBASE:MASTER:기력 += 50
	DOWNBASE:MASTER:체력 += 30
	DOWNBASE:기력 += 30
	DOWNBASE:체력 += 20

	;고정으로 획득하는 소스
	SOURCE:환락 = 500
	SOURCE:사랑 = 200

	;ABL:순종을 본다
	IF ABL:순종 <= 1
		SOURCE:환락 += (ABL:순종 * 60)
		SOURCE:사랑 += (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 3
		SOURCE:환락 += 200 + (ABL:순종 * 60)
		SOURCE:사랑 += 150 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 5
		SOURCE:환락 += 400 + (ABL:순종 * 60)
		SOURCE:사랑 += 300 + (ABL:친밀 * 30)
	ELSEIF ABL:순종 <= 8
		SOURCE:환락 += 550 + (ABL:순종 * 75)
		SOURCE:사랑 += 450 + (ABL:친밀 * 45)
	ELSEIF ABL:순종 <= 11
		SOURCE:환락 += 800 + (ABL:순종 * 75)
		SOURCE:사랑 += 600 + (ABL:친밀 * 50)
	ELSE
		SOURCE:환락 += 1500 + (ABL:순종 * 30)
		SOURCE:사랑 += 1200 + (ABL:친밀 * 20)
	ENDIF

	;호감 도를 본다
	IF CFLAG:2 <= 500
		SOURCE:환락 += CFLAG:2
		SOURCE:사랑 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
		SOURCE:사랑 += 300 + (CFLAG:2 - 300) / 3
	ELSE
		SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
		SOURCE:사랑 += 1000 + (CFLAG:2 - 1000) / 5
	ENDIF
	SIF SOURCE:환락 < 0
		SOURCE:환락 = 0
	SIF SOURCE:사랑 < 0
		SOURCE:사랑 = 0


	SOURCE:수동 = 200 + 100 * ABL:순종
	SOURCE:정복 = 200 + 100 * ABL:새드끼

	IF TFLAG:193 == -1
		TIMES SOURCE:환락 , 0.10
		TIMES SOURCE:정복 , 0.50
		TIMES SOURCE:수동 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:환락 , 1.00
		TIMES SOURCE:정복 , 1.00
		TIMES SOURCE:수동 , 1.00
	ELSE
		TIMES SOURCE:환락 , 2.00
		TIMES SOURCE:정복 , 2.00
		TIMES SOURCE:수동 , 2.00
	ENDIF
	;KR판 붉은실 고유기능
	CALL 붉은실발동
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
ENDIF

TIME += 30
RETURN 1

;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;식재료의 시장
@COM_ABLE614
;일괄관리
SIF GLOBAL_COMABLE(614)
	RETURN RESULT
;물건을 살 수 없는 장소
SIF !KAIDASHI_SHOP(CFLAG:MASTER:현재위치)
	RETURN 0
;이제(벌써) 사고 있어
SIF GETBIT(TFLAG:일일일회, 2)
	RETURN 0
;수면중
SIF CFLAG:MASTER:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

;食材を買える場所を判定する関数
@KAIDASHI_SHOP(ARG)
#FUNCTION
;식료품점
SIF ARG == GET_MAP_REPLACEMENT(216)
	RETURNF 1
RETURNF 0
