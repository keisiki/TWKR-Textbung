;-------------------------------------------------
;술집
;외출 커맨드
;-------------------------------------------------
@COM619
#DIM 店員
#DIMS 表情
#DIMS 台詞

DRAWLINE
店員 = GET_MAPID(CFLAG:PLAYER:현재위치) == 2 ? 11 # 18
表情 = 笑顔
IF FLAG:마지막에술집에방문한날 == 0
	台詞 = 「첫 손님이구나? 서비스할게」
ELSEIF DAY - FLAG:마지막에술집에방문한날 > 7
	台詞 = 「오랜만이네요―, 천천히 둘러봐요」
ELSEIF FLAG:마지막에술집에방문한날 == DAY
	台詞 = 「어서 오세요, 응, 어머머? 웬 일이야?」
ELSEIF FLAG:술집에서누적구입액 > 1000000 && FLAG:개시일 != DAY
	台詞 = 「어서 오세요, %마스터를% 위해서 널럴하게 들여놨지」
ELSEIF FLAG:개시일 == DAY
	台詞 = 「때가 안 좋았네. 아까 온 손님이 거의 다 사갔어」
	表情 = 通常
;ここは감주を除く
ELSEIF ITEMSALES:탁주 + ITEMSALES:청주 + ITEMSALES:오니고로시 > 30
	台詞 = 「어서 오세요, 좋은 술이 많이 있어―」
ELSE
	台詞 = 「어서 오십시오―」
ENDIF
SIF FLAG:술집매입강화
	台詞 = %台詞%/「현재 매입 강화 캠페인 중이야. 물건 조달해주면 톡톡히 쳐줄게」
CALL SPTALK, 0, 表情, 店員, 台詞, 1 
$INPUT_LOOP2
DRAWLINE
PRINTL [ 1] - 술을 산다
PRINTL [ 2] - 술을 판다
PRINTL [ 0] - 돌아간다
$INPUT_LOOP
INPUT

IF RESULT == 1
	CALL 술구입
	GOTO INPUT_LOOP2
ELSEIF RESULT == 2
	CALL 술도매
	GOTO INPUT_LOOP2
ELSEIF RESULT != 0
	GOTO INPUT_LOOP
ENDIF

FLAG:마지막에술집에방문한날 = DAY
IF FLAG:술집누적전매이익 > 3000000 && FLAG:술집매각율플래그 == 2
	FLAG:술집매각율플래그 = 3
	PRINTL 
	PRINTFORML 가게에서 나가려 했는데 불러 세웠다
	台詞 = 「두둑하게 술을 조달해줘서 정말 살겠어∼, 앞으로는 매입 금액 더 쳐줄게」
ELSEIF FLAG:술집누적전매이익 > 1000000 && FLAG:술집매각율플래그 == 1
	FLAG:술집매각율플래그 = 2
	PRINTL 
	PRINTFORML 가게에서 나가려 했는데 불러 세웠다
	台詞 = 「두둑하게 술을 조달해줘서 살겠어―, 매입 금액 더 쳐줄 테니 더 조달해주지 않을래?」
ELSEIF FLAG:술집누적전매이익 > 100000 && FLAG:술집매각율플래그 == 0
	FLAG:술집매각율플래그 = 1
	PRINTL 
	PRINTFORML 가게에서 나가려 했는데 불러 세웠다
	台詞 = 「늘 술을 조달해줘서 고마워. 사례로 다음 번부터 매입 금액 더 쳐줄게―」
ELSE
	台詞 = 「또 오세요―」
ENDIF
CALL SPTALK, 0, "笑顔", 店員, 台詞, 1 
TIME += 10
RETURN -1

@술구입
#DIM 선택한상품
#DIM 할인

DRAWLINE
;통신판매から無くなったので定価になります
;;할인을 20%로 설정(정가의 80%로 판매)
;할인 = 20

PRINTL 
IF FLAG:마지막에술집에방문한날 == 0
	PRINTFORML 「처음 왔으니 서비스야―」
	;할인을 40%로 설정(정가의 60%로 판매)
	할인 = 40
ELSE
	PRINTFORML 「뭐든지 싸게 파는거야―」
	;値引率を20％に設定(定価の80％で販売)
	할인 = 20
ENDIF
$INPUT_LOOP2
DRAWLINE
PRINTFORML 소지금:%금액표시(MONEY)%
DRAWLINE
CALL ITEM_SHOPPING(GETNUM(ITEM, "탁주"), "어렴풋이 달콤한 싸구려 술", 할인) ;ほんのり甘い安酒
CALL ITEM_SHOPPING(GETNUM(ITEM, "청주"), "표준적인 일본주", 할인) ;標準的な日本酒
CALL ITEM_SHOPPING(GETNUM(ITEM, "오니고로시"), "비싸게 구는 독한 술", 할인) ;お高くとまったきついお酒
CALL ITEM_SHOPPING(GETNUM(ITEM, "감주"), "드링크 느낌의 가벼운 술", 할인) ;ドリンク感覚の易しいお酒
DRAWLINE
PRINTL [999] 돌아간다
INPUT

IF RESULT == 999
	RETURN -1
ELSE
	선택한상품 = RESULT
	CALL ITEMDATA(선택한상품, "SHOP:술집")
	IF RESULT
		IF ITEMSALES:선택한상품 == 0
			PRINTFORMW 「미안해요. 다 팔렸어요」
		ELSEIF ITEM:선택한상품 >= 소지수제한
			PRINTFORMW 「손님, 그만큼 있으면 충분하잖아요? 오히려 우리한테 물건을 대주라―」
		ELSE
			CALL ITEMDATA(선택한상품, "구입", 할인)
			IF RESULT
				FLAG:술집에서누적구입액 += RESULT
				IF FLAG:술집에서누적구입액 > 100000 && !RAND:9
					PRINTL 
					PRINTL 「항상 사줘서 고마워, 이건 덤이야」
					PRINTFORMW ―덤으로 %ITEMNAME:GETNUM(ITEM, "우콘드링크")%를 받았다-
					ITEM:우콘드링크 ++
				ELSE
					PRINTL 
					PRINTFORMW 「매번 감사―」
				ENDIF

			ELSE
				RESTART
			ENDIF
		ENDIF
	ENDIF
ENDIF

@술도매
#DIM 할인율
#DIM 소지수
#DIM 선택한상품
#DIM 매각개수
#DIM 매각액

SELECTCASE FLAG:술집매각율플래그
	CASE 0
		;할인율을 90%로 설정(정가의 10%로 구입해 준다)
		할인율 = 90
	CASE 1
		;할인율을 85%로 설정(정가의 15%로 구입해 준다)
		할인율 = 85
	CASE 2
		;할인율을 80%로 설정(정가의 20%로 구입해 준다)
		할인율 = 80
	CASE 3
		;할인율을 70%로 설정(정가의 30%로 구입해 준다)
		할인율 = 70
ENDSELECT
SIF FLAG:술집매입강화
	할인율 -= 20
$INPUT_LOOP2
DRAWLINE
PRINT 「
IF FLAG:술집매입강화
	PRINT 매입 강화 캠페인중이니까 특별히
ELSEIF FLAG:술집매각율플래그
	PRINT 고객이니까 특별히
ENDIF
PRINTFORML 정가의 {100 - 할인율}\%로 살게」
DRAWLINE
PRINT 무엇을 팔까…  
PRINTFORML 소지금:%금액표시(MONEY)%
FOR LOCAL, GETNUM(ITEM, "탁주"), GETNUM(ITEM, "오니고로시") + 1
	LOCALS = [{LOCAL}]%ITEMNAME:(LOCAL)%
	IF ITEMPRICE:LOCAL
		PRINTFORML %LOCALS%	(\\{ITEMPRICE:(LOCAL) * (100 - 할인율) / 100})  	(소지:{ITEM:LOCAL})
	ENDIF
NEXT
PRINT [100] - 돌아간다
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN -1
ELSEIF INRANGE(RESULT, GETNUM(ITEM, "탁주"), GETNUM(ITEM, "오니고로시"))
	;선택한 번호에 따라 소지수를 격납
	소지수 = ITEM:RESULT
	
	;소지 0의 경우
	IF 소지수 == 0
		PRINTL 
		PRINTL 물건이 없기에 팔고 싶어도 팔 수 없다
	;소지 0으로 없는 경우 개수를 묻는다
	ELSE
		;다시 한번 INPUT를 사용하기 때문에 지금의 값을 퇴피
		선택한상품 = RESULT
		매각개수 = 0
		$SELL_LOOP
		PRINTFORML %ITEMNAME:선택한상품%를 몇 개 팔까…
		PRINTFORML 파는 수：{매각개수}
		PRINTFORML [-100]　[ -10]　[  -1]　[  +1]　[ +10]　[+100]
		PRINTBUTTON "[전부] ", 999
		PRINTBUTTON " [리셋]", -999
		PRINTL 
		PRINTBUTTON "[판다]", 0
		INPUT
		IF RESULT
			IF RESULT == 999
				매각개수 = 소지수
			ELSEIF RESULT == -999
				매각개수 = 0
			ELSE
				매각개수 = LIMIT(매각개수 + RESULT, 0, 소지수)
			ENDIF
			CLEARLINE 6
			GOTO SELL_LOOP
		ENDIF
		IF 매각개수 <= 0
			PRINTL 
			PRINTL 역시 파는 건 그만두자
			GOTO INPUT_LOOP2
		ENDIF
		IF 매각개수 > 소지수
			PRINTL 
			PRINTL 그렇게나 많이 가지고 있지 않다
			GOTO INPUT_LOOP2
		ENDIF
		
		;1개당의 매각값을 산출
		매각액 = ITEMPRICE:선택한상품 * (100 - 할인율) / 100
		ITEM:선택한상품 -= 매각개수
		MONEY += 매각액 * 매각개수
		;가게는 정가의 80%로 판매
		FLAG:술집누적전매이익 += ITEMPRICE:선택한상품 * (할인율 - 20) / 100 * 매각개수
		PRINTFORMW %ITEMNAME:선택한상품%를 ￥{매각액*매각개수}로 {매각개수}개 매각했습니다
		;판매개수를 증가시키는 처리
		ITEMSALES:선택한상품 += 매각개수
	ENDIF
	GOTO INPUT_LOOP2
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;在庫の変動
;SHOP_ITEM.erbにあるEVENTTURNENDから一日一回呼ばれる
;-------------------------------------------------
@술집의재고
#DIM 재고증가수
;宴会前日は酒が品薄に
IF FLAG:개시일 == DAY + 1
	ITEMSALES:탁주 += RAND:(2 + FLAG:술집매각율플래그 * 1)
	ITEMSALES:청주     += RAND:(2 + FLAG:술집매각율플래그 * 1)
	ITEMSALES:오니고로시   += RAND:(1 + FLAG:술집매각율플래그 * 1)
	ITEMSALES:감주     += RAND:(3 + FLAG:술집매각율플래그 * 2)
ELSE
	ITEMSALES:탁주 += RAND:(3 + FLAG:술집매각율플래그 * 3)
	ITEMSALES:청주     += RAND:(3 + FLAG:술집매각율플래그 * 2)
	ITEMSALES:오니고로시   += RAND:(2 + FLAG:술집매각율플래그 * 1)
	ITEMSALES:감주     += RAND:(4 + FLAG:술집매각율플래그 * 3)
ENDIF
;累積購入額に応じても在庫が増加
IF FLAG:술집에서누적구입액
	재고증가수 = MAX(FLAG:술집에서누적구입액 / 100000 - 3, 0) + 1
	ITEMSALES:탁주 += RAND:재고증가수
	ITEMSALES:청주     += RAND:재고증가수
	ITEMSALES:오니고로시   += RAND:재고증가수
	ITEMSALES:감주     += RAND:재고증가수
ENDIF
;最大数制限
ITEMSALES:탁주 = MIN(ITEMSALES:탁주, 32)
ITEMSALES:청주     = MIN(ITEMSALES:청주, 24)
ITEMSALES:오니고로시   = MIN(ITEMSALES:오니고로시, 16)
ITEMSALES:감주     = MIN(ITEMSALES:감주, 32)
;-------------------------------------------------
;실행 가능 판정
;-------------------------------------------------
;술집
@COM_ABLE619
;일괄관리
SIF GLOBAL_COMABLE(619)
	RETURN RESULT
;술집ではない場所
SIF !SAKE_SHOP(CFLAG:MASTER:현재위치)
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;시간정지중
SIF FLAG:70
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

;술집を判定する関数
@SAKE_SHOP(ARG)
#FUNCTION
;술집
SIF ARG == GET_MAP_REPLACEMENT(211)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(1012)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(933)
	RETURNF 1
RETURNF 0
