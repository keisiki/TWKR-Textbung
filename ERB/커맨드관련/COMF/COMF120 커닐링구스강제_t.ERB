;-------------------------------------------------
;커닐링구스강제
;조수에 의한 커맨드, 및 레즈비언 플레이, 레벨 1
;-------------------------------------------------
@COM120
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = TARGET
;봉사 쾌락
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 사정장소 플래그(-1=콘돔 1=질내 2=애널 3=수음 4=펠라티오 5=파이즈리 6=가랑이 7=풋잡 8=체표 9=애널 봉사 10=도구 11=촉수 12=A고문 13=V 꾸짖고)
TCVAR:PLAYER:2 = 4
;강도
TCVAR:PLAYER:4 = 1000

;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
DOWNBASE:체력 += 10
DOWNBASE:기력 += 100
SOURCE:PLAYER:쾌C = 80
SOURCE:PLAYER:성행동 += 2
SOURCE:쾌M += 50

SOURCE:성행동 = 300
SOURCE:달성 = 150
SOURCE:굴종 = 500
SOURCE:일탈 = 200
SOURCE:반감 = 200

;위 쪽으로 계산한 더러운 데이터
SOURCE:불결 = Y*80 + 50


;-------------------------------------------------
;특수소스처리
;-------------------------------------------------
;억압, 저항 소유에는, 억울소스 추가
SIF TALENT:자기애 < 0 || TALENT:저항
	SOURCE:울굴 += 400
;성기술보정
IF ABL:혀 >= 4
	SOURCE:PLAYER:쾌C += 100
	SOURCE:PLAYER:액체 += 400
ELSE
	SOURCE:PLAYER:쾌C += ABL:혀 * 20
	SOURCE:PLAYER:액체 += ABL:혀 * 80
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 입⇔조교자의 V의 더러움이 이동
STAIN:구 |= STAIN:PLAYER:3
STAIN:PLAYER:3 |= STAIN:구

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF HETEROSEX(TARGET, PLAYER)
	EXP:(50 + HETEROSEX(TARGET, PLAYER)) += 6
	EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 6
ENDIF

;노예가 고양이혀 소유의 경우는, 조교자에게 가학쾌락경험을 추가
SIF TALENT:TARGET:고양이혀
	EXP:가학쾌락경험 ++
EXP:펠라티오경험 ++

RETURN 1

@CAN_COM120(ARG)
SKIPDISP ARG
PRINTL 커닐링구스강제
;-------------------------------------------------
;실행 판정 처리
;-------------------------------------------------
A = 0
S = 0

;모든 명령에 공통의 요소를 고려
;(순종이 높다고 명령에 따르기 쉬운 등)
CALL COM_ORDER

;ABL:욕망
IF ABL:욕망
	SIF S
		PRINT  + 
	A += ABL:욕망 * 1
	PRINT 욕망
	PRINTV 'LV, ABL:욕망, '(, ABL:욕망 * 1, ')
	S = 1
ENDIF
;ABL:봉사정신
IF ABL:봉사정신
	SIF S
		PRINT  + 
	A += ABL:봉사정신 * 4
	PRINT 봉사정신
	PRINTV 'LV, ABL:봉사정신, '(, ABL:봉사정신 * 4, ')
	S = 1
ENDIF

IF MARK:쾌락각인
	SIF S
		PRINT  + 
	A += MARK:쾌락각인 * 2
	PRINT 쾌락각인
	PRINTV 'LV, MARK:쾌락각인, '(, MARK:쾌락각인 * 2, ')
	S = 1
ENDIF
;PALAM:욕정
IF GETPALAMLV(PALAM:욕정, 5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:욕정, 5) * 1
	PRINT 욕정
	PRINTV 'LV, GETPALAMLV(PALAM:욕정, 5), '(, GETPALAMLV(PALAM:욕정, 5) * 1, ')
	S = 1
ENDIF
;수줍음
IF TALENT:수치심 > 0
	PRINT  - 
	A -= 1
	PRINT 수줍음
	PRINTV '(, 1, ')
	S = 1
ENDIF
;악취 둔감
IF TALENT:악취내성 > 0
	SIF S
		PRINT  + 
	A += 3
	PRINT 악취 둔감
	PRINTV '(, 3, ')
	S = 1
ENDIF
;악취 민감
IF TALENT:악취내성 < 0
	PRINT  - 
	A -= 3
	PRINT 악취 민감
	PRINTV '(, 3, ')
	S = 1
ENDIF
;헌신적
IF TALENT:헌신적
	SIF S
		PRINT  + 
	A += 6
	PRINT 헌신적
	PRINTV '(, 6, ')
	S = 1
ENDIF
;쾌감에 부정(음란, 창부로 무효)
IF TALENT:쾌감응답 < 0 && !TALENT:음란
	PRINT  - 
	A -= 1
	PRINT 쾌감에 부정
	PRINTV '(, 1, ')
	S = 1
ENDIF
;음란
IF TALENT:음란 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT 음란
	PRINTV '(, 5, ')
	S = 5
ENDIF
;창부
IF TALENT:음란 > 1 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINT 창부
	PRINTV '(, 5, ')
	S = 5
ENDIF

Y = 0
;애액의 더러움
SIF STAIN:PLAYER:3 & 1
	Y += 1
;정액의 더러움
SIF STAIN:PLAYER:3 & 4
	Y += 3
;애널의 더러움
SIF STAIN:PLAYER:3 & 8
	Y += 7
SIF STAIN:PLAYER:3 & 16
	Y += 15

Y = Y * (2 - TALENT:악취내성) / 2

;서로 더러워져
IF Y
	PRINT  - 
	A -= Y
	PRINT 서로 더러워져
	;악취 둔감
	IF TALENT:악취내성 > 0
		PRINT , 악취 둔감
	ELSEIF TALENT:악취내성 < 0
		PRINT , 악취 민감
	ENDIF
	PRINTV '(, Y, ')
	S = 1
ENDIF

;합계를 표시(23이상으로 실행)
PRINT  = 
PRINTV A

V = 23
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 실행치
PRINTV V

WAIT
SKIPDISP 0
;실행할 수 없다
SIF A < V
	RETURN 0
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE120
;커닐링구스강제 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(120)
	RETURN RESULT
;조교자가 남자라면 안 돼
SIF !(TALENT:PLAYER:2 & 1)
	RETURN 0
;고양이혀의 노예의 경우는 기교 Lv2 이상 필요
SIF TALENT:고양이혀 && ABL:기교 < 2
	RETURN 0
;볼개그 사용중은 불가
SIF TEQUIP:볼개그
	RETURN 0
;수면간중
SIF CFLAG:장난 > 1
	RETURN 0
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
RETURN 1

