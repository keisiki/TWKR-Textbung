;-------------------------------------------------
;목욕탕플레이
;특수한 커맨드
;-------------------------------------------------
@COM187
;착탈의처리 종료시는 소스, 경험의 가산은 실시하지 않는다
IF TEQUIP:목욕탕플레이 == 1
	TEQUIP:목욕탕플레이 = 0
	;샤워도 제외한다
	TEQUIP:샤워 = 0
	RETURN 1
ELSE
	TEQUIP:목욕탕플레이 = 1
ENDIF
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
DOWNBASE:체력 += 0
DOWNBASE:기력 += 30

SOURCE:공포 = 100
SOURCE:노출 = 200
SOURCE:일탈 = 50
SOURCE:반감 = 100


;-------------------------------------------------
;특수소스처리
;-------------------------------------------------
;억압, 저항 소유에는, 억울소스 추가
SIF TALENT:자기애 < 0 || TALENT:저항
	SOURCE:울굴 += 50
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;장비시에 더러움을 리셋 한다(목욕탕에 와 더러워진 채로도 제발이라고 하는 것으로)
STAIN:구 = 0
STAIN:1 = 0
STAIN:2 = 2
STAIN:3 = 1
STAIN:4 = 8
STAIN:5 = 0

RETURN 1
;-------------------------------------------------
;장착 계속의 처리
;-------------------------------------------------
@EQUIP_COM187(ARG)
;목욕탕플레이
SIF ARG
	PRINTL ＜목욕탕플레이중＞

IF FLAG:70 == 1

ELSE
	DOWNBASE:ARG:체력 += 10
	DOWNBASE:ARG:기력 += 10

	SOURCE:ARG:공순 += 100
	SOURCE:ARG:반감 += 50
ENDIF

RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE187
;목욕탕플레이 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(187)
	RETURN RESULT
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;해제는 언제라도 가능
SIF TEQUIP:31
	RETURN 1
;;広くない場所はダメ,※넓은 욕실은 없다
;SIF !OPENPLACE(CFLAG:MASTER:현재위치)
;	RETURN 0
;순종이 1이상 없으면 안 돼
SIF ABL:순종 <= 1
	RETURN 0
;대상이나 조수가 동물귀로, 순종이 3 이하라면 안 돼
SIF TALENT:동물귀 && ABL:순종 <= 2
	RETURN 0
IF ASSI >= 0
	SIF TALENT:ASSI:동물귀 && ABL:순종 <= 2
		RETURN 0
ENDIF
;초대방 이외는 온수 기능이 없으면 안 돼
SIF (!GETBIT (FLAG:풀장, 1) && CFLAG:MASTER:현재위치 != OMANEKIBEYA())||!ITEM:간이풀장
	RETURN 0
;전라가 아니면 안 돼
SIF TEQUIP:0 || TEQUIP:1
	RETURN 0
;야외플레이중은 안 돼
SIF TEQUIP:29
	RETURN 0
;수치플레이중은 안 돼
SIF TEQUIP:30
	RETURN 0
;새댁플레이중은 안 돼
SIF TEQUIP:33
	RETURN 0
RETURN 1

