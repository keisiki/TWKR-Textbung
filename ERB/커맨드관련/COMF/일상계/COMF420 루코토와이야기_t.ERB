;-------------------------------------------------
;루코토와 대화
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM420
PRINTL 「뭔가 용무가 있으신가요?」
PRINTL [0] - 사람을 찾고 있다
PRINTL [1] - 청소를 하고 싶다
PRINTL [2] - git에 대해
PRINTL [100] - 돌아간다
INPUT
IF RESULT == 0
	PRINTL 「누구를 찾으세요?」
	CALL HITOSAGASI()
ELSEIF RESULT == 1
CALL OHEYA_CHECK
ELSEIF RESULT == 2
	PRINTDATA
		DATAFORM 「git을 사용하면 패치를 한 방에 Merge할 수 있어요」
		DATAFORM 「git이 있으면 변경한 곳을 쉽게 파악 가능해져요」
		DATAFORM 「과거의 코드도 확실하게 보존해주고요」
	ENDDATA
	PRINTW 아무래도 굉장한 것인가 보다
	RETURN -1
ELSEIF RESULT == 100
	RETURN -1
ELSE
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF
SIF RESULT == 0
	RETURN -1
; IF FLAG:자실잠금
; 	PRINTW 잠금을 풀지 않으면 
; 	RESTART
; ENDIF
SIF IN_HOME(RESULT) == 1
	CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)

IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:교양 < 1 && !MAIN_MAP && !INRANGE(CFLAG:MASTER:초기위치, 26,35)
	PRINTFORMW 미숙한 %마스터로%선 찾아낼 수 없다…
ELSE
	IF IN_HOME(RESULT) == 1
		;NEXTCOM = GETNUM(TRAINNAME, "이동")
		;TFLAG:195 = RESULT
		CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)
	ENDIF
ENDIF
RETURN -1

@OHEYA_CHECK
VARSET LOCAL
FOR LOCAL, MINROOM(), MAXROOM
	LOCALS = %NAME_FROM_PLACE(LOCAL)%
	SIF LOCALS == ""
		CONTINUE
	SIF CFLAG:MASTER:현재위치 == LOCAL
		SETCOLOR C_GREEN
	; PRINTBUTTON "["+LOCALS+"]", LOCAL
	; RESETCOLOR
	; PRINTFORM %" "*(22-STRLENS(LOCALS))%
	PRINTFORM [{LOCAL, 3}]%LOCALS, 24, LEFT%
	LOCALS = %SEIKETU(LOCAL), 15, RIGHT%
	PRINTFORM %LOCALS%　    
	RESETCOLOR
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [0] 돌아간다
;PRINTBUTTON "[확인 종료]", 0

@SEIKETU(ARG)
#FUNCTIONS
IF OUTROOF(ARG) && DAY:2 == 4
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "적설량:깔끔"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "적설량:소"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "적설량:약간 소"
		CASE IS < 2000
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:중"
		CASE IS < 3500
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:약간 대"
		CASE IS < 5000
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:대"
		CASEELSE
			SETCOLOR 0xFFFFFF
			RETURNF "적설량:눈더미"
	ENDSELECT
ELSE
	SELECTCASE YOGORE:ARG
		CASE IS < 100
			SETCOLOR C_GREEN
			RETURNF "청결도:최고"
		CASE IS < 500
			SETCOLOR C_M_GREEN
			RETURNF "청결도:고"
		CASE IS < 1000
			SETCOLOR C_P_GREEN
			RETURNF "청결도:약간 고"
		CASE IS < 2000
			SETCOLOR C_YELLOW
			RETURNF "청결도:중"
		CASE IS < 3500
			SETCOLOR C_V_PINK
			RETURNF "청결도:약간 저"
		CASE IS < 5000
			SETCOLOR C_RED
			RETURNF "청결도:저"
		CASEELSE
			SETCOLOR C_RED
			RETURNF "청결도:쓰레기산"
	ENDSELECT
ENDIF

@HITOSAGASI(ARG)
CALL HITOSAGASI_2(1)
CALL HITOSAGASI_2(2)
PRINTFORML [0] 돌아간다
;PRINTBUTTON "[확인 종료]", 0


;ARG=몇번째의 표시인가(1or2)
@HITOSAGASI_2(ARG)
SIF FLAG:정렬
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM
	IF FLAG:정렬
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	SIF AT_HOME(LOCAL) == 0
		CONTINUE
	SIF CFLAG:LOCAL:현재위치 < MINROOM()
		CONTINUE
	SIF CFLAG:LOCAL:현재위치 == MAXROOM && CFLAG:LOCAL:수면
		CONTINUE
	IF ARG == 1
		SIF !CFLAG:LOCAL:신사거주
			CONTINUE
		SETCOLOR C_S_BLUE
	ELSEIF ARG == 2
		SIF CFLAG:LOCAL:신사거주
			CONTINUE
	ENDIF
	PRINTFORM %CALLNAME:LOCAL,15,LEFT%
	RESETCOLOR
	LOCALS = %NAME_FROM_PLACE(CFLAG:LOCAL:현재위치)%
	; LOCALS = %NAME_FROM_PLACE(CFLAG:LOCAL:현재위치)%
	; SIF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:311
	; 	LOCALS = %CALLNAME:MASTER% 방
	; PRINTBUTTON "["+LOCALS+"]", CFLAG:LOCAL:현재위치
	; PRINTFORM %" "*(22-STRLENS(LOCALS))%
	PRINTFORM [{CFLAG:LOCAL:현재위치, 3}] %LOCALS, 24, LEFT%
	IF LOCALS == ""
		PRINTFORML 알 수 없는 위치에 있습니다({CFLAG:LOCAL:현재위치})
	ELSEIF CFLAG:LOCAL:수면
		PRINTL 취침중
	ELSEIF CFLAG:LOCAL:행동 == 5
		IF CFLAG:LOCAL:현재위치 == TFLAG:연회장 && TIME_PROGRESS(TIME:1) && (CFLAG:LOCAL:직종 == 49 || CFLAG:LOCAL:직종 == 51)
			PRINTL 연회중
		ELSE
			PRINTL 일하는중
		ENDIF
	ELSE
		PRINTL
	ENDIF
NEXT

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
;루코토와이야기한다
@COM_ABLE420
;자택외는 불가
SIF AT_HOME(MASTER) == 0
	RETURN 0
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;이동 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(420)
	RETURN RESULT
SELECTCASE MAIN_MAP
	CASE 0
		;루코토가 자고 있으면 안 됨
		SIF CFLAG:2:수면
			RETURN 0
		;루코토가 있는 곳
		SIF CFLAG:MASTER:현재위치 != CFLAG:2:현재위치
			RETURN 0
	CASE 3
		SIF CFLAG:MASTER:현재위치 != 323
			RETURN 0
	CASE 9
		SIF CFLAG:MASTER:현재위치 != 923
			RETURN 0
	CASEELSE
		RETURN 0
ENDSELECT

SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1

