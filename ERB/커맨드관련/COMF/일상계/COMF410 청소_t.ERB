;-------------------------------------------------
;청소
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM410
#DIM 청소전의더러움
#DIM 청소후의더러움
#DIM 청소력
#DIM SWEEPID
SWEEPID = CFLAG:MASTER:현재위치
TFLAG:무드상승억제 = 1
IF YOGORE:SWEEPID < OSOUJI_LIMIT(MASTER)
	DRAWLINE
	PRINTFORML 그렇게 지저분하지 않으니 아직 청소하지 않아도 될 것이다
	RETURN -1
ENDIF

SELECTCASE YOGORE:SWEEPID
	CASE IS < 500
		TFLAG:194 = 0
	CASE IS < 2000
		TFLAG:194 = 1
	CASE IS < 5000
		TFLAG:194 = 2
	CASEELSE
		TFLAG:194 = 3
ENDSELECT
;=================================================
LOCAL:4 = 0
SIF BASE:MASTER:TSP > 1500
	LOCAL:4 = 1
SIF BASE:MASTER:TSP > 2000
	LOCAL:4 = 2

IF YOGORE:(SWEEPID) > 5000 && TALENT:MASTER:61 == -2 && BASE:MASTER:체력 > (1500 - LOCAL:4 * 300) && BASE:MASTER:기력 > (2000 - LOCAL:4 * 100) && BASE:MASTER:TSP > 1000 && !CFLAG:MASTER:335 && !FLAG:70
	PRINTL 그나저나 참아주기 힘들게 지저분하다…
	PRINTL [0]평상심을 유지한다
	PRINTL [1]무리
	INPUT
	IF RESULT != 1
		GOTO CANCEL_LOOP
	ELSE
		CALL OSOUJI(LOCAL:4)
		RETURN 1
	ENDIF
;=================================================
ELSE
	$ CANCEL_LOOP
	IF (!TARGET || !SHIRAHU(TARGET))
		CALL 팬티발견
	ENDIF
	IF TARGET && ABL:친밀 * 500 + ABL:순종 * 500 + ABL:청소기능 * 1000 > CFLAG:310 - CFLAG:MASTER:310
		TFLAG:193 = 1
	ELSE
		TFLAG:193 = 0
	ENDIF

	청소력 = (2 + ABL:MASTER:청소기능 + 3 * TALENT:MASTER:청소중독)
	;커스텀대비による補正
	SELECTCASE ITEM:커스텀대비
		CASE 1
			청소력 += 2
		CASE 2
			청소력 += 3
		CASE 3
			청소력 += 4
	ENDSELECT
	SIF ITEM:커스텀대비
		PRINTFORML %CALLNAME:MASTER%용으로 커스텀한 특별 주문 빗자루가 윙윙 소리를 낸다！
	
	청소력 = 청소력 * (5 - TALENT:MASTER:61) / 5
	청소전의더러움 = YOGORE:(SWEEPID)
	IF TFLAG:193 && !CFLAG:수면
		YOGORE:(SWEEPID) = YOGORE:(SWEEPID) / (청소력 + 1)
	ELSE
		YOGORE:(SWEEPID) = YOGORE:(SWEEPID) / 청소력
	ENDIF
	IF (CFLAG:MASTER:현재위치 == 9 || CFLAG:MASTER:현재위치 == 10)
		PRINTL 내친 김에 벌레통도 청소했다
		PRINTFORML 신묘마루의 개인물품을 치우고 뒤집어서 흔들면 그만이니까 간단하다
		YOGORE:(54) = 0
	ENDIF
	청소후의더러움 = YOGORE:(CFLAG:MASTER:현재위치)
	EX:MASTER:오늘의청소회수 = EX:MASTER:오늘의청소회수 + 청소전의더러움 - 청소후의더러움
	IF FLAG:70
		IF ITEM:청소기 ;KR판 오리지날
			BASE:MASTER:TSP -= 20
		ELSE
			BASE:MASTER:TSP -= 50
		ENDIF
		LOCAL:3 = 0
		FOR LOCAL,1,CHARANUM
			SIF CFLAG:LOCAL:동실
				LOCAL:3 ++
		NEXT
		IF LOCAL:3 == 1
			EXP:MASTER:이상청소경험 ++
			IF !GETBIT(CFLAG:MASTER:3,11)
				EXP:MASTER:이상경험 ++
				SETBIT CFLAG:MASTER:3,11
			ENDIF
		ENDIF
	ELSE
		DOWNBASE:MASTER:체력 =150
		DOWNBASE:MASTER:기력 = 300
	ENDIF

	IF TARGET > 0 && !CFLAG:수면 && !FLAG:70 && !CFLAG:우후후
		;고정으로 획득하는 소스
		SOURCE:환락 = 400
		;신뢰
		TFLAG:98 = 3

		;ABL:순종을 본다
		IF ABL:순종 <= 1
			SOURCE:환락 += (ABL:순종 * 60)
		ELSEIF ABL:순종 <= 3
			SOURCE:환락 += 200 + (ABL:순종 * 60)
		ELSEIF ABL:순종 <= 5
			SOURCE:환락 += 500 + (ABL:순종 * 60)
		ELSEIF ABL:순종 <= 8
			SOURCE:환락 += 750 + (ABL:순종 * 75)
		ELSEIF ABL:순종 <= 11
			SOURCE:환락 += 1000 + (ABL:순종 * 75)
		ELSE
			SOURCE:환락 += 2000 + (ABL:순종 * 30)
		ENDIF

		;호감 도를 본다
		IF CFLAG:2 <= 500
			SOURCE:환락 += CFLAG:2
		ELSEIF CFLAG:2 <= 5000
			SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
		ELSE
			SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
		ENDIF
		SIF SOURCE:환락 < 0
			SOURCE:환락 = 0
		SOURCE:정복 = 300 + 100 * ABL:새드끼
	ELSEIF TARGET > 0 && CFLAG:우후후 && !FLAG:70
		DOWNBASE:기력 += 200
		SOURCE:반감 += 500
		SOURCE:울굴 += 100
		LOCAL:3 = 0
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:LOCAL:동실
				LOCAL:3 ++
		NEXT
		IF LOCAL:3 == 1
			EXP:MASTER:이상청소경험 ++
			IF !GETBIT(CFLAG:3, 11)
				EXP:이상경험 ++
				SETBIT CFLAG:3, 11
			ENDIF
		ENDIF
	ENDIF

	IF !FLAG:70
		IF ITEM:청소기 ;KR판 오리지날
			TIME += 10
		ELSE
			TIME += 30
		ENDIF
		SIF CFLAG:동행
			CFLAG:동행 += 30
	ENDIF
	SIF TFLAG:193 && !CFLAG:수면 && !FLAG:70 && !CFLAG:우후후
		EXP:청소경험 ++
	EXP:MASTER:청소경험 ++
ENDIF
RETURN 1

@OSOUJI(ARG)
VARSET LOCAL
SETCOLOR 0xEE1010
PRINTFORMW %조교자는% 격노했다.
;KR판 오리지날. 어쩨서인지는 모르겠지만 바로 아래 문장이 일판에선 PRINTW 임.
PRINTFORMW 반드시, 난장판인 %조사처리(GET_MAPNAME(MAIN_MAP),"를")% 정리해야만 한다고 결심했다
PRINTFORMW %조교자는% 신앙을 모른다.
PRINTFORMW %조교자는% 초상의 강간마이다.
PRINTW 시간을 멈추고 소녀를 희롱하며 살아왔다.
PRINTW 그러나 지저분한 것에 관해선 남보다 곱절은 더 민감했다.
RESETCOLOR
PRINTFORML 
PRINTFORML %조교자는% 시간을 멈추고 %GET_MAPNAME(MAIN_MAP)% 전체를 청소하며 다녔다!
PRINTFORML 
PRINTFORMW %CALLNAME:PLAYER% 청소중…
PRINTFORMW %CALLNAME:PLAYER% 청소중……
PRINTFORMW %CALLNAME:PLAYER% 청소중………
FOR LOCAL,MINROOM(), MAXROOM
IF YOGORE:( LOCAL) > 2000
	YOGORE:( LOCAL) = YOGORE:( LOCAL:1) / (3 + ABL:MASTER:청소기능)
FLAG:562 += 1
ENDIF
NEXT 
PRINTFORMW %조교자는% {FLAG:562}개의 방을 청소했다
CFLAG:MASTER:335 += 4 + RAND:(MAX(1,(13 - 2 * ABL:MASTER:청소기능)))
CFLAG:MASTER:현재위치 = CFLAG:MASTER:초기위치
FOR LOCAL,1,11
	CFLAG:LOCAL:현재위치 = 9
	CFLAG:LOCAL:335 = 1 + RAND:(MAX(ABL:MASTER:화술기능,1))
NEXT
FLAG:562 = 1
TIME += (500 - ARG * 100)
DOWNBASE:MASTER:체력 = (1500 - ARG * 600)
DOWNBASE:MASTER:기력 = (1500 - ARG * 300)
BASE:MASTER:TSP = 0
SIF ARG
	EXP:MASTER:청소경험 += (1 + RAND: (ARG * 3))
SIF !ARG
	EXP:MASTER:청소경험 += (1 + RAND: (2))
EXP:MASTER:이상청소경험 += RAND:5
@팬티발견
#DIM 자택캐릭터
VARSET LOCAL
[SKIPSTART]
SELECTCASE CFLAG:MASTER:현재위치
	;레이무 방
	CASE 15
		LOCAL = 1
	;낡은오두막(EX 멤버 방)
	CASE 16
		LOCAL = FLAG:더부살이캐릭터
	;작은창고(루코토)
	CASE 17
		LOCAL = 2
	;반침(카나)
	CASE 18
		LOCAL = 3
	;큰창고(스이카)
	CASE 19
		LOCAL = 10
	;서니방
	CASE 32
		LOCAL = 7
	;루나방
	CASE 33
		LOCAL = 5
	;스타방
	CASE 34
		LOCAL = 6
	;유메미방
	CASE 43
		LOCAL = 9
	;치유리방
	CASE 44
		LOCAL = 8
	;별채(미마 님 전용 별채)
	CASE 52
		LOCAL = 4
	CASE 54
		LOCAL = 71
	CASE 55
		LOCAL = FLAG:16
	CASEELSE
		LOCAL = 0
		FOR 자택캐릭터, 1, CHARANUM
			IF CFLAG:MASTER:현재위치 == CFLAG:(자택캐릭터):신사거주
				LOCAL = 자택캐릭터
			ENDIF
		NEXT
ENDSELECT
[SKIPEND]

FOR 자택캐릭터,1,CHARANUM
	IF CFLAG:MASTER:현재위치 == CFLAG:(자택캐릭터):신사거주
		LOCAL = 자택캐릭터
	ENDIF
NEXT
LOCAL:1 = 오늘의팬티(LOCAL)
LOCAL:2 =  RAND:5000
SIF FLAG:발원내용 == 202
	LOCAL:2 = RAND:4000
IF YOGORE:(CFLAG:MASTER:현재위치) > LOCAL:2 && LOCAL && LOCAL:1 && !FLAG:70
	TSTR:3 = %CALLNAME:LOCAL%{LOCAL:1}/
	TSTR:2 = %PANTSNAME(LOCAL:1, LOCAL)%
	FOUND_PANTS_TYPE = LOCAL:1
	FOUND_PANTS_OWNER = LOCAL
ENDIF

;캐릭터가 이제 청소하려고 생각하는 방의 흩어진 상태
@OSOUJI_LIMIT(ARG)
#FUNCTION
#DIM O_LIMIT
O_LIMIT = 300
SIF TALENT:ARG:게으름
	O_LIMIT *= 10
O_LIMIT = O_LIMIT * (3 + TALENT:ARG:악취내성) / 3
SIF TALENT:ARG:헌신적
	O_LIMIT /= 2
SIF TALENT:ARG:청소중독
	O_LIMIT /= 10
RETURNF O_LIMIT

@COM_ABLE410
;청소 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(410)
	RETURN RESULT
SIF CHK_DATENOW(CFLAG:MASTER:데이트중)
	RETURN 0
;더러워지지 않았으면 안 됨
SIF YOGORE:(CFLAG:MASTER:현재위치) < 100
	RETURN 0
;동침중에는 안 됨
SIF CFLAG:동침중
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
SIF CFLAG:행동 == 5 && CFLAG:350 != 40
	RETURN 0
;외출중은 안 됨, 초대방은 청소할 수 있다
SIF AT_HOME(MASTER) == 0 && CFLAG:MASTER:현재위치 != OMANEKIBEYA()
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;당신의 술기운이 0이상이라면 가짜
SIF BASE:MASTER:술기운 > 0
	RETURN 0
RETURN 1

;자동청소
@AUTO_SWEEP
#DIM SWEEPID
#DIM 掃除前の汚れ
#DIM 掃除後の汚れ
#DIM 清掃力
#DIM 掃除量
#DIM 掃除ダイス

;一人でいる場合、出発地点を掃除する
[IF_DEBUG]
PRINTFORML 자동청소フラグ:{GETBIT(FLAG:자동청소, 1)}、동행：{CFLAG:MASTER:동행}
[ENDIF]
IF (GETBIT(FLAG:자동청소, 1) && !CFLAG:MASTER:동행)

	SWEEPID = CFLAG:MASTER:현재위치

	;汚れの主観スキップ判定
	IF YOGORE:SWEEPID < OSOUJI_LIMIT(MASTER)
		RETURN -1
	ENDIF
	
	;체력기력0ならスキップ
	IF (BASE:MASTER:체력 == 0 || BASE:MASTER:기력 == 0)
		RETURN -1
	ENDIF
	
	;掃除処理
	清掃力 = (2 + ABL:MASTER:청소기능 + 3 * TALENT:MASTER:청소중독)
	
	;커스텀대비による補正
	SELECTCASE ITEM:커스텀대비
		CASE 1
			清掃力 += 2
		CASE 2
			清掃力 += 3
		CASE 3
			清掃力 += 4
	ENDSELECT
	
	;成功判定
	掃除ダイス = (4 + ABL:MASTER:청소기능) - RAND:11
	
	[IF_DEBUG]
	PRINTFORML [ベースの清掃力:{清掃力}][掃除ダイス:{掃除ダイス}]
	[ENDIF]
	
	;掃除ダイスが1以上なら成功(清掃0で30%、清掃6で90%)
	IF (掃除ダイス > 0)
	
		清掃力 = 清掃力 * (5 - TALENT:MASTER:61) / 5
		
		掃除前の汚れ = YOGORE:(SWEEPID)
		
		YOGORE:(SWEEPID) = YOGORE:(SWEEPID) / 清掃力
		
		掃除後の汚れ = YOGORE:(SWEEPID)
		
		;掃除量を計算して半分を戻して反映
		掃除量 = (掃除前の汚れ - 掃除後の汚れ) / 2
		掃除後の汚れ += 掃除量
		YOGORE:(SWEEPID) += 掃除量
		EX:MASTER:오늘의청소회수 = EX:MASTER:오늘의청소회수 + 掃除量
		
		PRINTFORML %조교자는% %NAME_FROM_PLACE(SWEEPID)%を出る前に掃除した
		PRINTFORML
		
		[IF_DEBUG]
		PRINTFORML [清掃力:{清掃力}][掃除前:{掃除前の汚れ}][掃除後:{掃除後の汚れ}]
		[ENDIF]
		
		;청소경험は1/3上昇
		SIF !RAND:3
			EXP:MASTER:청소경험 ++
		
	ELSE 
		;掃除失敗
		SELECTCASE RAND:10
			CASE 1
				PRINTFORML %조교자는% %NAME_FROM_PLACE(SWEEPID)%を出る前に掃除しようとして、バケツをひっくり返した
			CASE 2
				PRINTFORML %조교자가% 手に取ったホウキは突然暴れ始めてどこかへ行ってしまった
			CASE 3
				PRINTFORML %조교자는% %NAME_FROM_PLACE(SWEEPID)%をうまく掃除できなかった
			CASE 4
				PRINTFORML %조교자가% 掃除した%NAME_FROM_PLACE(SWEEPID)%は前より汚れているように見える
			CASE 5
				PRINTFORML %조교자는% %NAME_FROM_PLACE(SWEEPID)%を綺麗にしようと努力はした
			CASE 6
				PRINTFORML 掃除をしようとした%CALLNAME:PLAYER%の前に邪魔しようとする妖精の集団が現れた！
			CASE 7
				PRINTFORML %조교자가% 掃除している後ろで妖精がゴミを散らかしている...
			CASE 8
				PRINTFORML %조교자는% 片づけは明日でいいやと感じた
			CASEELSE
				PRINTFORML %조교자는% 今は掃除をする時ではないと感じた
		ENDSELECT
		PRINTFORML
		
		;청소경험は1/20上昇
		SIF !RAND:20
			EXP:MASTER:청소경험 ++
		
	ENDIF
	
	;消費TSP/ステータス/消費時間は全て通常の半分
	
	;自動時止め移動、またはTS中ならTSP消費
	IF ((GETBIT(FLAG:순간이동, 1) || FLAG:70) && BASE:MASTER:TSP > 25)
		BASE:MASTER:TSP -= 25
	ELSE
		DOWNBASE:MASTER:체력 = 25
		DOWNBASE:MASTER:기력 = 50
		TIME += 15
	ENDIF
ENDIF

RETURN 1
