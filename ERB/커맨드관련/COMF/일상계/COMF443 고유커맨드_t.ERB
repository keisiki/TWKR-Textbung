;-------------------------------------------------
;고유 커맨드
;일상계 커맨드, 레벨 X
;-------------------------------------------------
@COM443
TRYCALLFORM UNIQUE_COM_%NAME_UNIQUE_COM(TARGET)%
SIF RESULT == -1
	RETURN -1
TIME += 3
RETURN 1

@COM_ABLE443
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(443)
	RETURN RESULT
SIF TARGET <= 0
	RETURN 0
SIF !SHIRAHU(TARGET)
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
SIF !ABLE_UNIQUE_COM(TARGET)
	RETURN 0
RETURN 1


@NAME_UNIQUE_COM(CHARA)
#FUNCTIONS
#DIM CHARA
RETURNF GET_STR(0, "캐릭터데이터", CHARA, "고유커맨드")

@ABLE_UNIQUE_COM(CHARA)
#FUNCTION
#DIM CHARA
RETURNF GET_INT(0, "캐릭터데이터", CHARA, "고유커맨드 실행 가능")


;-------------------------------------------------
;레이무 용
;-------------------------------------------------
@UNIQUE_COM_운세뽑기계약
#DIM 운세뽑기값
IF FLAG:운세뽑기계약
	PRINTFORML 「이미 계약하고 있잖니」
	RETURN -1
ENDIF
IF !MAIN_MAP
	운세뽑기값 = 30
ELSE
	운세뽑기값 = 50
ENDIF
PRINTFORML 「낙원의 멋진 무녀가 매일 널 위해서 운세를 뽑아줄게」
PRINTFORML 「월 50 카리스마야. 싸지?」
SIF !MAIN_MAP
	PRINTFORML 「신사에 얹혀사는 동안에는 30이면 돼」
CALL ASK_M("부탁한다", MONEY:2 >= 운세뽑기값,"그만둔다", 1)
SIF RESULT
	RETURN -1
PRINTFORMW 「매번 감사! 이사하면 재계약해야 되거든」
MONEY:2 -= 운세뽑기값
FLAG:운세뽑기계약 = 1



;-------------------------------------------------
;아야 용
;-------------------------------------------------
@UNIQUE_COM_신문구독
IF FLAG:신문구독
	PRINTFORML 「이미 구독하고 계시잖아요」
	RETURN -1
ENDIF
PRINTFORML 「월\\3000에 환상향의 『현재』를 알 수 있다! 붕붕마루 신문을 구독하지 않으실래요?」
CALL ASK_M("필요 없어", 1,"구독한다", MONEY >= 3000,"카리스마로 구독한다", MONEY:2 >= 10)
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		MONEY -= 3000
	CASE 2
		MONEY:2 -= 10
ENDSELECT
PRINTFORMW 붕붕마루 신문을 구독하기로 했습니다.
PRINTFORMW 매월 첫날에 계약을 갱신합니다.
FLAG:신문구독 = 1




;-------------------------------------------------
;텐시용
;-------------------------------------------------
@UNIQUE_COM_날씨조작
IF CFLAG:MASTER:현재위치 == 천계
	PRINTFORML 「여기선 무리야」
	RETURN -1
ENDIF
IF FLAG:이상기상
	PRINTFORML 「이 날씨는 못 바꿔」
	RETURN -1
ENDIF
IF GETBIT(TFLAG:일일일회, 6)
	PRINTFORML 「오늘은 이제 안 해」
	RETURN -1
ENDIF
PRINTFORML 「대략적으로밖에 안 해. 귀찮거든」
$LOOP
CALL ASK_M("맑음", 1,"흐림", 1,"비", 1, "눈", DAY:2 == 4, "팬티", 1)
SELECTCASE RESULT
	CASE 0
		;비라면 저확률로 무지개
		SIF INRANGE(TIME:5, 4,7) && !RAND:7
			TIME:7 = 1
		;눈이라면 저확률로 다이아몬드 더스트
		SIF INRANGE(TIME:5, 8,13) && !RAND:7
			TIME:7 = 2
		TIME:5 = RAND:2
	CASE 1
		TIME:5 = RAND:2 + 2
	CASE 2
		;맑음을 바꾸려고 하면 무지개
		IF TIME:5 < 2 && !RAND:4
			TIME:7 = 1
		ELSE
			TIME:5 = 4 + RAND:4
		ENDIF
	CASE 3
		;맑음을 바꾸려고 하면 다이아몬드 더스트
		IF TIME:5 < 2 && !RAND:4
			TIME:7 = 2
		ELSE
			TIME:5 = 8 + RAND:6
		ENDIF
	CASE 4
		PRINTFORML 「바보 아냐?」
		RETURN -1
ENDSELECT

DOWNBASE:기력 = 300
SETBIT TFLAG:일일일회, 6

;(天候パッチ)風速を0にする
WIND_VELOCITY = 0
;(天候パッチ)1日が終わるまで天候変更を禁止する
FORBIDDEN_CHANGE_WEATHER = 1


;-------------------------------------------------
;모미지 용
;-------------------------------------------------
@UNIQUE_COM_천리안
PRINTFORML 「어디를 볼까요?」
CALL BROAD_HITOSAGASI



;-------------------------------------------------
;치르노용
;-------------------------------------------------
;아이스크림의 가격은 계절에 의해 변동한다(여름이 제일 높다)
@UNIQUE_COM_아이스를산다
#DIM 아이스값
#DIM 팩값
SELECTCASE DAY:2
CASE 1
	아이스값 = 400
CASE 2
	아이스값 = 500
CASE 3
	아이스값 = 300
CASE 4
	아이스값 = 200
ENDSELECT
팩값 = 아이스값 * 9

DRAWLINE
PRINTL 「아이스 원해?」
PRINTFORML 소지금 :%금액표시(MONEY)%
DRAWLINE
CALL EX_SHOPPING(GETNUM(ITEM, "아이스크림"), "아이스", "달고 맛있는 아이스", 아이스값, 1)
CALL EX_SHOPPING(GETNUM(ITEM, "아이스크림"), "아이스 세트", "다 같이 먹자", 팩값, 10, GETNUM(ITEM, "아이스크림") + 1)
DRAWLINE
PRINTL [999] 그만둔다
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "아이스크림")
	CALL ITEM_CARTON(GETNUM(ITEM, "아이스크림"), 아이스값, 1)
ELSEIF RESULT == GETNUM(ITEM, "아이스크림") + 1
	CALL ITEM_CARTON(GETNUM(ITEM, "아이스크림"), 팩값, 10)
ENDIF
RESTART

;-------------------------------------------------
;사쿠야 용
;-------------------------------------------------
@UNIQUE_COM_홍마관출장서비스
PRINTFORML 「어머, 뭔가 도와드릴까요?」
PRINTFORML 
CALL ASK_M("부탁하지 않는다", 1, "청소 대행", !TFLAG:출장청소, "물주기대행", ITEM:150)
SELECTCASE RESULT
CASE 0
	RETURN -1
CASE 1
	PRINTFORML 홉고블린에게 %GET_MAPNAME(MAIN_MAP)%의 청소를 부탁하겠습니까?
	CALL PAY_YEN_OR_CM(3000 - (홍마관복종됨() * 350),10 - (홍마관복종됨()))
	SIF !RESULT
		RETURN -1
	PRINTFORML 출장 청소 서비스를 의뢰했습니다.
	TFLAG:출장청소 = 1
CASE 2
	PRINTFORML 홉고블린에게 텃밭의 물주기를 부탁하겠습니까? (이번 달까지)
	CALL PAY_YEN_OR_CM(15000 - (홍마관복종됨() * 1750),70 - (홍마관복종됨() * 5))
	SIF !RESULT
		RETURN -1
	PRINTFORML 물주기대행을 의뢰했습니다.
	FLAG:물주기대행 = 1
ENDSELECT
RETURN 1



;-------------------------------------------------
;나즈린용
;-------------------------------------------------
@UNIQUE_COM_다우징
#DIM DYNAMIC CNT_TALK
#DIM DYNAMIC CNT_DOWSING
#DIM CAN_SLOT
#DIM CHK_SLOT
#DIM TMP_ID
IF !CNT_TALK
	PRINTFORML 「흠. 물건 찾기 상담인가」
	PRINTFORML 「확실히 그건 내 전문 분야인걸」
	PRINTFORMW 「그러면 자세하게 이야기를 들어볼까」
	CNT_TALK ++
	CAN_SLOT = CAN_DOWSING()
	IF !CAN_SLOT
		PRINTFORML 「이봐이봐. 딱히 찾고 싶은 것도 없는데 다우징해달라고 한 거야?」
		PRINTFORMW 「구경거리가 아니라고? 참 내…」
		RETURN
	ENDIF
ELSE
	PRINTFORMW 「뭐가 더 남았어?」
ENDIF

FOR CHK_SLOT, 1, 6
	SIF !GETBIT(CAN_SLOT, CHK_SLOT)
		CONTINUE
	PRINTFORML [{CHK_SLOT}] - 의뢰{CHK_SLOT}　＜%STR_DATA_IRAI(IRAI_SLOT:CHK_SLOT, "의뢰명", IRAI_SLOT_TO_CLIENT(CHK_SLOT))%＞
NEXT
PRINTFORML 
PRINTFORML [0] - 그만둔다
DO
	INPUT
	CHK_SLOT = RESULT
	SELECTCASE CHK_SLOT
	CASE 1 TO 5
		SIF !GETBIT(CAN_SLOT, CHK_SLOT)
			CONTINUE
		IF IRAI_EX:CHK_SLOT
			PRINTFORML 「이봐이봐, 그 의뢰라면 이미 조사했잖아」
			CONTINUE
		ENDIF
		PRINTFORML 「그렇군」
		PRINTFORML 「다우징에는 약간의 시주를 받는데, 괜찮겠지?」
		CALL ASK_M("그만둔다", 1, "\\1000을 희사한다", MONEY >= 1000, "5CM를 희사한다", MONEY:2 >= 5)
		SELECTCASE RESULT
		CASE 0
			PRINTFORMW 「이런, 그건 유감인걸」
			BREAK
		CASE 1
			MONEY -= 1000
		CASE 2
			MONEY:2 -= 5
		ENDSELECT
		CNT_DOWSING ++
		SELECTCASE IRAI_SLOT_TO_TASKNAME(CHK_SLOT)
		CASE "수색(인물)"
			CNT_DOWSING ++
			TMP_ID = IRAI_SLOT_TO_PERSON(CHK_SLOT)
			IRAI_EX:CHK_SLOT = TMP_ID
			IF CALLNAME:TMP_ID == "나즈린"
			;나즈 대사는 인물에 대한 반응 분기가 많지만 만들 수 없다
			;분명히 자신이 상대라면 이런 느낌이 될 것이다
				PRINTFORML 「아까부터 조금 전부터 펜듈럼이 쭉 반응하고 있는데」
				PRINTFORML 「그 의뢰는 나와 관련 있는 것 아닐까?」
				PRINTFORMW 「왠지 그런 느낌이 들어」
				BREAK
			ENDIF
			CALL DOWSING_TSUBUYAKI
			PRINTFORML 「결론이 나왔어」
			PRINTFORML 「%조사처리(CALLNAME:TMP_ID,"가")% 중요 인물이야」
			PRINTFORML 「내친 김에 있는 곳도 알아내둘까?」
			CALL ASK_M("그만둔다", 1, "\\500을 희사한다", MONEY >= 500, "3CM를 희사한다", MONEY:2 >= 3)
			SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「이런, 그건 유감인걸」
				BREAK
			CASE 1
				MONEY -= 500
			CASE 2
				MONEY:2 -= 3
			ENDSELECT
			IF CFLAG:TMP_ID:현재위치 == SUKIMA()
				PRINTFORML 「으응~, 반응이 안정되지 않는데」
				PRINTFORMW 「지금 당장 만나는 것은 어려운 것 같네」
			ELSE
				PRINTFORMW 「그녀는 현재, “%STR:(5000 + GET_MAPID(CFLAG:TMP_ID:현재위치))%”의 주변에 있는 것 같아」
			ENDIF
		CASE "수색(장소)"
			CNT_DOWSING ++
			TMP_ID = IRAI_SLOT_TO_PLACE(CHK_SLOT)
			IRAI_EX:CHK_SLOT = TMP_ID + 5000
			CALL DOWSING_TSUBUYAKI
			PRINTFORML 「결론이 나왔어」
			PRINTFORML 「네가 목표로 해야 할 장소는 “%STR:(5000 + GET_MAPID(TMP_ID))%” 다」
			PRINTFORML 「좀 더 상세한 정보가 필요한가?」
			
			CALL ASK_M("그만둔다", 1, "\\2000을 희사한다", MONEY >= 2000, "10CM를 희사한다", MONEY:2 >= 10)
			SELECTCASE RESULT
			CASE 0
				PRINTFORMW 「이런, 그건 유감인걸」
				BREAK
			CASE 1
				MONEY -= 2000
			CASE 2
				MONEY:2 -= 10
			ENDSELECT
			IRAI_EX:CHK_SLOT = TMP_ID
			PRINTFORMW 「%STR:(5000 + GET_MAPID(TMP_ID))%주변, 특히“%GET_PLACENAME(TMP_ID)%”의 반응이 강하네」
		CASEELSE
			PRINTFORMW 「어? ……으응~, 낚싯대의 상태가 이상한데」
			PRINTFORML 
			PRINTFORML 　에러 발생중(로그의 제공을 부탁합니다)
			PRINTFORML 　클릭으로 에러 로그 생성
			PRINTFORMW ◆여기에서
			PRINTFORML IRAI_SLOT:CHK_SLOT　{IRAI_SLOT:CHK_SLOT}
			PRINTFORML IRAI_DATA:CHK_SLOT　{IRAI_DATA:CHK_SLOT}
			CALL SHOW_IRAI_DATA(IRAI_SLOT:CHK_SLOT, CHK_SLOT)
			PRINTFORML 
			PRINTFORML ◆여기까지
			PRINTFORMW 　복붙 대기
		ENDSELECT
		
	CASE 0
		IF CNT_DOWSING
			PRINTDATAL
				DATA 「도움이 되었을까?」
				DATA 「행운을 빌어둘게」
				DATA 「이제 된 거야?」
			ENDDATA
			PRINTFORMW 「뭐, 마음 길게 먹고 찾아봐」
		ELSE
			PRINTFORMW 「변죽만 울리지 말아줬으면 하는데」
		ENDIF
		RETURN
	CASEELSE
		$RETRY
		PRINTFORMW 「응, 뭐라고?」
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1
RESTART

;수탁이 끝난 의뢰의 다우징 가부 판정
;반환값은 BIT로 각 의뢰의 다우징 가부
@CAN_DOWSING()
#FUNCTION
#DIM CHK_SLOT
#DIM DYNAMIC CAN_SLOT
FOR CHK_SLOT, 1, 6
	SIF !IRAI_SLOT_CAN_DOWSING(CHK_SLOT)
		CONTINUE
	SETBIT CAN_SLOT, CHK_SLOT
NEXT
RETURNF CAN_SLOT

;개개의 의뢰슬롯의 다우징 가부 판정
@IRAI_SLOT_CAN_DOWSING(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF IRAI_SLOT_TO_PROGRESS(CHK_SLOT) != "의뢰중"
	RETURNF 0
SIF !GROUPMATCH(IRAI_SLOT_TO_TASKNAME(CHK_SLOT), "수색(인물)", "수색(장소)")
	RETURNF 0
RETURNF CHK_SLOT

;다우징중의 혼잣말
@DOWSING_TSUBUYAKI
#DIMS COMMENT, 3
COMMENT:0 '= "「호오…… 이건」"
STRDATA COMMENT:1
	DATA 「……꽤 좁혀냈군」
	DATA 「음, 이러면 앞뒤가 안 맞는데」
	DATA 「이런, 그렇게 되는 건가」
	DATA 「그럼, 이렇게 하면…………」
ENDDATA
STRDATA COMMENT:2
	DATA 「……그런 건가」
	DATA 「뭐야, 간단한 거잖아」
	DATA 「좋아, 이거다」
	DATA 「이쯤일까」
ENDDATA
CALL PRINT_GROUP(COMMENT, 3)





;-------------------------------------------------
;스타용
;-------------------------------------------------
@UNIQUE_COM_기척을찾는다
PRINTFORML 「흐응, 누군가 찾아?」
PRINTFORML 「그렇네…… 용돈 줄래?」
CALL ASK_M("역시 그만", 1, "\\500 줄게", MONEY >= 500, "3CM 줄게", MONEY:2 >= 3)
SELECTCASE RESULT
CASE 0
	PRINTDATAW
		DATAFORM 「우―, %CALLNAME:MASTER% 구두쇠」
		DATAFORM 「……하아」
		DATAFORM 스타 사파이어는 말없이 이쪽을 째려보고 있다
	ENDDATA
	RETURN
CASE 1
	MONEY -= 500
CASE 2
	MONEY:2 -= 3
ENDSELECT
PRINTDATAW
	DATAFORM 「와이~」
	DATAFORM 「했다―」
	DATAFORM 「용돈이다―」
	DATAFORM 「말귀 밝은 사람은 좋아해」
	DATAFORM 스타 사파이어는 흡족해하고 있다
ENDDATA
PRINTFORML 「그럼 시작할게♪」
PRINTFORMW 「음, 보자……」
PRINTFORML 
CALL HITOSAGASI
PRINTFORMW 

;-------------------------------------------------
;마리사 용
;교우 관계 넓은 것 같고, 귀는 빠를 것이다
;-------------------------------------------------
@UNIQUE_COM_사람돕기
#DIM PREV_DAY
#DIM IRAI_ABLE
#DIM CLIENT_ID
PREV_DAY = DAY
PRINTFORMW 「에? “곤란한 여자아이를 몰라”?」

CALL CAN_IRAI_START
IRAI_ABLE = RESULT
IF IRAI_ABLE && IRAI_CHARA_TO_PROGRESS(TARGET) == "의뢰미수탁"
	PRINTFORML 「있고 말이야, 여기 혼자야!」
	CALL IRAI_START(TARGET)
	SIF IRAI_CHARA_TO_PROGRESS(TARGET) != "의뢰중"
		PRINTFORMW 「뭐야, 보통 이 흐름에서 거절이야?」
	RETURN
ENDIF

CLIENT_ID = SEARCH_UNIQUE_COM_사람돕기()
SELECTCASE CLIENT_ID
CASE 0
	PRINTFORML 「으응~」
	PRINTFORMW 「내가 아는 한에서는 없는 것 같네」
CASEELSE
	PRINTFORML 「%TEXTR("으응~/그러니까/분명히……")%」
	PRINTDATAW
		DATAFORM 「%조사처리(CALLNAME:CLIENT_ID,"가")% 그런 이야기를 하고 있다고 생각해」
		DATAFORM 「%조사처리(CALLNAME:CLIENT_ID,"가")% 무슨 말을 했더라」
		DATAFORM 「%조사처리(CALLNAME:CLIENT_ID,"가")% 곤란하고 있다고 소문으로 들었다구」
		DATAFORM 「%CALLNAME:CLIENT_ID%의 곳에라도 가 보면 괜찮지 않을까?」
		DATAFORM 「%조사처리(CALLNAME:CLIENT_ID,"를")% 만나러 가면 좋을 것 같아」
	ENDDATA
ENDSELECT



@SEARCH_UNIQUE_COM_사람돕기
#FUNCTION
#DIM DYNAMIC CLIENT_ID, 캐릭터수상한
#DIM DYNAMIC CLIENT_CNT
FOR LOCAL:0, 1, CHARANUM
	SIF IRAI_CHARA_TO_PROGRESS(LOCAL:0) != "의뢰미수탁"
		CONTINUE
	CLIENT_ID:CLIENT_CNT = LOCAL:0
	CLIENT_CNT ++
NEXT
SIF !CLIENT_CNT
	RETURNF 0
RETURNF CLIENT_ID:(RAND:CLIENT_CNT)



;-------------------------------------------------
;니토리 용
;-------------------------------------------------
@UNIQUE_COM_도구를산다
#DIM 買うもの
#DIM 割引
TFLAG:194 = 1
割引 = 66
;購入条件設定
FOR LOCAL, 0, 3050
	CALL ITEMDATA(LOCAL, "SHOP:캇파의 도구점")
	IF RESULT
		CALL ITEMDATA(LOCAL, "SALES")
		ITEMSALES:LOCAL = RESULT
	ENDIF
NEXT
DRAWLINE
CALL LIST_ITEM("캇파 도구")
DRAWLINE
PRINTL 「지금이라면 싸게 해 두어」
PRINTFORML 구입할 아이템을 선택해주세요 소지금 :\\%TOSTR(MONEY,"#,###")%
DRAWLINE
CALL SHOW_SHOPPING("캇파의 도구점", "캇파 도구", 割引)
DRAWLINE
;未実装
;PRINTL [990] - 도구의 개조　[991] - 도구의 개발
PRINTL [990] - 도구의 개조
PRINTL [999] - 돌아간다　　　　　
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == 990
	CALL TOOL_CUSTOM
	SIF RESULT
		RETURN RESULT
ELSEIF RESULT == 991
	;未実装
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "SHOP:캇파의 도구점", 割引)
	IF RESULT && ITEMSALES:買うもの >= 0 && ITEM:買うもの < 소지수제한
		CALL ITEMDATA(買うもの, "구입", 割引)
		SIF RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF


;도구 개조 함수
;현재 상태로서는 풀장 뿐이지만
;개조 아이템이 증가하면(자) 한층 더 비대화 하는 것이 눈에 보이므로 독립
;구입으로 RETURN -1, 구입하지 않을 때는 RETURN 0
;아무것도 하지 않고 말미에 도달하면(자) 구입한 취급이 됩니다
@TOOL_CUSTOM
#DIM 거품개조전제조건
거품개조전제조건 = GETBIT (FLAG:풀장, 0) && GETBIT (FLAG:풀장, 1) && GETBIT (FLAG:풀장, 2)
DRAWLINE
PRINTFORML 개조할 아이템을 선택해주세요 소지금 :\\%TOSTR(MONEY,"#,###")%
DRAWLINE
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "TYPE:개조가능")
	SIF RESULT && ITEM:LOCAL
		PRINTFORML [{LOCAL, 3}] - %ITEMNAME:LOCAL%
NEXT
PRINT [999] - 역시 그만둔다
INPUT
SELECTCASE RESULT
CASE 999
	RETURN
CASE GETNUM(ITEM, "착유기")
	SIF !ITEM:착유기
		RETURN
	DRAWLINE
	PRINTL 「어떻게 개조해?」
	PRINT 현재 상태：
	DRAWLINE
	SIF GETBIT (FLAG:착유기강화, 0)
		PRINT [모유 탱크]
	SIF GETBIT (FLAG:착유기강화, 1)
		PRINT [진동 패드]
	SIF GETBIT (FLAG:착유기강화, 2)
		PRINT [펌프 강화]
	PRINTL
	IF !GETBIT (FLAG:착유기강화, 0)
		PRINTL [  1] - 모유 탱크  (\5000)： 짜낸 젖을 보관한다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 모유 탱크 ： 짜낸 젖을 보관한다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:착유기강화, 1)
		PRINTL [  2] - 진동 패드  (\5000)： 미세 진동으로 유두를 자극해 산출량을 늘린다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 진동 패드 ： 미세 진동으로 유두를 자극해 산출량을 늘린다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:착유기강화, 2)
		PRINTL [  2] - 펌프 강화  (\5000)： 펌프를 강화해 더 강한 압력으로 젖을 짜낸다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 펌프 강화 ： 펌프를 강화해 더 강한 압력으로 젖을 짜낸다
		RESETCOLOR
	ENDIF
	PRINTL [999]역시 그만둔다
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF !GETBIT (FLAG:착유기강화, 0) && MONEY >= 5000
			PRINTL 자그마한 모유 탱크를 달았다!
			SETBIT FLAG:착유기강화, 0
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 2
		IF !GETBIT (FLAG:착유기강화, 1) && MONEY >= 5000
			PRINTL 미세 진동 패드를 부착했다!
			SETBIT FLAG:착유기강화, 1
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 3
		IF !GETBIT (FLAG:착유기강화, 2) && MONEY >= 5000
			PRINTL 고출력 펌프로 교체했다!
			SETBIT FLAG:착유기강화, 2
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	ENDSELECT
CASE GETNUM(ITEM, "낚싯대")
	SIF !ITEM:낚싯대
		RETURN
	DRAWLINE
	PRINTL 「어떻게 개조해?」
	PRINT 현재 상태：
	SIF GETBIT (FLAG:낚싯대강화, 0)
		PRINT [진동 유도]
	SIF GETBIT (FLAG:낚싯대강화, 1)
		PRINT [IR 유도]
	SIF GETBIT (FLAG:낚싯대강화, 2)
		PRINT [소나 유도]
	PRINTL
	DRAWLINE
	IF !GETBIT (FLAG:낚싯대강화, 0)
		PRINTL [  1] - 진동 유도  (\5000)： 가까이 있는 움직이는 물고기를 향해 자동으로 이동해 낚을 확률을 높힌다 
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 진동 유도： 가까이 있는 움직이는 물고기를 향해 자동으로 이동해 낚을 확률을 높힌다 
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:낚싯대강화, 1)
		PRINTL [  2] - 다중 바늘  (\5000)： 바늘을 여러개로 늘려 낚일 확률을 높힌다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 다중 바늘 ： 바늘을 여러개로 늘려 낚일 확률을 높힌다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:낚싯대강화, 2)
		PRINTL [  3] - 소나 유도  (\5000)： 어느정도 떨어진 물고기의 소리를 탐지, 자동으로 이동해 낚아챈다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 소나 유도： 어느정도 떨어진 물고기의 소리를 탐지, 자동으로 이동해 낚아챈다
		RESETCOLOR
	ENDIF
	PRINTL [999]역시 그만둔다
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF !GETBIT (FLAG:낚싯대강화, 0) && MONEY >= 5000
			PRINTL 낚싯 바늘을 개조했다!
			SETBIT FLAG:낚싯대강화, 0
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 2
		IF !GETBIT (FLAG:낚싯대강화, 1) && MONEY >= 5000
			PRINTL 낚싯 바늘을 늘렸다!
			SETBIT FLAG:낚싯대강화, 1
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 3
		IF !GETBIT (FLAG:낚싯대강화, 2) && MONEY >= 5000
			PRINTL 낚싯 바늘에 소형 소나를 달았다!
			SETBIT FLAG:낚싯대강화, 2
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	ENDSELECT
CASE GETNUM(ITEM, "로터")
	SIF !ITEM:로터
		RETURN
	DRAWLINE
	PRINTL 「어떻게 개조해?」
	PRINT 현재 상태：
	DRAWLINE
	SIF GETBIT (FLAG:로터강화, 0)
		PRINT [배터리 개선]
	SIF GETBIT (FLAG:로터강화, 1)
		PRINT [저전력 모터]
	SIF GETBIT (FLAG:로터강화, 2)
		PRINT [진동 강화]
	SIF GETBIT (FLAG:로터강화, 3)
		PRINT [자가 발전]
	PRINTL
	IF !GETBIT (FLAG:로터강화, 0)
		PRINTL [  1] - 배터리 개선 (\5000)：삽입된 로터가 더 오래 작동한다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 배터리 개선：삽입된 로터가 더 오래 작동한다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:로터강화, 1)
		PRINTL [  2] - 저전력 모터 (\5000)：삽입된 로터가 더더욱 오래 작동한다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 저전력 모터：삽입된 로터가 더더욱 오래 작동한다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:로터강화, 2)
		PRINTL [  3] - 진동 강화 (\5000)：로터가 주는 쾌감이 증가
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 진동 강화：로터가 주는 쾌감이 증가
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:로터강화, 3)
		PRINTL [  4] - 자가 발전 (\5000)：삽입된 상태에서 생체전기를 활용해 아주 오랫동안 작동할 수 있다, 꺼내기 힘들게 몸 속에 고정되는건 덤
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 자가 발전：삽입된 상태에서 생체전기를 활용해 아주 오랫동안 작동할 수 있다, 꺼내기 힘들게 몸 속에 고정되는건 덤
		RESETCOLOR
	ENDIF
	PRINTL [999]역시 그만둔다
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF !GETBIT (FLAG:로터강화, 0) && MONEY >= 5000
			PRINTL 배터리를 교체했다!
			SETBIT FLAG:로터강화, 0
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 2
		IF !GETBIT (FLAG:로터강화, 1) && MONEY >= 5000
			PRINTL 향상된 모터를 장착했다!
			SETBIT FLAG:로터강화, 1
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 3
		IF !GETBIT (FLAG:로터강화, 2) && MONEY >= 5000
			PRINTL 진동이 강화됐다!
			SETBIT FLAG:로터강화, 2
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 4
		IF !GETBIT (FLAG:로터강화, 3) && MONEY >= 5000
			PRINTL 충전기를 장착해 더욱 오래 작동할 수 있게되었다!
			SETBIT FLAG:로터강화, 3
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	ENDSELECT
CASE GETNUM(ITEM, "간이풀장")
	SIF !ITEM:간이풀장
		RETURN
	DRAWLINE
	PRINTL 「어떻게 개조해?」
	PRINT 현재 상태：
	DRAWLINE
	SIF GETBIT (FLAG:풀장, 0)
		PRINT [자주모터]
	SIF GETBIT (FLAG:풀장, 1)
		PRINT [온수 기능]
	SIF GETBIT (FLAG:풀장, 2)
		PRINT [샤워]
	SIF GETBIT (FLAG:풀장, 3)
		PRINT [이상한 거품]
	PRINTL
	
	IF !GETBIT (FLAG:풀장, 0)
		PRINTL [  1] - 모터(\5000)：이동의 부담이 없어진다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 모터：이동의 부담이 없어진다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:풀장, 1)
		PRINTL [  2] - 온수 기능(\5000)：넓은 장소에서 편하게 한다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 온수 기능：넓은 장소에서 편하게 한다·목욕탕플레이가 생긴다
		RESETCOLOR
	ENDIF
	IF !GETBIT (FLAG:풀장, 2)
		PRINTL [  3] - 샤워(\5000)：샤워로 씻을 수 있다·물주기도 할 수 있다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 샤워：샤워로 씻을 수 있다·물주기도 할 수 있다
		RESETCOLOR
	ENDIF
	IF 거품개조전제조건 == 0
		PRINTFORML [4] - 3개 모두 개조(1개당\\5000)
		PRINTL [?] - ?????
	ELSEIF !GETBIT (FLAG:풀장, 3)
		PRINTL [5] - 이상한 거품(\5000)：와카사기히메가 자유롭게 이동할 수 있게 된다
	ELSE
		SETCOLOR C_GRAY
		PRINTL [완료] - 이상한 거품：와카사기히메가 자유롭게 이동할 수 있게 된다
		RESETCOLOR
		PRINTL 　　　　더 이상 개조할 수 없다
	ENDIF
	
	PRINTL [999]역시 그만둔다
	INPUT
	SELECTCASE RESULT
	CASE 999
		RETURN
	CASE 1
		IF !GETBIT (FLAG:풀장, 0) && MONEY >= 5000
			PRINTL 이동의 부담이 없어졌다!
			SETBIT FLAG:풀장, 0
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 2
		IF !GETBIT (FLAG:풀장, 1) && MONEY >= 5000
			PRINTL 넓은 장소에서 편하게 있을 수 있게 되었다!
			PRINTL 목욕탕 플레이가 가능해졌다!
			SETBIT FLAG:풀장, 1
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 3
		IF !GETBIT (FLAG:풀장, 2) && MONEY >= 5000
			PRINTL 샤워를 사용할 수 있게 되었다!
			PRINTL 물주기가 편해졌다!
			SETBIT FLAG:풀장, 2
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	CASE 4
		IF 거품개조전제조건 == 0 && MONEY >= 5000 * (!GETBIT (FLAG:풀장, 0) + !GETBIT (FLAG:풀장, 1) + !GETBIT (FLAG:풀장, 2))
			PRINTL 여러 가지 기능이 추가되었다!
			MONEY -= 5000 * (!GETBIT (FLAG:풀장, 0) + !GETBIT (FLAG:풀장, 1) + !GETBIT (FLAG:풀장, 2))
			SETBIT FLAG:풀장, 0
			SETBIT FLAG:풀장, 1
			SETBIT FLAG:풀장, 2
		ELSE
			RETURN
		ENDIF
	CASE 5
		IF !GETBIT (FLAG:풀장, 3) && 거품개조전제조건 && MONEY >= 5000
			PRINTL 와카사기히메에게 이상한 거품을 사 주었다!
			SETBIT FLAG:풀장, 3
			MONEY -= 5000
		ELSE
			RETURN
		ENDIF
	ENDSELECT
CASEELSE
	RETURN
ENDSELECT
RETURN -1





;-------------------------------------------------
;우동게용
;-------------------------------------------------
@UNIQUE_COM_약을산다
#DIM 買うもの
DRAWLINE
CALL LIST_ITEM("토끼 약품")
DRAWLINE
PRINTFORML 구입할 아이템을 선택해주세요 소지금 :%금액표시(MONEY)%
DRAWLINE
PRINTL
VARSET LOCALS
CALL EX_SHOPPING(GETNUM(ITEM, "로션"), "로션 물병(와인)", "로션이 든 큰병. 5회분은 있을 것이다. ", 1000, 5)
CALL EX_SHOPPING(GETNUM(ITEM, "콘돔"), "콘돔 이득용 팩", "50개나 들어가 있다. ", 1000, 50)
CALL EX_SHOPPING(GETNUM(ITEM, "피임약"), "필(5정들이)", "밝은 가족 계획", 5000, 5)
CALL ITEM_SHOPPING(GETNUM(ITEM, "춘약"), "호의를 품기 쉬워진다")
CALL ITEM_SHOPPING(GETNUM(ITEM, "수면제"), "악용하면 안 돼요?")
CALL ITEM_SHOPPING(GETNUM(ITEM, "영양드링크"), "타우린 1000mg 배합")
CALL ITEM_SHOPPING(GETNUM(ITEM, "정력제"), "더러운 부자 납품업자")
CALL ITEM_SHOPPING(GETNUM(ITEM, "우콘드링크"), "술 마시기 전에")
CALL ITEM_SHOPPING(GETNUM(ITEM, "붕대"), "상처를 입은 데나 숨기고 싶은 장소에 감자")
CALL ITEM_SHOPPING(GETNUM(ITEM, "영양강화제"), "요리에 섞으면 부족한 영양분을 보충할 수 있다")
CALL ITEM_SHOPPING(GETNUM(ITEM, "항불안제"), "불안으로 인한 정신 이상을 진정시킨다")
SIF TARGET == [[테위]] && !ITEM:토부「개운대문」
	CALL ITEM_SHOPPING(GETNUM(ITEM, "토부「개운대문」"),"럭키 색골이 일어나는 꿈의 아이템")
DRAWLINE
PRINTL [999] 그만둔다
INPUT

IF RESULT == 999
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "토부「개운대문」") && TARGET == [[테위]] && !ITEM:토부「개운대문」
	CALL BOTTAKURI
ELSEIF RESULT == GETNUM(ITEM, "로션")
	CALL ITEM_CARTON(RESULT, 1000, 5)
ELSEIF RESULT == GETNUM(ITEM, "콘돔")
	CALL ITEM_CARTON(RESULT, 1000, 50)
ELSEIF RESULT == GETNUM(ITEM, "피임약")
	CALL ITEM_CARTON(RESULT, 5000, 5)
ELSE
	買うもの = RESULT
	CALL ITEMDATA(買うもの, "TYPE:토끼 약품")
	IF RESULT && ITEMSALES:買うもの >= 1 ;&& ITEM:買うもの < 소지수제한
		CALL ITEMDATA(買うもの, "구입")
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF
RESTART

@BOTTAKURI
SELECTCASE FLAG:개운대문구입회수
CASE 0
	LOCAL = 1000
CASE 1 TO 3
	LOCAL = 10000
CASE 3 TO 10
	LOCAL = 20000
CASE 11 TO 20
	LOCAL = 30000
CASEELSE
	LOCAL = 50000
ENDSELECT
SIF TALENT:TARGET:연모 && LOCAL >= 20000
	LOCAL /= 2
LOCAL = 물가적용(LOCAL)
SIF !FLAG:개운대문구입회수
	PRINTFORML 「지금이라면 시험 가격 \\{LOCAL}! 가져가 이 도둑―!」
IF LOCAL <= 10000
ELSE
	IF TALENT:TARGET:연모
		PRINTFORML 「%마스터라%면 \\{LOCAL}로 좋아」
	ELSE
		PRINTFORML 「그렇다면 \\{LOCAL}가 되겠어」
	ENDIF
	CALL ASK_M("웃기지마", 1, "삽니다", MONEY >= LOCAL, "그런 일 말 할 입장이야? ", CFLAG:TARGET:약점잡힘 && !CFLAG:TARGET:약점잡음)
	SELECTCASE RESULT
		CASE 0
			RETURN
		CASE 1
		CASE 2
			PRINTFORMW 약점과 바꿔 공짜로 Get 했다
			ITEM:130 ++
			CFLAG:TARGET:약점잡힘 = 0
			RETURN
	ENDSELECT
ENDIF

IF MONEY >= LOCAL
	ITEM:130 ++
	MONEY -= LOCAL
	FLAG:개운대문구입회수 ++
	PRINTFORMW \\{LOCAL}를 지불해 토부「개운대문」를 구입했다
ENDIF

;-------------------------------------------------
;미스치 용
;-------------------------------------------------
@UNIQUE_COM_포장마차에서식사
#DIM 동행인원수카운트
#DIM 음주가능인원수
#DIM ATSUKAN
#DIM OOMORI
#DIM KAIKEI
#DIM KAKAKU, 2

동행인원수카운트 = 1
음주가능인원수 = 0
ATSUKAN = 0
KAIKEI = 0
OOMORI = 0
VARSET KAKAKU
SIF CAN_DRINK(MASTER)
	음주가능인원수 = 1
FOR LOCAL, 1, CHARANUM
	SIF !CFLAG:LOCAL:동행
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:공복시각)
	 	CONTINUE
	동행인원수카운트 ++
	SIF CAN_DRINK(LOCAL)
		음주가능인원수 ++
NEXT
PRINTFORML 「어서오세요, {동행인원수카운트}명 같네」
KAKAKU:0 = 3 * 동행인원수카운트
KAKAKU:1 = 5 * 동행인원수카운트
CALL ASK_M("꼬치구이 진열 한 사람 몫 3CM", MONEY:2 >= KAKAKU:0 && TIME_PROGRESS(TCVAR:MASTER:공복시각),"꼬치구이 진열(대) 한 사람 몫 5CM", MONEY:2 >= KAKAKU:1 && TIME_PROGRESS(TCVAR:MASTER:공복시각),"테이크 아웃　5CM", MONEY:2 >= 5 && !ITEM:야트메꼬치,"그만둔다", 1)
IF RESULT == 3
	PRINTFORML 「또 와」
	RETURN -1
ENDIF
IF RESULT == 2
	ITEM:야트메꼬치 ++
	MONEY:2 -= 5
	PRINTFORML 「식기 전에 먹어」
	SOURCE:환락 += 200
	TFLAG:98 = 1
	TIME += 5
	RETURN 1
ENDIF
MONEY:2 -= KAKAKU:RESULT
KAIKEI = KAKAKU:RESULT

SIF RESULT == 1
	OOMORI = 1

IF MONEY:2 >= 2 * 음주가능인원수
	PRINTFORML 「한사람당 2 카리스마로 데운 술도 곁들일래?」(술기운·기력회복량 업)
	CALL ASK_YN
	IF !RESULT
		ATSUKAN = 1
		KAIKEI += 2 * 음주가능인원수
		MONEY:2 -= 2 * 음주가능인원수
	ENDIF
ENDIF
;신뢰
TFLAG:98 += 3
;동행중 전원이 돌린다

FOR LOCAL, 0, CHARANUM
	SIF !CFLAG:LOCAL:동행 && LOCAL
		CONTINUE
	 SIF !TIME_PROGRESS(TCVAR:LOCAL:공복시각)
	 	CONTINUE
	CALL EAT_YATSUME(LOCAL, OOMORI, ATSUKAN)
NEXT

SOURCE:환락 += KAIKEI * 100
PRINTFORML 「모두{KAIKEI}카리스마가 되겠습니다」
PRINTFORML 「또 와」
TIME += 30

@EAT_YATSUME(대상, ARG, ARG:1)
#DIM 회복전체력
#DIM 회복전기력
#DIM 회복전정력
#DIM 회복후체력
#DIM 회복후기력
#DIM 회복후정력
#DIM 대상

회복전체력 = BASE:대상:체력
회복전기력 = BASE:대상:기력
회복전정력 = BASE:대상:정력

;수북히 담음의 경우
IF ARG == 1
	CALL 만복도상승(대상,"주식")
	CALL RECOVER_PERMIL(대상, 100,"체력")
	CALL RECOVER_PERMIL(대상, 200,"기력")
	CALL RECOVER_PERMIL(대상, 100,"정력")
	CALL TB외식하기("호화로움")
ELSE
	CALL 만복도상승(대상,"경식")
	CALL RECOVER_PERMIL(대상, 50,"체력")
	CALL RECOVER_PERMIL(대상, 100,"기력")
	CALL RECOVER_PERMIL(대상, 50,"정력")
	CALL TB외식하기("좋음")
ENDIF
IF 대상
;고정으로 획득하는 소스
SOURCE:대상:환락 = 1000
SOURCE:대상:사랑 = 300

;ABL:욕망을 본다
IF ABL:대상:욕망 <= 1
	SOURCE:대상:환락 += (ABL:대상:욕망 * 60)
ELSEIF ABL:대상:욕망 <= 3
	SOURCE:대상:환락 += 400 + (ABL:대상:욕망 * 60)
ELSEIF ABL:대상:욕망 <= 5
	SOURCE:대상:환락 += 800 + (ABL:대상:욕망 * 60)
ELSEIF ABL:대상:욕망 <= 8
	SOURCE:대상:환락 += 1000 + (ABL:대상:욕망 * 75)
ELSEIF ABL:대상:욕망 <= 11
	SOURCE:대상:환락 += 1700 + (ABL:대상:욕망 * 75)
ELSE
	SOURCE:대상:환락 += 2500 + (ABL:대상:욕망 * 35)
ENDIF

;ABL:대상:봉사정신을 본다
IF ABL:대상:봉사정신 <= 1
	SOURCE:대상:사랑 += (ABL:대상:봉사정신 * 30)
ELSEIF ABL:대상:봉사정신 <= 3
	SOURCE:대상:사랑 += 200 + (ABL:대상:봉사정신 * 30)
ELSEIF ABL:대상:봉사정신 <= 5
	SOURCE:대상:사랑 += 500 + (ABL:대상:봉사정신 * 30)
ELSEIF ABL:대상:봉사정신 <= 8
	SOURCE:대상:사랑 += 700 + (ABL:대상:봉사정신 * 45)
ELSEIF ABL:대상:봉사정신 <= 11
	SOURCE:대상:사랑 += 1200 + (ABL:대상:봉사정신 * 45)
ELSE
	SOURCE:대상:사랑 += 1800 + (ABL:대상:봉사정신 * 20)
ENDIF

;호감 도를 본다
IF CFLAG:대상:2 <= 500
	SOURCE:대상:환락 += CFLAG:대상:2
ELSEIF CFLAG:대상:2 <= 5000
	SOURCE:대상:환락 += 750 + (CFLAG:대상:2 - 500) / 3
ELSE
	SOURCE:대상:환락 += 2000 + (CFLAG:대상:2 - 5000) / 4
ENDIF
SIF SOURCE:대상:환락 < 0
	SOURCE:대상:환락 = 0
SOURCE:대상:수동 = 200 + 110 * ABL:대상:순종
SOURCE:대상:정복 = 200 + 110 * ABL:대상:새드끼

SIF TALENT:대식가
	TIMES SOURCE:대상:환락 , 1.50
ENDIF
;데운 술 부탁했을 경우
IF ARG:1 && CAN_DRINK(대상)
	TIMES SOURCE:대상:환락 , 1.10
	CALL RECOVER_PERMIL(대상, 50,"기력")
	SIF 대상 != MASTER
		CALL KIGEN_CHANGE(대상, 50,1)
	CALL DRUNKEN(대상, 52)
ENDIF

회복후체력 = BASE:대상:체력
회복후기력 = BASE:대상:기력
회복후정력 = BASE:대상:정력

;PRINTFORML 체력+{ABS(회복후체력 - 회복전체력)}(%CALLNAME:대상%)
;PRINTFORML 기력+{ABS(회복후기력 - 회복전기력)}(%CALLNAME:대상%)
;SIF ABS(회복후정력 - 회복전정력) > 0
;	PRINTFORML 정력+{ABS(회복후정력 - 회복전정력)}(%CALLNAME:대상%)
;PRINTFORMW 
TIME += 20
SIF ARG == 1
	TIME += 10

;-------------------------------------------------
;코가사 용
;-------------------------------------------------
@UNIQUE_COM_우산의수리
PRINTFORML 코가사에게 접는 우산을 고쳐달라고 했다
IF ITEM:망가진우산
	PRINTFORMW 「좀 더 소중하게 써주라」
	DOWNBASE:기력 = 200
	SOURCE:환락 += 1000
	ITEM:망가진우산 = 0
	ITEM:접는우산 = 1
	TIME += 45
ELSE
	PRINTFORMW 「소중하게 써줘」
	DOWNBASE:기력 = 100
	SOURCE:환락 += 1000 + ABL:친밀 * 50
	TFLAG:호감도보너스 = FLAG:우산내구도 / 10
	TFLAG:98 = FLAG:우산내구도 / 1000
	FLAG:우산내구도 = 0
ENDIF
SETBIT TFLAG:일일일회, 13
TIME += 30

;-------------------------------------------------
;에이린용
;-------------------------------------------------
@UNIQUE_COM_생약을판다
#DIM 후보번호
#DIM 후보개수
#DIM 매각개수

PRINTFORML 소지금:\\%TOSTR(MONEY,"#,###")%
DRAWLINE
;소지하고 있는 소재 아이템의 매각 가능물·개수를 리스트화
LOCAL:1 = 999
FOR LOCAL:0 ,600, 700
	IF UNIQUE_COM_HARBAL_SALES(LOCAL:0)
		IF ITEM:(LOCAL:0)
			LOCALS:0 = [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}    
			LOCAL:1 += STRLENS(LOCALS:0)
			SIF ITEM:(LOCAL:0) < 10
				LOCAL:1 ++
			IF LOCAL:1 > 90
				IF LOCAL:1 >= 999
				;最初のみ改行しない
				ELSE
					PRINTL 　
				ENDIF
				LOCAL:1 = 0 
				PRINT 　　
				ENDIF
			PRINTFORM [{LOCAL:0}] %ITEMNAME:(LOCAL:0), 16, LEFT%×{ITEM:(LOCAL:0)}
			SIF ITEM:(LOCAL:0) < 10
				PRINT  
			PRINT 　　
		ENDIF
	ENDIF
NEXT
IF ITEM:정액
	LOCALS:0 = [3009] %ITEMNAME:3009, 16, LEFT%×{ITEM:정액}    
	LOCAL:1 += STRLENS(LOCALS:0)
	SIF ITEM:정액 < 10
		LOCAL:1 ++
	IF LOCAL:1 > 90
		IF LOCAL:1 >= 999
		;最初のみ改行しない
		ELSE
			PRINTL 　
		ENDIF
		LOCAL:1 = 0 
		PRINT 　　
		ENDIF
	PRINTFORM [3009] %ITEMNAME:3009, 16, LEFT%×{ITEM:정액}
	SIF ITEM:정액 < 10
		PRINT  
	PRINT 　　
ENDIF
IF ITEM:정액콘돔
	LOCALS:0 = [3010] %ITEMNAME:3010, 16, LEFT%×{ITEM:정액콘돔}    
	LOCAL:1 += STRLENS(LOCALS:0)
	SIF ITEM:정액 < 10
		LOCAL:1 ++
	IF LOCAL:1 > 90
		IF LOCAL:1 >= 999
		;最初のみ改行しない
		ELSE
			PRINTL 　
		ENDIF
		LOCAL:1 = 0 
		PRINT 　　
		ENDIF
	PRINTFORM [3010] %ITEMNAME:3010, 16, LEFT%×{ITEM:정액콘돔}
	SIF ITEM:정액콘돔 < 10
		PRINT  
	PRINT 　　
ENDIF
PRINTL 
PRINTL 　　[100] 그만둔다
DRAWLINE
PRINTFORML 「무엇을 팔까?」
$INPUT_LOOP01
INPUT
후보번호 = RESULT

IF 후보번호 == 100
;100は先送り、売却不能物、600未満および700以上の入力は無効化する
ELSEIF (UNIQUE_COM_HARBAL_SALES(후보번호) == 0 || 후보번호 < 600 || 후보번호 > 699) && !GROUPMATCH(후보번호,3009,3010)
	CLEARLINE 1
	GOTO INPUT_LOOP01
ENDIF
SELECTCASE 후보번호
	CASE 100
		RETURN -1
	CASEELSE
		;★売却可能な素材を上のCASEに追加
		후보번호 = RESULT
		후보개수 = ITEM:(후보번호)
		CLEARLINE 1
		IF 후보개수
			SELECTCASE 후보번호
				CASE 601, 612, 635
					PRINTFORML 「%조사처리(ITEMNAME:후보번호,"라")%. 강한 독을 가지고 있기 때문이야말로, 사용 방법에 따라서는……말야.
				CASE 603, 618
					PRINTFORML 「%조사처리(ITEMNAME:후보번호,"라")%. 강장의 생약으로서 유명하지만……후후, 어떻게 사용했으면 하는걸까?
				CASE 605, 624
					PRINTFORML 「%조사처리(ITEMNAME:후보번호,"라")%. 평범한 사람이 이것에 손을 대면 쓸모가 없게 되니까 주의하도록 해.
				CASE 634
					PRINTFORML 「%조사처리(ITEMNAME:후보번호,"라")%. 발굴까지 해내다니, 꽤 재주가 있네.
				CASE 646
					PRINTFORML 「%조사처리(ITEMNAME:후보번호,"라")%. 벌목까지 해내다니, 꽤 재주가 있네.
				CASE 606, 609, 615, 625, 640, 643
					PRINTFORML 「%ITEMNAME:후보번호%…!   잘도 손에 넣었군요, 이런 것.
				CASE 3009, 3010
					PRINTFORML 「정액...그래, 특수한 약재로도 필요하지.
				CASEELSE
					PRINTFORM 「%조사처리(ITEMNAME:후보번호,"라")%.
			ENDSELECT
			PRINTFORM 　\\{(ITEMPRICE:(후보번호) / 2)}로 매입할건데, 어떻게 할래?」
			SETCOLOR 0x777777
			PRINTFORML   몇개 팝니까? (1～{후보개수}의 개수를 입력, 0으로 돌아온다)
			RESETCOLOR
			PRINTL   
			PRINTBUTTON "[0] 그만둔다", 0
			PRINT     
			PRINTBUTTON "[1] 1개", 1
			PRINT     
			IF 후보개수 >= 5
				PRINTBUTTON "[5] 5개", 5
				PRINT     
			ENDIF
			IF 후보개수 >= 10
				PRINTBUTTON "[10] 10개", 10
				PRINT    
			ENDIF
			$INPUT_LOOP02
			INPUT
			SELECTCASE RESULT
				CASE 0
					RESTART
				CASE 1 TO 후보개수
					CLEARLINE 2
					매각개수 = RESULT
					PRINTFORMW 「고마워. 그러면 %조사처리(ITEMNAME:후보번호,"를")% {매각개수}개, \\{(ITEMPRICE:(후보번호) / 2 * 매각개수)}로 매입할게」
					MONEY += (ITEMPRICE:(후보번호) / 2 * 매각개수)
					ITEM:(후보번호) -= 매각개수
					RESTART
				CASEELSE
					CLEARLINE 1
					GOTO INPUT_LOOP02
			ENDSELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP01
		ENDIF
ENDSELECT
RESTART

;売却リストに載せないアイテムを定義
@UNIQUE_COM_HARBAL_SALES(ARG)
#FUNCTION
SELECTCASE ARG
	;★ここに売却リストに載せないアイテムを記入
	CASE 600, 602, 610, 611, 616, 621, 626 TO 631, 636, 638, 639, 642, 644, 645, 647, 648, 660 TO 669
		RETURNF 0
	;まだ未登録のアイテム
	CASE 649 TO 659, 670 TO 699
		RETURNF 0
	CASEELSE
		RETURNF ARG
ENDSELECT

;-------------------------------------------------
;우루미 용
;-------------------------------------------------
@UNIQUE_COM_물고기교환
#DIM PREV_DAY
#DIM CNG_NUM

IF PREV_DAY == DAY
	PRINTFORMW 「뭔디? 오늘은 벌써 교환해 줬잖어?」
	RETURN
ENDIF
CNG_NUM = GET_INT(0, "캐릭터데이터", TARGET, "고유커맨드：물고기교환비율")

PRINTFORML 「맛있는 물고기가 갖고 싶어?」
PRINTFORML 「그러네에。그냥 물고기 {CNG_NUM}마리라면 교환해도 좋아」
PRINTFORML 「어쩔래？」
CALL ASK_M("그만둔다", 1, "교환받는다", ITEM:물고기 >= CNG_NUM)
SELECTCASE RESULT
CASE 0
	PRINTFORML 「고런가」
CASE 1
	PRINTFORML 「매번감사。한나、둘、서이……짜、확실히 수는 맞네」
	PRINTFORML 「자、가져 가라!」
	PREV_DAY = DAY
	CALL LOST_ITEM("물고기", CNG_NUM, 1)
	CALL GET_ITEM("맛있는물고기", 1, 2)
ENDSELECT


@UNIQUE_COM_FISH_CHANGE_RATE
#FUNCTION
RETURNF 


;-------------------------------------------------
;에이카용
;-------------------------------------------------
@UNIQUE_COM_오늘의돌쌓기테마

	PRINTFORMW 「오늘 돌멩이 콘테스트의 커플 부문 주제를 알고 싶어？」
IF STONE_PLAYCOUNT:2 == 16 && (STONE_UKETSUKEDAY + 14 - DAY:0) > 0 && (STONE_UKETSUKEDAY + 14 - DAY:0) < 15
	PRINTFORMW 「아쉽지만…{STONE_UKETSUKEDAY + 14 - DAY:0}일 후까지 돌멩이 콘테스트는 휴식이야~」
	PRINTFORMW 「그러니까……그건 %조사처리(CALLNAME:MASTER,"가")% 제일 잘 알고 있잖아？」
ELSE
	SELECTCASE DAY:3
		CASE 1
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌은 봄바람과 함께』……봄이 주제네」
			PRINTFORML
			PRINTFORML 「봄을 좋아하는 애가 잘해. 반대로 여름, 가을, 겨울을 좋아하는 아이는 싫어하는 경향이 있는것 같아…」
			PRINTFORMW 「봄의 이미지가 강한, 꽃을 좋아하는 아이도 잘할지도 몰라？」
			PRINTFORMW 「사계절 테마는 정기적으로 하고 있기 때문에 이 계절이 서툴러도 다른 계절에 도전해 주었으면 좋겠어」
		CASE 2
			PRINTFORMW 「오늘의 콘테스트명은『쌓아 올릴 접시가 한 장 모자라다…』……접시가 주제네」
			PRINTFORML
			PRINTFORML 「나 이외의 미즈코는 밥을 먹지 않기 때문에 식기는 쌓아 올리기 위한 것 밖에 없지~」
			PRINTFORMW 「식기나 접시를 좋아하는 아이가 특기네. 나이프나 접시 이외의 식기를 좋아하면 영향을 주지 않을 것 같아」
			PRINTFORMW 「그리고 요리를 잘하거나 접시를 잘 다루는 메이드 분들도 유리한 것 같아」
		CASE 3
			PRINTFORMW 「오늘의 콘테스트명은『이번에 쌓은 돌은 무섭다』……호러가 주제네」
			PRINTFORML
			PRINTFORML 「이봐, 미즈코의 존재가 애당초 호러라고 말하진 않았다구?」
			PRINTFORMW 「뭐 알기 쉽게 피라던가 어둠이라던가 피라던가 그런걸 좋아하면 잘하는것 같아」
			PRINTFORMW 「참고로 우산의 요괴가 도전한 적이 있는데 그 아이는 테마 가점은 없었어」
		CASE 4
			PRINTFORMW 「오늘의 콘테스트명은『제목 없는 돌멩이 콘테스트』……음악이 주제네」
			;일본의 「題名のない音楽会」, 제목 없는 음악회의 패러디? 로 추정.
			PRINTFORML
			PRINTFORML 「돌을 쌓으면서 노래를 부르거나 연주해 달라고 하는거야」
			PRINTFORMW 「그러니까 당연히 음악이나 악기가 특기거나、좋아한다면 평가가 올라가~」
		CASE 5
			PRINTFORMW 「오늘의 콘테스트명은『유메카와 메르헨 돌쌓기』……메르헨이 주제네」
			PRINTFORML
			PRINTFORML 「귀여운 것을 좋아하면 잘하는 것 같아。반대로 멋있는 것이나 무서운 것을 좋아하면 조금 서투를 수 있어」
			PRINTFORMW 「귀여운 것을 좋아한다고 부끄러워서 말을 못하는 아이라도 하트나 핑크를 좋아한다면 안전해」
			PRINTFORMW 「세세한 부분이라고… '유메카와'라는 이름 그대로 실은 꿈 요소도 조금 가점 대상이네」
		CASE 6
			PRINTFORMW 「오늘의 콘테스트명은『미즈코에게도 의상을』……옷감(布飾)이 테마네」
			PRINTFORML
			PRINTFORML 「옷감으로, 쌓은 돌을 장식하는 데코레이션 대회야」
			PRINTFORMW 「인형이나 봉제인형을 좋아하면 좀 잘하는 것 같아」
			PRINTFORMW 「참고로 바늘을 좋아해도 잘하는 경향이 있는데… 그 바늘은 정말로 장식용인가？」
		CASE 7
			PRINTFORMW 「오늘의 콘테스트명은『무지개빛으로 쌓아올려』……컬러풀이 테마네」
			PRINTFORML
			PRINTFORML 「특정 색상뿐만 아니라 컬러풀한 것이 조건이기 때문에 특별한 미적 센스가 요구된다는 평판이야」
			PRINTFORMW 「좋아하는 색깔인가…… 그렇다기보다는 싫어하는 색이 없는 사람이 평가를 올리기 쉽지 않을까?」
		CASE 8
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌은 여름바람과 함께』……여름이 테마야」
			PRINTFORML
			PRINTFORML 「여름을 좋아하는 애가 잘해. 반대로 봄, 가을, 겨울을 좋아하는 아이는 싫어하는 경향이 있는것 같아…」
			PRINTFORMW 「여름의 이미지가 강한 꽃을 좋아하는 아이도 잘할지도 몰라？」
			PRINTFORMW 「사계절 테마는 정기적으로 하고 있기 때문에 이 계절이 서툴러도 다른 계절에 도전해 주었으면 좋겠어」
		CASE 9
			PRINTFORMW 「오늘의 콘테스트명은『대륙풍 돌쌓기』……마치 대륙풍이 테마네」
			PRINTFORML
			PRINTFORML 「당연히 일본식이나 서양식을 좋아하면 잘 못하는 것 같아」
			PRINTFORMW 「우우우… 대륙풍이라고 하면… 그 귀여운 사선의 이미지가…」
		CASE 10
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌 동물원』……동물이 테마야」
			PRINTFORML
			PRINTFORML 「동물 같은 돌멩이 작품을 만드는 것은 알기 쉽고 인기 있는 주제야」
			PRINTFORMW 「주의점으로는 알기 쉽고, 동물 이미지의 사족보행 동물과, 새같은 것과, 벌레나 물고기는 각각 점수가 다르다는 점일까?」
			PRINTFORMW 「동물의 동료라는 것을 알면서도 벌레를 동물에 넣고 싶지 않아？」
		CASE 11
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌 배틀！！』……싸움이 주제네」
			PRINTFORML
			PRINTFORML 「싸움이 주제니까… 싸움이나 칼이나 총을 잘 다루는 사람이 잘할까나ー」
			PRINTFORMW 「반대로 칼이나 총, 피비린내를 싫어하는 우리 미즈코한테는 별로 잘하는 주제가 아닐수도 있어」
			PRINTFORMW 「참고로 칼과 총 중 하나는 잘하지만 다른 하나는 잘 못할 것……같은 애는 걱정하게 될지도 모르지만…」
			PRINTFORMW 「가점과 감점이 합쳐져 득점 없음! …같은 일은 없으니까 안심해. 확실히 가점이 우선이니까」
		CASE 12
			PRINTFORMW 「오늘의 콘테스트명은『ROCK TO THE FUTURE』……미래가 주제네」
			PRINTFORML
			PRINTFORML 「뭐, 미즈코에게 미래따윈 없지만…」
			PRINTFORMW 「기계장치나 미래같은 걸 자아내는 게 특기라면 유리하겠지만、반대로 레트로를 좋아하는 사람은 고전할 것 같아」
			PRINTFORMW 「뭐, 미즈코에게는 과거라는 것도 거의 존재하지 않지만…」
		CASE 13
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌은 가을바람과 함께』……가을이 테마네」
			PRINTFORML
			PRINTFORML 「가을을 좋아하는 애가 잘해. 반대로 봄,여름,겨울을 좋아하는 아이는 싫어하는 경향이 있는것 같아…」
			PRINTFORMW 「가을 이미지가 강한 잎사귀를 좋아하는 아이도 잘할지도 모르지」
			PRINTFORMW 「사계절 테마는 정기적으로 하고 있기 때문에 이 계절이 서툴러도 다른 계절에 도전해 주었으면 좋겠어」
		CASE 14
			PRINTFORMW 「오늘의 콘테스트명은『ONCE UPON TSUMIISHI』……복고풍이 테마네」
			PRINTFORML
			PRINTFORML 「뭐, 미즈코에게 과거란 거의 없지만…」
			PRINTFORMW 「잃어버렸거나 옛적을 자아내는 게 특기라면 유리하겠지만、반대로 미래적인 것을 좋아하는 사람은 고전하게 되는 것 같아」
			PRINTFORMW 「뭐, 미즈코에게는 미래라는 것도 존재하지 않지만…」
		CASE 15
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌 마켓』……고급이 주제네」
			PRINTFORML
			PRINTFORML 「쌓은 돌 바이어가 채점 담당으로, 비싼 값을 치러서라도 사고 싶은 작품을 고르는 거야」
			PRINTFORMW 「역시 비싼걸 평소에 좋아하는 사람일수록 잘 만드는것 같아」
			PRINTFORMW 「앗!  비싼 것을 좋아한다 해도, 빈곤신만큼은 그만두는 것이 좋다고 생각해…」
		CASE 16
			PRINTFORMW 「오늘의 콘테스트명은『울트라 슈퍼디럭스 쌓은 돌』……화려함이 주제네」
			PRINTFORML
			PRINTFORML 「원래 조심스레 쌓아야 할 돌멩이를 화려하게 만든다는 것은 어떻게 보면 싸움을 거는 주제지」
			PRINTFORMW 「화려한 것을 좋아한다면 유리하겠지만, 심플하거나 섬세한 것을 좋아하면 싫어하는 경향이 있어」
			PRINTFORMW 「그리고 흑색, 백색, 회색 같은 심플한 색을 선호하는 사람도 이 주제만큼은 싫어하는 것 같아…」
		CASE 17
			PRINTFORMW 「오늘의 콘테스트명은『일본식 쌓은 돌』……그대로 일본식이 테마네」
			PRINTFORML
			PRINTFORML 「당연히 대륙풍이나 서양식을 좋아하면 잘 못하는 것 같아」
			PRINTFORMW 「삼도천도 취급적으로는 일본풍…이라는 걸까?」
		CASE 18
			PRINTFORMW 「오늘의 콘테스트명은『돌과 친해지자』……그대로 광석이 테마네」
			PRINTFORML
			PRINTFORML 「쌓는 돌로 취급하는 일반적인 돌뿐만 아니라, 여러가지 돌을 사용해서 쌓아보자！」
			PRINTFORMW 「보석을 훔치려고 하면 지장님께 짓눌리니까 그만두는 것을 추천할게」
		CASE 19
			PRINTFORMW 「오늘의 콘테스트명은『로지컬 돌쌓기 시스템』……지성이 주제네」
			PRINTFORML
			PRINTFORML 「똑똑한 사람이 진심으로 현명하게 만든다면 도대체 어떤 작품이 만들어질까…라는 발상으로 태어난 테마야」
			PRINTFORMW 「똑똑한 사람은 물론, 책을 즐겨 읽는 사람도 잘하는 것 같아」
			PRINTFORMW 「나도 돌쌓기 관련 책이 있으면 열심히 읽을텐데ー」
		CASE 20
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌은 겨울바람과 함께』……겨울이 테마야」
			PRINTFORML
			PRINTFORML 「겨울을 좋아하는 애가 잘해. 반대로 봄,여름,가을을 좋아하는 아이는 싫어하는 경향이 있는 것 같아…」
			PRINTFORMW 「겨울 이미지가 강한, 얼음이나 눈을 좋아하는 애들도 잘할꺼야？」
			PRINTFORMW 「사계절 테마는 정기적으로 하고 있기 때문에 이 계절이 서툴러도 다른 계절에 도전해 주었으면 좋겠어」
		CASE 21
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌 콘테스트 커플부몬』……혼돈이 주제네」
			PRINTFORML
			PRINTFORML 「혼돈스러운 느낌을 좋아하는 애가 잘하고, 반대로 질서적인 느낌을 좋아하는 애가 싫어하는 것 같아」
			PRINTFORMW 「내일은 질서 관련 테마니까、이런게 서툴다면 주제가 바뀌는 내일까지 기다리는 방법도 있어」
		CASE 22
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌 콘테스트 커플부문』……질서가 테마야」
			PRINTFORML
			PRINTFORML 「질서있는 느낌을 좋아하는 애가 잘하고, 반대로 혼돈적인 느낌을 좋아하는 애가 싫어하는 것 같아」
			PRINTFORMW 「혼돈스러운 느낌을 좋아하는 사람은……어제였었지? 아쉽다」
		CASE 23
			PRINTFORMW 「오늘의 콘테스트명은『서양식 쌓은 돌』……그대로 서양식이 테마네」
			PRINTFORML
			PRINTFORML 「당연히 대륙풍이나 일본식을 좋아하면 잘 못하는 것 같아」
			PRINTFORMW 「서양식 쌓은 돌이라고 하면 스톤헨지라고 안경쓴 여자애가 말했었는데…」
			PRINTFORMW 「스톤헨지……첼린지해볼래?」
		CASE 24
			PRINTFORMW 「오늘의 콘테스트명은『쌓은 돌의 별이 되어라』……천체가 주제네」
			PRINTFORML
			PRINTFORML 「밤하늘이 테마인데 태양을 좋아해도 활약할 수 있어」
			PRINTFORMW 「다만 어둠은 천체의 이미지가 있을 것 같습니다만, 가점도 감점도 없기 때문에 주의해 주세요」
		CASE 25
			PRINTFORMW 「오늘의 콘테스트명은『물의 콘테스트』……물이 주제네」
			PRINTFORML
			PRINTFORML 「어떻게 보면 가장 친숙한 존재인 물이 주제야」
			PRINTFORMW 「수중생물을 좋아하는 사람이 많을지도 몰라?」
			PRINTFORMW 「덧붙여서 삼도천에 잠수하는 것은 절대로 그만두는 편이 좋아。목숨이 아깝다면 말이야」
		CASE 26
			PRINTFORMW 「오늘의 콘테스트명은『모리야 선물쌓기 놀이대회』……모리야가 주제네」
			PRINTFORML
			PRINTFORML 「오늘은 모리야 분들이 투자하고 있는 특별회… 그래서 회장이 광고로 꽉 찼어!」
			PRINTFORMW 「모리야는 범위가 좁다고 생각했는데, 바람이라든가 대지라든가 그리고 무녀라든가 신령을 좋아해도 잘하는 경향이 있는 것 같아」
			PRINTFORMW 「참고로 개구리와 뱀중 하나는 잘하지만 다른 하나는 잘 못해…같은 애는 걱정하게 될지도 모르지만…」
			PRINTFORMW 「가점과 감점이 합쳐져 득점 없음!…같은 일은 없으니까 안심해。확실히 가점이 우선이니까」
		CASE 27
			PRINTFORMW 「오늘의 콘테스트명은『매지컬 스톤 밸런싱』……마법사가 주제네」
			PRINTFORML
			PRINTFORML 「그대로 마법이나 마녀에 정통한 사람이 물론 강하긴 하지만…」
			PRINTFORMW 「평가하는 건 우리 미즈코니까! 까마귀나 빗자루만으로도 마녀 취급할 거야」
			PRINTFORMW 「뭐라면 책만으로도 마법사 판정을 해버릴지도 몰라」
		CASE 28
			PRINTFORMW 「오늘의 콘테스트명은『바니의 날』……말 그대로 바니가 주제네」
			PRINTFORML
			PRINTFORML 「매월 28일은 바니의 날! 그래서 거기에 맞춘 기획인데……」
			PRINTFORMW 「도대체 뭘까? 바니의 날은…… 나도 거기에 맞춰서 입었고……」
			PRINTFORMW 「엣또…… 일단 바니에 관련된 것을 좋아하는 사람이 유리해」
			PRINTFORMW 「달이라거나…… 그리고 카드라던가…… 메달이라던가……」
			PRINTFORMW 「그리고 부끄러운 복장을 못하는 사람은 안될 것 같아」
		CASE 29
			PRINTFORMW 「오늘의 콘테스트명은『자연의 마리아주, 돌과 식물의 조화』……식물이 테마야」
			PRINTFORML
			PRINTFORML 「식물자체를 좋아하면 너무 좋고, 그렇지 않아도 꽃이나 식물을 뭔가 좋아했으면 하는 느낌이야」
			PRINTFORMW 「덧붙여서 버섯은 식물이 아닙니다! 쟌넨!」
		CASE 30
			PRINTFORMW 「오늘의 콘테스트명은『자연의 마리아주, 하늘과 바람의 조화』……바람이 테마야」
			PRINTFORML
			PRINTFORML 「바람 그 자체나 하늘을 나는 생물을 좋아하면 유리한 것 같아」
			PRINTFORMW 「그냥 바람이 아니라 하늘이라는 부분도 중요하니까 땅속을 좋아하는 사람은 고전할지도 모르겠네」
			PRINTFORMW 「천체를 좋아하는 것은 별로 보정이 걸리지 않기 때문에 그것만큼은 주의해야 할지도」
		CASE 31
			PRINTFORMW 「오늘은 무엇이든 특별한 날！」
			PRINTFORML
			PRINTFORML 「다른 날의 주제 중에 하나 골라서, 그 주제로 돌을 쌓을 수 있어」
			PRINTFORMW 「우승하면 그 지정한 날의 테마로 우승한 것이 되는 거야」
			PRINTFORMW 「즉, 깜빡 잊고 지냈다면 다음은 31일 후가 되지 않기 위한 구제조치네!」
	ENDSELECT
ENDIF

;-------------------------------------------------
;용주 - 메구무용
;-------------------------------------------------
@UNIQUE_COM_용주의거래
#DIM ORB_PRICE
#DIM SALES

IF !ITEM:용주
	PRINTFORML 용주를 가지고 있지 않다…
	RETURN -1
ENDIF

;売価はカリスマ取引レート×90×√話術Lvで決定
ORB_PRICE = CHARISMA_NOW() * 90
SELECTCASE ABL:MASTER:화술기능
	CASE 0
		TIMES ORB_PRICE, 1.0
	CASE 1
		TIMES ORB_PRICE, 1.2
	CASE 2
		TIMES ORB_PRICE, 1.4
	CASE 3
		TIMES ORB_PRICE, 1.7
	CASE 4
		TIMES ORB_PRICE, 2.0
	CASE 5
		TIMES ORB_PRICE, 2.2
	CASE 6
		TIMES ORB_PRICE, 2.4
	CASEELSE
		TIMES ORB_PRICE, 2.6
ENDSELECT

PRINTL 
PRINTDATAW
	DATAFORM 「가지고 있기 싫다면、내가 사지。물론、그만한 대가는 치르겠다」
	DATAFORM 「응? 그런 걸 사들여서 어쩔거냐고? 그것 참、뭐랄까?」
	DATAFORM 「어디서 구해온 것인지는 모르겠지만。뭐、그 물건, 어딘가에 쓸모가 있을지도 모른다」
	DATAFORM 「그걸 손에 쥐고 있다는 건、그 녀석에게 지켜봐지고 있다는 것인가。거참、사람은 겉보기와는 다른 것이군」
	DATAFORM 「누군가에게 부추겨진…것도 아닌 것 같다。그럼、거래라고 할까」
	DATAFORM 「희토류의 공급원은 많은 것보다 더 좋을 것도 없고。리스크 헤지(위험분산)、라는 거다」
ENDDATA
PRINTFORML 「용주1개를\\{ORB_PRICE}으로 매입하지。 어때?」
PRINTL 
DRAWLINE
PRINTFORML 소지금: %금액표시(MONEY)%　　현재의₩/카리스마 레이트: %금액표시(CHARISMA_NOW())%←→1CM
SETCOLOR 0x777777
PRINTFORML 　몇 개 파시겠습니까? (1~{ITEM:용주}의 개수를 입력、0으로 돌아간다)
RESETCOLOR
PRINTL   
PRINTBUTTON "[0] 그만둔다", 0
PRINT     
PRINTBUTTON "[1] 1개", 1
PRINT     
IF ITEM:용주 >= 5
	PRINTBUTTON "[5] 5개", 5
	PRINT     
ENDIF
IF ITEM:용주 >= 10
	PRINTBUTTON "[10] 10개", 10
	PRINT    
ENDIF
$INPUT_LOOP01
INPUT
SELECTCASE RESULT
	CASE 0
		CLEARLINE 2
		PRINTFORMW 「그런가。거래액은 카리스마 거래율에 따른 시가니까、마음만 먹으면 또 와주게」
		RETURN
	CASE 1 TO ITEM:용주
		CLEARLINE 2
		SALES = RESULT
		PRINTFORMW 「그러면 용주를 {SALES}개、\\{ORB_PRICE * SALES}으로 매입하도록 하지」
		CALL GET_MONEY_YEN(ORB_PRICE * SALES, 1)
		CALL LOST_ITEM("용주", SALES, 2)
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP01
ENDSELECT