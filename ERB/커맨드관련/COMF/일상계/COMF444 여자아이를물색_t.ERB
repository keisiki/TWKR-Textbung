;-------------------------------------------------
;여자아이를 물색
;일상계 커맨드, 레벨 1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;생성하는 MOB소녀의 종류

IF FLAG:데이트상대
	PRINTFORMW 어마어마하게 귀여운 아가씨가 바로 옆에 있다
	RETURN -1
ENDIF
;잠복モード中は不可
IF GETBIT(FLAG:잠복,0)
	PRINTFORMW 다른 아이한테 시선을 돌릴 처지가 아니다
	RETURN -1
ENDIF

$BUSSYOKU
IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %마스터는% 귀여운 아가씨가 없는지 물색했다
PRINTFORMW ……저 아이는 어떨까?
SETBIT TFLAG:일일일회, 9
SELECTCASE CAN_NANPA(CFLAG:MASTER:현재위치)
	CASE 1
		YUKIZURI_RACE '= TEXTR("마을 아가씨/유부녀")
	CASE 2
		YUKIZURI_RACE = 요정
	CASE 3
		YUKIZURI_RACE = 메이드 요정
	CASE 4
		;３日に一度販売員が出没
		IF ((GETTIME()/1000000000) % 100) % 3 == 1 && !RAND:7
			YUKIZURI_RACE = 販売員
		ELSE
			YUKIZURI_RACE = 이나바
		ENDIF
	CASE 5
		YUKIZURI_RACE = 좀비 페어리
	CASE 6
		YUKIZURI_RACE '= TEXTR("둔갑여우/둔갑고양이")
	CASE 7
		YUKIZURI_RACE = 캇파
	CASE 8
		YUKIZURI_RACE = 백랑텐구
	CASE 9
		IF !RAND:10
			YUKIZURI_RACE = 오니
		ELSE
			YUKIZURI_RACE = 좀비 페어리
		ENDIF
	CASE 10
		YUKIZURI_RACE = 옥토끼
	CASE 11
		YUKIZURI_RACE '= TEXTR("둔갑너구리/茨たぬき")
ENDSELECT



;PRINTFORMW %YUKIZURI_RACE%
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)
TEQUIP:RANDOM_CHARANUM:상반신착의상황 = 1
$CHOICE
CALL CHARA_STATE(RANDOM_CHARANUM)
IF CSTR:RANDOM_CHARANUM:처녀상실이력 != ""
	DRAWLINE
	CALL COLORMESSAGE(@"처녀상실이력：%CSTR:RANDOM_CHARANUM:처녀상실이력%",C_PINK,1)
	PRINTFORML 
ENDIF
CFLAG:RANDOM_CHARANUM:현재위치 = SUKIMA()
DEBUGPRINTFORML %CSTR:RANDOM_CHARANUM:모브코화상문자열_옷_통상%
CALL ASK_M("마음이 간다", 1,"다른 아가씨를 찾는다", 1,"직전의 아가씨를 찾는다", TFLAG:직전모브코맵 == CFLAG:MASTER:현재위치 /100,"이전 알게 된 아가씨를 찾는다", 1,"물색 중지", 1)
IF RESULT == 4
	CFLAG:RANDOM_CHARANUM:현재위치 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:갈아입음실행 = 0
	CFLAG:RANDOM_CHARANUM:출금 = 2
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:직전모브코맵 = CFLAG:MASTER:현재위치 /100
	SAVECHARA "TEMP_MOB", "직전에 만난 아이", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL(0)
	IF RESULT == -1
		;만약을 위해 신품으로 해 둔다
		CALL 모브코화상리셋(RANDOM_CHARANUM)
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		RETURN -1
	ENDIF
	CFLAG:RANDOM_CHARANUM:출금 = 0
ELSEIF RESULT == 2
	CALL 모브코화상리셋(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:직전모브코맵 = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:현재위치 = CFLAG:MASTER:현재위치
CFLAG:RANDOM_CHARANUM:데이트중 = MAIN_MAP

IF CFLAG:MASTER:현재위치 / 100 == MAIN_MAP
	;拠点内で시간정지せずに物色した場合は、동행扱い
	IF !FLAG:70
		CFLAG:RANDOM_CHARANUM:동행 = 180
		CFLAG:MASTER:동행 = 180
	ENDIF
	CFLAG:RANDOM_CHARANUM:거점내방 = 1
	CFLAG:RANDOM_CHARANUM:만난위치 = 0
	;CFLAG:RANDOM_CHARANUM:자택위치 = 0
	CFLAG:RANDOM_CHARANUM:내방시간 = CSVCFLAG(RANDOM_CHARANUM,352)
	CFLAG:RANDOM_CHARANUM:귀가시간 = CSVCFLAG(RANDOM_CHARANUM,353)
	CFLAG:RANDOM_CHARANUM:취침시간 = CSVCFLAG(RANDOM_CHARANUM,354)
	CFLAG:RANDOM_CHARANUM:기상시간 = CSVCFLAG(RANDOM_CHARANUM,355)
ELSE
	;引っ越ししたりして定義しなおす場合もあるので、毎回確認
	CFLAG:RANDOM_CHARANUM:거점내방 = 0
	CFLAG:RANDOM_CHARANUM:만난위치 = CFLAG:MASTER:현재위치
	;CFLAG:RANDOM_CHARANUM:자택위치 = CFLAG:MASTER:현재위치
	CFLAG:RANDOM_CHARANUM:내방시간 = 0
	CFLAG:RANDOM_CHARANUM:귀가시간 = 0
	CFLAG:RANDOM_CHARANUM:취침시간 = 0
	CFLAG:RANDOM_CHARANUM:기상시간 = 0
ENDIF
CFLAG:RANDOM_CHARANUM:데일리 = 30
TARGET = RANDOM_CHARANUM
CALL 능력치세팅(TARGET)
RETURN 1


@COM_ABLE444
;일괄관리
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA(CFLAG:MASTER:현재위치)
	RETURN 0
SIF !INRANGE(TIME, 540,1140)
	RETURN 0
; SIF !CFLAG:RANDOM_CHARANUM:출금
; 	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

@CAN_NANPA(ARG)
#FUNCTION

SIF ARG == GET_MAP_REPLACEMENT(202)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(438)
	RETURNF 2
SIF ARG == GET_MAP_REPLACEMENT(309)
	RETURNF 3
SIF ARG == GET_MAP_REPLACEMENT(415)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(416)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(431)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(434)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(927)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(928)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(601)
	RETURNF 6
SIF ARG == GET_MAP_REPLACEMENT(704)
	RETURNF 7
SIF ARG == GET_MAP_REPLACEMENT(806)
	RETURNF 8
SIF ARG == GET_MAP_REPLACEMENT(938)
	RETURNF 9
SIF ARG == GET_MAP_REPLACEMENT(1003)
	RETURNF 10
SIF ARG == GET_MAP_REPLACEMENT(540)
	RETURNF 11

RETURNF 0


@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		SIF MOBGIRL_PLACE:LOCAL == CFLAG:MASTER:현재위치 / 100
			SETCOLOR C_P_GREEN
		SIF MOBGIRL_PLACE:LOCAL == 90
			SETCOLOR C_PINK
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[없음]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[그만둔다]", 99

@LOAD_MOBGIRL(ARG)
CALL PRINT_SAVED_GIRL_LIST
INPUT
LOCAL = RESULT
SIF !INRANGE(LOCAL, 0, MAXMOB -1)
	RETURN -1
IF !ARG && MOBGIRL_PLACE:LOCAL != CFLAG:MASTER:현재위치 / 100
	IF MOBGIRL_PLACE:LOCAL == 90
		PRINTFORMW 가게 밖에서는 만나 주지 않는다…
	ELSE
		PRINTFORML {MOBGIRL_PLACE:LOCAL}
		PRINTFORMW 여기에는 없을 것이다
	ENDIF
	RETURN -1
ELSEIF ARG && MOBGIRL_PLACE:LOCAL != 90
	PRINTFORMW 「하아！？　그런 암컷 따위、여기에는ー없ー어！」
	PRINTFORML 
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(LOCAL))
	CALL 모브코화상리셋(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(LOCAL)
	PRINTFORML %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% \@ ARG ? 지명했다 # 찾았다 \@
	IF MOBGIRL_FP:LOCAL != ""
		モブ子一人称 = %MOBGIRL_FP:LOCAL%
		モブ子二人称 = %MOBGIRL_SP:LOCAL%
	ENDIF
	IF CSTR:RANDOM_CHARANUM:모브코화상문자열_옷_통상 == ""
		PRINTFORML 외견이 미설정입니다. 타입을 골라 주세요
		PRINTBUTTON "[마을 아가씨] ", "마을 아가씨"
		PRINTBUTTON "[유부녀] ", "유부녀"
		PRINTBUTTON "[요정] ", "요정"
		PRINTBUTTON "[메이드 요정] ", "메이드 요정"
		PRINTBUTTON "[좀비 페어리] ", "좀비 페어리"
		PRINTBUTTON "[캇파] ", "캇파"
		PRINTBUTTON "[백랑텐구] ", "백랑텐구"
		PRINTBUTTON "[이나바] ", "이나바"
		PRINTBUTTON "[옥토끼] ", "옥토끼"
		PRINTBUTTON "[오니] ", "오니"
		PRINTBUTTON "[둔갑너구리] ", "둔갑너구리"
		PRINTBUTTON "[둔갑여우] ", "둔갑여우"
		PRINTBUTTON "[둔갑고양이] ", "둔갑고양이"
		PRINTBUTTON "[랜덤] ", "랜덤"
		INPUTS
		CALL SET_MOB_IMAGE_STR(RANDOM_CHARANUM, RESULTS)
	ENDIF
	;過去に合成されて裸画像の笑顔に[装飾_照れ]が乗ってない場合に追記
	SIF STRFIND(CSTR:RANDOM_CHARANUM:모브코화상문자열_알몸_미소, "装飾_照れ") == -1
		CSTR:RANDOM_CHARANUM:모브코화상문자열_알몸_미소 += "[装飾9:装飾_照れ][乗色:朱色]"
;過去（ver4.760以前）に保存されて種族のTALENT設定が適合しない場合に修正を行う
	SELECTCASE CAN_NANPA(CFLAG:MASTER:현재위치)
		;里
		CASE 1
			;以前の194 인간が現varでは194 유령になっている
			IF TALENT:RANDOM_CHARANUM:유령
				TALENT:RANDOM_CHARANUM:유령 = 0
				TALENT:RANDOM_CHARANUM:인간 = 1
			ENDIF
		;霧の湖、紅魔館、および地底の요정たち
		CASE 2, 3, 5
			;以前の190 요정が現verでは190 인간になっている
			IF TALENT:RANDOM_CHARANUM:인간
				TALENT:RANDOM_CHARANUM:인간 = 0
				TALENT:RANDOM_CHARANUM:요정 = 1
			ENDIF
		;竹林
		CASE 4
			;イナバ…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:요괴 != 6
				TALENT:RANDOM_CHARANUM:요괴 = 6
		;冥界
		CASE 6
			;化け狸、狐、猫…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:요괴 != 6
				TALENT:RANDOM_CHARANUM:요괴 = 6
		;요괴の山山麓
		CASE 7
			;河童…河童が新設された
			SIF TALENT:RANDOM_CHARANUM:요괴 != 4
				TALENT:RANDOM_CHARANUM:요괴 = 4
		;요괴の山山頂
		CASE 8
			;白狼天狗…以前の197 天狗が現verでは197 추가종족（巫女）になっている
			IF TALENT:RANDOM_CHARANUM:추가종족
				TALENT:RANDOM_CHARANUM:추가종족 = 0
				TALENT:RANDOM_CHARANUM:요괴 = 5
			ENDIF
		;地底　鬼か요정
		CASE 9
			;鬼…以前の195 鬼が現verでは195　츠쿠모가미になっている
			IF TALENT:RANDOM_CHARANUM:츠쿠모가미
				TALENT:RANDOM_CHARANUM:츠쿠모가미 = 0
				TALENT:RANDOM_CHARANUM:요괴 = 2
			;요정は同上
			ELSEIF TALENT:RANDOM_CHARANUM:인간
				TALENT:RANDOM_CHARANUM:인간 = 0
				TALENT:RANDOM_CHARANUM:요정 = 1
			ENDIF
		;月　一応分けておく
		CASE 10
			;玉兎…妖獣が新設された
			SIF TALENT:RANDOM_CHARANUM:요괴 != 6
				TALENT:RANDOM_CHARANUM:요괴 = 6
	ENDSELECT

	IF FLAG:70	;시간정지中は反応なし

	ELSEIF TALENT:RANDOM_CHARANUM:풍속양
	ELSEIF CFLAG:RANDOM_CHARANUM:패배자 && (TALENT:RANDOM_CHARANUM:섹프 || TALENT:RANDOM_CHARANUM:사모)
		PRINTFORMW %CALLNAME:MASTER%의 모습을 알아차린 %조사처리(CALLNAME:RANDOM_CHARANUM,"는")% 분한 듯이, 그러면서도 기대하는 듯한 표정으로 머뭇거리고 있다…
	ELSEIF CFLAG:RANDOM_CHARANUM:패배자 && !TALENT:RANDOM_CHARANUM:연모 && !TALENT:RANDOM_CHARANUM:사모 && !TALENT:RANDOM_CHARANUM:애욕
		PRINTFORMW %CALLNAME:MASTER%의 모습을 알아차린 %조사처리(CALLNAME:RANDOM_CHARANUM,"는")% 표정이 굳어졌다
	ELSEIF TALENT:RANDOM_CHARANUM:연모
		PRINTFORMW %CALLNAME:MASTER%의 모습을 알아차린 %조사처리(CALLNAME:RANDOM_CHARANUM,"는")% 기쁜 듯이 이쪽으로 달려왔다
	ELSEIF TALENT:RANDOM_CHARANUM:애욕
		PRINTFORMW %CALLNAME:MASTER%의 모습을 알아차린 %조사처리(CALLNAME:RANDOM_CHARANUM,"는")% 이쪽으로 오자마자 팔에 기대기 시작했다…
	ELSEIF ABL:RANDOM_CHARANUM:친밀 >= 5
		PRINTFORMW %CALLNAME:MASTER%의 모습을 알아차린 %조사처리(CALLNAME:RANDOM_CHARANUM,"는")% 손을 흔들었다
	ENDIF
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML %조사처리(MOBNAME,"를")% 잊어, 대신에 %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% 기억합니까?
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %마스터는% 몰인정하게도 %조사처리(MOBNAME,"를")% 깨끗히 잊고, 
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:자주가는지역
SIF TALENT:RANDOM_CHARANUM:풍속양
	MOBGIRL_PLACE:CHOSEN_MOB = 90
TALENT:RANDOM_CHARANUM:스침 = 2
CFLAG:RANDOM_CHARANUM:모브코세이브슬롯 = CHOSEN_MOB
MOBGIRL_FP:CHOSEN_MOB = %モブ子一人称%
MOBGIRL_SP:CHOSEN_MOB = %モブ子二人称%
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:모브코프로필1, RANDOM_CHARANUM
PRINTFORMW %조사처리(CALLNAME:RANDOM_CHARANUM,"를")% \@ TALENT:RANDOM_CHARANUM:풍속양 ? 마음에 두려고 킵했다 # 기억했다 \@


;-------------------------------------------------
;모브코프로필2を更新する
;패배자でなければ、その日の過ごし方の様子でプロフィール２を追記
;패배자の場合は、陥落状態に合わせて更新
;-------------------------------------------------
@MOBGIRL_PROFILE2()
#DIM 횟수

횟수 = EXP:RANDOM_CHARANUM:V성교경험 + EXP:RANDOM_CHARANUM:A성교경험

;패배자でない場合は、性的関係の進展でテキストを構成
IF CFLAG:RANDOM_CHARANUM:패배자 == 0 
	;出会って日が浅い
	IF DAY:0 - PRINTDATEF_TO_DAY(CSTR:RANDOM_CHARANUM:모브코프로필1) <= 30
		CSTR:RANDOM_CHARANUM:모브코프로필2 = 만난지 얼마 안 된 %CALLNAME:MASTER%
	ELSE
		CSTR:RANDOM_CHARANUM:모브코프로필2 = %CALLNAME:MASTER%
	ENDIF
	;にゅーぷらん
	IF TALENT:RANDOM_CHARANUM:풍속양
		SIF GROUPMATCH(TALENT:RANDOM_CHARANUM:풍속양, 1, 2) && CFLAG:RANDOM_CHARANUM:1001 == 3
			TALENT:RANDOM_CHARANUM:풍속양 += 2
		IF CFLAG:RANDOM_CHARANUM:1001  > 6
			LOCALS = 에게 갑자기 정이 들어、더 이상 일인지 진심인지 구분이 가지 않게 되었다
		ELSEIF CFLAG:RANDOM_CHARANUM:1001  > 3
			LOCALS = 에게 단순한 손님 이상의 감정을 가져、봉사를 해드릴 수 있다는 것에 기쁨을 느끼고 있다
		ELSE
			LOCALS = 를 단골 손님으로 간주하고 리피트 되는 지명을 기쁘게 생각하고 있다
		ENDIF
	;생해금
	ELSEIF CFLAG:RANDOM_CHARANUM:생해금
		IF TALENT:RANDOM_CHARANUM:유부녀 == 1
			LOCALS = 에게 열중해 버려, 남편이나 가정을 돌보지 않고 문란한 사이가 되어 있다
		ELSE
			LOCALS = %조사만처리(CALLNAME:MASTER,"를")% 암컷으로서 본능적으로 받아들여, 질내사정으로 아이를 배도 좋다고까지 느끼고 있다
		ENDIF
	;합의ックス
	ELSEIF GETBIT(CFLAG:RANDOM_CHARANUM:기정사실 , 1)
		IF TALENT:RANDOM_CHARANUM:유부녀 == 1
			LOCALS = %조사만처리(CALLNAME:MASTER,"를")% 주체할 수 없던 정욕의 배출구로 삼고 있는 것 같다
		ELSE
			LOCALS = 에게 끌려 몸을 허락하고 말았다
		ENDIF
	;수면간(一応)
	ELSEIF EXP:RANDOM_CHARANUM:수면간경험
		LOCALS = 의 계략에 빠져, 자는 사이에 몸을 좋아하게 되었다
	;합의なしックス
	ELSEIF CFLAG:RANDOM_CHARANUM:대주고말았다
		IF TALENT:RANDOM_CHARANUM:유부녀 == 1
			LOCALS = 의, 남편에게는 없는 매력에게 저항할 수 없는 것 같다
		ELSE
			LOCALS = %조사만처리(CALLNAME:MASTER,"와")% 흘러가는대로 음행에 이르고 말았다
		ENDIF
	;괘씸각인
	ELSEIF MARK:RANDOM_CHARANUM:괘씸각인
		IF TALENT:RANDOM_CHARANUM:유부녀 == 1
			LOCALS = 에게 안됀다고 생각하면서도 성적인 시선을 보내고 있다
		ELSE
			LOCALS = 에게 성적인 관심을 가지게 된 것 같다
		ENDIF
	;시간각인
	ELSEIF MARK:RANDOM_CHARANUM:시간각인
		LOCALS = 의 눈에 띄어, 멈춘 시간 속에서 몸을 개발당하고 말았다
	ELSE
		LOCALS = 의 인상은 그럭저럭인 것 같다
	ENDIF
	CSTR:RANDOM_CHARANUM:모브코프로필2 += LOCALS

;패배자の場合は、陥落強度に応じてテキストを構成
ELSE
	IF TALENT:RANDOM_CHARANUM:연모
		CSTR:RANDOM_CHARANUM:모브코프로필2 = 탄막놀이의 승패따위는 아무래도 상관없이, 이제는 %CALLNAME:MASTER%에게 마음 속 깊이 빠져있는 것 같다
	ELSEIF TALENT:RANDOM_CHARANUM:애욕
		IF GETBIT(TALENT:MASTER:성별, 1)
			IF 횟수 >= 75
				CSTR:RANDOM_CHARANUM:모브코프로필2 = %CALLNAME:MASTER%의 자지에 완전히 굴복해, 또 다시 범해지는 것을 이제나저제나 하고 기대하고 있다
			ELSE
				CSTR:RANDOM_CHARANUM:모브코프로필2 = %CALLNAME:MASTER%에게 완전히 굴복해, 또 다시 사랑받는 것을 이제나저제나 하고 기대하고 있다
			ENDIF
		ELSE
			CSTR:RANDOM_CHARANUM:모브코프로필2 = 과거의 위세는 어디로 갔는지, 마음도 몸도 %CALLNAME:MASTER%색으로 흠뻑 물들어 버렸다
		ENDIF
	ELSEIF TALENT:RANDOM_CHARANUM:사모
		CSTR:RANDOM_CHARANUM:모브코프로필2 = %CALLNAME:MASTER%의 완력뿐만 아니라 사람됨이 신경쓰이기 시작했다
	ELSEIF TALENT:RANDOM_CHARANUM:섹프
		CSTR:RANDOM_CHARANUM:모브코프로필2 = %CALLNAME:MASTER%에게 엉망으로 울려져 육욕의 포로가 되어버렸다
	ELSEIF MARK:RANDOM_CHARANUM:쾌락각인
		CSTR:RANDOM_CHARANUM:모브코프로필2 = 입으로는 부정하면서도 몸은 정직하게 쾌락에 반응하는 것 같다
	ENDIF
ENDIF

;-------------------------------------------------
;PRINT_DATE_Fの●年目 ■の★日の文字列を
;DAY:0（총날짜）と同じ日数に読み替える関数
;ARGS CSTR:RANDOM_CHARANUM:모브코프로필1を
;代入して、出会った日付を割り出す使用前提
;-------------------------------------------------
@PRINTDATEF_TO_DAY(ARGS)
#FUNCTION
#DIM YEAR
#DIM MOUTH
#DIMS 계절
#DIM DAYS
#DIMS 컷
#DIM TOTAL

SPLIT ARGS, "：", LOCALS
컷 = %LOCALS:1%
SPLIT 컷, "년째 ", LOCALS
컷 = %LOCALS:1%
YEAR = TOINT(LOCALS:0)
SPLIT 컷, "월", LOCALS
계절 = %LOCALS:0%
컷 = %LOCALS:1%
SPLIT 컷, "일", LOCALS
DAYS = TOINT(LOCALS:0)
SELECTCASE 계절
	CASE "봄"
		MOUTH = 0
	CASE "여름"
		MOUTH = 31
	CASE "가을"
		MOUTH = 62
	CASE "겨울"
		MOUTH = 93
ENDSELECT
TOTAL = (YEAR - 1) * 124 + MOUTH + DAYS
RETURNF TOTAL

