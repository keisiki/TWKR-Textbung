;-------------------------------------------------
;술벌레의 상태를 본다
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM450
#DIM 양조량
#DIM 술ID

VARSET LOCAL

술ID = ALC_ID()
양조량 = GET_INT(0, "술데이터", 술ID, "양조수")
LOCALS '= @"%조사처리(VAR_WATER,"와")% 바꿔넣는다"
LOCAL = 1
IF VAR_WATER == ""
	LOCALS '= "갈아넣을 물도 없이 퍼낼 수는 없다"
	LOCAL = 0
ENDIF
PRINTFORML %마스터는% 술벌레가 들어있는 병을 들여다보았다
PRINTFORML 병은 %조사처리(VAR_FERMENT,"으로")% 채워져 있다…
DRAWLINE
{
CALL ASK_M("아무것도 안 한다",1,@"%LOCALS%", LOCAL,"아이템을 더한다",1,
"시간조작 주조（TSP2400소비）",BASE:MASTER:8 >= 2400,"대량 가속(TSP 6000소비)", BASE:MASTER:8 >= 6000)
}
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		PRINTFORML %마스터는% %조사처리(VAR_FERMENT,"를")% 퍼내고 %VAR_WATER%와 바꿔넣었다
		IF 양조량 > 0
			CALL GET_ITEM(@"%VAR_FERMENT%", 양조량, 2)
		ENDIF
		;(주조패치)술지게미 입수
		SIF VAR_FERMENT == "음양주" || VAR_FERMENT == "숙성주" || VAR_FERMENT == "고주" || VAR_FERMENT == "귀양주" || VAR_FERMENT == "귀양숙성주" || VAR_FERMENT == "귀양고주"
			CALL GET_ITEM("酒粕", 1, 1)
		VAR_FERMENT = %VAR_WATER%
		VAR_WATER =
		DAY:10 = 0
		;入れ替え時にエサをやれる
		IF ITEM:어박 || ITEM:육골분
			PRINTFORML 술벌레에게 먹이를 줄까…
			{
			CALL ASK_M("어박을 준다", ITEM:어박,
			"육골분을 준다", ITEM:육골분,
			"먹이는 주지 않는다",1)
			}
			SELECTCASE RESULT
				CASE 0
					CALL LOST_ITEM("어박", 1, 1)
					PRINTFORML 술벌레는 어박을 쪼아 먹어 조금 활동적이게 된 것 같다…
					DAY:10 += 1
				CASE 1
					CALL LOST_ITEM("육골분", 1, 1)
					PRINTFORML 술벌레는 육골분을 쪼아 먹어 조금 활동적이게 된 것 같다…
					DAY:10 += 2
				CASE 2
					PRINTFORML 먹이는 주지 않기로 했다
			ENDSELECT
		ENDIF
	CASE 2
		CALL COM450_1
	CASE 3
		IF GET_STR(0, "술데이터", 술ID, "변화처") == "0"
			PRINTFORMW %VAR_FERMENT%에는 해도 의미가 없다
			RETURN -1
		ENDIF
		PRINTFORML %마스터는% 시간정지 능력을 응용해 억지로 병 속의 시간을 진행시켰다
		BASE:MASTER:8 -= 2400
		CALL 주조
	CASE 4
		IF GET_STR(0, "술데이터", 술ID, "변화처") == "0"
			PRINTFORMW %VAR_FERMENT%에는 해도 의미가 없다
			RETURN -1
		ENDIF
		PRINTFORML %마스터는% 시간을 지워버리고『결과』만을 얻었다
		BASE:MASTER:8 -= 6000
		DAY:10 += 9
		CALL 주조
ENDSELECT
TIME += 5
RETURN 1

@COM450_1
IF LOCAL == 0
	{
	CALL ASK_M("고구마＊１０", ITEM:고구마 >= 10 && CAN_FERMENT(),
	"벌꿀＊１０", ITEM:벌꿀 >= 10 && CAN_FERMENT(),
	"나무딸기＊２０", ITEM:나무딸기 >= 20 && CAN_FERMENT(),
	"머루＊２０", ITEM:머루 >= 20 && CAN_FERMENT(),
	"곰팡이핀포도＊２０", ITEM:곰팡이핀포도 >= 20 && CAN_FERMENT(),
	"다음",1,
	"그만둔다",1)
	}
	SELECTCASE RESULT
		CASE 0
			CALL LOST_ITEM("고구마", 10, 1)
			VAR_FERMENT = 고구마넣은물
		CASE 1
			CALL LOST_ITEM("벌꿀", 10, 1)
			VAR_FERMENT = 벌꿀물
		CASE 2
			CALL LOST_ITEM("나무딸기", 20, 1)
			VAR_FERMENT = 나무딸기물
		CASE 3
			CALL LOST_ITEM("머루", 20, 1)
			CALL GET_ITEM("포머스", 1, 1)
			VAR_FERMENT = 포도즙
		CASE 4
			CALL LOST_ITEM("곰팡이핀포도", 20, 1)
			CALL GET_ITEM("포머스", 1, 1)
			VAR_FERMENT = 귀부포도즙
		CASE 5
			LOCAL = 1
			CLEARLINE 8
			RESTART
		CASE 6
	ENDSELECT
ENDIF
IF LOCAL == 1
	{
	CALL ASK_M("탁주＊５", ITEM:탁주 >= 5 && VAR_FERMENT == "그냥 물",
	"감미재＊１０", ITEM:감미재 >= 10 && CAN_FERMENT(),
	"숙성전럼", ITEM:숙성전럼 && CAN_FERMENT(),
	"숙성전프랑부아즈", ITEM:숙성전프랑부아즈 && CAN_FERMENT(),
	"증류주", ITEM:증류주 && VAR_FERMENT == "포도즙",
	"숙성전브랜디", ITEM:숙성전브랜디 && CAN_FERMENT(),
	"다음",1,
	"그만둔다",1)
	}
	SELECTCASE RESULT
		CASE 0
			PRINTFORML %마스터는% 병에 탁주를 부어 넣었다
			PRINTFORML 이로써 조금 일찍 완성될 것이다…
			DAY:10 += 3
			CALL LOST_ITEM("탁주", 5, 1)
		CASE 1
			CALL LOST_ITEM("감미재", 10, 1)
			VAR_FERMENT = 설탕물
		CASE 2
			CALL LOST_ITEM("숙성전럼", 1, 1)
			VAR_FERMENT = 숙성전럼
		CASE 3
			CALL LOST_ITEM("숙성전프랑부아즈", 1, 1)
			VAR_FERMENT = 숙성전프랑부아즈
		CASE 4
			CALL LOST_ITEM("증류주", 1, 1)
			VAR_FERMENT = 포도즙(ALC첨가)
		CASE 5
			CALL LOST_ITEM("숙성전브랜디", 1, 1)
			VAR_FERMENT = 숙성전브랜디
		CASE 6
			LOCAL = 2
			CLEARLINE 9
			RESTART
		CASE 7
	ENDSELECT
ENDIF
;(주조패치)음양주 귀양주 삼증주 추가
IF LOCAL == 2
	{
	CALL ASK_M("상급식재", ITEM:상급식재 && CAN_FERMENT(),
	"초목회", ITEM:초목회 && VAR_FERMENT == "탁주",
	"쌀누룩", ITEM:쌀누룩 && VAR_FERMENT == "기적의 물",
	"쌀누룩", ITEM:쌀누룩 && VAR_FERMENT == "음양주",
	"증류주+감미재", (ITEM:증류주 && ITEM:감미재) && VAR_FERMENT == "청주",
	"다음",1,
	"그만둔다",1)
	}
	SELECTCASE RESULT
		CASE 0
			CALL LOST_ITEM("상급식재", 1, 1)
			VAR_FERMENT = 쌀누룩
		CASE 1
			CALL LOST_ITEM("초목회", 1, 1)
			VAR_FERMENT = 아쿠모찌자케
		CASE 2
		CALL LOST_ITEM("쌀누룩", 1, 1)
			VAR_FERMENT = 모로미(쌀)
		CASE 3
			CALL LOST_ITEM("쌀누룩", 1, 1)
			VAR_FERMENT = 모로미(재양숙성)
		CASE 4
			CALL LOST_ITEM("증류주", 1, 1)
			CALL LOST_ITEM("감미료", 1, 1)
			VAR_FERMENT = 삼증주
		CASE 5
			LOCAL = 0
			CLEARLINE 5
			RESTART
		CASE 3
	ENDSELECT
ENDIF
RETURN



;술벌레의 상태를 본다
@COM_ABLE450
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;이동 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(450)
	RETURN RESULT
;부엌
SIF CFLAG:MASTER:현재위치 != 거점_술벌레
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

@CAN_FERMENT(ARG)
#FUNCTION
SIF DAY:10 >= 3
	RETURNF 0
SIF GROUPMATCH(VAR_FERMENT, "그냥 물", "안개의 호수의 물", "지옥 온천의 물", "기적의 물")
	RETURNF 1
RETURNF 0
