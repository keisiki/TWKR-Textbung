;-------------------------------------------------
;모리야 신사로 이동
;-------------------------------------------------
@COM365
VARSET LOCAL
FOR LOCAL, 1, CHARANUM
	IF CFLAG:(LOCAL):동행
		LOCAL:1 ++
		SIF CFLAG:LOCAL:쇠약 || BASE:LOCAL:기력 == 0
			LOCAL:2 ++
		SIF !CHARA_HOLIDAY(LOCAL) && BASE:LOCAL:일량
			LOCAL:3 ++
	ENDIF
NEXT
IF NEMUKE() >= 720
;	PRINTL 폐문 시간을 넘었기에 외출할 수는 없습니다
	PRINTL 졸음기가 있으니 외출하지 않는 편이 나을 것이다
	RETURN -1
ELSEIF LOCAL:1 > 1
	PRINTL 두 명 이상 데리고 나갈 수는 없다
	RETURN -1
ELSEIF LOCAL:2 || BASE:MASTER:기력 == 0
	PRINTFORML 몸 상태가 좋지 못하기에 외출할 수는 없다 \@ BASE:MASTER:기력 == 0 ? (기력 제로) # (동행자가 몸 상태 안 좋음)\@
	RETURN -1
ELSEIF LOCAL:3 && FESTIVAL() == ""
	PRINTL 동행자가 일하는 날이기에 외출할 수는 없다
	RETURN -1
ELSEIF TIME:5 == 9 || TIME:5 == 5
	PRINTL 악천후이기에 외출할 수는 없다
	RETURN -1
ELSEIF TALENT:MASTER:100 < -4 
	PRINTL 이 몸으로 외출하는 건 위험하다
	RETURN -1
ENDIF
LOCAL:2 = 0

FOR LOCAL,1,CHARANUM
	IF CFLAG:(LOCAL):동행
		CFLAG:LOCAL:데이트중 = SPECIAL_AREAMOVE() / 100
		CFLAG:LOCAL:현재위치 = SPECIAL_AREAMOVE()
		CFLAG:MASTER:데이트중 = SPECIAL_AREAMOVE() / 100
		CFLAG:MASTER:현재위치 = SPECIAL_AREAMOVE()
		LOCAL:2 = LOCAL
	ENDIF
NEXT
PRINTFORML %CALLNAME:(LOCAL:2)%더러 %조사처리(STR:SPECIAL_AREAMOVE(),"으로")% 데려가달라고 합니다
TIME += SPECIAL_AREAMOVE_TIME()


;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE365
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;일괄관리
;SIF GLOBAL_COMABLE(365)
;	RETURN RESULT
;수면중
SIF CFLAG:수면
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
SIF !SPECIAL_AREAMOVE()
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
RETURN 1

[SKIPSTART]
;-------------------------------------------------
@SPECIAL_AREAMOVE()
#FUNCTION
SELECTCASE CFLAG:MASTER:현재위치
;분사
	CASE 21
	;모리야 캐릭터와 동행중
		SIF CFLAG:31:동행 || !CFLAG:32:동행 || !CFLAG:33:동행
			RETURNF 840
;간헐천지하센터
	CASE 712
		RETURNF 970
;산정에의 길
	CASE 732
		RETURNF 810
ENDSELECT
RETURNF 0
;-------------------------------------------------
@SPECIAL_AREAMOVE_TIME()
#FUNCTION
SELECTCASE CFLAG:MASTER:현재위치
;분사
	CASE 21
		RETURNF 1
;간헐천지하센터
	CASE 712
		RETURNF 10
;산정에의 길
	CASE 732
		RETURNF 5
ENDSELECT
[SKIPEND]