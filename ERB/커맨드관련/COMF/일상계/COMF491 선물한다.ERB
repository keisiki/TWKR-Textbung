;-------------------------------------------------
;선물한다
;일상계 커맨드 레벨 1
;-------------------------------------------------
@COM782
PRINTFORML %마스터는% %타겟에게% 슬며시 과자상자를 건냈다...
CALL SUC_FAIL(0, 100-(MIN(ABL:MASTER:화술기능 + ABL:친밀 + 10, 60) + 10), 100-(MIN(ABL:MASTER:화술기능 + ABL:친밀 + 10, 60)))
SELECTCASE TFLAG:193
	;실패
	CASE -1
		PRINTFORML ...그러나 마음에 안 드는듯, 거절당해버렸다...
		SIF TFLAG:192 == -2
			RETURN 0
	;대성공
	CASE 1
		TFLAG:98 = 1
		IF CHK_DATENOW(CFLAG:TARGET:데이트중)
			CALL 붉은실발동
			EXP:MASTER:데이트경험 ++
			EXP:TARGET:데이트경험 ++
		ENDIF
ENDSELECT

DOWNBASE:MASTER:체력 += 50
DOWNBASE:MASTER:기력 += 200

IF TARGET > 0 && CFLAG:빡침 && !CFLAG:수면
	;고정으로 획득하는 소스
	SOURCE:정복 = 500
	;ABL:순종 체크
	IF ABL:순종 <= 1
		SOURCE:정복 += 2500 - (ABL:순종 * 40)
	ELSEIF ABL:순종 <= 3
		SOURCE:정복 += 1500 - (ABL:순종 * 35)
	ELSEIF ABL:순종 <= 5
		SOURCE:정복 += 1000 - (ABL:순종 * 40)
	ELSEIF ABL:순종 <= 8
		SOURCE:정복 += 750 - (ABL:순종 * 20)
	ELSEIF ABL:순종 <= 11
		SOURCE:정복 += 350 - (ABL:순종 * 20)
	ELSE
		SOURCE:정복 += 150 - (ABL:순종 * 10)
	ENDIF
	
	;ABL:친밀 체크
	IF ABL:친밀 <= 1
		SOURCE:정복 -= (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 3
		SOURCE:정복 -= (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 5
		SOURCE:정복 -= (ABL:친밀 * 50)
	ELSEIF ABL:친밀 <= 8
		SOURCE:정복 -= (ABL:친밀 * 40)
	ELSEIF ABL:친밀 <= 11
		SOURCE:정복 -= (ABL:친밀 * 30)
	ELSE
		SOURCE:정복 -= (ABL:친밀 * 20)
	ENDIF
	
	SIF SOURCE:정복 < 0
		SOURCE:정복 = 0
	CALL 주도도상승(TARGET, 2)
	SIF RAND:2
		CALL 주도도상승(TARGET, 2)
ENDIF
;성공하고 있으면(자) 분노 상태 해제
IF TFLAG:193 == 0
	CFLAG:빡침 = 0
	CFLAG:언짢음 = 0
	BASE:분노 /= 2
	PRINTFORML %타겟은% 감사히 과자상자를 받았다
	IF TARGET == 70&&연속이벤트방지:35 <= 3
		연속이벤트방지:35 += 1
	ELSEIF TARGET == 29&&연속이벤트방지:28 <= 2
		연속이벤트방지:28 += 1
	ENDIF
ELSEIF TFLAG:193 == 1
	CFLAG:빡침 = 0
	CFLAG:언짢음 = 0
	BASE:분노 = 0
	PRINTFORML %타겟은% 과자상자를 받고는 무척이나 즐거워 하고 있다...
ELSE
	;굳이 없음!유감!
ENDIF
SIF BASE:분노 < 0
	BASE:분노 = 0
IF ITEM:과자상자 && MARK:TARGET:반발각인
	IF TFLAG:193 == -1
		TFLAG:193 = -2
	ELSE
		MARK:TARGET:반발각인 -= 1
		SIF TFLAG:193 == 1&&MARK:TARGET:반발각인&&RAND:2 == 0
			MARK:TARGET:반발각인 -= 1
		TFLAG:193 = 2
		ITEM:과자상자 --
	ENDIF
ENDIF

;소요시간
IF TFLAG:193 == -1
	IF ABL:순종 <= 5
		TIME += 45
	ELSEIF ABL:순종 <= 10
		TIME += 30
	ELSE
		TIME += 15
	ENDIF
ELSE
	IF ABL:순종 <= 5
		TIME += 90
	ELSEIF ABL:순종 <= 10
		TIME += 60
	ELSE
		TIME += 45
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE782
;사과하는 실행 판정
;SIF !TFLAG:100
;	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(782)
	RETURN RESULT
;수면중
SIF CFLAG:수면
	RETURN 0
SIF FLAG:USERCOM시작 == 0
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
SIF !ITEM:과자상자
	RETURN 0
RETURN 1

