;-------------------------------------------------
;하늘의 소리를 듣는다
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM422
PRINTL [0] - 사람을 찾고 있다
PRINTL [1] - 청소를 하고 싶다
PRINTL [2] - 밖에 있는 사람을 찾고 싶다
SIF STALKED
	PRINTFORML [3] - 【쫒기】대상변경（현재 대상: %CALLNAME:STALKED%）
PRINTL [100] - 돌아간다
INPUT
IF RESULT == 0
	CALL HITOSAGASI()
ELSEIF RESULT == 1
	CALL OHEYA_CHECK
ELSEIF RESULT == 2
	CALL BROAD_HITOSAGASI
	TIME += 3
	RETURN 1
ELSEIF RESULT == 100
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF

IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:교양 < 1 && !INRANGE(CFLAG:MASTER:초기위치, 26,35)
	PRINTFORMW 미숙한 %마스터로%선 찾아낼 수 없다…
ELSE
	IF IN_HOME(RESULT) == 1
		SIF AT_HOME(MASTER) == 0
			GOTO MURIDESU
		CALL SKIP_MOVE(CFLAG:MASTER:현재위치, RESULT)
	ENDIF
ENDIF

TIME += 10
RETURN 1
$MURIDESU
PRINTW 여기에서는 갈 수 없다…
TIME += 10
RETURN 1

@BROAD_HITOSAGASI
$KAKUNIN
FOR LOCAL,0,11
	SIF LOCAL == MAIN_MAP
		CONTINUE
	PRINTL
	LOCAL:1 = 5000 + LOCAL
	PRINTFORMLC [{LOCAL}] - %STR:(LOCAL:1)%
	CALL 광역체크(LOCAL,1)
	CALL 광역체크(LOCAL,2)
	PRINTL
NEXT
DRAWLINE
PRINT [100] - 확인 종료
$INPUT_LOOP
INPUT
IF RESULT == 100 || RESULT == MAIN_MAP
	RETURN -1
ELSEIF RESULT < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF RESULT >= 11
	PRINTW 미작성입니다
	CLEARLINE 2
	GOTO INPUT_LOOP
ENDIF
CALL 광역체크(RESULT,2)
CALL ASK_YN("다른 지역을 본다","확인 종료")
SIF !RESULT
	GOTO KAKUNIN

@광역체크(ARG,ARG:1)
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC CHARAARRAY,캐릭터수상한
#DIM DYNAMIC COLORARRAY,캐릭터수상한
#DIMS DYNAMIC CNAMEARRAY,캐릭터수상한
#DIM DYNAMIC CHARACOUNTER
VARSET LOCAL
VARSET CNAMEARRAY,"힣"
FOR LOCAL,1,CHARANUM
	SIF AT_HOME(LOCAL) == 1
		CONTINUE
	SIF CFLAG:LOCAL:현재위치 == MAXROOM && CFLAG:LOCAL:수면
		CONTINUE
	FOR LOCAL:1,10,100,10
		IF CFLAG:LOCAL:현재위치 == ARG * 100 + LOCAL:1
			;그 지역에 있는 캐릭터를표시
			;패치로 변경하면 일시적인 배열변수에 캐릭터들의 목록을 저장합니다.
			IF ARG:1 == 2
				CHARAARRAY:CHARACOUNTER = LOCAL
				IF CFLAG:LOCAL:행동 == 5
					COLORARRAY:CHARACOUNTER = 2
				ELSE
					COLORARRAY:CHARACOUNTER = 1
				ENDIF
				CHARACOUNTER ++
			ENDIF
			LOCAL:2 ++
		ENDIF
	NEXT
NEXT
;저장된 목록을 실제로 출력하는 부분
IF ARG:1 == 2
	FOR LCOUNT,0,CHARACOUNTER
		CNAMEARRAY:LCOUNT = %CALLNAME:(CHARAARRAY:LCOUNT)%
	NEXT
	ARRAYSORT CNAMEARRAY
	FOR LCOUNT,0,CHARACOUNTER
		FOR LCOUNT2,0,CHARACOUNTER
			SIF CNAMEARRAY:LCOUNT == CALLNAME:(CHARAARRAY:LCOUNT2) && COLORARRAY:LCOUNT2 == 2
				SETCOLOR 255, 100, 0
		NEXT
		PRINTFORM %CNAMEARRAY:LCOUNT%
		SIF LCOUNT != CHARACOUNTER-1
			PRINT ,
		RESETCOLOR
	NEXT
ENDIF

;그 지역에 있는 인원수
IF ARG:1 == 1
	IF LOCAL:2
		PRINTFORML {LOCAL:2}인
	ELSE
		PRINTL 아무도 없다
	ENDIF
ENDIF


;하늘의 소리를 듣는다
@COM_ABLE422
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(421)
	RETURN RESULT
;새전함
SIF !하늘의성가(CFLAG:MASTER:현재위치) && CFLAG:MASTER:현재위치 != 천계
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1