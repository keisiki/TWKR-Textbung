;-------------------------------------------------
;자위한다
;日常系コマンド、レベル1
;-------------------------------------------------
@COM451
#DIM C_ID
#DIM AMT
IF TARGET
	PRINTFORMW 자위할 땐 말이야、아무한테도 방해받지 않고, 자유롭게. 뭐라고 할까, 구원받지 않으면 안 돼. 혼자서 조용히 넉넉하게……
	RETURN -1
ENDIF
PRINTFORML 누구를 떠올립니까？
DRAWLINE
FOR LOCAL,1,CHARANUM
	IF !IS_NAMED(LOCAL) || !CFLAG:LOCAL:안면
		CONTINUE
	ENDIF
	PRINTFORMLC [{LOCAL, 3}] %CALLNAME:LOCAL%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 아무도 떠오르지 않는다 [1000] 그만둔다
INPUT
C_ID = RESULT
SELECTCASE C_ID
	CASE 1 TO CHARANUM
		CALL PRINT_FACE, C_ID, "発情", "裸", "_1"
		PRINTFORML %마스터는% %조사처리(CALLNAME:C_ID,"를")% 생각하면서 성욕을 처리했다
	CASE 999
		PRINTFORML %마스터는% %TEXTR("천체의 운행을/산천초목을/생로병사를")% 생각하면서 성욕을 처리했다
	CASEELSE
		RETURN -1
ENDSELECT
AMT = 50 + RAND:50
IF TALENT:MASTER:농후정액
	AMT *= 2
	PRINTFORMW ……진한 것이 {AMT} 나왔다
ELSE
	PRINTFORMW ……{AMT} 나왔다
ENDIF
BASE:MASTER:정력 -= AMT
ITEM:정액 += AMT / 10
DOWNBASE:MASTER:기력 = 100

TIME += 20
RETURN 1

;기다린다
@COM_ABLE451
;ショップ画面では不可
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(451)
	RETURN RESULT
;気力0では不可
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;精力0では不可
SIF BASE:MASTER:정력 <= 0
	RETURN 0
;ウフフ中は不可
SIF CFLAG:우후후
	RETURN 0
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室以外では不可
SIF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
	RETURN 0
RETURN 1