;-------------------------------------------------
;식사를 대접한다
;日常系コマンド、レベル1
;-------------------------------------------------
@COM415
VARSET LOCAL
CALL AssembleForMeal
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:(100 - ABL:MASTER:요리기능 * 2)
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= 9
			;10％で大成功
			EXP:MASTER:회화경험 ++
			TFLAG:193 = 1
			TFLAG:98 = 2
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
			TFLAG:98 = -2
		ELSE
			;残りは成功
			TFLAG:193 = 0
			TFLAG:98 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		IF RAND:(100 - ABL:MASTER:요리기능 * 2) <= 9
			;10％で大成功
			EXP:MASTER:회화경험 ++
			TFLAG:193 = 1
			TFLAG:98 = 2
		ELSE
			;残りは成功
			TFLAG:193 = 0
			TFLAG:98 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -2
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

;信頼
TFLAG:98 += 3
SIF ABL:MASTER:요리기능 > 1
	TFLAG:98 ++ 
SIF ABL:MASTER:요리기능 > 3
	TFLAG:98 ++
;先制発生時は失敗しない
SIF TCVAR:20 && TFLAG:192 >= 0
	TFLAG:193 = MAX(TFLAG:193,0)
;장난を仕込まれていない
IF !TASTE_GIVEUP(TARGET, DISH_RADICAL()) && TFLAG:192 >= 0
	;味付けがツボだと失敗しない
	IF CFLAG:TARGET:미각기호 & TCVAR:MASTER:308
		TFLAG:193 = MAX(TFLAG:193,0)
		TFLAG:98 += 2
	ENDIF
ENDIF


DOWNBASE:MASTER:기력 += 50

;その場に居るTARGET全員で回す
;TARGET:0以外を先に処理して最後にTARGET:0
LOCAL:2 = TARGET
LOCAL:3 = TFLAG:193
FOR LOCAL:4, 1, GET_TARGETNUM() + 1
	TARGET = TARGET:(LOCAL:4)
	SIF TARGET == LOCAL:2
		CONTINUE
	SIF CFLAG:수면
		CONTINUE
	IF RAND:3 == 0&&!TALENT:TARGET:연모&&!TALENT:TARGET:연인
		PRINTFORML %CALLNAME:TARGET%은 접시를 슥 밀었다
		CONTINUE
	ENDIF
	CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:(100 - ABL:MASTER:요리기능 * 2)
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 99
				LOCAL:1 = 99
			IF LOCAL <= 9
				TFLAG:193 = 1
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失敗
				TFLAG:193 = -1
			ELSE
				;残りは成功
				TFLAG:193 = 0
			ENDIF
		;強制成功or大成功
		CASE 1
			IF RAND:(100 - ABL:MASTER:요리기능 * 2) <= 9
				TFLAG:193 = 1
			ELSE
				;残りは成功
				TFLAG:193 = 0
			ENDIF
		;強制失敗
		CASE -1,-2
			TFLAG:193 = -1
		;コマンド終了
		;CASE -2
			;RETURN 0
	ENDSELECT
	CALL COM415_1
NEXT
TARGET = LOCAL:2
TFLAG:193 = LOCAL:3
CALL COM415_1

SELECTCASE DISHTYPE
	CASE 1,3
		TIME += 20
		SIF CFLAG:동행
			CFLAG:동행 += 20
	CASE 2,4,5
		TIME += 40
		SIF CFLAG:동행
			CFLAG:동행 += 40
ENDSELECT

;CALL RESET_DISH

RETURN 1

@COM415_1
#DIM 空腹
#DIM DISH_ITAZURA
空腹 = 0
IF DISHTYPE == 3
	SIF TIME_PROGRESS(TCVAR:디저트공복시각)
		空腹 = 1
ELSE
	SIF TIME_PROGRESS(TCVAR:공복시각)
		空腹 = 1
ENDIF

SIF !空腹
	RETURN

;固定で獲得するソース
SELECTCASE DISHSPOIL
	CASE 0
		SOURCE:환락 = 1600
	CASE 1
		SOURCE:환락 = 400
	CASE 2
		SOURCE:환락 = 100
		SOURCE:불결 = 400
		SOURCE:반감 = 400
ENDSELECT

;ABL:순종をみる
IF ABL:순종 <= 1
	SOURCE:환락 += (ABL:순종 * 60)
ELSEIF ABL:순종 <= 3
	SOURCE:환락 += 500 + (ABL:순종 * 60)
ELSEIF ABL:순종 <= 5
	SOURCE:환락 += 700 + (ABL:순종 * 60)
ELSEIF ABL:순종 <= 8
	SOURCE:환락 += 1200 + (ABL:순종 * 75)
ELSEIF ABL:순종 <= 11
	SOURCE:환락 += 2000 + (ABL:순종 * 75)
ELSE
	SOURCE:환락 += 3000 + (ABL:순종 * 30)
ENDIF

;호감도をみる
IF CFLAG:2 <= 500
	SOURCE:환락 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:환락 += 1000 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:환락 += 2500 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0

;장난요리を先に処理
DISH_ITAZURA = TASTE_GIVEUP(TARGET, DISH_RADICAL())
SELECTCASE DISH_ITAZURA
CASE 0
	;미각기호を見る
	SIF CFLAG:TARGET:미각기호 & TCVAR:MASTER:308
		SOURCE:환락 += 200
CASE DISH_RADICAL_엄청매운
	SOURCE:고통 += 500
CASE DISH_RADICAL_격감
	SOURCE:반감 += 300
CASE DISH_RADICAL_고추냉이
	SOURCE:고통 += 500
CASE DISH_RADICAL_레몬
	SOURCE:불결 += 300
ENDSELECT
IF DISH_ITAZURA
	SOURCE:반감 += 500
	TFLAG:98 -= 2
	IF TALENT:유치
		TIMES SOURCE:고통, 1.2
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
	ENDIF
	IF CFLAG:행동 == 5
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
		TFLAG:98 -= 2
	ENDIF
	SOURCE:환락 = 0
	IF TALENT:대식가
		TIMES SOURCE:반감, 1.2
		TIMES SOURCE:불결, 1.2
		TFLAG:98 -= 2
	ENDIF
	CALL KIGEN_CHANGE(TARGET, 70, -1)
	CALL 식사(TARGET)
	RETURN
ENDIF

SOURCE:수동 = 500 + 100 * ABL:순종
SOURCE:정복 = 500 + 100 * ABL:새드끼

IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:정복 , 0.50
	TIMES SOURCE:수동 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:정복 , 1.00
	TIMES SOURCE:수동 , 1.00
	CALL KIGEN_CHANGE(TARGET,30,1)
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
	IF TALENT:기분 < 0
		CALL KIGEN_CHANGE(TARGET,100,1)
	ELSE
		CALL KIGEN_CHANGE(TARGET,50,1)
	ENDIF
ENDIF
SOURCE:환락 = SOURCE:환락 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
SOURCE:정복 = SOURCE:정복 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
SOURCE:수동 = SOURCE:수동 * (ABL:MASTER:요리기능 + DISHQUALITY * 2) / 5
IF CFLAG:행동 == 5
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
	TFLAG:98 += 2
ENDIF
CALL 식사(TARGET)
SIF TALENT:대식가
	TIMES SOURCE:환락 , 1.50

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE415
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;식사実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(415)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
SIF TARGET < 1
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;요리
SIF !DISHTYPE
	RETURN 0
;軽食以外は屋内じゃなきゃ無理
SIF DISHTYPE != 1 && !INROOM(CFLAG:MASTER:현재위치)
	RETURN 0
;食べたばかり
IF DISHTYPE == 3
	SIF !TIME_PROGRESS(TCVAR:디저트공복시각)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:공복시각)
		RETURN 0
ENDIF
;장난をした요리は別コマンド
SIF TCVAR:MASTER:308 > 8
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1

