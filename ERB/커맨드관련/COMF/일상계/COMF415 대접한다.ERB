;-------------------------------------------------
;식사를 대접한다
;日常系コマンド、レベル1
;414_[먹는다]와 거의 같음,함수도 그쪽의 것을 유용
;-------------------------------------------------
@COM415
#DIM NOW_TARGET
#DIM EAT_TIME
CALL AssembleForMeal
CVARSET TCVAR, GETNUM(TCVAR, "요리평가치"), 0

IF DISH_TYPE == "주식"
	EAT_TIME = 40
ELSE
	EAT_TIME = 20
ENDIF

;구상 측의 제어
CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
SELECTCASE TFLAG:192
CASE 1
	IF RAND:(100 - ABL:MASTER:요리기능 * 2) <= 9
		TCVAR:TARGET:요리평가치 = 700
	ELSE
		TCVAR:TARGET:요리평가치 = 500
	ENDIF
CASE -1
	TCVAR:TARGET:요리평가치 = 400
CASE -2
	TFLAG:193 = -2
	RETURN
CASEELSE
	TCVAR:TARGET:요리평가치 = 0
ENDSELECT
NOW_TARGET = TARGET
;일단 현재 TARGET 이외가 먹는다
FOR LOCAL, 1, GET_TARGETNUM() + 1
	TARGET = TARGET:LOCAL
	SIF TARGET == NOW_TARGET
		CONTINUE
	;구상 측의 제어
	CALL KOJO_MESSAGE_SEND("SUCCESS",-1,TARGET,-1,-1)
	SELECTCASE TFLAG:192
	CASE 1
		IF RAND:(100 - ABL:MASTER:요리기능 * 2) <= 9
			TCVAR:TARGET:요리평가치 = 700
		ELSE
			TCVAR:TARGET:요리평가치 = 500
		ENDIF
	CASE -1
		TCVAR:TARGET:요리평가치 = 300
	CASE -2
		CONTINUE
	CASEELSE
		TCVAR:TARGET:요리평가치 = 0
	ENDSELECT
	;시식
	CALL COM414_実食(TARGET, EAT_TIME, TARGET == DISH_ASSI || CFLAG:TARGET:행동 == 5)
NEXT
;그 후에 TARGET이 먹는다
TARGET = NOW_TARGET
;현재의 TARGET은 보너스 확정
CALL COM414_実食(TARGET, EAT_TIME, 1)
;신뢰도,약간 큰, 평균 3
TFLAG:98 = (TCVAR:TARGET:요리평가치 / 100) / 2 + 1
TIME += EAT_TIME
SIF CFLAG:MASTER:동행
	CFLAG:MASTER:동행 += EAT_TIME
주문내용_술 = 0
RETURN 1




;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE415
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;식사実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(415)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;TARGETがいない
SIF TARGET < 1
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
;요리가 없다
SIF DISH_NAME == ""
	RETURN 0
;주식は屋内じゃなきゃ無理
SIF DISH_TYPE == "주식" && !INROOM(CFLAG:MASTER:현재위치) ;KR판 임의수정, 일판 414 대응에 따라 일단 같이 수정함
	RETURN 0
;食べたばかり
IF DISH_TYPE == "디저트"
	SIF !TIME_PROGRESS(TCVAR:디저트공복시각)
		RETURN 0
ELSE
	SIF !TIME_PROGRESS(TCVAR:공복시각)
		RETURN 0
ENDIF
;風呂場飯禁止
SIF BATHCHECK(CFLAG:MASTER:현재위치)
	RETURN 0
SIF CFLAG:MASTER:우후후
	RETURN 0
RETURN 1

