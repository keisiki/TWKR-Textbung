;-------------------------------------------------
;술을대접한다
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM332
VARSET LOCALS

PRINTL 무엇을 권할까?
CALL 술일람표시(1)
IF RESULT
	;마시는 술의 종류
	TFLAG:194 = RESULT
	ITEM:RESULT --
ELSE
	RETURN -1
ENDIF
PRINTFORML %마스터는% %조사처리(ITEMNAME:RESULT,"를")% 따라%CALLNAME:TARGET%에게 내밀었다…

;잠정적으로 성공 플래그를 세운다
TFLAG:193 = 0

IF !TALENT:TARGET:연모 && CFLAG:TARGET:만취간 == 2
	PRINTFORML %타겟은% 이제 %마스터와%는 술을 마실 생각이 없는 것 같다…
	SOURCE:반감 += 3000
	BASE:무드 = 0
	BASE:분노 = 1500
;	TFLAG:193 = -2
	RETURN -1
ENDIF
IF BASE:술기운 > MAXBASE:술기운 * 2 / 3
	PRINTFORML %타겟은% 더 이상 마실 생각이 없는 것 같다
;	TFLAG:193 = -1
	RETURN -1
ELSEIF TALENT:TARGET:술내성 == -2 && CFLAG:TARGET:술을못함 < 5
	SELECTCASE CFLAG:TARGET:술을못함
		CASE IS < -10
			LOCALS = %타겟은% 불쾌감을 노골적으로 나타냈다
		CASE IS <= -3
			LOCALS = 이전 불쾌한 기분이 든 %타겟은% 마시고 싶지 않을 것 같다…
		CASEELSE
		IF TARGET == 55
			LOCALS = %타겟은% 술을 마실 수는 없다며 거절했다
		ELSE
			LOCALS = 술을 못하는 %타겟은% 술 생각이 없는 것 같다…
		ENDIF
	ENDSELECT
	PRINTFORML %LOCALS%
	PRINTFORML [0] 포기한다
	PRINTFORML [1] 설득한다
	$LOOP
	INPUT
	IF RESULT == 0 || TARGET == 55
	;히자리는 종교적 신조이므로 절대 마시지 않는다
		PRINTFORML %타겟이% 술을 마실 기색이 없어 포기했다…
		TFLAG:193 = -5
		RETURN 0
	ELSEIF RESULT == 1
		IF (ABL:친밀 * 200 + ABL:MASTER:화술기능 * 100 + MIN(3, CFLAG:술을못함) * 300) * BASE:무드 / MAXBASE:무드 >= 1500
			PRINTFORML %CALLNAME:MASTER%의 설득에 의해 %타겟은% 흠칫흠칫 술에 입을 대기 시작했다…
			CFLAG:술을못함 += 1
			TFLAG:193 = 1
		ELSEIF CFLAG:술을못함 < -10
			PRINTFORML %타겟은% 마침내 화를 내고 말았다
			SOURCE:반감 += 3000
			BASE:무드 = 0
			BASE:분노 = 1500
			TFLAG:호감도마이너스 = 5
			TFLAG:193 = -2
			RETURN 1
		ELSE
			PRINTFORML %마스터가% 뭐라고 하든, %타겟은% 술에 입을 대지 않았다…
			SOURCE:반감 += 500
			BASE:무드 /= 2
			TFLAG:호감도마이너스 = 1
			TFLAG:193 = -1
			RETURN 1
		ENDIF
	ELSE
		GOTO LOOP
	ENDIF
ENDIF

;약주, 감주때만, 회복 효과를 부여
IF (TFLAG:194 == 59 || TFLAG:194 == 56) && TFLAG:193 >= 0
	SELECTCASE TFLAG:194
		CASE 56		;감주　100미만의 회복량이므로 별도 표기
			;KR판 오리지날. 일판에서는 "감주" 가 아닌 "표시첨부"
			CALL RECOVER(MASTER, 50, "체력", "감주")
			CALL RECOVER(MASTER, 80, "기력", "감주")
			CALL RECOVER(TARGET, 50, "체력", "감주")
			CALL RECOVER(TARGET, 80, "기력", "감주")
		CASE 59		;약주
			CALL RECOVER(MASTER, 100, "체력", "약주")
			CALL RECOVER(MASTER, 150, "기력", "약주")
			CALL RECOVER(TARGET, 100, "체력", "약주")
			CALL RECOVER(TARGET, 150, "기력", "약주")
		ENDSELECT
ELSE
	DOWNBASE:MASTER:기력 += 100
	DOWNBASE:MASTER:체력 += 50
ENDIF
CALL DRUNKEN(MASTER,TFLAG:194)

CALL COM332_1

TIME += 30
RETURN 1

@COM332_1
#DIM 술기운증가량
#DIM 미보정
DOWNBASE:기력 += 10

;고정으로 획득하는 소스
SOURCE:환락 = 500

CALL DRUNKEN(TARGET,TFLAG:194)

;연속실행시는 반감이 발생
SIF PREVCOM == SELECTCOM
	SOURCE:반감 = 100

;ABL:순종을 본다
IF ABL:순종 <= 1
	SOURCE:환락 += (ABL:순종 * 30)
ELSEIF ABL:순종 <= 3
	SOURCE:환락 += 400 + (ABL:순종 * 30)
ELSEIF ABL:순종 <= 5
	SOURCE:환락 += 600 + (ABL:순종 * 30)
ELSEIF ABL:순종 <= 8
	SOURCE:환락 += 800 + (ABL:순종 * 50)
ELSEIF ABL:순종 <= 11
	SOURCE:환락 += 1000 + (ABL:순종 * 70)
ELSE
	SOURCE:환락 += 1500 + (ABL:순종 * 40)
ENDIF

;호감 도를 본다
IF CFLAG:2 <= 500
	SOURCE:환락 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:환락 += 700 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:환락 += 2500 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0

SOURCE:수동 = 200 + 100 * ABL:순종
SOURCE:정복 = 200 + 100 * ABL:새드끼

IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:정복 , 0.50
	TIMES SOURCE:수동 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:정복 , 1.00
	TIMES SOURCE:수동 , 1.00
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
ENDIF

;맛에 의한 보정
미보정 = ALCOHOL_TASTE(TFLAG:194)
IF TALENT:TARGET:술내성 == -2 && ALCOHOL_TASTE(TFLAG:194) < 15
	IF CFLAG:TARGET:술을못함 < 3
		미보정 /= 2
		PRINTFORML %타겟은% 조금 입을 붙인 것만으로 그만두어 버렸다
	ELSEIF CFLAG:TARGET:술을못함 < 5
		미보정 = 미보정 / 3 * 2
		PRINTFORML %타겟은% 이전부터 음주에 저항이 없어진 것 같다
	ELSE
		PRINTFORML %타겟은% 깨끗이 잔을 비웠다
	ENDIF
ENDIF

SOURCE:환락 = SOURCE:환락 * 미보정 / 10
SOURCE:정복 = SOURCE:정복 * 미보정 / 10
SOURCE:수동 = SOURCE:수동 * 미보정 / 10

;신뢰
TFLAG:98 = ALCOHOL_TASTE(TFLAG:194) / 5

IF ALCOHOL_TASTE(TFLAG:194) < 10
	PRINTFORMW %CALLNAME:TARGET%의 입에는 맞지 않았던 것 같다…
	IF TALENT:TARGET:술내성 == -2
		CFLAG:TARGET:술을못함 --
		TFLAG:호감도마이너스 = 1
		TFLAG:98 = -1
	ENDIF
ELSEIF ALCOHOL_TASTE(TFLAG:194) >= 15
	SELECTCASE TFLAG:194
		CASE 513	;하룻밤의쿠시나다의 특수 텍스트
			SELECTCASE TALENT:TARGET:술내성
				CASE 3
					PRINTFORMW %타겟은% %조사처리(ITEMNAME:(TFLAG:194),"를")% 마시기 쉽다며 술고래에 맞게 벌컥벌컥 과음하고 있다…
				CASE 2
					PRINTFORMW %타겟은% %조사처리(ITEMNAME:(TFLAG:194),"를")% 마시기 쉽다며 꿀꺽꿀꺽 삼키고 있다…
				CASE 1
					PRINTFORMW %타겟은% %조사처리(ITEMNAME:(TFLAG:194),"를")% 마시기 쉽다며 훌쩍 마셔 버렸다…
				CASEELSE
					PRINTFORMW %타겟은% %조사처리(ITEMNAME:(TFLAG:194),"를")% 마음에 들어하는 것 같다…
			ENDSELECT
		CASEELSE
			PRINTFORMW %타겟은% %조사처리(ITEMNAME:(TFLAG:194),"를")% 마음에 들어하는 것 같다…
	ENDSELECT
	SIF CFLAG:TARGET:술을못함 < 5
		CFLAG:TARGET:술을못함 ++
	CALL KIGEN_CHANGE(TARGET,40,1)
ENDIF

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE332
;술소지 판정
SIF !ITEM:51 && !ITEM:52 && !ITEM:53 && !ITEM:54 && !ITEM:55 && !ITEM:56 && !ITEM:57 && !ITEM:58 && !ITEM:59
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(332)
	RETURN RESULT
;수면중
SIF CFLAG:수면
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
;임신자각상태는 불가
SIF CFLAG:임신자각상태 == 1
	RETURN 0
;목욕탕
SIF BATHCHECK(CFLAG:MASTER:현재위치)
	RETURN 0
;데이트중
SIF CHK_DATENOW(CFLAG:데이트중) && CFLAG:현재위치 != OMANEKIBEYA()
	RETURN 0
;외출처에서 실행시는 친밀 3을 요구
SIF CFLAG:MASTER:현재위치 > 99 && ABL:친밀 < 3
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
SIF TARGET < 1
	RETURN 0
RETURN 1

