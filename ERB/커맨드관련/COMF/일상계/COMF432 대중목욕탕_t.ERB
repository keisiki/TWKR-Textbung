;-------------------------------------------------
;목욕탕
;일상계 커맨드, 레벨 1
;-------------------------------------------------
@COM432
#DIM 회복전체력1
#DIM 회복전기력1
#DIM 회복후체력1
#DIM 회복후기력1
#DIM 회복전체력2
#DIM 회복전기력2
#DIM 회복후체력2
#DIM 회복후기력2
#DIMS 台詞
회복전체력1 = BASE:MASTER:체력
회복전기력1 = BASE:MASTER:기력
IF DAY * 1440 + TIME - EX:MASTER:전회입욕시각 < 360
	TFLAG:194 = 1
	PRINTFORML 방금 막 목욕한 직후다
	RETURN -1
ELSEIF PLACE_SENTO(CFLAG:MASTER:현재위치) == 2
	;접수대 를 화나게 해 버렸을 경우
	IF TFLAG:209 == 4
		CALL SPTALK, 0, "怒り", 14, "「………………」",1
		PRINTFORML 접수대 쪽에서 날선 시선이 날아오고 있다……
		PRINTFORML 다른 날에 다시 오는 편이 좋을 것 같다……
		RETURN -1
	;접수대 통상 운행
	ELSE
		;돈 부족했다 이벤트를 보고 있다
		IF TFLAG:209 == 3
			台詞 = 「……어라, 웬 일이야? 혹시 목욕값 가져왔어?」
		;그만두는을 선택했지만 역시 들어가고 싶어졌다
		ELSEIF TFLAG:209 == 2
			台詞 = 「……뭐야, 혹시 마음이 변했어?」
		;그 이외
		ELSE
			;게을러 그다지 의욕이 없는 접수대
			台詞 = 「……어섭셔―」
		ENDIF
		台詞 = %台詞%/「목욕비는 \2000이면 돼」
		CALL SPTALK, 0, "通常", 14, @"%台詞%",1
		DRAWLINE
		SIF MAIN_MAP == 2
			PRINTL [2] \50000 내고 전세낸다
		PRINTL [1] \2000 내고 입욕한다
		PRINTL [0] 그만둔다
		$LOOP_hitozato
		INPUT
		IF RESULT == 2 && MAIN_MAP == 2
			IF MONEY < 50000
				{
				台詞 = 「……어, 전세내고 싶다고?」
						/「그렇다면 말야, 당연히 대금이라는게 필요해」
						/「가난한 사람은 얌전히 입욕으로 참아!」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				RETURN -1
			ELSE
				MONEY -= 50000
				{
				台詞 = 「……어, 전세내고 싶다고?」
						/「흐-음……뭐 어디에 쓸 건지는 대충 상상이 가지만……」
						/「돈 잘 내는 손님한테는 관대하니까, 그러면 24시까지 자유롭게 써도 돼」
						/「이제 편히 쉬다 나와요……」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				CALLF 욕탕대절(DAY, 1)
				RETURN 1
			ENDIF
		ELSEIF RESULT == 1
			IF MONEY < 2000
				{
				台詞 = 「……응, 편히 쉬다 나와요」
						/「아니 잠깐, 돈이 부족하잖아!」
						/「…………」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				;도발 내성 없는 접수대 빡침
				IF TFLAG:209 >= 2
					台詞 = 「…………저기 있지. 나도 이래봬도 한가하지가 않아요」/「……이러면 화낸다?」
					CALL SPTALK, 0, "怒り", 14, @"%台詞%",1
					TFLAG:209 = 4
				ELSE
					{
					台詞 = 「…………저기 있지. 괜히 오지랖 떠는 걸지도 모르겠는데」
							/「……만약 돈 없어서 힘들기라도 하면」
							/「여기서 발품깨나 팔아야하지만 온천이 있으니 거기 가보지 그래?」
					}
					CALL SPTALK, 0, "通常", 14, @"%台詞%",1
					TFLAG:209 = 3
				ENDIF
				RETURN -1
			ELSE
				MONEY -= 2000
				台詞 = 「……응, 편히 쉬다 나와요」
				SIF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:수면 
					台詞 = 「……아, 말해두겠는데 내가 지켜보고 있으니 이상한 짓 하지 마」
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:수면 
					회복전체력2 = BASE:TARGET:체력
					회복전기력2 = BASE:TARGET:기력
					CALL ODEKAKE_OFURO(TARGET,1)
					회복후체력2 = BASE:TARGET:체력
					회복후기력2 = BASE:TARGET:기력
					PRINTFORMW %조교자는% %타겟과% 탈의실 앞에서 일단 헤어져 목욕탕으로 향했다
				ENDIF

				CALL ODEKAKE_OFURO(MASTER,1)

				DRAWLINE
				PRINTW …
				PRINTW ……
				PRINTW …………
				PRINTFORMW %조교자는% 욕탕에 몸을 담갔다
				PRINTW …………
				PRINTW ……
				PRINTW …
				;대중목욕탕은 30분 고정
				TIME += 30
				;大衆浴場総入湯回数はフラグごとオミット
				;대중목욕탕시한입탕회수+1
				TFLAG:210 += 1
				;대중목욕탕플래그를 1으로 설정
				TFLAG:209 = 1
				RETURN 1
			ENDIF
		ELSEIF RESULT == 0
			;도발 내성 없는 접수대 빡침
			IF TFLAG:209 >= 2
				台詞 = 「…………저기 있지. 나도 이래봬도 한가하지가 않아요」/「……이러면 화낸다?」
				CALL SPTALK, 0, "怒り", 14, @"%台詞%",1
				TFLAG:209 = 4
			ELSE
				{
				台詞 = 「관두려고? ……뭐, 상관없지만」
						/「…………저기 있지. 괜히 오지랖 떠는 걸지도 모르겠는데」
						/「……만약 돈 아까워서 그런 거면」
						/「여기서 발품깨나 팔아야하지만 온천이 있으니 거기 가보지그래?」
				}
				CALL SPTALK, 0, "通常", 14, @"%台詞%",1
				TFLAG:209 = 2
			ENDIF
			RETURN -1
		ELSE
			GOTO LOOP_hitozato
		ENDIF
	ENDIF
ELSEIF PLACE_SENTO(CFLAG:MASTER:현재위치) == 9
	LOCAL = 0
	;접수대를 화나게 해 버렸을 경우
	IF TFLAG:211 == 4
		PRINTFORML 접수대 쪽에서 날선 시선이 날아오고 있다……
		PRINTFORML 다른 날에 다시 오는 편이 좋을 것 같다……
		RETURN -1
	;접수대 요괴 통상 운행
	ELSE
		;처음으로 구 지옥 온천에서 입욕 명령을 선택한 경우. 주위를 돌린 후 뉴 플랜 개방
		IF !STRCOUNT(CSTR:0:0, "지옥온천/")
			CSTR:0:0 = %CSTR:0:0%지옥온천/
			PRINTL 「いらっしゃいましぇだぎゃー！」
			PRINTL 「おんやぁー！　珍しいぎゃ、人間がこげんにゃ所ににゃーの用だぎゃ？」
			PRINTFORMW  %CALLNAME:PLAYER%は番台の妖怪に入浴したいことを告げた
			PRINTL 「にゃーぎゃて？……ふん……ふん」
			PRINTL 「……にゃーのにゃーの、お客しゃまだったぎゃ！」
			IF !FLAG:주회수
				PRINTL 「やけんど、よおーこぎゃな所まで来ただぎゃ！　あんしゃん、変わりモンだぎゃねー」
			ELSE
				PRINTW 「そんにゃら、あんしゃん…ぐっーどてゃーみんぐぎゃ！」
				PRINTL 「きょーはぎゃーが考ギャーた『にゅーぷらん』とーとーお披露目の日なんだぎゃ！」
				PRINTL 「きれーどころのメスっこをてゃーくしゃん揃えて」
				PRINTW 「お客しゃまぎゃ、てゃーっぷり金を落っことすの、お待ちしとったぎゃ！」
				PRINTL 「ほれっ、ほれっ！　みゃよってにゃーで、しゃっしゃと入るぎゃ！」
				PRINTFORMW 番台の妖怪は、げっへっへと厭らしい笑みを浮かべ、%CALLNAME:PLAYER%を急き立ててくる……
				GOTO SKIP
			ENDIF
		;돈 부족했다 이벤트를 보고 있다
		ELSEIF TFLAG:211 == 3
			PRINTL 「돈 없는 놈은 상대하기 싫타. 돈을 두둑히 얹으면 손님 나리로 대해야쥐!」
		;그만둔다를 선택했지만 역시 들어가고 싶어졌다
		ELSEIF TFLAG:211 == 2
			PRINTL 「모―처럼 요런 데까지 와놓고 온천에나 가겠다니 평생의 6할은 손해 본다야!」
		;그 이외
		ELSE
			;PRINTL 「어서 오십시여다―!」
			;;처음으로 옛지옥온천에서 입욕 커맨드를 선택했을 경우
			;IF TFLAG:211 == 0 && TFLAG:212 == 0 && CFLAG:401 == 0
			;	PRINTL 「잉야―! 웬 일이대야. 인간이 요기까지 웬 용무디야?」
			;	PRINTFORMW  %조교자는% 접수대를 보는 요괴에 입욕하고 싶다는 뜻을 전했다
			;	PRINTL 「무에라―? ……흠. ……흠」
			;	PRINTL 「……뭐꼬 뭐꼬, 손님이었남!」
			;	PRINTL 「근댜 용케도 이런 데까지 다 왔네! 댁도 별종이댜―」
			;ENDIF
		ENDIF
		;それ以外
		PRINTL 「그럼, 받아둘 건 받아두겠다야―」
		;옛지옥온천입탕회수가 통산 15회 이상 나와 아직에 -등응을 한번도 선택하고 있지 않다
		; IF CFLAG:401 >= 15 && CFLAG:402 == 0
		; 	PRINTL 「댁한테는 두둑하게 벌어재꼈다야!」
		; 	PRINTL 「더더 벌어재껴야지! 돈! 돈!! 돈!!! 돈을 원한댜―!」
		; 	PRINTL 「하니 돈 원하니까, 나가 생각한 뉴 플랜이 있다야!」
		; 	PRINTFORMW  접수대의 요괴는 %CALLNAME:PLAYER%에게 그렇게 말하고는, 크힐힐 징그러운 웃음을 내며 얼른! 얼른! 하고 %조교자를% 재촉해댔다……
		; ELSE
		PRINTL 「이맨치 다 주라!」
		PRINTFORML 접수대의 요괴는, 바로 옆에 놓여 있던 메모 같은 것을 손가락으로 가리키고 얼른! 얼른! 하고 %조교자를% 재촉해댔다……
		$SKIP
		DRAWLINE
		PRINTL [1] \5000 내고 입욕한다
		;옛지옥온천입탕회수가 통산 15회가 되었을 때 출현
		;뉴 플랜 선택사항
		SIF FLAG:주회수
			PRINTFORML [2] \\10000払って%\@ STRCOUNT(CSTR:0:0,"거품목욕") ? お風呂を借りる # 『にゅーぷらん』を試してみる \@%
		PRINTL [0] 그만둔다
		$LOOP_titei
		INPUT
		SELECTCASE RESULT
			CASE 0
				;番台ブチギレ
				IF TFLAG:211 >= 2
					PRINTL 「んがー！！　んがー！！　んがー！！」
					PRINTL 「金を落としゃん奴に構ってるヒマはにゃーだぎゃー！！」
					TFLAG:211 = 4
				ELSE
					PRINTL 「んあー、入らないんだったらしゃっしゃとどっか行くぎゃー！」
					PRINTL 「金にならん奴はお客しゃまでもなんでもにゃいぎゃー！」
					TFLAG:211 = 2
				ENDIF
				RETURN -1
			CASE 1
				IF MONEY < 5000
					PRINTW  접수대의 요괴는 열심히 금액이 맞는지 아닌지 세면서 확인하고 있다……
					PRINTW 「…………」
					PRINTW 「이 양반 너무하다야―! 돈 모자란데 참 유들유들하게도 내겠단 소리를 다 했대!」
					;접수대 빡침
					IF TFLAG:211 >= 2
						PRINTL 「우가―!! 우가―!! 우가―!!」
						PRINTL 「돈을 안 낸 놈이랑 어울릴 짬은 없대야―!!」
						TFLAG:211 = 4
					ELSE
						PRINTL 「이 양반, 여기까지 와놓고 대체 뭐하러 온겨―!!」
						PRINTL 「날 놀리러 온 기야―?!」
						TFLAG:211 = 3
					ENDIF
					RETURN -1
				ELSE
					MONEY -= 5000
					PRINTW  접수대의 요괴는 열심히 금액이 맞는지 아닌지 세면서 확인하고 있다……
					PRINTL 「읍쌰, 편히 들어갔다 나온나―!」
					IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:수면 && TALENT:TARGET:성별 != 2
						PRINTL (크힐힐, 암컷 아도 같이 데려오다니 좋은 놈이다야)
						PRINTL (여기선 나으 슈퍼 테크니크를 구사해 마음껏 저 암컷 아를 눈으로 범해줘야지―!)
						PRINTFORMW %조교자는% %타겟과% 함께 탈의실로 향했다
						PRINTFORMW %타겟은% %조교자가% 보내는 시선에 부끄러운 내색이다……
						회복전체력2 = BASE:TARGET:체력
						회복전기력2 = BASE:TARGET:기력
						$OFURO
						CALL ODEKAKE_OFURO(TARGET,2)
						IF CHK_DATENOW(CFLAG:TARGET:데이트중)
							;붉은실은 KR판 고유기능 또는 구잔재
							CALL 붉은실발동
							EXP:MASTER:데이트경험 ++
							EXP:TARGET:데이트경험 ++
						ENDIF
							CALL DATE_GOES_ON(1)
						SOURCE:TARGET:노출 += 300 + (ABL:TARGET:친밀 * 15)
						회복후체력2 = BASE:TARGET:체력
						회복후기력2 = BASE:TARGET:기력
					ENDIF
					CALL ODEKAKE_OFURO(MASTER,2)
					DRAWLINE
					PRINTW …
					PRINTW ……
					PRINTW …………
					IF TARGET > 0 && CFLAG:TARGET:6 > 0 && !CFLAG:TARGET:수면
						PRINTFORMW %조교자는% \@ LOCAL ? 혼자 쓸쓸하게 개인실의 # \@ 온천에서 편히 쉬었다
					ELSE
						PRINTFORMW %조교자는% 온천에서 편히 쉬었다
					ENDIF
					PRINTW …………
					PRINTW ……
					PRINTW …
					;옛지옥온천은 30분 고정
					TIME += 30
					;旧地獄温泉総入湯回数はフラグごとオミット
					;옛지옥온천시한입탕회수+1
					TFLAG:212 += 1
					;옛지옥온천플래그를 1로 설정
					TFLAG:211 = 1
					PRINTL 이곳의 탕으로도 술을 만들 수 있을지 모른다……
					PRINTL 물을 길어 돌아가겠습니까?
					PRINTL [0] 아니오　[1] 네
					INPUT
					IF RESULT
						TFLAG:물긷기 = 3
						PRINTFORML %조사처리(STR:(7000 + TFLAG:물긷기),"를")% 길었다
						IF MAIN_MAP != 9
							PRINTL 가진 상태로 걷기는 힘들기에 귀가합니다
							CALL 외출처로부터귀가
						ENDIF
					ENDIF
						RETURN 1
				ENDIF
			CASE 2
				SIF !FLAG:주회수
					GOTO LOOP_titei
				IF FLAG:생활능력무
					PRINTL 「おみゃーしゃん…手足も声も震えとるぎゃ」
					PRINTL 「やめとけやめとけ。こーんな意気地なし、メスっこも呆れてみゃうぎゃー」
					GOTO LOOP_titei
				ELSEIF FLAG:데이트상대
					PRINTL 「おみゃーしゃん、今はやめたほーがいーんでにゃーぎゃ？」
					PRINTW 「隣の娘っこ、えっれぇー顔してにらんでくるぎゃ…」
					GOTO LOOP_titei
				ELSEIF GETBIT(TFLAG:일일일회, 9)
					PRINTL 「……おんやぁ？」
					PRINTW 「わりぃーな、おみゃーしゃん。いみゃ部屋はいっぴゃーだぎゃ」
					GOTO LOOP_titei
				ELSEIF (TARGET > 0 && CFLAG:TARGET:태도 > 0 && !CFLAG:TARGET:수면) || CFLAG:MASTER:동행
					PRINTL 「わりぃーな、おみゃーしゃん」
					PRINTL 「このぷらんは、ひとりよーなんだぎゃ！」
					GOTO LOOP_titei
				ENDIF
				CALL INVITE_NEWPLAN
				SIF RESULT < 2
					RETURN RESULT
				LOCAL = 1
				GOTO OFURO
			CASEELSE
				GOTO LOOP_titei
		ENDSELECT
	ENDIF
ENDIF
회복후체력1 = BASE:MASTER:체력
회복후기력1 = BASE:MASTER:기력
DRAWLINE
PRINTFORML 체력+{ABS(회복후체력1 - 회복전체력1)}(%CALLNAME:MASTER%)
PRINTFORML 기력+{ABS(회복후기력1 - 회복전기력1)}(%CALLNAME:MASTER%)
IF TARGET
	PRINTFORML 체력+{ABS(회복후체력2 - 회복전체력2)}(%CALLNAME:TARGET%)
	PRINTFORML 기력+{ABS(회복후기력2 - 회복전기력2)}(%CALLNAME:TARGET%)
ENDIF

;목욕탕
@COM_ABLE432
;이동 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(432)
	RETURN RESULT
;정지 중에는 불가
SIF FLAG:70 == 1
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
;욕실이 있는 장소 이외는 불가
SIF !PLACE_SENTO(CFLAG:MASTER:현재위치)
	RETURN 0
;物陰
SIF FLAG:잠복
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;대절하고 있다면 필요는 없다
SIF 욕탕대절(DAY)
	RETURN 0
RETURN 1

@PLACE_SENTO(ARG)
#FUNCTION
;마을
IF ARG == GET_MAP_REPLACEMENT(223)
	RETURNF 2
;지저
ELSEIF ARG == GET_MAP_REPLACEMENT(934)
	RETURNF 9
ELSE
	RETURNF 0
ENDIF

@ODEKAKE_OFURO(ARG,ARG:1)
;ARG=1 마을　ARG=2 지저 =3풀장
RESET_STAIN ARG
STAIN:ARG:4 = 0
CFLAG:ARG:질내사정 = 0
CFLAG:ARG:애널사정 = 0
CFLAG:ARG:구내정액 = 0
CFLAG:ARG:안면정액 = 0
CFLAG:ARG:손에정액 = 0
CFLAG:ARG:목욕탕 = MIN(CFLAG:ARG:목욕탕 - 1000,0)
SELECTCASE ARG:1
	CASE 1
		CALL RECOVER_PERMIL(ARG, 200, "체력", 1)
		CALL RECOVER_PERMIL(ARG, 300, "기력", 1)
	CASE 2
		CALL RECOVER_PERMIL(ARG, 300, "체력", 1)
		CALL RECOVER_PERMIL(ARG, 400, "기력", 1)
	CASE 3
		CALL RECOVER_PERMIL(ARG, 60, "체력", 1)
		CALL RECOVER_PERMIL(ARG, 150, "기력", 1)
ENDSELECT
SIF ARG == MASTER
	EX:MASTER:전회입욕시각 = DAY * 1440 + TIME
