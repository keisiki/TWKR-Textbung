;-------------------------------------------------
;弾幕勝負
;日常系コマンド、レベル1
;-------------------------------------------------
@COM480
#DIM DYNAMIC 고로시모드
#DIM OPPONENT_REMAIN

HANDICAP_FIXED = 0
HANDICAP_RAND = 0

IF TCVAR:TARGET:탄막승부불능
	PRINTFORML %CALLNAME:TARGET%는 이미 많이 지친 것 같다. 내일 다시 신청하자.
	RETURN -1
ENDIF

$INPUT_LOOP
PRINTFORML 탄막 놀이를 하시겠습니까?
CALL ASK_M("역시 그만둔다", 1, "도전한다", 1, "놀이가 아니야", 1)
SELECTCASE RESULT
	CASE 0
		RETURN -1
	CASE 1
		PRINTFORMW 탄막 놀이 개시!!
		고로시모드 = 0
	CASE 2
		PRINTFORML 그럼 탄막 결투를 하시겠습니까?
		CALL ASK_M("역시 그만둔다", 1, "응", 1)
		SELECTCASE RESULT
			CASE 0
				RETURN -1
			CASE 1
				PRINTFORMW 탄막 전투 개시!!
				고로시모드 = 1
		ENDSELECT
ENDSELECT
TRYCALLFORM M_KOJO_MESSAGE_COM_K{TARGET}_DANMAKU("戦闘前", 0)
CALL 뉴탄막전투(MASTER,TARGET,고로시모드)
TCVAR:TARGET:탄막승부불능 = 1
EXP:MASTER:전투경험 += 2
EXP:전투경험 += 2

TIME += 60
DOWNBASE:기력 += 500
DOWNBASE:MASTER:기력 += 1000
DOWNBASE:체력 += 500
DOWNBASE:MASTER:체력 += 1000

RETURN 1
;--------------------------------------------------------------------------------


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE480
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(480)
	RETURN RESULT
SIF TARGET <= 0
	RETURN 0
SIF !SHIRAHU(TARGET)
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
;戦闘能力0以外
SIF ABL:TARGET:전투능력 < 0
	RETURN 0
SIF !전투훈련가능(CFLAG:MASTER:현재위치)&&!전투가능처(CFLAG:MASTER:현재위치)
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:기력 < 100 || BASE:MASTER:체력 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:기력 < 100 || BASE:TARGET:체력 < 500
	RETURN 0
RETURN 1


@전투가능처(ARG)
#FUNCTION

IF IN_HOME(ARG)
	RETURNF 0
ENDIF

SELECTCASE ARG
	CASE 20
		RETURNF 1
	CASE 130
		RETURNF 1
	CASE 210
		RETURNF 1
	CASE 310,340,370,380
		RETURNF 1
	CASE 410,420,430,460,470
		RETURNF 1
	CASE 510,560,570
		RETURNF 1
	CASE 620,640,650,670,680,690
		RETURNF 1
	CASE 710,720,730,740,750,760
		RETURNF 1
	CASE 810,820,830,840,890
		RETURNF 1
	CASE 910,920,930,970
		RETURNF 1
	CASE 1010,1050,1060,1070,1080,1090
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT