@DANMAKU_OPPONENT_STATUS(ARG, ARG:1)
;特殊능력표시
LOCAL:1 = 0
FOR LOCAL, 0, 10
	SIF GETBIT(CFLAG:ARG:탄막특능, LOCAL)
		LOCAL:1 ++
NEXT
IF LOCAL:1 == 10
	CALL DANMAKU_SA_OUTPUT(ARG, LOCAL:1)
ELSEIF LOCAL:1 == 0
	PRINTFORML 탄막스킬 미취득
ELSE
	FOR LOCAL, 0, 10
		CALL DANMAKU_SA_OUTPUT(ARG, LOCAL)
	NEXT
ENDIF

SIF ARG:1
	RETURN
;전투능력の差でメッセージ分岐
SELECTCASE ABL:ARG:전투능력 - ABL:MASTER:전투능력
	CASE -6 TO -4
		PRINTFORML 눈 가리고 앉아있어도 이긴다.
	CASE -4 TO -2
		PRINTFORML 눈 가리고 있어도 이길 것 같다.
	CASE -1
		PRINTFORML 질 것 같지는 않다.
	CASE 0
		PRINTFORML 아마 이길 것 같다.
	CASE 1
		PRINTFORML 못 이길 상대는 아니다.
	CASE 2
		PRINTFORML 상대는 꽤 강한 것 같다.
	CASE 3
		PRINTFORML 적어도 당신보다 배는 강한 것 같다.
	CASE 4
		PRINTFORML 기적이 일어나지 않으면 살해당할 것이다.
	CASE 5,6
		PRINTFORML 상대가 거인이라 치면, 당신은 개미똥 이하다.
	CASEELSE
		
ENDSELECT

@DANMAKU_SA_OUTPUT(OPPONENT, ARG:0)
#DIM OPPONENT

SIF ARG:0 < 10
	LOCAL = GETBIT(CFLAG:OPPONENT:탄막특능, ARG:0)
	IF ARG:0 == 10
		SETCOLOR C_RED
		PRINTFORML 【백재무궁】: 모든 특수 능력을 겸비한다. 최강
	ELSEIF LOCAL && ARG:0 == 0
		SETCOLOR C_P_BLUE
		PRINTFORML 【언소자약】: %CALLNAME:MASTER%의 주사위수 Down
	ELSEIF LOCAL && ARG:0 == 1
		SETCOLOR C_P_BLUE
		PRINTFORML 【온유돈독】: %CALLNAME:MASTER%의 주사위면 Down
	ELSEIF LOCAL && ARG:0 == 2
		SETCOLOR C_RED
		PRINTFORML 【용기늠름】: %CALLNAME:OPPONENT%의 주사위수 Up
	ELSEIF LOCAL && ARG:0 == 3
		SETCOLOR C_RED
		PRINTFORML 【호방뇌락】: %CALLNAME:OPPONENT%의 주사위면 Up
	ELSEIF LOCAL && ARG:0 == 4
		SETCOLOR C_P_PURPLE
		PRINTFORML 【괴력난신】: %CALLNAME:OPPONENT%의 주사위값이 %CALLNAME:MASTER%의 주사위값보다 작아도 그 차이가 일정수 이하라면 잔기가 줄지 않는다
	ELSEIF LOCAL && ARG:0 == 5
		SETCOLOR C_P_PURPLE
		PRINTFORML 【습람심식】: %CALLNAME:OPPONENT%의 초기 잔기가 4가 된다
	ELSEIF LOCAL && ARG:0 == 6
		SETCOLOR C_P_PURPLE
		PRINTFORML 【잔인무도】: %CALLNAME:MASTER%의 피탄에서 복귀했을 때의 폭탄수가 ―1 된다
	ELSEIF LOCAL && ARG:0 == 7
		SETCOLOR C_P_PURPLE
		PRINTFORML 【건곤일척】: %CALLNAME:OPPONENT%가 일정 확률로 폭탄을 사용하게 된다
	ELSEIF LOCAL && ARG:0 == 8
		SETCOLOR C_RED
		PRINTFORML 【견개고고】: %CALLNAME:OPPONENT%의 주사위 수정치 Up
	ELSEIF LOCAL && ARG:0 == 9
		SETCOLOR C_P_BLUE
		PRINTFORML 【유수암한】: %CALLNAME:MASTER%의 주사위 수정치 Down
	ENDIF
RESETCOLOR


;-------------------------------------------------
;戦闘中の処理
;-------------------------------------------------
@DANMAKU_BATTLE(OPPONENT)
#DIM 플레이어잔기
#DIM 상대잔기
#DIM 남은HP
#DIM 남은폭탄횟수
#DIM 턴수
#DIM 플레이어주사위면
#DIM 상대주사위면
#DIM 플레이어주사위수
#DIM 상대주사위수
#DIM 플레이어주사위값
#DIM 상대주사위값
#DIM 플레이어수정치
#DIM 상대수정치
#DIM 상대세기
#DIM 영력충전플래그
#DIM 자만심플래그
#DIM OPPONENT
#DIM BombPower
#DIM B_Min
#DIM B_Max
#DIM 지참아이템
#DIM DamageThreshold
#DIM Pichun
#DIM Charge
플레이어잔기 = 3
상대잔기 = 3
상대세기 = ABL:OPPONENT:전투능력
남은HP = MAXBASE:MASTER:체력
남은폭탄횟수 = 2
턴수 = 1
영력충전플래그 = 0
자만심플래그 = 0
Pichun = 0
Charge = 0
B_min = ABL:MASTER:전투능력 * 7 * (ABL:MASTER:전투능력 / 2 + 1)
B_Max = ABL:MASTER:전투능력 * 11 * (ABL:MASTER:전투능력 / 2 + 1) + 6
SIF TALENT:OPPONENT:자존심 > 0 && ABL:MASTER:전투능력 < 상대세기 && 상대세기 > 2 && TALENT:OPPONENT:스침 == 1
	자만심플래그 = 1
;【洽覧深識】の효과
SIF GETBIT(CFLAG:OPPONENT:탄막특능, 5)
	상대잔기 ++
;【익스텐드】の효과
SIF TALENT:MASTER:익스텐드 == 1
	플레이어잔기 ++
;【루나슈터】の효과
SIF TALENT:MASTER:루나슈터 == 1
	남은폭탄횟수 ++
;탄막승부補助아이템の持ち込み
CALL DANMAKU_ITEM_USE
지참아이템 = RESULT

;플레이어の주사위수は2.5n + 期待値2 + 期待値1
;주사위면は(n+1)*期待値10*期待値7*n/2

WHILE 플레이어잔기 > 0 && 상대잔기 > 0
	;주사위수計算
	플레이어주사위수 = (ABL:MASTER:전투능력 + RAND:5) * 2 + (ABL:MASTER:전투능력 + RAND:3) / 2
	상대주사위수 = (상대세기 + RAND:3 + 2) * 2 + 상대세기 / 2 * RAND:2
	SELECTCASE 상대잔기
		CASE IS > 2
			IF 자만심플래그 && 상대잔기 > 2
				CALL COLORMESSAGE(@"%조사처리(CALLNAME:OPPONENT,"는")% %마스터를% 얕잡아 보고 있다",C_YELLOW,1)
				상대주사위수 -= 상대세기 / 2
			ENDIF
		CASE 2
			상대주사위수 += 상대세기 / 2
		CASE 1
			상대주사위수 += 상대세기 / 2 + 1
	ENDSELECT

	;주사위면計算
	플레이어주사위면 = (ABL:MASTER:전투능력 + 1) * (6 + RAND:8) + RAND:(15 + TFLAG:행운보정) * ABL:MASTER:전투능력 / 2
	상대주사위면 = (상대세기 + 1) * (9 + RAND:7) + RAND:10 - (상대잔기 - 1) * 상대세기
	;수정치計算
	IF (상대세기 - ABL:MASTER:전투능력) < 0
		플레이어수정치 = (ABL:MASTER:전투능력 - 상대세기 + 3) * (-RAND:4 - 8) * ABL:MASTER:전투능력 / 2
		상대수정치 = (ABL:MASTER:전투능력 - 상대세기 + 3) * (RAND:4 + 8) * 상대세기 / 2
	ELSEIF (상대세기 - ABL:MASTER:전투능력) > 0
		플레이어수정치 = (상대세기 - ABL:MASTER:전투능력 + 3) * (RAND:4 + 8) * ABL:MASTER:전투능력 / 2
		상대수정치 = (상대세기 - ABL:MASTER:전투능력 + 3) * (-RAND:4 - 8) * 상대세기 / 2
	ELSE
		플레이어수정치 = 0
		상대수정치 = 0
	ENDIF
	
	;イベント用
	플레이어수정치 -= (HANDICAP_FIXED + TFLAG:GipsLV)
	플레이어수정치 -= RAND:(HANDICAP_RAND + TFLAG:GipsLV * ABL:MASTER:전투능력 + 1)
	;【言笑自若】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 0)
		플레이어주사위수 -= (상대세기 / 2 + RAND:3 + 1) * RAND:2
	;【温柔敦厚】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 1)
		플레이어주사위면 -= (상대세기 / 2 + 1) * RAND:4 + RAND:5
	;【勇気凛々】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 2)
		상대주사위수 += (상대세기 / 2 + RAND:3 + 2) * RAND:2
	;【豪放磊落】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 3)
		상대주사위면 += 상대세기 * 2 + 6 / (RAND:3 + 1)
	;【狷介孤高】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 8)
		상대수정치 += 상대세기 * (RAND:5 + 5) + RAND:5
	;【幽愁暗恨】の효과
	SIF GETBIT(CFLAG:OPPONENT:탄막특능, 9)
		플레이어수정치 -= 상대세기 * (RAND:4 + 4) + RAND:9
		
	;【영력충전】の효과
	IF 영력충전플래그
		Charge = 상대세기 / 2 + 1
		플레이어주사위수 += Charge
		CALL COLORMESSAGE(@"【영력충전】에 의해、주사위수가 {Charge}업！",C_RED,1)
		Charge = (상대세기 / 2 + 1) * 2
		플레이어주사위면 += Charge
		CALL COLORMESSAGE(@"【영력충전】에 의해、주사위면이 {Charge}업！",C_RED,1)
		Charge = 상대세기 * (RAND:3 + 2)
		플레이어수정치 += Charge
		CALL COLORMESSAGE(@"【영력충전】에 의해、수정치가 {Charge}업！",C_RED,1)
		영력충전플래그 = 0
	ENDIF

	;【국사무쌍약】使用中　남은HP増大・주사위면に補正
	SIF TCVAR:MASTER:국사무쌍약
		플레이어주사위면 += MAX(RAND:(MAX((TCVAR:MASTER:국사무쌍약), 1) * 2), 1)

	;탄막승부補助아이템の효과
	CALL DANMAKU_ITEM_EFFECT(지참아이템)
	플레이어주사위면 += RESULT:0
	플레이어주사위수 += RESULT:1
	;念のためリセット
	VARSET RESULT
	플레이어주사위수 = MAX(플레이어주사위수, 1)
	상대주사위수 = MAX(상대주사위수, 1)
	플레이어주사위면 = MAX(플레이어주사위면, 1)
	상대주사위면 = MAX(상대주사위면, 1)

	PRINTL 
	PRINTFORML -----------------------------------------------------
	PRINTFORML TURN:{턴수}
	PRINTFORML -----------------------------------------------------
	PRINTL 
	
	CALL DICE_PRINT(플레이어주사위수, 플레이어주사위면, 플레이어수정치, 상대주사위수, 상대주사위면, 상대수정치, "確定前")
	PRINTFORMW 운명의 주사위 롤!!
	;イベント用
	IF (HANDICAP_FIXED || HANDICAP_RAND)
		CALL KOJO_MESSAGE_SEND("DANMAKU", 상대잔기, OPPONENT, -1, -1, "ハンデ")
	ENDIF
	;ダイス値計算
	플레이어주사위값 = DICE_VALUE(플레이어주사위수, 플레이어주사위면, 플레이어수정치)
	상대주사위값 = DICE_VALUE(상대주사위수, 상대주사위면, 상대수정치)
	LOCAL = 상대주사위값 - 플레이어주사위값
	
	;지참아이템【바렛골렘】の효과　基本ダイス差を0.8배に ;TODO 일판에서도 바렛골렘이라면서 숫자는 현자의돌임.
	SIF LOCAL && 지참아이템 == 854
		TIMES LOCAL, 0.8
	
	CALL DICE_PRINT(플레이어주사위값, 0, 0, 상대주사위수, 상대주사위면, 상대수정치, "確定後")
	$BEFORE_BATTLE
	PRINTL 
	PRINTFORML 남은HP: {남은HP}　남은 봄 사용횟수: {남은폭탄횟수}　봄 위력: Min:{B_Min}, Max:{B_Max}　남은잔기: {플레이어잔기}
	
	IF 지참아이템
		SETCOLOR 0x666666
		PRINTFORML (아이템 효과: %ITEMNAME:지참아이템% …%DANMAKU_ITEM_EXPLANATION(지참아이템)%)
		RESETCOLOR
	ENDIF
	
	IF TCVAR:MASTER:국사무쌍약
		SETCOLOR 0x666666
		SIF TCVAR:MASTER:국사무쌍약
			PRINTFORML   （효과발동중: 국사무쌍약　　…주사위면이 미세하게 좋아졌다）
		RESETCOLOR
	ENDIF
	
	CALL ASK_M("회피에 전념한다", 1, "승부한다", 1)
	SELECTCASE RESULT
		CASE 0
			;【영력충전】の효과
			IF TALENT:MASTER:영력충전 == 1
				영력충전플래그 = 1
				CALL COLORMESSAGE(@"%CALLNAME:MASTER%의【영력충전】발동!",C_RED,2)
			ENDIF
			PRINTFORML 날아오는 탄막을 회피하는 것에 전념하기로 했다.
			PRINTFORMW ...
			PRINTL 
			CALL DICE_PRINT(플레이어주사위값, 상대주사위값, 0, 0, 0, 0, "勝負")
			PRINTW 
			;【바렛골렘】の효과  回避選択時はさらに0.9배（トータル0.72배程度に）
			IF LOCAL && 지참아이템 == 855
				TIMES LOCAL, 0.9
				PRINTFORML 골렘이 방패가 되어 탄막을 막았다…！
			ENDIF
			$UseEpitaph
			;【그레이즈능숙함】の효과
			IF TALENT:MASTER:그레이즈능숙함 == 1
				IF ABL:MASTER:전투능력 < 3
					LOCAL = (LOCAL * 2) / (ABL:MASTER:전투능력 + 2)
				ELSE
					LOCAL = (LOCAL * 3) / (ABL:MASTER:전투능력 + 3)
				ENDIF
			ENDIF
			DamageThreshold = MAX(ABL:MASTER:전투능력 / 2, 1)			
			IF LOCAL <= 20 * DamageThreshold
				PRINTFORML 살짝 긁혔을 뿐이다.
				SIF LOCAL > 0
					남은HP -= LOCAL * 4
			ELSEIF LOCAL <= 50
				PRINTFORML 약간 타격을 입었다.
				남은HP -= LOCAL * 5
			ELSEIF LOCAL <= 150
				PRINTFORML 꽤 타격을 입었다.
				남은HP -= LOCAL * 6
			ELSE
				PRINTFORML 상당한 타격을 입었다.
				남은HP = 남은HP - MAXBASE:MASTER:체력 / 2 + 1
			ENDIF
			PRINTFORMW HP: {남은HP}
			IF 남은HP <= 0
				Pichun = 1
			ENDIF
		CASE 1
			;【乾坤一擲】の효과
			IF GETBIT(CFLAG:OPPONENT:탄막특능, 7)
				IF RAND:100 + 1 < 10 + 상대세기 * 2
					LOCAL:1 = 상대세기 * (7 + RAND:5) * (상대세기 / 2 + 1) + RAND:7
					상대주사위값 += LOCAL:1
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%의【건곤일척】발동!
						PRINTFORMW %CALLNAME:OPPONENT%의 주사위 값이 {LOCAL:1} 만큼 증가했다!
					RESETCOLOR
					;口上呼び出し
					CALL KOJO_MESSAGE_SEND("DANMAKU", 상대잔기, OPPONENT, -1, -1, "乾坤一擲")
					LOCAL = 상대주사위값 - 플레이어주사위값
				ENDIF
			ENDIF
			IF 남은폭탄횟수 >= 1
				PRINTFORML 봄을 사용합니까?
				CALL ASK_M("사용한다", 1, "사용하지 않는다", 1)
				SELECTCASE RESULT
					CASE 0
						CALL UseBomb
						BombPower = RESULT
						플레이어주사위값 += BombPower
						PRINTFORMW 주사위 값이 {BombPower} 만큼 증가했다!
						남은폭탄횟수 --
						LOCAL = 상대주사위값 - 플레이어주사위값
					CASE 1
						
				ENDSELECT
			ENDIF
			PRINTFORML 승부다!
			PRINTFORMW ...
			PRINTL 
			CALL DICE_PRINT(플레이어주사위값, 상대주사위값, 0, 0, 0, 0, "勝負")
			IF (지참아이템 == 870 ||지참아이템 == 871) && LOCAL > 0 && BASE:MASTER:TSP >= 500
				SETBGCOLOR 1, 24, 60
				{
				CALL COLORMESSAGE(@"%TEXTR("보이나！　이녀석의 움직임이！　뭘 하려는 건지, 완전히！ 
				/『읽을 수 있다』………움직임의『궤적』을 읽을 수 있다……『미래로의 움직임의 궤적』이…！")%", 0xFF1493, 2, 1)
				}
				PRINTFORML %마스터는% 『에피타프』에 의해 자기 자신의 피탄을 예언했다！
				CALL RECOVER(MASTER, -500, "TSP")
				CALL COLORMESSAGE(@"남은TSP：{BASE:MASTER:TSP}", C_RED, 2)
				CALL ASK_M("회피에 전념한다",1,"『킹・크림슨』！(TSP1500)",지참아이템 == 871 && BASE:MASTER:TSP >= 1500)
				SELECTCASE RESULT
					CASE 0
						PRINTFORML %마스터는% 순간적으로 회피로 전환했다！
						RESETBGCOLOR
						GOTO UseEpitaph
					CASE 1
						CLEARLINE 1
						PRINTL 
						SETCOLOR 0xFF1493
						{
						PRINTFORML %TEXTR("『킹・크림슨』의 능력 중에는 이 세상의 시간이 날아가버려……그리고 모든 인간은 이 시간 속에서 움직인 발자취를 기억하지 못해！
						/『하늘의 구름은 조각조각 날아간 것을 알지 못한다！』『꺼진 불꽃은 꺼지는 순간을 불꽃 자신조차 인지하지 못해！』/『결과』뿐이다！！이 세상은 『결과』만이 남는다！！
						/시간이 날아간 세계에서『움직임』은 모두 무의미해지는 것이다！！")%
						}
						PRINTFORMW 이것이 내 『킹・크림슨』의 능력！『시간을 지우고』뛰어넘는다……！！
						RESETCOLOR
						RESETBGCOLOR
						CALL RECOVER(MASTER, -1500, "TSP")
						PRINTFORML %CALLNAME:MASTER%의 후방으로、탄막이 날아간다――
						IF RAND:3 + 4 < 상대세기
							PRINTFORMW 계속 %CALLNAME:MASTER%의 있을 수 없는 곳에서의 반격을、%조사처리(CALLNAME:OPPONENT,"는")% 흘린 핏방울의 수로 알아차려 회피했다！
							LOCAL = 0
						ELSE
							PRINTFORMW 경악에 눈을 휘둥그레질 새도 없이、%조사처리(CALLNAME:OPPONENT,"는")% 사각에서 이해할 수 없는 반격을 받았다！
							LOCAL = -100
						ENDIF
				ENDSELECT
			ENDIF
			PRINTW 
			IF LOCAL > 0
				IF 남은폭탄횟수 >= 1
					PRINTFORML 피할 길이 없어져 버린 %마스터는% 봄을 사용했다!
					CALL UseBomb
					BombPower = RESULT

					SETCOLOR C_RED
					PRINTFORM 주사위값의 차:{LOCAL} 
					IF LOCAL <= BombPower
						PRINTFORML ≤ {BombPower}
						PRINTFORMW %마스터는% 봄으로 %CALLNAME:OPPONENT%의 탄막을 소거했다!
						LOCAL = 0
					ELSE
						PRINTFORMW ＞ {BombPower}
					ENDIF
					RESETCOLOR
					남은폭탄횟수 --
				ENDIF
			ELSEIF LOCAL < 0
				;【怪力乱神】の효과
				IF GETBIT(CFLAG:OPPONENT:탄막특능, 4) && (-LOCAL) < 상대세기 * 10
					SETCOLOR C_RED
						PRINTFORML %CALLNAME:OPPONENT%의 【괴력난신】발동!
						PRINTFORMW %CALLNAME:MASTER%의 탄막이 %조사처리(CALLNAME:OPPONENT,"를")% 피탄시키기 전에 소거되었다!
						;口上呼び出し
						CALL KOJO_MESSAGE_SEND("DANMAKU", 상대잔기, OPPONENT, -1, -1, "怪力乱神")
						LOCAL = 0
					RESETCOLOR
				ENDIF
			ENDIF
			IF LOCAL < 0
				CALL COLORMESSAGE(@"격파했다! 해냈어!",C_YELLOW,1)
				상대잔기 --
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 상대잔기, OPPONENT, -1, -1, "被弾")
				PRINTFORMW 상대 잔기: {상대잔기}
			ELSEIF LOCAL == 0
				PRINTFORMW 서로 탄막을 맞출수 없었다.
			ELSE
				Pichun = 1
			ENDIF
		CASE 2

	ENDSELECT
	IF Pichun
		PRINTFORML ＼피츙／
		PRINTFORML 피탄당하고 말았다......
		플레이어잔기 --
		남은HP = MAXBASE:MASTER:체력
		남은폭탄횟수 = 2
		PRINTFORMW LIFES: {플레이어잔기}
		;【루나슈터】の효과
		SIF TALENT:MASTER:루나슈터 == 1
			남은폭탄횟수 ++
		;【残忍酷薄】の효과
		IF GETBIT(CFLAG:OPPONENT:탄막특능, 6) && 플레이어잔기 > 0
			CALL COLORMESSAGE(@"%CALLNAME:OPPONENT%의 【잔인무도】발동!",C_RED,2)
			;【세방울의천수】の효과
			IF ITEM:세방울의천수 && !RAND:2
				PRINTFORMW 개구리 신의 가호로【잔인무도】효과를 방어했다!
			ELSE
				;口上呼び出し
				CALL KOJO_MESSAGE_SEND("DANMAKU", 상대잔기, OPPONENT, -1, -1, "残忍酷薄")
				남은폭탄횟수 --
			ENDIF
		ENDIF
	ENDIF
	Pichun = 0	
	턴수 ++
WEND

PRINTL 
IF 플레이어잔기 <= 0
	PRINTFORMW %마스터는% 져 버렸다……
	CALL COLORMESSAGE(@"패배자!",C_RED,2,1)
ELSEIF 상대잔기 <= 0
	PRINTFORML %마스터는% %조사처리(CALLNAME:OPPONENT,"와")%의 승부에서 이겼다!
	CALL COLORMESSAGE(@"인생의 승리자!",C_GREEN,2,1)
	CALL DANMAKU_GIPS_REWARD
ENDIF
TFLAG:GipsLV = 0
PRINTL 

; 악보 획득 판정. 1.20u3 버전에서는 이식되지 않았기에 주석 처리. KR판 미이식
;楽譜入手処理
;IF (RAND:(100) < 30)
;	RESULT = 楽譜入手_탄막승부(OPPONENT)
;	SIF RESULT != -1
;		PRINTL
;ENDIF

RETURN 상대잔기


@DICE_PRINT(ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARGS)
IF ARGS == "確定前"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}d{ARG:1}
	IF ARG:2 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:2}
	ELSEIF ARG:2 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:2}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:0 + ARG:2}, Max:{ARG:0 * ARG:1 + ARG:2})  VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:1,2は使わない
ELSEIF ARGS == "確定後"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:3}d{ARG:4}
	IF ARG:5 > 0
		SETCOLOR C_RED
		PRINTFORM +{ARG:5}
	ELSEIF ARG:5 < 0
		SETCOLOR C_BLUE
		PRINTFORM {ARG:5}
	ENDIF
	RESETCOLOR
	PRINTFORM (Min:{ARG:3 + ARG:5}, Max:{ARG:3 * ARG:4 + ARG:5}) 
	PRINTFORML :%CALLNAME:TARGET%
;ARG:2,3,4,5は使わない
ELSEIF ARGS == "勝負"
	PRINTFORM %CALLNAME:MASTER%: 
	SETCOLOR C_GREEN
	PRINTFORM {ARG:0}  
	RESETCOLOR
	PRINTFORM VS  
	SETCOLOR C_GREEN
	PRINTFORM {ARG:1} 
	RESETCOLOR
	PRINTFORML :%CALLNAME:TARGET%
ENDIF


@DICE_VALUE(ARG:0, ARG:1, ARG:2)
#FUNCTION
;初期化しないとダメ
LOCAL:1 = 0
FOR LOCAL, 0, ARG:0
	LOCAL:1 += RAND:(ARG:1) + 1
NEXT

RETURNF LOCAL:1 + ARG:2


@SHOW_DANMAKU_RULE
PRINTFORML 서로 주사위를 던져 나온 눈의 크기를 겨룬다는 게 기본 룰입니다
PRINTFORMW 처음은 서로 잔기를 3개 보유하고 있으며, 전투능력이 클수록 주사위 수와 면이 증가합니다.
PRINTFORMW 플레이어만, 자신의 주사위눈을 본 다음 회피에 전념할지 승부할지를 선택할 수 있습니다(이때, 상대의 주사위눈은 보이지 않습니다).
PRINTFORMW 회피에 전념하기로 선택했을 경우는, 잔기가 줄어들지 않는 대신에 주사위눈 차이에 따라 HP에 대미지를 받습니다.
PRINTFORMW HP가 0이 되면 플레이어의 잔기가 1 줄어듭니다. 잔기가 0이 되면 플레이어의 패배입니다.
PRINTFORMW 승부하기로 선택하고 상대보다 낮은 주사위눈이었을 경우는, 무조건 플레이어의 잔기가 1 줄어듭니다.
PRINTFORMW 승부하기로 선택하고 상대보다 높은 주사위눈을 냈을 때에만 상대의 잔기를 1 줄일 수 있습니다.
;残機0ならまだプレイできるじゃんとか言わない
PRINTFORMW 플레이어의 잔기가 0이 되기 전에 상대의 잔기를 0으로 만들면 플레이어의 승리가 됩니다.
PRINTFORMW 또한 플레이어는 폭탄을 사용해 주사위의 결과에 플러스할 수 있으므로, 이때다 싶은 순간에 사용합시다.
PRINTFORMW 더해서 주사위 승부에 패배해 플레이어의 잔기가 줄어들 때, 폭탄을 사용해 잔기가 줄어드는 것을 막을 때가 있습니다.
PRINTFORMW 단, 잔기가 줄어들면 폭탄 사용 횟수가 강제적으로 2가 되므로, 사용하지 않은 채로 잔기가 줄면 손해를 봅니다.


@UseBomb
#DIM BombPower
BombPower = ABL:MASTER:전투능력 * (7 + RAND:5) * (ABL:MASTER:전투능력 / 2 + 1) + RAND:7

CALL COLORMESSAGE(@"봄 위력 {BombPower}",C_YELLOW,1)
;【루나슈터】の효과
IF TALENT:MASTER:루나슈터 == 1
	BombPower = BombPower * (RAND:3 + 2)
	CALL COLORMESSAGE(@"【루나슈터】의 효과로 봄 위력이 {BombPower} 만큼 증가했다!",C_YELLOW,2)
ENDIF
;【세방울의천수】の효과
IF ITEM:세방울의천수
	;LOCAL = MAX(RAND:750, 400)
	LOCAL = BombPower / 10
	BombPower += LOCAL
	CALL COLORMESSAGE(@"%CALLNAME:MASTER%의 주머니에서 조용히 수많은 개구리들이 튀어나왔다!",C_YELLOW,1)
	CALL COLORMESSAGE(@"봄 위력이 {LOCAL} 만큼 증가했다!",C_YELLOW,1)
ENDIF
RETURN BombPower

@DANMAKU_ITEM_USE
#DIM 지참아이템
#DIMS 효과텍스트
지참아이템 = 0
DRAWLINE
PRINTFORML 탄막승부 보조 아이템을 사용할까요?
DRAWLINE
FOR LOCAL,850, 900
	SIF !DANMAKU_ITEM_AVAILABLE(LOCAL)
		CONTINUE
	PRINTFORML [{LOCAL}] - %ITEMNAME:LOCAL% 
NEXT
DRAWLINE
PRINTFORML [999] - 사용하지 않는다
INPUT
IF !DANMAKU_ITEM_AVAILABLE(RESULT)
	RETURN 0
ENDIF

지참아이템 = RESULT
PRINTFORML %조사처리(ITEMNAME:지참아이템,"을")% 사용합니까？
PRINTFORML %DANMAKU_ITEM_EXPLANATION(지참아이템)%
CALL ASK_YN
IF !RESULT
	;先に進む
ELSE
	RESTART
ENDIF
ITEM:지참아이템 -= 1
RETURN 지참아이템

@DANMAKU_ITEM_EXPLANATION(ARG)
#FUNCTIONS
#DIMS 효과텍스트
SELECTCASE ARG
	CASE 850
		효과텍스트 = 주사위면이 어렴풋이 좋아진다
	CASE 851
		효과텍스트 = 주사위수가 어렴풋이 증가한다
	CASE 852
		효과텍스트 = 주사위면이 조금 좋아진다
	CASE 853
		효과텍스트 = 주사위수가 조금 증가한다
	CASE 854
		효과텍스트 = 주사위면이 조금 좋아지고、주사위수가 조금 증가한다
	CASE 855
		효과텍스트 = 받은 데미지를 조금 경감한다
	CASE 870
		효과텍스트 = 주사위에서 지더라도 회피를 택한 것이 된다　발동마다 TSP500소비
	CASE 871
		효과텍스트 = 피탄을 무효화하며 상대의 잔기를 줄이고 치트 능력이지만 TSP소비가엄청나게 크다
	CASEELSE
		효과텍스트 = 
ENDSELECT
RETURNF 효과텍스트

@DANMAKU_ITEM_EFFECT(ARG)
#DIM Dnum
#DIM Dface
Dnum = 0
Dface = 0
SELECTCASE ARG
	CASE 850	;폭발성시약
		Dface = MAX(RAND:((ABL:MASTER:전투능력) + 3), 2)
	CASE 851	;아이시클봄
		Dnum = MAX(RAND:((ABL:MASTER:전투능력) + 1), 1)
	CASE 852	;태을의불씨
		Dface = MAX(RAND:((ABL:MASTER:전투능력) * 2), 4)
	CASE 853	;매혹의미끼
		Dnum = MAX(RAND:((ABL:MASTER:전투능력) * 2), 3)
	CASE 854	;현자의돌
		Dface = MAX(RAND:((ABL:MASTER:전투능력 + 1) * 3), 6)
		Dnum = MAX(RAND:((ABL:MASTER:전투능력 + 1) * 2), 5)
	CASE 855	;바렛골렘
	CASE 870	;에피타프
	CASE 871	;キンクリ
ENDSELECT
RETURN Dface, Dnum

@DANMAKU_ITEM_AVAILABLE(ARG)
#FUNCTION
SIF !INRANGE(ARG, 850, 900)
	RETURNF 0
SIF DANMAKU_ITEM_EXPLANATION(ARG) == ""
	RETURNF 0
SIF !ITEM:ARG
	RETURNF 0
RETURNF 1

@DANMAKU_GipsEquip(ARGS)
IF ARGS == "椛口上" || ITEM:루나슈터양성깁스
	PRINTFORML %마스터는% 루나슈터양성깁스를 장비하고、버클의 손잡이에 손을 대었다
	CALL ASK_M("역시 그만둔다", 1, "Easy",1, "Normal", 1, "Hard", 1, "Lunatic" , 1, "『위험・수동모드』", 1 )
ELSE
	PRINTFORML %마스터는% 슈터깁스를 장비하고、버클의 손잡이에 손을 대었다
	CALL ASK_M("역시 그만둔다", 1, "Easy",1, "Normal", 1, "Hard", 1)
ENDIF
SELECTCASE RESULT
	CASE 0
		RETURN
	CASE 1
		TFLAG:GipsLV = 1
	CASE 2
		TFLAG:GipsLV = 3
	CASE 3
		TFLAG:GipsLV = 5
	CASE 4
		TFLAG:GipsLV = 10
	CASE 5
		PRINTFORML 부하 레벨을 설정해주세요
		PRINTL 참고 부하 수준　Easy:1　Normal:3　Hard:5　Lunatic:10
		PRINTL [0] [1] [3] [5] [10] [15] [30] [50] [100]
		INPUT
		TFLAG:GipsLV = MAX(RESULT, 0)
ENDSELECT

@DANMAKU_GIPS_REWARD
#DIM Reward
SIF TFLAG:GipsLV <= 0
	RETURN
PRINTFORML 깁스의 효과로 %마스터는% 넉넉한 경험치를 받았다
Reward = MIN(15, TFLAG:GipsLV)
;SELECTCASE GipsLV
;	CASE IS >= 10
;		Reward = MIN(15, GipsLV)
;	CASE IS >= 5
;		Reward = 4
;	CASE IS >= 3
;		Reward = 2
;	CASEELSE
;		Reward = 1
;ENDSELECT
IF ABL:MASTER:전투능력 <= ABL:TARGET:전투능력
	IF TFLAG:GipsLV >= 10
		Reward *= 3
		CALL COLORMESSAGE(@"상대가 동격 이상이기에 추가 경험치가 3배",C_YELLOW,1)
	ELSE
		Reward *= 2
		CALL COLORMESSAGE(@"상대가 동격 이상이기에 추가 경험치가 2배",C_YELLOW,1)
	ENDIF
ENDIF
CALL AddEXP("전투경험" , MASTER, Reward)