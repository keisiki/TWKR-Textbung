;-------------------------------------------------
;스킨십
;일상계 커맨드 레벨 1
;-------------------------------------------------
@COM302
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
			TFLAG:98 = 1
			IF CHK_DATENOW(CFLAG:TARGET:데이트중)
			;KR판 오리지날 붉은실
			CALL 붉은실발동
				EXP:MASTER:데이트경험 ++
				EXP:TARGET:데이트경험 ++
			ENDIF
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失敗
			TFLAG:193 = -1
			TFLAG:98 = -2
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
		;실패한 경우에도 연회석에서는 10%로 성공
		SIF TFLAG:193 == -1 && RAND:100 <= 9 && CFLAG:350 == 49
			TFLAG:193 = 0
	;強制成功or大成功
	CASE 1
		LOCAL = RAND:100
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
		TFLAG:98 = 1
		IF CHK_DATENOW(CFLAG:TARGET:데이트중)
			;KR판 오리지날 붉은실
			CALL 붉은실발동
			EXP:MASTER:데이트경험 ++
			EXP:TARGET:데이트경험 ++
		ENDIF
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制失敗
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT
;선제 발생시는 실패하지 않는다
SIF TCVAR:20
	TFLAG:193 = MAX(TFLAG:193,0)

DOWNBASE:기력 += 30
DOWNBASE:MASTER:기력 += 30


;고정으로 획득하는 소스
SOURCE:환락 = 100
SOURCE:사랑 = 100
SOURCE:노출 = 20
SOURCE:반감 = 50

;ABL:친밀을 본다
IF ABL:친밀 <= 1
	SOURCE:사랑 += (ABL:친밀 * 30)
ELSEIF ABL:친밀 <= 3
	SOURCE:환락 += 100 + (ABL:친밀 * 40)
	SOURCE:사랑 += 200 + (ABL:친밀 * 30)
ELSEIF ABL:친밀 <= 5
	SOURCE:환락 += 400 + (ABL:친밀 * 50)
	SOURCE:사랑 += 400 + (ABL:친밀 * 30)
ELSEIF ABL:친밀 <= 8
	SOURCE:환락 += 700 + (ABL:친밀 * 60)
	SOURCE:사랑 += 600 + (ABL:친밀 * 40)
ELSEIF ABL:친밀 <= 11
	SOURCE:환락 += 1200 + (ABL:친밀 * 80)
	SOURCE:사랑 += 800 + (ABL:친밀 * 40)
ELSE
	SOURCE:환락 += 2000 + (ABL:친밀 * 50)
	SOURCE:사랑 += 1800 + (ABL:친밀 * 20)
ENDIF

;호감 도를 본다
IF CFLAG:2 <= 500
	SOURCE:환락 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:환락 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:환락 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0

LOCAL = 0
;친밀이 낮으면 호감도 저하
IF ABL:친밀 == 0
	LOCAL -= 3
ELSEIF ABL:친밀 == 1
	LOCAL -= 2
ELSEIF ABL:친밀 == 2
	LOCAL -= 1
ENDIF
;호감도가 낮으면 호감도 저하
IF CFLAG:2 <= 50
	LOCAL -= 3
ELSEIF CFLAG:2 <= 100
	LOCAL -= 2
ELSEIF CFLAG:2 <= 250
	LOCAL -= 1
ENDIF


SOURCE:수동 = 100 + 200 * ABL:순종
SOURCE:정복 = 100 + 200 * ABL:새드끼

;호감도 변화
TFLAG:97 += LOCAL

IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:사랑 , 0.50
	TIMES SOURCE:정복 , 0.50
	TIMES SOURCE:수동 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:사랑 , 1.00
	TIMES SOURCE:정복 , 1.00
	TIMES SOURCE:수동 , 1.00
	TFLAG:무드보너스 = 20
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:사랑 , 2.00
	TIMES SOURCE:정복 , 2.00
	TIMES SOURCE:수동 , 2.00
	TFLAG:무드보너스 = 30
ENDIF
TIME += 10
IF CHK_DATENOW(CFLAG:TARGET:데이트중)
	;KR판 오리지날 붉은실
	IF TALENT:MASTER:붉은실&&(RAND:50==0)
		SETCOLOR C_RED
		PRINTFORML %CALLNAME:MASTER%의 운명의 붉은 실이 빛나고 있습니다.
		SETCOLOR C_PINK
		PRINTFORML %타겟은% %CALLNAME:MASTER%에게서 운명을 느낀듯 하다.
		RESETCOLOR
		SIF !RELATION:TARGET:MASTER
				RELATION:TARGET:MASTER=100
			RELATION:TARGET:MASTER+=20
	ENDIF
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
ELSE
	;스킨십 한 플래그
	TCVAR:303 = 1440 * DAY + TIME
ENDIF

RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE302
;스킨십 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(302)
	RETURN RESULT
;1시간 간격
SIF TIME_PROGRESS(TCVAR:303) < 60 && !CHK_DATENOW(CFLAG:데이트중)
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
;동침중
SIF CFLAG:동침중
	RETURN 0
;업무중
SIF CFLAG:행동 == 5
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 1

