;-------------------------------------------------
;アイテム画面
;-------------------------------------------------
@COM490
#DIM 대상
#DIM 전원
#DIM CLIENT_ID
#DIM 공허로회귀
#DIM 사용아이템
#DIMS 식품특징
VARSET LOCAL
VARSET LOCALS
대상 = MASTER
공허로회귀 = 0
PRINTL 무엇을 사용합니까？
$LOOP
DRAWLINE
FOR LOCAL,120, 699
	IF 대상 != MASTER
		IF 사용가능아이템(LOCAL) == 2 && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ELSE
		IF 사용가능아이템(LOCAL) && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ENDIF
NEXT
SIF ITEM:950&&사용가능아이템(950)
	CALL PRINT_ITEM_EXPLANATION(950)
SIF ITEM:961&&사용가능아이템(961)
	CALL PRINT_ITEM_EXPLANATION(961)
SIF ITEM:1020&&사용가능아이템(1020)
	CALL PRINT_ITEM_EXPLANATION(1020)
SIF ITEM:1023&&사용가능아이템(1023)
	CALL PRINT_ITEM_EXPLANATION(1023)
SIF ITEM:1024&&사용가능아이템(1024)
	CALL PRINT_ITEM_EXPLANATION(1024)
SIF ITEM:1035&&사용가능아이템(1035) && 대상
	CALL PRINT_ITEM_EXPLANATION(1035)
SIF ITEM:1037&&사용가능아이템(1037)
	CALL PRINT_ITEM_EXPLANATION(1037)
SIF ITEM:1038&&사용가능아이템(1038)
	CALL PRINT_ITEM_EXPLANATION(1038)
SIF ITEM:1042&&사용가능아이템(1042)
	CALL PRINT_ITEM_EXPLANATION(1042)
SIF ITEM:1048&&사용가능아이템(1048)
	CALL PRINT_ITEM_EXPLANATION(1048)
SIF ITEM:1050&&사용가능아이템(1050)
	CALL PRINT_ITEM_EXPLANATION(1050)
SIF ITEM:3001&&사용가능아이템(3001)
	CALL PRINT_ITEM_EXPLANATION(3001)
SIF ITEM:3005&&사용가능아이템(3005)
	CALL PRINT_ITEM_EXPLANATION(3005)
PRINTL
DRAWLINE
SIF ITEM:곤충채집세트
	PRINTFORML [901] 곤충 채집통을 확인한다
PRINTFORML [900] 콜렉션의 스키마 자동 사양 변환（현재\@ FLAG:C스키마자동사양 ? ON# OFF\@）
IF TARGET && SHIRAHU(TARGET)
	SIF 대상 != MASTER
		SETCOLOR C_YELLOW
	PRINTFORML [998] %CALLNAME:TARGET%에 사용한다
	RESETCOLOR
ENDIF

PRINTL [999] 그만둔다
INPUT
사용아이템 = RESULT
;アイテム以外の処理
SELECTCASE RESULT
	CASE 999
		RETURN -1
	CASE 900
		FLAG:C스키마자동사양 = !FLAG:C스키마자동사양
		PRINTFORMW 자동 사양을\@ FLAG:C스키마자동사양 ? ON# OFF\@(으)로 했습니다
		GOTO LOOP
	CASE 901
		;[虫捕りをする]へ転送
		CALL SHOW_MUSHI_CAGE
		GOTO LOOP
	CASE 998
		IF 대상 != MASTER
			대상 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			대상 = TARGET
		ENDIF
		GOTO LOOP
ENDSELECT

;使用可能アイテム以外の入力、またはアイテム未所持の場合は再入力
IF 사용가능아이템(사용아이템) == 0 || ITEM:사용아이템 <= 0
	GOTO LOOP
ENDIF
;相手使用時にMASTERのみアイテムの選択は再入力
IF 대상 != MASTER && 사용가능아이템(사용아이템) == 1
	GOTO LOOP
ENDIF
;IF DRUG(사용아이템,대상)
;	PRINTL 이미 약을 복용했으니 오늘은 이제 그만두자
;	GOTO LOOP
;ENDIF
IF ITEM_SWEETS(사용아이템) && !TIME_PROGRESS(TCVAR:대상:디저트공복시각)
	PRINTL 간식은 아직 먹고 싶지 않다
	GOTO LOOP
ENDIF
TFLAG:193 = 사용아이템
TFLAG:194 = 대상 == MASTER ? MASTER # 1

SELECTCASE 사용아이템
	CASE 120
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		SIF 대상 == MASTER
			PRINTL ……싸구려 맛이 났다
		식품특징 = %ITEMNAME:사용아이템%/단/정키/사각사각/
		CALL RECOVER(대상, ITEM_TASTE(대상, 50, 식품특징), "체력", "초코에 의한 회복")
		CALL RECOVER(대상, ITEM_TASTE(대상, 50, 식품특징), "기력", "초코에 의한 회복")
		CALL 만복도상승(대상, "디저트", 60)
		LOCAL:1 ++
	CASE 121
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			식품특징 = %ITEMNAME:사용아이템%/단/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 식품특징), "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 300, 식품특징), "기력", "초코에 의한 회복")
			CALL 만복도상승(대상, "디저트")
			LOCAL:1 ++
		ENDIF
	CASE 122
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			PRINTFORML 맛있어!
			식품특징 = %ITEMNAME:사용아이템%/단/품위있는/호화/고급식재/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 식품특징), "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 500, 식품특징), "기력", "초코에 의한 회복")
			CALL 만복도상승(대상, "디저트")
			LOCAL:1 ++
		ENDIF
	CASE 123
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			PRINTFORML 사랑이 느껴져!
			식품특징 = %ITEMNAME:사용아이템%/애정/단/
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 식품특징), "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 1000, 식품특징), "기력", "초코에 의한 회복")
			CALL 만복도상승(대상, "디저트")
			LOCAL:1 ++
		ENDIF
	CASE 212
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		LOCAL:1 ++
		;端午の節句は여름의季語だそうなので봄철じゃなかった
		식품특징 = %ITEMNAME:사용아이템%/단/
		CALL RECOVER(대상, ITEM_TASTE(대상, 100, 식품특징), "체력", "떡잎으로 싼 떡에 의한 회복")
		CALL RECOVER(대상, ITEM_TASTE(대상,   0, 식품특징), "기력", "떡잎으로 싼 떡에 의한 회복")
		CALL 만복도상승(대상, "디저트")
	CASE 213
		SIF TARGET && SHIRAHU(TARGET)
			PRINT 둘이 나란히 
			PRINTDATA
				DATAFORM 「아이스」
				DATAFORM 「냉동 귤」
				DATAFORM 「(무늬)격이 2개 붙은 소다 아이스」
				DATAFORM 「2개(살)로 1조의 달걀 아이스」
				DATAFORM 「츄-애완동물」
				DATAFORM 「파피코」
				DATAFORM 「연유가 찬 milk bar」
				DATAFORM 「아즈 어금니-」
				DATAFORM 「수박 바」
				DATAFORM 「가튼, 이라고 귤」
				DATAFORM 「블랙 번개 아이스」
				DATAFORM 「눈구경 큰 복」
			ENDDATA
		PRINTFORML (을)를 먹었다
		식품특징 = %ITEMNAME:사용아이템%/차가운/단/
		CALL RECOVER(MASTER, ITEM_TASTE(MASTER, 150, 식품특징), "기력", "아이스크림에 의한 회복")
		CALL 만복도상승(MASTER, "디저트")
			IF  TARGET && SHIRAHU(TARGET)
			CALL RECOVER(TARGET, ITEM_TASTE(TARGET, 150, 식품특징), "기력", "아이스크림에 의한 회복")
			CALL 만복도상승(TARGET, "디저트")
			ENDIF
		LOCAL:1 ++
	CASE 215
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 사용했다
		SETBIT TCVAR:MASTER:특별한차, 0
		LOCAL:1 ++
	CASE 252
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		TCVAR:MASTER:수면제강도 = 4
		LOCAL:1 ++
	CASE 352
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		식품특징 = %ITEMNAME:사용아이템%/야채/과일/단/
		CALL RECOVER(대상, ITEM_TASTE(대상, 50, 식품특징), "기력", "딸기에 의한 회복")
		LOCAL:1 ++
	CASE 364
		식품특징 = %ITEMNAME:사용아이템%/야채/과일/단/
		IF FLAG:70 == 1
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL RECOVER(MASTER, ITEM_TASTE(대상, 100, 식품특징), "기력", "수박에 의한 회복")
		ELSE
			PRINTFORML \@ TARGET != MASTER ? 다같이# \@ %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			FOR 전원, 0, CHARANUM
				SIF CFLAG:전원:현재위치 != CFLAG:MASTER:현재위치
					CONTINUE
				SIF !SHIRAHU(전원)
					CONTINUE
				CALL RECOVER(전원, ITEM_TASTE(대상, 100, 식품특징), "기력", "수박에 의한 회복")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:사용아이템%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
		PRINTL 맛없어! 한잔 더!
		; 한판 주, 맛 데이터에 苦い가 없는데 苦い를 쓰고 있음. 일단 주의
		식품특징 = %ITEMNAME:사용아이템%/苦い/苦い/苦い/건강한/야채/이파리/야취/
		CALL RECOVER(대상,100, "체력", "녹즙에 의한 회복")
		CALL RECOVER(대상, ITEM_TASTE(대상, 200, 식품특징), "기력", "녹즙에 의한 회복")
		LOCAL:1 ++
	CASE 501
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상, 약학지식(200), "체력", "영양드링크에 의한 회복")
			CALL RECOVER(대상, 약학지식(100), "기력", "영양드링크에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 약학지식(20)
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 502
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상, 약학지식(300), "정력", "정력제에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 503
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 파이팅 한발, 가득！
			CALL RECOVER(대상, 약학지식(500), "체력", "불끈불끈Z에 의한 회복")
			CALL RECOVER(대상, 약학지식(300), "기력", "불끈불끈Z에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 약학지식(60)
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	;CASE 504
	;		IF FLAG:70
	;			PRINTFORML 時間停止を解除してから使おう
	;			GOTO LOOP
	;		ENDIF
	;		IF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
	;			PRINTFORML これは自室で飲もう
	;			GOTO LOOP
	;		ENDIF
	;		PRINTFORML ストロング虚無を口にした途端、虚無の力で思考はぼやけ、すべてがどうでもよくなってきた
	;		PRINTFORML %CALLNAME:MASTER%は缶を空けながら一日を無為に過ごした…
	;		공허로회귀 = 1
	CASE 505	;반짝반짝Ｘ
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 24시간 싸울 수 있을 것 같은 생각이 든다…！
			CALL RECOVER(대상, 약학지식(800), "체력", "반짝반짝Ｘ에 의한 회복")
			CALL RECOVER(대상, 약학지식(600), "기력", "반짝반짝Ｘ에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 약학지식(180)
			CFLAG:대상:철야 = 1
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 506	;정력제『천원凸파』
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 너의××× 그리고 하늘을 찔러라!
			CALL RECOVER(대상, 약학지식(400), "체력", "정력제『천원凸파』에 의한 회복")
			CALL RECOVER(대상, 약학지식(300), "기력", "정력제『천원凸파』에 의한 회복")
			CALL RECOVER(대상, 약학지식(800), "정력", "정력제『천원凸파』에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 507
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상, 약학지식(150), "기력", "포션에 의한 회복")
			CALL RECOVER(대상, 약학지식(150), "TSP", "포션에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 508
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 마력이 넘쳐 온다…
			CALL RECOVER(대상, 약학지식(300), "기력", "매직포션에 의한 회복")
			CALL RECOVER(대상, 약학지식(600), "TSP", "매직포션에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 509
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상, 약학지식(200), "기력", "잠깨기포션에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 약학지식(240)
			CFLAG:대상:철야 = 1
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 510
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 간기능이 높아져, 취하기 어려워졌다
			TFLAG:멀미약 = 1
			FLAG:숙취 = FLAG:숙취 * 4 / 5
			LOCAL:1 ++
	CASE 511	;엘릭서
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
		PRINTL 피로가 저 쪽으로 물리쳤다…！
		CALL RECOVER(대상, MAXBASE:대상:체력 < 9999 ? 9999 # MAXBASE:대상:체력, "체력", "엘릭서에 의한 회복")
		CALL RECOVER(대상, MAXBASE:대상:기력 < 9999 ? 9999 # MAXBASE:대상:기력, "기력", "엘릭서에 의한 회복")
		CALL RECOVER(대상, MAXBASE:대상:정력 < 9999 ? 9999 # MAXBASE:대상:정력, "정력", "엘릭서에 의한 회복")
		CALL RECOVER(대상, MAXBASE:대상:TSP  < 9999 ? 9999 # MAXBASE:대상:TSP,  "TSP",  "엘릭서에 의한 회복")
		CFLAG:대상:졸음쫒기강도 = 300
		TCVAR:대상:약 = 1
		LOCAL:1 ++
	CASE 512	;선단
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 심상치 않은 힘이 넘친다……！
			CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 5, 500))
			CALL BUFF_BASE(대상, BASE_기력, MAX(MAXBASE:대상:기력 / 5, 500))
			CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 5, 500))
			LOCAL:1 ++
	CASE 514	;선향옥토
		IF TFLAG:선향옥토 > 0
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 향로에 지폈다
			PRINTL 현혹적인 훈향이 주위에 감돈다……
			TFLAG:선향옥토 = 240
			LOCAL:1 ++
		ENDIF
	CASE 515	;국사무쌍약　３回目まではバフ効果
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
		IF TCVAR:대상:국사무쌍약 < 3
			PRINTL 힘이 넘쳐 흐른다……！
			CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 15, 150))
			CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 10, 150))
			TCVAR:대상:국사무쌍약 ++
			LOCAL:1 ++
		ELSE
			;四回目で爆発、デバフに
			SETCOLOR C_RED
			PRINTFORMW ！！
			PRINTFORML ……폭발해서 아프로 머리가 되었다……너무 많이 마신 것 같다……
			RESETCOLOR
			CALL BUFF_BASE(대상, BASE_체력, MIN((-1 * (MAXBASE:대상:체력 / 5)), -450))
			CALL BUFF_BASE(대상, BASE_정력, MIN((-1 * (MAXBASE:대상: 정력 / 3)), -450))
			TCVAR:대상:국사무쌍약 = 0
			LOCAL:1 ++
		ENDIF
	CASE 516
		IF TFLAG:GG설치위치 == CFLAG:MASTER:현재위치
			PRINTFORMW %TEXTR(@"「제가 키웠습니다」（%GET_MAPNAME(MAIN_MAP)%거주　%CALLNAME:MASTER%씨）/본 품종은 안정성을 고려하고 있지만, 만일 불편하신 분이 있다면 거리를 두고 충분한 휴식을 취해 주세요")%
		ELSEIF TFLAG:GG설치위치
			PRINTFORML 가싱가든은 하루 한도 덧없는 생명
			PRINTFORMW 한 그루를 최후까지 지켜보자
			GOTO LOOP
		ELSEIF INROOM(CFLAG:MASTER:현재위치)
			PRINTFORMW 야외에서 하자
			GOTO LOOP
		;目撃者がいると駄目
		ELSEIF (TARGET > 0 || DATE_HITOGOMI(CFLAG:MASTER:현재위치) > 0) && !FLAG:70
			PRINTFORMW 여기서는 남의 눈이 있다……몰래 장치해야 할 것이다
			GOTO LOOP
		ELSE
			PRINTFORML %마스터는% 마법의 씨앗을 뿌렸다
			PRINTFORML 순식간에 마법 생물이 피어나기 시작해, 주위에 정욕을 돋구는 향기가 가득하다……
			TFLAG:GG설치위치 = CFLAG:MASTER:현재위치
			LOCAL:1 ++
		ENDIF
	CASE 517	;포이즌바디
		IF TFLAG:포이즌바디 > 0
			PRINTFORML 이 이상 섭취하면 간 기능에 장애가 온다
			GOTO LOOP
		ELSEIF HAS_PENIS(대상)
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 복용했다
			PRINTFORML 빙글빙글 몸 속에서 소용돌이치는 뭔가의 감각과 함께, 거기가 뜨거워졌다……
			PRINTFORML （%CALLNAME:MASTER%의 정액에 발정 작용 부여　효과발휘 중에는 사정할 때 체력・기력소비 증가）
			PRINTFORML %마스터는% 두통・현기증・구역질・관절통이 업습했지만, 건강보다 중요한 것도 있다며 마음을 굳게 먹고 견뎠다
			TFLAG:포이즌바디 = 240
			LOCAL:1 ++
		ELSE
			PRINTFORML 이 약은 남성 전용인 것 같다
			GOTO LOOP
		ENDIF
	CASE 520
			IF !TFLAG:키스마크
				PRINTFORML 지금 사용해도 의미가 없다
				GOTO LOOP
			ELSE
				PRINTFORML %마스터는% %조사처리(ITEMNAME:사용아이템,"를")% 감아 키스 마크를 숨겼다
				TFLAG:붕대 = 1
				LOCAL:1 ++
			ENDIF
	CASE 521
			IF FLAG:70
				PRINTFORML 시간 정지를 해제하고 나서 사용하자
				GOTO LOOP
			ENDIF
			PRINTFORML %마스터는% 역서를 열었다
			PRINTFORML 점쟁이가 나왔다
			PRINTFORMW 
			IF CFLAG:[[레이무]]:현재위치 == CFLAG:MASTER:현재위치
				PRINTFORML 재빠르게 레이무는 점쟁이의 머리에 불제봉을 내려쳤다.
				PRINTFORML 가련한 점쟁이는 머V리 가 되서 떠나갔다.
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
						BASE:LOCAL:이성 = MAXBASE:LOCAL:이성
						BASE:LOCAL:무드 = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ELSE
				PRINTFORML 「점술을 통해서 가야할 길을 봐 주겠소」
				$EKISYA
				CALL ASK_M("오늘의 운세", !GETBIT(FLAG:999, 1), "곤란해 하고있는 사람을 찾는다.", 1, "손톱깍이를 대령하라", BASE:MASTER:TSP >= 20, "미라다의돌 해주(100카리스마)", MONEY:2 >= 100, "엄지척!", 1, "공물로 바친다", 1)
				SELECTCASE RESULT
					CASE 0
						SELECTCASE RAND:10
							CASE 0
								TFLAG:행운보정 = 3
								PRINTFORML 「절호조다, 복권이라도 뽑으러 가는게 어때？」
							CASE 1 TO 3
								TFLAG:행운보정 = 1
								PRINTFORML 「양호하다, 미루고 있던 일이 있으면 지금 해라」
							CASE 4,5
								PRINTFORML 「좋지도, 나쁘지도 않음, 이라는 느낌이다.」
							CASE 6 TO 8
								TFLAG:행운보정 = -1
								PRINTFORML 「저조하다, 무리는 하지 말아라」
							CASE 9
								TFLAG:행운보정 = -3
								PRINTFORML 「...너무 안좋다, 오늘 하루는 얌전히 있어라」
						ENDSELECT
					CASE 1
						CLIENT_ID = SEARCH_UNIQUE_COM_사람돕기()
						IF !CLIENT_ID
							PRINTFORML 「그런 사람은 딱히 보이지 않는구나」
							PRINTFORML 「굳이 말하자면 나다, 홍백 무녀에게 노려지고 있다」
							PRINTFORML ...그건 어떻게 해줄수도 없을 것 같다.
						ELSE
							PRINTFORML 「%CALLNAME:CLIENT_ID% 인가?, 저 아이가 도움을 요구하고 있는 것 같구나」
						ENDIF
					CASE 2
						PRINTFORMW %마스터가% 점쟁이를 가만히 바라보자 눈길이 되돌아 왔다.
						PRINTFORMW ……뭐야 그 손톱은.
						PRINTFORMW 인간을 그만두었다는 어필일진 몰라도, 너무 길다.
						PRINTFORMW 일상 생활에서 불편하지 않나?, 타인을 실수로 긁어버리면 어떻게 하려는 거지?
						PRINTFORMW 자신은 언제 부드러운 부분에 손대도 괜찮게 매일 체크하고 있는데...
						PRINTFORMW 아니아니 애초에 무언가에 걸려 생살을 뜯어지게하면 아프지 않은가...
						PRINTFORMW 손톱 밑에 때가 껴도 불편하지 않은건가?
						PRINTFORMW 참을 수 없게 된 %마스터는% 시간을 멈추어 점쟁이의 손톱을 정중하게 잘라 가지런히 했다
						PRINTFORMW %마스터는% 큰 달성감을 맛보았다……
						PRINTFORML 한편 돌연 손톱이 짧아진 점쟁이는 곤혹하고 있다……
						CALL RECOVER_PERMIL(대상,40,"기력")
						BASE:MASTER:TSP -= 20
					CASE 3
						PRINTFORML 누구의 것을 해주 할까?
						FOR LOCAL,1,CHARANUM
							SIF CFLAG:LOCAL:몸목적
								PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
						NEXT
						PRINTFORML 
						DRAWLINE
						PRINTFORML [999] 그만둔다
						INPUT
						IF RESULT == 999
							GOTO EKISYA
						ELSEIF CFLAG:RESULT:몸목적
							PRINTFORML 「이런 도구는 좋지 않다고 생각한다」
							PRINTFORMW 점쟁이가 무엇인가 복잡한 의식을 거행하자, 미라다의 돌은 서서히 빛을 잃어 바스라졌다.
							TIME += 25
							CFLAG:RESULT:몸목적 = 0
							MONEY:2 -= 100
						ENDIF
					CASE 4
						PRINTFORML %마스터는% 점쟁이와 시선을 주고 받고는 무언으로 엄지척을 했다
						PRINTFORML 점쟁이는 잠깐 주저하다가, 강력한 엄지척 돌려주어 왔다
						PRINTFORMW 두 사람 사이에 우정이 자라났다.
						GOTO EKISYA
					CASE 5
						PRINTFORML %마스터는% 시간을 멈춰서 점쟁이의 옷을 벗긴 뒤, 속옷을 신께 공물로 바쳤다.
						CALL BUFF_BASE(MASTER,BASE_TSP,100,1)
						PRINTFORMW 자신도 모르는 사이에 속옷을 탈취당한 점쟁이는 당황하고 있다.
				ENDSELECT
				PRINTFORMW 
				DRAWLINE
				IF RAND:5 == 0
					GOTO EKISYA
				ENDIF
				$EKISYA_DIVIDE
				PRINTFORML 레이무가 나타나 점쟁이의 머리를 불제봉으로 내리쳤다.
				PRINTFORML 가련한 점쟁이는 머리가 2개로 갈라져 머V리가 됐다.
				PRINTFORMW 레이무는 %마스터를% 잠시 바라보더니 떠나갔다.
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
						BASE:LOCAL:이성 = MAXBASE:LOCAL:이성
						BASE:LOCAL:무드 = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ENDIF
	CASE 530
		CALL SET_MIRADA
		IF RESULT == -1
			RETURN -1
		ELSE
			LOCAL:1 ++
		ENDIF
	CASE 550	;야트메꼬치
		IF !TIME_PROGRESS(TCVAR:대상:공복시각)
			PRINTFORML %조사처리(CALLNAME:대상,"은")% 아직 배가 고프지 않다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL 만복도상승(대상,"경식")
			CALL TB외식하기("좋음")
			식품특징 = %ITEMNAME:사용아이템%/생선 요리/안주/
			CALL RECOVER_PERMIL(대상, ITEM_TASTE(대상, 40, 식품특징), "체력", 0, "야트메꼬치에 의한 회복")
			CALL RECOVER_PERMIL(대상, ITEM_TASTE(대상, 80, 식품특징), "기력", 0, "야트메꼬치에 의한 회복")
			CALL RECOVER_PERMIL(대상, ITEM_TASTE(대상, 50, 식품특징), "정력", 0, "야트메꼬치에 의한 회복")
			LOCAL:1 ++
		ENDIF
	;(酒造パッチ)아쿠마키
	CASE 576
		IF !TIME_PROGRESS(TCVAR:대상:공복시각)
			PRINTFORML %조사처리(CALLNAME:대상,"은")% 아직 배가 고프지 않다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL 만복도상승(대상,"주식")
			식품특징 = %ITEMNAME:사용아이템%/일식/쌀/경단/단/쫄깃한/가정적/봄철/
			CALL RECOVER(대상, ITEM_TASTE(대상, 150, 식품특징), "체력", "아쿠마키에 의한 회복")
			CALL RECOVER(대상, ITEM_TASTE(대상, 150, 식품특징), "기력", "아쿠마키에 의한 회복")
			LOCAL:1 ++
		ENDIF
	CASE 626	;나무딸기
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		식품특징 = %ITEMNAME:사용아이템%/과일/단/시큼한/야취/
		CALL RECOVER(대상, ITEM_TASTE(대상, 50, 식품특징), "기력", "나무딸기에 의한 회복")
		LOCAL:1 ++
	CASE 628	;으름덩굴
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		식품특징 = %ITEMNAME:사용아이템%/과일/단/야취/
		CALL RECOVER(대상,80, "체력", "으름덩굴에 의한 회복")
		CALL RECOVER(대상, ITEM_TASTE(대상, 80, 식품특징), "기력", "으름덩굴에 의한 회복")
		LOCAL:1 ++
	CASE 630	;선도
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		PRINTL 힘이 넘친다……！
		CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 10, 500))
		CALL BUFF_BASE(대상, BASE_기력, MAX(MAXBASE:대상:기력 / 10, 500))
		CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 10, 500))
		LOCAL:1 ++
	;(酒造パッチ)머루
	CASE 665
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		식품특징 = %ITEMNAME:사용아이템%/과일/단/시큼한/야취/
		CALL RECOVER(대상, ITEM_TASTE(대상, 50, 식품특징), "기력", "머루에 의한 회복")
		LOCAL:1 ++
	;(酒造パッチ)휴대용타코
	CASE 669
		IF !TIME_PROGRESS(TCVAR:대상:공복시각)
			PRINTFORML %조사처리(CALLNAME:대상,"은")% 아직 배가 고프지 않다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL 만복도상승(대상,"경식")
			;개인 주, 현재 タマネギ(양파)의 맛 데이터는 없음
			식품특징 = 양파/토마토/양배추/주식/야채/짐승고기/스파이스/치즈/매운/푸짐/농후/기능적/야취/선명한/사각사각/
			CALL RECOVER(대상, ITEM_TASTE(대상, 120, 식품특징), "체력", "타코에 의한 회복")
			CALL RECOVER(대상, ITEM_TASTE(대상, 120, 식품특징), "기력", "타코에 의한 회복")
			LOCAL:1 ++
		ENDIF
	CASE 950 ; 선도 농축액
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		IF RAND:5
			PRINTL 몸 속에서부터 힘이 넘쳐 흐르는 느낌이...!
			CALL BUFF_BASE(대상, BASE_기력, MAX(MAXBASE:대상:기력 / 20, 250))
			CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 20, 250))
			IF 대상 == MASTER && TALENT:MASTER:인간 != 2
				IF 선인화의길 < 10
					IF RAND:4 == 0
						PRINTL 몸 속에 선도의 성분이 들어가 조금씩 신체를 변화시키는 느낌이 든다...
						선인화의길++
						IF 선인화의길 >= 10
							PRINTFORMW 그러나 이 이상은 없는듯 하다, 지금부터는 신체를 단련시켜야 할 것이다
						ENDIF
					ENDIF
				ELSE
					PRINTFORML 몸에 쌓인 선도 성분은 더 이상 신체를 강화시키진 않는 모양이다
				ENDIF
			ENDIF
		ELSE
			PRINTL ...아무런 일도 일어나지 않는다
		ENDIF
		LOCAL:1 ++
	CASE 961 ;치르밀키트
		PRINTFORML 치르밀 키트의 포장을 까니 3병의 치르밀이 나왔다
		CALL COLORMESSAGE("그대로 까서 넣기만 하면 먹을수 있는 치르밀!", C_YELLOW, 2,2)
		CALL COLORMESSAGE("열을 가하면 변질되므로 꼭 그대로 먹어야해!", C_YELLOW, 2,2)
		ITEM:치르밀 += 3
		LOCAL:1 ++
	CASE 1020
		IF 대상 == MASTER
			PRINTFORML 가타도키-정® 1개를 먹였다
			IF TALENT:MASTER:요괴
				PRINTFORML 요력이 조금 회복되는 느낌이 든다
				CALL RECOVER(대상, 500, "체력", "요력 회복")
				CALL RECOVER(대상, 750, "기력", "요력 회복")
				FLAG:체내요력 += 200
			ELSE
				PRINTFORML ...이유 모를 불안감에 호흡이 괴롭다
				PRINTFORML 기력이 상했다
				BASE:MASTER:기력 -= 100
				PALAM:대상:공포 += 100
			ENDIF
			LOCAL:1 ++
		ELSEIF 대상 != TARGET || !TARGET || !TALENT:대상:요괴
			PRINTFORML 요괴말고는 먹이면 탈 난다
			GOTO LOOP
		ELSE
			PRINTFORML 가타도키-정® 1개를 먹였다
			CALL RECOVER(대상, 500, "체력", "요력 회복")
			CALL RECOVER(대상, 750, "기력", "요력 회복")
			CFLAG:대상:호감도 ++
			PALAM:대상:우월 += 10
			LOCAL:1 ++
		ENDIF
	CASE 1023
		IF TFLAG:아드레날린
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ENDIF
		PRINTFORML 약을 입에 털어넣자 심장이 쿵쾅거리고 근육에 힘이 돋는다!
		TFLAG:아드레날린 = 1
		CALL T_부상(MASTER,"힘",-30)
		CALL T_부상(MASTER,"물리내성",500)
		LOCAL:1 ++
	CASE 1024
		IF TFLAG:엑스트라인텔리
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ENDIF
		PRINTFORML 약을 입에 털어넣자 갑자기 사고능력이 빨라졌다!
		TFLAG:엑스트라인텔리 = 1
		LOCAL:1 ++
	CASE 1035
		IF 대상 == TARGET
			IF TALENT:대상:당신의노예 && !TALENT:대상:분유강화
				PRINTFORML %CALLNAME:대상%에게 락트 주사를 놓았다...
				IF TALENT:대상:모유체질
					PRINTFORML 가슴이 살짝 팽팽해진것 같은 느낌이다
					BASE:대상:모유 = 9999
					TALENT:대상:분유강화 = 5
				ELSE
					PRINTFORML ! 모유가 터져나온다!
					TALENT:대상:모유체질 = 1
					TALENT:대상:분유강화 = 5
					CFLAG:대상:분유해제 = 5
				ENDIF
				IF RAND:10 == 0 && TALENT:대상:모유체질 != 2
					PRINTFORML ...아무래도 모유가 멎을것 같지 않다
					CFLAG:대상:분유해제 = 0
					TALENT:대상:모유체질 = 2
				ENDIF
				RETURN
			ENDIF
		ENDIF
		PRINTFORMW 사용할수 없는 대상입니다
	CASE 1037
		IF !HAS_PENIS(대상)
			PRINTFORML 이 약은 자지가 나 있어야 쓸 수 있다
			GOTO LOOP
		ELSEIF 대상 != MASTER && !TALENT:대상:당신의노예 && !TALENT:대상:섹프 && !TALENT:대상:애욕 && !TALENT:대상:연모 && !TALENT:대상:연인
			PRINTFORML 먹으라 해도 먹을것 같지 않다, 좀 더 몸의 대화를 나누는 사이가 되지 않으면...
		ELSE
			IF 대상 != MASTER
				PRINTFORM %타겟은% 
			ENDIF
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			TCVAR:대상:미약정액 = 1
		ENDIF
	CASE 1038
		IF 대상 != MASTER
			;몰라
		ELSE
			IF !TIME_PROGRESS(TCVAR:MASTER:공복시각)
				PRINTFORML 아직은 배가 고프지 않다
				GOTO LOOP
			ENDIF
			PRINTFORM 도시락을 개봉하자, 방금 조리된듯한 
			SELECTCASE RAND:5
				CASE 0
					PRINTFORML 햄버그 정식이 들어있었다
					CALL 영양소증가(5,20,20,20,0)
				CASE 1
					PRINTFORML 가라아게 정식이 들어있었다
					CALL 영양소증가(0,5,30,10,0)
				CASE 2
					PRINTFORML 장어 덮밥이 들어있었다
					CALL 영양소증가(50,20,10,30,5)
				CASE 3
					PRINTFORML 하코즈시 정식이 들어 있었다
					CALL 영양소증가(40,10,5,10,0)
				CASE 4
					PRINTFORML 토마호크 스테이크가 들어 있었다
					CALL 영양소증가(0,20,25,25,0)
			ENDSELECT
			;달의 음식은 영양 보충을 위한 첨가제가 기본 첨가되어 있을것
			CALL 영양소증가(10,10,10,10,10)
			PRINTFORMW 훌륭한 맛이었다...
			CALL TB외식하기("호화로움")
			LOCAL:1++
		ENDIF
	CASE 1042
		IF !HAS_PENIS(대상)
			PRINTFORML 이 약은 자지가 나 있어야 쓸 수 있다
			GOTO LOOP
		ELSEIF 대상 != MASTER && !TALENT:대상:당신의노예 && !TALENT:대상:섹프 && !TALENT:대상:애욕 && !TALENT:대상:연모 && !TALENT:대상:연인
			PRINTFORML 먹으라 해도 먹을것 같지 않다, 좀 더 몸의 대화를 나누는 사이가 되지 않으면...
		ELSE
			IF 대상 != MASTER
				PRINTFORM %타겟은% 
			ENDIF
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			MAXBASE:대상:정력 = MAX(MAXBASE:대상:정력,MIN(MAXBASE:대상:정력 * 105 / 100, 5000))
			BASE:대상:정력 = MIN(MAXBASE:대상:정력,BASE:대상:정력 + 750)
			PRINTFORML 정력이 회복되는 느낌이 든다...!
			SIF 대상 == MASTER
				TALENT:대상:농후정액 = 1
			LOCAL:1++
		ENDIF
	CASE 1048
		IF 대상 != MASTER && !곰방대_같이피기(대상)
			PRINTFORML %타겟은% 피고 싶은 마음이 없는 모양이다
			GOTO LOOP
		ENDIF

		PRINTFORML 무엇을 필까?
		DRAWLINE
		PRINTFORML [01] 연초 * {ITEM:연초분말}
		PRINTFORML [02] 로터스 * {ITEM:로터스분말}
		DRAWLINE
		PRINTFORML [99] 돌아간다
		$곰방대루프
		INPUT
		IF RESULT == 99
			GOTO LOOP
		ELSE
			SELECTCASE RESULT
				CASE 1
					IF ITEM:연초분말
						ITEM:연초분말--
						LOCAL = GETNUM(ITEM,"연초분말")
					ELSE
						GOTO 곰방대루프
					ENDIF
				CASE 2
					IF ITEM:로터스분말
						ITEM:로터스분말--
						LOCAL = GETNUM(ITEM,"로터스분말")
					ELSE
						GOTO 곰방대루프
					ENDIF
				CASEELSE
					GOTO 곰방대루프
			ENDSELECT
		ENDIF
		D_섭취타입 = 1
		CALL 약물효과발동(대상,LOCAL)
		CALL RECOVER(대상, 100, "기력", "휴식 타임")
		IF 대상 == MASTER
			PRINTFORML %마스터는% %조사처리(D_이름,"을")% 피웠다
			TIME += 5
			D_섭취타입 = 3
			FOR A,1,CHARANUM
				IF CFLAG:A:현재위치 == CFLAG:MASTER:현재위치
					PRINTFORML 주위에 있던 %CALLNAME:A%에게 %조사처리(D_이름,"의")% 연기가 흘러갔다
					CALL 약물효과발동(A,LOCAL)
				ENDIF
			NEXT
		ELSE
			PRINTFORML %타겟은% %마스터가% 빌려준 곰방대로 %조사처리(D_이름,"을")% 피웠다
			TIME += 5
		ENDIF
	CASE 1050
		PRINTFORML 수면향을 피웠다
		CALL 지역별버프생성(CFLAG:MASTER:현재위치,1,75,180)
		LOCAL:1++
		TIME += 1
	CASE 3001
		PRINTFORML 돈봉투를 열어 5000\\을 꺼냈다
		LOCAL:1++
		MONEY += 5000
	CASE 3001
		IF !TALENT:대상:임신
			PRINTFORML 임신한 사람한테만 쓸 수 있다
		ENDIF
		IF 대상 != MASTER && !TALENT:대상:당신의노예 && !CFLAG:대상:수면
			PRINTFORML 먹으라 해도 먹을리가 없다
		ENDIF
		PRINTFORML %CALLNAME:대상%에게 보라색 사탕을 먹였다
		PRINTFORML 배가 갑자기 부풀어오른다...!
		LOCAL:1++
		CFLAG:대상:임신경과일수 += 20
		TBCFLAG:대상:뇌데미지 += 20
		MAXBASE:대상:기력 = (MAXBASE:대상:기력 * 85) / 100
	CASEELSE
		GOTO LOOP
ENDSELECT
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:사용아이템 -= 1
	SIF 공허로회귀
		CALL OYASUMI
	RETURN 0
ENDIF


@SET_MIRADA
PRINTFORML 대상을 선택해 주세요.
PRINTFORML 황색은 이미 유효, 핑크는 이미 함락 소질이 붙어 있는 캐릭터입니다.
SIF FLAG:정렬
	CALL CHARASORT_EX
FOR LOCAL:1, 1, EX_CHARANUM + 1
	IF FLAG:정렬
		LOCAL = RESULT:(LOCAL:1)
		SIF LOCAL < 0
			CONTINUE
	ELSE
		LOCAL = LOCAL:1
		SIF LOCAL >= CHARANUM
			BREAK
	ENDIF
	;SIF TALENT:LOCAL:스침
	;	CONTINUE
	SIF CFLAG:LOCAL:출금
		CONTINUE
	SIF CFLAG:LOCAL:몸목적
		SETCOLOR C_YELLOW
	SIF TALENT:LOCAL:섹프 || TALENT:LOCAL:연모
		SETCOLOR C_PINK
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [999] 그만둔다
INPUT
SIF !INRANGE(RESULT,1,CHARANUM)
	RETURN -1
SIF CFLAG:RESULT:몸목적
	RETURN -1
IF TALENT:RESULT:섹프 || TALENT:RESULT:연모
	TCVAR:RESULT:발정 = 1
ELSE
	CFLAG:RESULT:몸목적 = 1
ENDIF
PRINTFORMW %조사처리(CALLNAME:RESULT,"를")% 떠올리면서 미라다의 돌을 강하게 잡자, 돌은 희미하게 빛을 띠었다
RETURN 1

@사용가능아이템(ARG)
;1だとMASTERのみ、2だとあなた以外にも使える
#FUNCTION
SIF CFLAG:MASTER:우후후 && !DRUG(ARG)
	RETURNF 0
;(酒造パッチ)あくまきと머루、タコス
SELECTCASE ARG
	CASE 120 TO 123, 215, 252,364, 500, 510, 514, 515, 516, 517, 520, 521, 530, 1023,1024,1038,961,1050,3001
		RETURNF 1
	CASE 212, 213, 352, 501, 503, 505 TO 509, 511, 512, 550, 576,  626, 628, 630, 665, 669, 1035,1037,1020,1042,950,1048,3005
		RETURNF 2
	CASE 502
		SIF HAS_PENIS(TARGET)
			RETURNF 2
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@DRUG(ARG,ARG:1)
#FUNCTION
SIF GROUPMATCH(ARG, 501, 502, 503, 505, 506, 507, 508, 509, 511)
	RETURNF 1

@ITEM_SWEETS(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 120, 121, 123, 124, 212, 213)
	RETURNF 1

@PRINT_ITEM_EXPLANATION(ARG)
PRINTFORM [{ARG}] %ITEMNAME:ARG, 18, LEFT%
SIF ARG > 99
	PRINT   
SIF ITEM:ARG < 10
	PRINT  
PRINTFORM 　{ITEM:ARG}개　
SETCOLOR C_L_GRAY
	PRINTFORML %ITEM_EXPLANATION(ARG)%
RESETCOLOR

@약학지식(ARG)
#FUNCTION
#DIM 회복량
#DIM 강화횟수
회복량 = ARG
;回復量 = 基準値 * (1.1 ^ (조제Lv + 薬に関係しそうな知識))
강화횟수 = TALENT:MASTER:조제Lv + TALENT:MASTER:금단의지식 + TALENT:MASTER:조합지식
WHILE 강화횟수 > 0
	TIMES 회복량, 1.1
	강화횟수 --
WEND
RETURNF 회복량

;미각에 대한 회복 보정
;만복도가 오르지 않는 식품은 기력만 보정
@ITEM_TASTE(CHARA, 기준치, 식품특징)
#FUNCTION
#DIM CHARA
#DIM 기준치
#DIMS 식품특징
#DIMS TASTE_CHARA_GOOD
#DIMS TASTE_CHARA_BAD
#DIM 회복량
TASTE_CHARA_GOOD = %GET_STR(, "캐릭터데이터", CHARA, "요리：좋아하는 맛")%
TASTE_CHARA_BAD = %GET_STR(, "캐릭터데이터", CHARA, "요리：싫어하는 맛")%

회복량 = 기준치 + (STR_MULTI_COUNT(TASTE_CHARA_GOOD, 식품특징) - STR_MULTI_COUNT(TASTE_CHARA_BAD, 식품특징)) * 10

;제철 등 계절에 따른 보정
SELECTCASE GET_MONTH()
	CASE "봄의 달"
		IF STRCOUNT(식품특징, "봄철/") && !STRCOUNT(TASTE_CHARA_BAD, "봄철/")
			TIMES 회복량, 1.3
		ENDIF
	CASE "여름의 달"
		;夏は아이스크림が2.0倍(嫌いじゃなければ）
		IF STRCOUNT(식품특징, "아이스크림/") && !STRCOUNT(TASTE_CHARA_BAD, "아이스크림/")
			TIMES 회복량, 2.0
		ELSEIF STRCOUNT(식품특징, "수박/")
			TIMES 회복량, 1.3
		ENDIF
	CASE "가을의 달"
		IF STRCOUNT(식품특징, "으름덩굴/")
			TIMES 회복량, 1.3
		ENDIF
	CASE "겨울의 달"
		;冬は아이스크림が0.5倍(好きじゃなければ）
		IF STRCOUNT(식품특징, "아이스크림/") && !STRCOUNT(TASTE_CHARA_GOOD, "아이스크림/")
			TIMES 회복량, 0.5
		ENDIF
ENDSELECT

RETURNF 회복량

;아이템
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
RETURN 1
