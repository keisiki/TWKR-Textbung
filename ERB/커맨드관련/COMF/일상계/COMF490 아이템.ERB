;-------------------------------------------------
;アイテム画面
;-------------------------------------------------
@COM490
#DIM 회복전체력1
#DIM 회복전기력1
#DIM 회복전정력1
#DIM 회복후체력1
#DIM 회복후기력1
#DIM 회복후정력1
#DIM 회복전체력2
#DIM 회복전기력2
#DIM 회복전정력2
#DIM 회복후체력2
#DIM 회복후기력2
#DIM 회복후정력2
#DIM 대상
#DIM 전원
#DIM CLIENT_ID
#DIM 공허로회귀
#DIM 사용아이템
회복전체력1 = BASE:MASTER:체력
회복전기력1 = BASE:MASTER:기력
회복전정력1 = BASE:MASTER:정력
회복전체력2 = BASE:TARGET:체력
회복전기력2 = BASE:TARGET:기력
회복전정력2 = BASE:TARGET:정력
VARSET LOCAL
VARSET LOCALS
대상 = MASTER
공허로회귀 = 0
PRINTL 무엇을 사용합니까？
$LOOP
DRAWLINE
FOR LOCAL,120, 699
	IF 대상
		IF 사용가능아이템(LOCAL) == 2 && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ELSE
		IF 사용가능아이템(LOCAL) && ITEM:LOCAL
			CALL PRINT_ITEM_EXPLANATION(LOCAL)
		ENDIF
	ENDIF
NEXT
SIF ITEM:1023&&사용가능아이템(1023)
	CALL PRINT_ITEM_EXPLANATION(1023)
SIF ITEM:1024&&사용가능아이템(1024)
	CALL PRINT_ITEM_EXPLANATION(1024)
SIF ITEM:1035&&사용가능아이템(1035) && 대상
	CALL PRINT_ITEM_EXPLANATION(1035)
PRINTL
DRAWLINE
PRINTFORML [900] 콜렉션의 스키마 자동 사양 변환（현재\@ FLAG:C스키마자동사양 ? ON# OFF\@）
IF TARGET && SHIRAHU(TARGET)
	SIF 대상
		SETCOLOR C_YELLOW
	PRINTFORMLC [998] %CALLNAME:TARGET%에 사용한다
	RESETCOLOR
ENDIF

PRINTLC [999] 그만둔다
INPUT
사용아이템 = RESULT
IF DRUG(사용아이템,대상)
	PRINTL 오늘은 이제 그만두자
	GOTO LOOP
ENDIF
IF ITEM_SWEETS(사용아이템) && !TIME_PROGRESS(TCVAR:대상:디저트공복시각)
	PRINTL 아직 먹고 싶지 않다
	GOTO LOOP
ENDIF
IF RESULT == 900
	FLAG:C스키마자동사양 = !FLAG:C스키마자동사양
	PRINTFORMW 자동 사양을\@ FLAG:C스키마자동사양 ? ON# OFF\@(으)로 했습니다
	GOTO LOOP
ENDIF
SELECTCASE 사용아이템
	CASE 120
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			PRINTL ……싸구려 맛이 났다
			CALL RECOVER(MASTER,50, "체력", "초코에 의한 회복")
			TCVAR:대상:디저트공복시각 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 121
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL RECOVER(MASTER,300, "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER,300, "기력", "초코에 의한 회복")
			TCVAR:대상:디저트공복시각 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 122
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			PRINTFORML 맛있어!
			CALL RECOVER(MASTER,500, "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER,500, "기력", "초코에 의한 회복")
			TCVAR:대상:디저트공복시각 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 123
		IF 대상 == MASTER
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			PRINTFORML 사랑이 느껴져!
			CALL RECOVER(MASTER,1000, "체력", "초코에 의한 회복")
			CALL RECOVER(MASTER,1000, "기력", "초코에 의한 회복")
			TCVAR:대상:디저트공복시각 = 1440 * DAY + TIME + 180
			LOCAL:1 ++
		ENDIF
	CASE 212
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			LOCAL:1 ++
			CALL RECOVER(MASTER,100, "체력", "떡잎으로 싼 떡에 의한 회복")
			TCVAR:대상:디저트공복시각 = 1440 * DAY + TIME + 180
	CASE 213
		SIF TARGET && SHIRAHU(TARGET)
			PRINT 둘이 나란히 
			PRINTDATA
				DATAFORM 「아이스」
				DATAFORM 「냉동 귤」
				DATAFORM 「(무늬)격이 2개 붙은 소다 아이스」
				DATAFORM 「2개(살)로 1조의 달걀 아이스」
				DATAFORM 「츄-애완동물」
				DATAFORM 「파피코」
				DATAFORM 「연유가 찬 milk bar」
				DATAFORM 「아즈 어금니-」
				DATAFORM 「수박 바」
				DATAFORM 「가튼, 이라고 귤」
				DATAFORM 「블랙 번개 아이스」
				DATAFORM 「눈구경 큰 복」
			ENDDATA
		PRINTFORML (을)를 먹었다
			CALL RECOVER(MASTER,150, "기력", "아이스크림에 의한 회복")
			TCVAR:MASTER:디저트공복시각 = 1440 * DAY + TIME + 180
			IF  TARGET && SHIRAHU(TARGET)
				CALL RECOVER(TARGET,150, "기력", "아이스크림에 의한 회복")
				TCVAR:디저트공복시각 = 1440 * DAY + TIME + 180
			ENDIF
		LOCAL:1 ++
	CASE 215
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 사용했다
		SETBIT TCVAR:MASTER:특별한차, 0
		LOCAL:1 ++
	CASE 352
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		CALL RECOVER(대상,50, "기력", "딸기에 의한 회복")
		SIF 대상 == (NO:9)
			CALL RECOVER(대상,50, "기력", "딸기에 의한 회복")
		LOCAL:1 ++
	CASE 364
		IF FLAG:70 == 1
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL RECOVER(MASTER,100, "기력", "수박에 의한 회복")
		ELSE
			PRINTFORML 다같이 %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			FOR 전원, 0, CHARANUM
				SIF CFLAG:전원:현재위치 != CFLAG:MASTER:현재위치
					CONTINUE
				SIF !SHIRAHU(전원)
					CONTINUE
				CALL RECOVER(전원,100, "기력", "수박에 의한 회복")
			NEXT
		ENDIF
		LOCAL:1 ++
	;CASE 400
	;	PRINTFORML %CALLNAME:MASTER%は%ITEMNAME:사용아이템%を開いた
	;	CALL COM401
	;	LOCAL:1 ++
	CASE 500
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
		PRINTL 맛없어! 한잔 더!
		CALL RECOVER(대상,100, "체력", "녹즙에 의한 회복")
		CALL RECOVER(대상,200, "기력", "녹즙에 의한 회복")
		LOCAL:1 ++
	CASE 501
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상,200, "체력", "영양드링크에 의한 회복")
			CALL RECOVER(대상,100, "기력", "영양드링크에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 502
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			SIF GETBIT(TALENT:대상:성별, 1)
				CALL RECOVER(대상,300, "정력", "정력제에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 503
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 실 있고 개, -응！
			CALL RECOVER(대상,500, "체력", "불끈불끈Z에 의한 회복")
			CALL RECOVER(대상,300, "기력", "불끈불끈Z에 의한 회복")
			CFLAG:대상:385 = 60
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	;CASE 504
	;		IF FLAG:70
	;			PRINTFORML 時間停止を解除してから使おう
	;			GOTO LOOP
	;		ENDIF
	;		IF CFLAG:MASTER:현재위치 != CFLAG:MASTER:초기위치
	;			PRINTFORML これは自室で飲もう
	;			GOTO LOOP
	;		ENDIF
	;		PRINTFORML ストロング虚無を口にした途端、虚無の力で思考はぼやけ、すべてがどうでもよくなってきた
	;		PRINTFORML %CALLNAME:MASTER%は缶を空けながら一日を無為に過ごした…
	;		공허로회귀 = 1
	CASE 505	;반짝반짝Ｘ
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 24시간 싸울 수 있을 것 같은 생각이 든다…！
			CALL RECOVER(대상,800, "체력", "반짝반짝Ｘ에 의한 회복")
			CALL RECOVER(대상,600, "기력", "반짝반짝Ｘ에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 180
			CFLAG:대상:철야 = 1
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 506	;정력제『천원凸파』
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 너의××× 그리고 하늘을 찔러라!
			CALL RECOVER(대상,400, "체력", "정력제『천원凸파』에 의한 회복")
			CALL RECOVER(대상,300, "기력", "정력제『천원凸파』에 의한 회복")
			CALL RECOVER(대상,800, "정력", "정력제『천원凸파』에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 507
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상,150, "기력", "포션에 의한 회복")
			CALL RECOVER(대상,150, "TSP", "포션에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 508
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 마력이 넘쳐 온다…
			CALL RECOVER(대상,300, "기력", "매직포션에 의한 회복")
			CALL RECOVER(대상,600, "TSP", "매직포션에 의한 회복")
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 509
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			CALL RECOVER(대상,200, "기력", "잠깨기포션에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 240
			CFLAG:대상:철야 = 1
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 510
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 간기능이 높아져, 취하기 어려워졌다
			TFLAG:멀미약 = 1
			LOCAL:1 ++
	CASE 511	;엘릭서
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 피로가 저 쪽으로 물리쳤다…！
			CALL RECOVER(대상, MAXBASE:대상:체력, "체력", "엘릭서에 의한 회복")
			CALL RECOVER(대상,9999, "기력", "엘릭서에 의한 회복")
			SIF 대상 == MASTER
				CALL RECOVER(대상,9999, "TSP", "엘릭서에 의한 회복")
			CALL RECOVER(대상,9999, "정력", "엘릭서에 의한 회복")
			CFLAG:대상:졸음쫒기강도 = 300
			TCVAR:대상:약 = 1
			LOCAL:1 ++
	CASE 512	;선단
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
			PRINTL 심상하지 않은 힘이 넘친다……！
			CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 5, 500))
			CALL BUFF_BASE(대상, BASE_기력, MAX(MAXBASE:대상:기력 / 5, 500))
			CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 5, 500))
			LOCAL:1 ++
	CASE 514	;선향옥토
		IF TFLAG:선향옥토 > 0
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 향로에 지폈다
			PRINTL 현혹적인 훈향이 주위에 감돈다……
			TFLAG:선향옥토 = 240
			LOCAL:1 ++
		ENDIF
	CASE 515	;국사무쌍약　３回目まではバフ効果
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 마셨다
		IF TCVAR:대상:국사무쌍약 < 3
			PRINTL 힘이 넘쳐 흐른다……！
			CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 15, 150))
			SIF GETBIT(TALENT:대상:성별, 1)
				CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 10, 150))
			TCVAR:대상:국사무쌍약 ++
			LOCAL:1 ++
		ELSE
			;四回目で爆発、デバフに
			SETCOLOR C_RED
			PRINTFORMW ！！
			PRINTFORML ……폭발해서 아프로 머리가 되었다……너무 많이 마신 것 같다……
			RESETCOLOR
			CALL BUFF_BASE(대상, BASE_체력, MIN((-1 * (MAXBASE:대상:체력 / 5)), -450))
			SIF GETBIT(TALENT:대상:성별, 1)
				CALL BUFF_BASE(대상, BASE_정력, MIN((-1 * (MAXBASE:대상: 정력 / 3)), -450))
			TCVAR:대상:국사무쌍약 = 0
			LOCAL:1 ++
		ENDIF
	CASE 516
		IF TFLAG:GG설치위치 == CFLAG:MASTER:현재위치
			PRINTFORMW %TEXTR(@"「제가 키웠습니다」（%GET_MAPNAME(MAIN_MAP)%거주　%CALLNAME:MASTER%씨）/본 품종은 안정성을 고려하고 있지만, 만일 불편하신 분이 있다면 거리를 두고 충분한 휴식을 취해 주세요")%
		ELSEIF TFLAG:GG설치위치
			PRINTFORML 가싱가든은 하루 한도 덧없는 생명
			PRINTFORMW 한 그루를 최후까지 지켜보자
			GOTO LOOP
		ELSEIF INROOM(CFLAG:MASTER:현재위치)
			PRINTFORMW 야외에서 하자
			GOTO LOOP
		;目撃者がいると駄目
		ELSEIF (TARGET > 0 || DATE_HITOGOMI(CFLAG:MASTER:현재위치) > 0) && !FLAG:70
			PRINTFORMW 여기서는 남의 눈이 있다……몰래 장치해야 할 것이다
			GOTO LOOP
		ELSE
			PRINTFORML %마스터는% 마법의 씨앗을 뿌렸다
			PRINTFORML 순식간에 마법 생물이 피어나기 시작해, 주위에 정욕을 돋구는 향기가 가득하다……
			TFLAG:GG설치위치 = CFLAG:MASTER:현재위치
			LOCAL:1 ++
		ENDIF
	CASE 517	;포이즌바디
		IF TFLAG:포이즌바디 > 0
			PRINTFORML 이 이상 섭취하면 간 기능에 장애가 온다
			GOTO LOOP
		ELSEIF HAS_PENIS(대상)
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 복용했다
			PRINTFORML 빙글빙글 몸 속에서 소용돌이치는 뭔가의 감각과 함께, 거기가 뜨거워졌다……
			PRINTFORML （%CALLNAME:MASTER%의 정액에 발정 작용 부여　효과발휘 중에는 사정할 때 체력・기력소비 증가）
			PRINTFORML %마스터는% 두통・현기증・구역질・관절통이 업습했지만, 건강보다 중요한 것도 있다며 마음을 굳게 먹고 견뎠다
			TFLAG:포이즌바디 = 240
			LOCAL:1 ++
		ELSE
			PRINTFORML 이 약은 남성 전용인 것 같다
			GOTO LOOP
		ENDIF
	CASE 520
			IF !TFLAG:키스마크
				PRINTFORML 지금 사용해도 의미가 없다
				GOTO LOOP
			ELSE
				PRINTFORML %마스터는% %조사처리(ITEMNAME:사용아이템,"를")% 감아 키스 마크를 숨겼다
				TFLAG:붕대 = 1
				LOCAL:1 ++
			ENDIF
	CASE 521
			IF FLAG:70
				PRINTFORML 시간 정지를 해제하고 나서 사용하자
				GOTO LOOP
			ENDIF
			PRINTFORML %마스터는% 역서를 열었다
			PRINTFORML 점쟁이가 나왔다
			PRINTFORMW 
			IF CFLAG:1:현재위치 == CFLAG:MASTER:현재위치
				PRINTFORML 재빠르게 레이무는 점쟁이의 머리에 불제봉을 내려쳤다.
				PRINTFORML 가련한 점쟁이는 머V리 가 되서 떠나갔다.
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
						BASE:LOCAL:이성 = MAXBASE:LOCAL:이성
						BASE:LOCAL:무드 = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ELSE
				PRINTFORML 「점술을 통해서 가야할 길을 봐 주겠소」
				$EKISYA
				CALL ASK_M("오늘의 운세", !GETBIT(FLAG:999, 1), "곤란해 하고있는 사람을 찾는다.", 1, "손톱깍이를 대령하라", BASE:MASTER:TSP >= 20, "미라다의돌 해주(100카리스마)", MONEY:2 >= 100, "엄지척!", 1, "공물로 바친다", 1)
				SELECTCASE RESULT
					CASE 0
						SELECTCASE RAND:10
							CASE 0
								TFLAG:행운보정 = 3
								PRINTFORML 「절호조다, 복권이라도 뽑으러 가는게 어때？」
							CASE 1 TO 3
								TFLAG:행운보정 = 1
								PRINTFORML 「양호하다, 미루고 있던 일이 있으면 지금 해라」
							CASE 4,5
								PRINTFORML 「좋지도, 나쁘지도 않음, 이라는 느낌이다.」
							CASE 6 TO 8
								TFLAG:행운보정 = -1
								PRINTFORML 「저조하다, 무리는 하지 말아라」
							CASE 9
								TFLAG:행운보정 = -3
								PRINTFORML 「...너무 안좋다, 오늘 하루는 얌전히 있어라」
						ENDSELECT
					CASE 1
						CLIENT_ID = SEARCH_UNIQUE_COM_사람돕기()
						IF !CLIENT_ID
							PRINTFORML 「그런 사람은 딱히 보이지 않는구나」
							PRINTFORML 「굳이 말하자면 나다, 홍백 무녀에게 노려지고 있다」
							PRINTFORML ...그건 어떻게 해줄수도 없을 것 같다.
						ELSE
							PRINTFORML 「%CALLNAME:CLIENT_ID% 인가?, 저 아이가 도움을 요구하고 있는 것 같구나」
						ENDIF
					CASE 2
						PRINTFORMW %마스터가% 점쟁이를 가만히 바라보자 눈길이 되돌아 왔다.
						PRINTFORMW ……뭐야 그 손톱은.
						PRINTFORMW 인간을 그만두었다는 어필일진 몰라도, 너무 길다.
						PRINTFORMW 일상 생활에서 불편하지 않나?, 타인을 실수로 긁어버리면 어떻게 하려는 거지?
						PRINTFORMW 자신은 언제 부드러운 부분에 손대도 괜찮게 매일 체크하고 있는데...
						PRINTFORMW 아니아니 애초에 무언가에 걸려 생살을 뜯어지게하면 아프지 않은가...
						PRINTFORMW 손톱 밑에 때가 껴도 불편하지 않은건가?
						PRINTFORMW 참을 수 없게 된 %마스터는% 시간을 멈추어 점쟁이의 손톱을 정중하게 잘라 가지런히 했다
						PRINTFORMW %마스터는% 큰 달성감을 맛보았다……
						PRINTFORML 한편 돌연 손톱이 짧아진 점쟁이는 곤혹하고 있다……
						CALL RECOVER_PERMIL(대상,40,"기력")
						BASE:MASTER:TSP -= 20
					CASE 3
						PRINTFORML 누구의 것을 해주 할까?
						FOR LOCAL,1,CHARANUM
							SIF CFLAG:LOCAL:몸목적
								PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
						NEXT
						PRINTFORML 
						DRAWLINE
						PRINTFORML [999] 그만둔다
						INPUT
						IF RESULT == 999
							GOTO EKISYA
						ELSEIF CFLAG:RESULT:몸목적
							PRINTFORML 「이런 도구는 좋지 않다고 생각한다」
							PRINTFORMW 점쟁이가 무엇인가 복잡한 의식을 거행하자, 미라다의 돌은 서서히 빛을 잃어 바스라졌다.
							TIME += 25
							CFLAG:RESULT:몸목적 = 0
							MONEY:2 -= 100
						ENDIF
					CASE 4
						PRINTFORML %마스터는% 점쟁이와 시선을 주고 받고는 무언으로 엄지척을 했다
						PRINTFORML 점쟁이는 잠깐 주저하다가, 강력한 엄지척 돌려주어 왔다
						PRINTFORMW 두 사람 사이에 우정이 자라났다.
						GOTO EKISYA
					CASE 5
						PRINTFORML %마스터는% 시간을 멈춰서 점쟁이의 옷을 벗긴 뒤, 속옷을 신께 공물로 바쳤다.
						CALL BUFF_BASE(MASTER,BASE_TSP,100,1)
						PRINTFORMW 자신도 모르는 사이에 속옷을 탈취당한 점쟁이는 당황하고 있다.
				ENDSELECT
				PRINTFORMW 
				DRAWLINE
				IF RAND:5 == 0
					GOTO EKISYA
				ENDIF
				$EKISYA_DIVIDE
				PRINTFORML 레이무가 나타나 점쟁이의 머리를 불제봉으로 내리쳤다.
				PRINTFORML 가련한 점쟁이는 머리가 2개로 갈라져 머V리가 됐다.
				PRINTFORMW 레이무는 %마스터를% 잠시 바라보더니 떠나갔다.
				FOR LOCAL,1,CHARANUM
					IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
						BASE:LOCAL:이성 = MAXBASE:LOCAL:이성
						BASE:LOCAL:무드 = 0
					ENDIF
				NEXT
				LOCAL:1 ++
			ENDIF
	CASE 530
		CALL SET_MIRADA
		IF RESULT == -1
			RETURN -1
		ELSE
			LOCAL:1 ++
		ENDIF
	CASE 550
		IF !TIME_PROGRESS(TCVAR:대상:공복시각)
			PRINTFORML %조사처리(CALLNAME:대상,"은")% 아직 공복이 되지 않다
			GOTO LOOP
		ELSE
			PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
			CALL 만복도상승(대상,"경식")
			CALL RECOVER_PERMIL(대상,40,"체력")
			CALL RECOVER_PERMIL(대상,80,"기력")
			CALL RECOVER_PERMIL(대상,50,"정력")
			LOCAL:1 ++
		ENDIF
	CASE 626	;나무딸기
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		CALL RECOVER(대상,50, "기력", "나무딸기에 의한 회복")
		LOCAL:1 ++
	CASE 628	;으름덩굴
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		CALL RECOVER(대상,80, "체력", "으름덩굴에 의한 회복")
		CALL RECOVER(대상,80, "기력", "으름덩굴에 의한 회복")
		LOCAL:1 ++
	CASE 630	;선도
		PRINTFORML %조사처리(ITEMNAME:사용아이템,"를")% 먹었다
		PRINTL 힘이 넘친다……！
		CALL BUFF_BASE(대상, BASE_체력, MAX(MAXBASE:대상:체력 / 10, 500))
		CALL BUFF_BASE(대상, BASE_기력, MAX(MAXBASE:대상:기력 / 10, 500))
		SIF GETBIT(TALENT:대상:성별, 1)
			CALL BUFF_BASE(대상, BASE_정력, MAX(MAXBASE:대상:정력 / 10, 500))
		LOCAL:1 ++
	CASE 998
		IF 대상
			대상 = MASTER
		ELSEIF SHIRAHU(TARGET) && TARGET
			대상 = TARGET
		ENDIF
		GOTO LOOP
	CASE 999
		RETURN -1
	CASE 1023
		IF TFLAG:아드레날린
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ENDIF
		PRINTFORML 약을 입에 털어넣자 심장이 쿵쾅거리고 근육에 힘이 돋는다!
		TFLAG:아드레날린 = 1
		CALL T_부상(MASTER,"힘",-30)
		CALL T_부상(MASTER,"물리내성",500)
		LOCAL:1 ++
	CASE 1024
		IF TFLAG:엑스트라인텔리
			PRINTFORML 이미 사용하고 있다
			GOTO LOOP
		ENDIF
		PRINTFORML 약을 입에 털어넣자 갑자기 사고능력이 빨라졌다!
		TFLAG:엑스트라인텔리 = 1
		LOCAL:1 ++
	CASE 1035
		IF 대상 == TARGET
			IF TALENT:대상:당신의노예 && !TALENT:대상:분유강화
				PRINTFORML %CALLNAME:대상%에게 락트 주사를 놓았다...
				IF TALENT:대상:모유체질
					PRINTFORML 가슴이 살짝 팽팽해진것 같은 느낌이다
					BASE:대상:모유 = 9999
					TALENT:대상:분유강화 = 5
				ELSE
					PRINTFORML ! 모유가 터져나온다!
					TALENT:대상:모유체질 = 1
					TALENT:대상:분유강화 = 5
					CFLAG:대상:분유해제 = 5
				ENDIF
				IF RAND:10 == 0 && TALENT:대상:모유체질 != 2
					PRINTFORML ...아무래도 모유가 멎을것 같지 않다
					CFLAG:대상:분유해제 = 0
					TALENT:대상:모유체질 = 2
				ENDIF
				RETURN
			ENDIF
		ENDIF
		PRINTFORMW 사용할수 없는 대상입니다
	CASEELSE
		GOTO LOOP
ENDSELECT
IF LOCAL:1
	IF !FLAG:70
		TIME += 5
	ELSE
		BASE:MASTER:TSP -= 5
	ENDIF
	ITEM:사용아이템 -= 1
	회복후체력1 = BASE:대상:체력
	회복후기력1 = BASE:대상:기력
	회복후정력1 = BASE:대상:정력
	회복후체력2 = BASE:대상:체력
	회복후기력2 = BASE:대상:기력
	회복후정력2 = BASE:대상:정력
	;@RECOVERとの二重表記を避けるために、100未満を回復量にとるもののみ、以下の表記を行う
	IF GROUPMATCH(사용아이템, 120, 352, 626, 628)
		SETCOLOR C_GREEN
		IF !대상
			SIF ABS(회복후체력1 - 회복전체력1)
				PRINTFORML 체력＋{ABS(회복후체력1 - 회복전체력1)}（%CALLNAME:대상%）
			SIF ABS(회복후기력1 - 회복전기력1)
				PRINTFORML 기력＋{ABS(회복후기력1 - 회복전기력1)}（%CALLNAME:대상%）
			SIF ABS(회복후정력1 - 회복전정력1)
				PRINTFORML 정력＋{ABS(회복후정력1 - 회복전정력1)}（%CALLNAME:대상%）
		ELSE
			SIF ABS(회복후체력2 - 회복전체력2)
				PRINTFORML 체력＋{ABS(회복후체력2 - 회복전체력2)}（%CALLNAME:대상%）
			SIF ABS(회복후기력2 - 회복전기력2)
				PRINTFORML 기력＋{ABS(회복후기력2 - 회복전기력2)}（%CALLNAME:대상%）
			SIF ABS(회복후정력2 - 회복전정력2)
				PRINTFORML 정력＋{ABS(회복후정력2 - 회복전정력2)}（%CALLNAME:대상%）
		ENDIF
		RESETCOLOR
	ENDIF
	SIF 공허로회귀
		CALL OYASUMI
	RETURN 0
ENDIF


@SET_MIRADA
PRINTFORML 대상을 선택해 주세요.
PRINTFORML 황색은 이미 유효, 핑크는 이미 함락 소질이 붙어 있는 캐릭터입니다.
FOR LOCAL,1,CHARANUM
	;SIF TALENT:LOCAL:스침
	;	CONTINUE
	SIF CFLAG:LOCAL:출금
		CONTINUE
	SIF CFLAG:LOCAL:몸목적
		SETCOLOR C_YELLOW
	SIF TALENT:LOCAL:섹프 || TALENT:LOCAL:연모
		SETCOLOR C_PINK
	PRINTFORMLC [{LOCAL}] %CALLNAME:LOCAL%
	RESETCOLOR
NEXT
PRINTFORML 
DRAWLINE
PRINTFORML [999] 그만둔다
INPUT
SIF !INRANGE(RESULT,1,CHARANUM)
	RETURN -1
SIF CFLAG:RESULT:몸목적
	RETURN -1
IF TALENT:RESULT:섹프 || TALENT:RESULT:연모
	TCVAR:RESULT:발정 = 1
ELSE
	CFLAG:RESULT:몸목적 = 1
ENDIF
PRINTFORMW %조사처리(CALLNAME:RESULT,"를")% 떠올리면서 미라다의 돌을 강하게 잡자, 돌은 희미하게 빛을 띠었다
RETURN 1

@사용가능아이템(ARG)
;1だとMASTERのみ、2だとあなた以外にも使える
#FUNCTION
SELECTCASE ARG
	CASE 120 TO 123, 215, 364, 500, 510, 514, 515, 516, 517, 520, 521, 530, 1023,1024
		RETURNF 1
	CASE 212, 213, 352, 501 TO 503, 505 TO 509, 511, 512, 550, 626, 628, 630,1035
		RETURNF 2
	CASEELSE
		RETURNF 0
ENDSELECT

@DRUG(ARG,ARG:1)
#FUNCTION
SIF GROUPMATCH(ARG, 501, 502, 503, 505, 506, 507, 508, 509, 511) && TCVAR:(ARG:1):약
	RETURNF 1

@ITEM_SWEETS(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 120, 121, 123, 124, 212, 213)
	RETURNF 1

@PRINT_ITEM_EXPLANATION(ARG)
PRINTFORM [{ARG}] %ITEMNAME:ARG, 18, LEFT%
SIF ARG > 99
	PRINT   
SIF ITEM:ARG < 10
	PRINT  
PRINTFORM 　{ITEM:ARG}개　
SETCOLOR C_L_GRAY
	PRINTFORML %ITEM_EXPLANATION(ARG)%
RESETCOLOR

;アイテム
@COM_ABLE490
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(490)
	RETURN RESULT
SIF CFLAG:MASTER:우후후
	RETURN 0
RETURN 1
