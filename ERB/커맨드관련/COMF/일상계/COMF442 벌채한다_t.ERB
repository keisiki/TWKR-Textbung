;벌채한다
@COM442
#DIM CHARA
#DIM 목재종류
#DIM 크기
#DIM 벌채파워
#DIM 감소치
#DIM 벌채시간
#DIM 채집파워
#DIM 채집물
#DIM 장작패기
#DIM 탐색횟수

;통나무を抱えてるときは장작패기するか選択　장작패기したら체력기력消費時間経過させる
IF ITEM:통나무
	;依頼の報酬など、벌채以外で통나무を入手してしまった場合の退避用
	SIF !FLAG:통나무종류
		FLAG:통나무종류 = MAX(RAND:12, 1)
	DRAWLINE
	PRINTFORML 먼저 가지고 있는 통나무를 어떻게든 하지 않으면
	CALL WOOD_CHOPPING(FLAG:통나무종류)
	IF ITEM:통나무 == 0
		벌채파워 = TALENT:MASTER:벌채Lv * 20
		감소치 = 350 - 벌채파워
		벌채시간 = 180 - 벌채파워
		IF ITEM:전기톱
			감소치 /= 2
			벌채시간 /= 2
		ENDIF
		IF FLAG:70
			BASE:MASTER:TSP -= 감소치
		ELSE
			DOWNBASE:MASTER:체력 = 감소치
			IF ITEM:전기톱
				DOWNBASE:MASTER:기력 = 감소치 -50
			ELSE
				DOWNBASE:MASTER:기력 = 감소치 -25
			ENDIF
			TIME += 벌채시간
		ENDIF
		RETURN 1
	ELSE
		RETURN -1
	ENDIF
ENDIF
;夕方～夜の벌채はやめる
IF TIME:2 >= 5 && !FLAG:70
	DRAWLINE
	PRINTFORML 이 시간에 시작하면 자르고 있는 사이에 캄캄해져 버린다…
	RETURN -1
ENDIF
;大雨、台風、吹雪のときはやめる
IF GROUPMATCH(날씨, 5, 9) || GROUPMATCH(FLAG:이상기상, 9, 10)
	DRAWLINE
	PRINTFORML 이런 날씨에서 벌채는 위험할 것 같다
	RETURN -1
ENDIF
;あからさまな人の敷地での盗伐はやめる
SELECTCASE CFLAG:MASTER:현재위치
	CASE 25, 50
		IF ABL:[[레이무]]:친밀 < 5
			DRAWLINE
			PRINTFORML 여기서 무단으로 벌채를 하기에는 좀 거북한 느낌이 든다
			RETURN -1
		ENDIF
	CASE 사각의죽림, 433
		IF ABL:[[모코우]]:친밀 < 5 && ABL:[[테위]]:친밀 < 5 && ABL:[[카구야]]:친밀 < 5 && ABL:[[에이린]]:친밀 < 5
			DRAWLINE
			PRINTFORML 여기서 무단으로 벌채를 하기에는 좀 거북한 느낌이 든다
			RETURN -1
		ENDIF
	CASE 절경의언덕, 805
		IF ABL:[[아야]]:친밀 < 5 && ABL:[[하타테]]:친밀 < 5
			DRAWLINE
			PRINTFORML 여기서 무단으로 벌채를 하기에는 좀 거북한 느낌이 든다
			RETURN -1
		ENDIF
ENDSELECT

TFLAG:무드상승억제 = 1

;先に벌채파워算出
벌채파워 = 1 + (TALENT:MASTER:벌채Lv * 2) + TALENT:MASTER:교양
;参加者判定
IF FLAG:데이트상대 && !FLAG:70
	CHARA = FLAG:데이트상대
ELSEIF TARGET && ABL:TARGET:친밀 >= 10 && !FLAG:70
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF
;参加者がいる場合は벌채파워追加
SIF CHARA && !FLAG:70
	벌채파워 += (TALENT:TARGET:벌채Lv * 2) + TALENT:TARGET:교양

탐색횟수 = 0

$LOOP_SELECT

REPEAT 5
	REUSELASTLINE  \@ CHARA ? %조사처리(CALLNAME:CHARA,"과")% 함께 #  \@ 벌채할 것을 수색중.%"." * COUNT%
	TWAIT 150, 1
REND

;벌채후보の목재종류とその크기を決定
목재종류 = WOOD_SPECIES(벌채가능(CFLAG:MASTER:현재위치))
크기 = 0
IF RAND:5 == 0 && 목재종류 <= 600 && 목재종류 != 12
	크기 ++
	SIF RAND:3 == 0
		크기 ++
ENDIF

;벌채후보をテキスト出力
IF 목재종류 == 646
	PRINTFORML 잔가지를 꺾어 보니, 향긋한 나무가 나 있다…
ELSEIF 목재종류 == 12
	PRINTFORML 대나무가 규칙적으로 곧게 뻗어 있다…
ELSE
	PRINTFORML 이 나무는 어떨까…
ENDIF
DRAWLINE
SETCOLOR C_YELLOW
PRINTFORM 찾은 \@ 목재종류 == 12 ? 것 # 나무 \@：%WOOD_NAME(목재종류)%
SELECTCASE 크기
	CASE 1
		PRINTFORM (큰 나무)
	CASE 2
		PRINTFORM (거목)
ENDSELECT
RESETCOLOR

;체력기력の감소치と벌채にかかる時間を算出
감소치 = WOOD_EFFORT(목재종류, 크기) - (WOOD_EFFORT(목재종류, 크기) * (벌채파워 / 70))
SIF ITEM:전기톱
	감소치 -= SQRT(WOOD_EFFORT(목재종류, 크기)) * 10
IF CHARA && !FLAG:70
	벌채시간 = 감소치 / (4 + TALENT:MASTER:벌채Lv + SQRT(TALENT:TARGET:벌채Lv))
ELSE
	벌채시간 = 감소치 / (4 + TALENT:MASTER:벌채Lv)
ENDIF

;벌채시간に応じてテキスト出力
SETCOLOR C_L_GRAY
SELECTCASE 벌채시간
	CASE IS <= 60 
		PRINTFORML 　（비교적 쉽게 벨 수 있을 것 같다）
	CASE IS <= 120
		PRINTFORML 　（베는 것은 그럭저럭 시간이 걸릴 것이다）
	CASE IS <= 180
		PRINTFORML 　（좀 애를 먹을 것 같다…）
	CASE IS <= 240
		PRINTFORML 　（이건 고생할 것 같다…）
	CASE IS <= 300
		PRINTFORML 　（마음을 다잡고 차근차근 하지 않으면 안될 것 같다…）
	CASE IS <= 360
		PRINTFORML 　（어마어마한 일이 될 것 같다…）
	CASEELSE
		PRINTFORML 　（지금의 솜씨로는 꽤 힘든 일이 될 것 같다…）
ENDSELECT
RESETCOLOR
PRINTL 
;체력・기력が実施後マイナスになる場合は、목재종류選択に戻る
IF (!FLAG:70 && (BASE:MASTER:체력 <= 감소치 || BASE:MASTER:기력 <= 감소치)) || (FLAG:70 && BASE:MASTER:TSP <= (벌채시간 * 10) + (탐색횟수 * 5))
	PRINTFORMW 이걸 베기엔 지금은 조금 조건이 나쁘다……다른 것을 찾자
	PRINTL 
	탐색횟수 ++
	;탐색횟수が10回を超えた場合は無限ループ除けのためにリターンする
	IF 탐색횟수 >= 10
		PRINTFORMW ……너무 열중하면 헤멜 것 같아, 이번에는 이 정도로 해 두자
		IF !FLAG:70
			TIME += 탐색횟수 * 5
		ELSE
			BASE:MASTER:TSP -= 탐색횟수 * 5
		ENDIF
		RETURN -1
	ENDIF
	GOTO LOOP_SELECT
ENDIF
PRINTFORML 이것을 벌채할까요？
CALL ASK_YN
IF !RESULT
	CLEARLINE 4
ELSE
	CLEARLINE 8
	탐색횟수 ++
	GOTO LOOP_SELECT
ENDIF
CALL DQPRINT("당신은 나무를 자른다ー", 1)
CALL DQPRINT("헤이 헤이 호-", 1)
CALL DQPRINT("헤이 헤이 호-", 1)
SIF 벌채시간 >= 540
	CALL DQPRINT("………………………", 1)
SIF 벌채시간 >= 480
	CALL DQPRINT("……………………", 1)
SIF 벌채시간 >= 420
	CALL DQPRINT("…………………", 1)
SIF 벌채시간 >= 360
	CALL DQPRINT("………………", 1)
SIF 벌채시간 >= 300
	CALL DQPRINT("……………", 1)
SIF 벌채시간 >= 240
	CALL DQPRINT("…………", 1)
CALL DQPRINT("………", 1)
CALL DQPRINT("……", 1)

;時間経過と체력消費
IF FLAG:70
	BASE:MASTER:TSP -= (벌채시간 * 10) + (탐색횟수 * 5)
ELSE
	DOWNBASE:MASTER:체력 = 감소치
	DOWNBASE:MASTER:기력 = 감소치
	TIME += 벌채시간 + (탐색횟수 * 10)
ENDIF

;경험追加処理
EXP:MASTER:벌채경험 += MAX((벌채시간 / 20), 1)
;試行回数が少ないだろうから確定
SIF ITEM:나무꾼의마음가짐
	EXP:MASTER:벌채경험 ++
;크기補正
SIF 크기
	EXP:MASTER:벌채경험 += (크기 * 4)
;手伝ってくれたキャラの경험
IF CHARA
	EXP:CHARA:벌채경험 += MAX((벌채시간 / 20), 1)
	SIF 크기
		EXP:CHARA:벌채경험 += (크기 * 4)
ENDIF

;데이트경험
IF CHK_DATENOW(CFLAG:CHARA:데이트중) && CHARA > 0
	EXP:MASTER:데이트경험 ++
	EXP:CHARA:데이트경험 ++
ENDIF
SETCOLOR 0x00FF00
;アイテム入手処理
SELECTCASE 목재종류
	CASE 12, 646
		IF 목재종류 == 12
			;竹だけはすぐに製材番号に変換 FLAG:통나무종류には記録しない
			PRINTFORMDL 시간정지 능력을 응용하여, 순식간에 건조!
			PRINTFORMW 대나무 제재를 손에 넣었다
			LOG_SALES:12 ++
		ELSE
			;香木ほかの処理
			PRINTFORMW %조사처리(WOOD_NAME(목재종류),"를")% 손에 넣었다
			SIF ITEM:목재종류 < 99
				ITEM:목재종류 ++
		ENDIF
	CASEELSE
		;傷んだ木は即座に목재に変換(未実装　テンポ悪くなりそう)

		;普通の木に関してはまとめて
		ITEM:통나무 ++
		FLAG:통나무종류 = 목재종류
		;크기をFLAGに反映
		FLAG:통나무종류 += 100 * 크기
		PRINTFORML 시간정지 능력을 응용하여, 순식간에 건조!
		PRINTFORMW %WOOD_NAME(FLAG:통나무종류)%의 통나무를 손에 넣었다

ENDSELECT
RESETCOLOR
;デバッグ用
;IF FLAG:통나무종류
;	PRINTFORMW 　　確認：%WOOD_NAME(FLAG:통나무종류)%
;ENDIF
PRINTL 

;장작패기確認 장작패기時間・체력消費はサービス
IF FLAG:통나무종류
	CALL WOOD_CHOPPING(FLAG:통나무종류)
ENDIF

;オマケ채집물  채집Lvに依存  あくまでおまけなので控えめに
채집파워 = TALENT:MASTER:채집Lv * 10 + ABL:MASTER:교양 * 5 + TFLAG:행운보정 * 3
SIF CHARA && !FLAG:70
	채집파워 += (10 + TALENT:CHARA:채집Lv * 10 + ABL:CHARA:교양 * 5) / 2

채집물 = 0
SELECTCASE RAND:(1 + 2 * SQRT(채집파워))
	CASE 0 TO 6
		;失敗
	CASE 7, 9, 11, 13, 15, 17, 19, 21, 23
		IF 벌채가능(CFLAG:MASTER:현재위치) == 2
			;竹林では何も手に入らない
		ELSEIF DAY:2 == 1 || DAY:2 == 2;春夏
			채집물 = 626;キイチゴ
		ELSEIF DAY:2 == 3;秋
			SELECTCASE RAND:3;キイチゴ、アケビ、ぶどう
				CASE 0
					채집물 = 377
				CASE 1
					채집물 = 626
				CASEELSE
					채집물 = 628
			ENDSELECT
		ENDIF
		SIF 채집물
			PRINTFORMW 나무에 무엇인가 달려있다…
	CASEELSE
		IF 벌채가능(CFLAG:MASTER:현재위치) == 2;春の竹林ではタケノコ
			SIF DAY:2 == 1
				채집물 = 629
		ELSEIF DAY:2 == 1 || DAY:2 == 3;春秋
			SELECTCASE RAND:2;普通のキノコか山菜
				CASE 0
					채집물 = 600
				CASEELSE
					채집물 = 611
			ENDSELECT
		ELSEIF DAY:2 == 2;夏
			채집물 = 600;			普通のキノコのみ
		ELSE
			SIF RAND:2 == 0;			冬はさらに50％の確率で氷の鱗
				채집물 = 609
		ENDIF
		SIF 채집물
			PRINTFORMW 땅바닥에 무언가 있다…
ENDSELECT
IF 채집물
	SETCOLOR 0x00FF00
	PRINTFORMW %조사처리(ITEMNAME:채집물,"을")% 손에 넣었다
	RESETCOLOR
	SIF ITEM:채집물 < 99
		ITEM:채집물 ++
	EXP:MASTER:채집경험 ++
	SIF CHARA 
		EXP:CHARA:채집경험 ++
ENDIF

;공복시각と参加者の疲労度
TCVAR:MASTER:공복시각 -= 25
IF CHARA
	TCVAR:CHARA:공복시각 -= 25
	CALL ADD_TIRED(CHARA, POWER((크기 + 1) + 1, 3) * 100)
ENDIF

RETURN 1

;-------------------------------------------------
;実行가능判定
;-------------------------------------------------
;벌채する
@COM_ABLE442
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(442)
	RETURN RESULT
SIF CFLAG:우후후
	RETURN 0
;벌목 가능한 장소에서만
SIF !벌채가능(CFLAG:MASTER:현재위치)
	RETURN 0
;데이트중은 데이트상대하고만
SIF FLAG:데이트상대 && TARGET != FLAG:데이트상대
	RETURN 0
;수면중
SIF CFLAG:MASTER:수면
	RETURN 0
;체력・기력부족
SIF BASE:MASTER:기력 < 800 || BASE:MASTER:체력 < 800
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;현재 위치에서 벌목 지점을 산출하는 함수
;-----------------------------------------------------------
@벌채가능(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 50
			RETURNF 1
		CASE 사각의죽림
			RETURNF 2
		CASE 숲의입구
			RETURNF 3
		CASE 산록의수해
			RETURNF 4
		CASE 절경의언덕
			RETURNF 5
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 25;진수의숲
			RETURNF 1
		CASE 433;사각의죽림
			RETURNF 2
		CASE 507 ;버섯군생지
			RETURNF 3
		CASE 702;山道・강으로가는길
			RETURNF 4
		CASE 805;산정
			RETURNF 5
	ENDSELECT
ENDIF
RETURNF 0

;-----------------------------------------------------------
;벌채スポットから伐れる목재종류を割り出す関数
;ARG 벌채가능
;-----------------------------------------------------------
@WOOD_SPECIES(ARG)
#FUNCTION
#DIM 후보

$LOOP

후보 = RAND:12
SELECTCASE ARG
	CASE 1;鎮守の森、妖精の大樹
		SIF GROUPMATCH(후보, 1, 2, 3, 6, 7, 8, 9)
			RETURNF 후보
	CASE 2;사각의죽림
		;竹材に固定
		RETURNF 12
	CASE 3;魔法の森
		SIF 후보 == 0 && RAND:2 == 0
			RETURNF 646
		SIF GROUPMATCH(후보, 1, 2, 4, 6, 7, 8, 9)
			RETURNF 후보
	CASE 4;麓の樹海
		SIF 후보 == 0 && RAND:2 == 0
			RETURNF 646
		SIF GROUPMATCH(후보, 1, 2, 3, 5, 7, 9, 10)
			RETURNF 후보
	CASE 5;절경의언덕 標高高め
		SIF GROUPMATCH(후보, 3, 5, 7, 9, 10, 11)
			RETURNF 후보
ENDSELECT
GOTO LOOP

;-----------------------------------------------------------
;통나무・竹材の名称を返す関数
;ARG:0 …FLAG:통나무종류　（목재종류番号でも通る）
;ARG:1 =1 「の大木・の巨木」を文字列に反映しない
;      省略した場合は =0 (巨木・大木表示あり)
;RETURNF 1～ 普通の木 100～ 〇〇の大木 200～ 〇〇の巨木
;-----------------------------------------------------------
@WOOD_NAME(ARG:0, ARG:1 = 0)
#FUNCTIONS
#DIM 번호보정
#DIMS 가명

번호보정 = ARG:0 % 100

SELECTCASE 번호보정
	CASE 646
		RETURNF "향목"
	CASEELSE
		가명 = %WoodName:번호보정%
ENDSELECT

IF ARG:1 == 0
	SELECTCASE ARG:0
		CASE 101 TO 199
			가명 += "(큰 나무)"
		CASE 201 TO 299
			가명 += "(거목)"
	ENDSELECT
ENDIF
RETURNF 가명
;-----------------------------------------------------------
;통나무・竹材の벌채にかかる체력・기력を返す関数
;ARG:0 목재종류番号	ARG:1 크기
;-----------------------------------------------------------
@WOOD_EFFORT(ARG:0, ARG:1)
#FUNCTION
#DIM 베이스

SELECTCASE ARG:0
	CASE 646;香木
		베이스 = 600
	CASEELSE
		베이스 = WoodEffort:(ARG:0)
ENDSELECT

SELECTCASE ARG:1
	CASE 1;大木
		베이스 += 700
	CASE 2;巨木
		베이스 += 1000
ENDSELECT
RETURNF 베이스

;-----------------------------------------------------------
;장작패기をするかどうかを確認して、목재を入手する
;통나무を割る場合は체력・기력消費と時間の経過は別に記述をすること
;ARG 	FLAG:통나무종류　で통나무の薪割を行う
;		省略時は=0  一般薪割・통나무無しでも가능・체력기력消費あり
;-----------------------------------------------------------
@WOOD_CHOPPING(ARG = 0)
#DIM 장작패기
#DIM 벌채파워
#DIM 감소치
#DIM 벌채시간

IF ARG
	PRINTFORM 통나무를 여기서 쪼개 목재로 해볼까?
	장작패기 = (1 + ARG / 100) * 5
ELSE
	PRINTFORM 장작패기를 해볼까?
	장작패기 = 3
ENDIF

IF (ITEM:목재 + 장작패기) > 99
	CALL COLORMESSAGE(@"　（목재는 더 이상 가질 수 없을만큼 많이 있다…）",C_GRAY,2)
ELSE
	PRINTW 
ENDIF
SIF ARG
	CALL COLORMESSAGE(@"　（주의: 목재로 만들어 버리면, 제재로 가공할 수 없습니다）",C_RED,1)
CALL ASK_YN
IF !RESULT
	PRINTFORMW 장작패기를 해서 목재를 {장작패기}개 손에 넣었다
	IF ARG
		ITEM:통나무 = 0
		FLAG:통나무종류 = 0
	;一般薪割の場合は、체력기력時間消費
	ELSE
		벌채파워 = TALENT:MASTER:벌채Lv * 20
		감소치 = 350 - 벌채파워
		벌채시간 = 180 - 벌채파워
		IF ITEM:전기톱
			감소치 /= 2
			벌채시간 /= 2
		ENDIF
		IF FLAG:70
			BASE:MASTER:TSP -= 감소치
		ELSE
			DOWNBASE:MASTER:체력 = 감소치
			IF ITEM:전기톱
				DOWNBASE:MASTER:기력 = 감소치 - 50
			ELSE
				DOWNBASE:MASTER:기력 = 감소치 - 25
			ENDIF
			TIME += 벌채시간
		ENDIF
	ENDIF
	;목재数の増加処理
	ITEM:목재 = MIN(장작패기 + ITEM:목재, 99)
ELSE
	IF ARG
		CLEARLINE 5
		PRINTFORMW 통나무는 마을의 목재상이나 산의 제재소에 이대로 가져가기로 하자……
	ENDIF
ENDIF
PRINTL 

