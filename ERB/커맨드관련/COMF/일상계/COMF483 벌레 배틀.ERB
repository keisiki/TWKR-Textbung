;벌레배틀
@COM483
#DIM CHARA
#DIM 同室人数
#DIM 参加可能人数
#DIM 順位
VARSET MUSHI_BATTLER
VARSET MUSHI_TEAM
VARSET MUSHI_POWER
;TARGETがメイン
IF TCVAR:TARGET:벌레배틀참가여부 == -1
	PRINTFORML %CALLNAME:TARGET%의 벌레들은 지쳐 있다. 배틀은 그만두자.
	RETURN -1
ENDIF

MUSHI_BATTLER:1 = MASTER
;参加可能人数チェック
参加可能人数 = 0
同室人数 = GET_TARGETNUM()
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:행동 == 5
		CONTINUE
	SIF BASE:CHARA:기력 < 500 || BASE:CHARA:체력 < 500
		CONTINUE
	SIF TCVAR:CHARA:벌레배틀참가여부 == -1
		CONTINUE
	参加可能人数 ++
NEXT
;TARGETは実行判定で可能なはずなのでエラーチェック
IF 参加可能人数 == 0
	THROW
;参加者一人(TARGETのみ）
ELSEIF 参加可能人数 == 1
	;一人の時は確認をとる
	MUSHI_CHARANUM = 2
	CHARA = TARGET
	PRINTFORML %CALLNAME:CHARA%와 대전합니다　\@ CFLAG:CHARA:벌레배틀직접승리 ? ★직접승리완료 # \@
	PRINTFORM %CALLNAME:CHARA%의 벌레 배틀 파워：
	CALL COLOR_STAMP(MBP_RANK(CHARA) + 1, 0, "★", RANKCOLOR(MBP_RANK(CHARA) + 1), 1)
	PRINTL 
	;＠試合前口上
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "구상색")
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "試合前")
	PRINTFORML %RESULTS%
	RESETCOLOR
	PRINTL [1] 대전개시!
	PRINTL [0] 역시 그만둔다
	INPUT
	SIF !RESULT
		RETURN -1
	;1はMASTER
	MUSHI_BATTLER:2 = CHARA
	TCVAR:CHARA:벌레배틀참가여부 = 2
ELSE
	MUSHI_CHARANUM = 1
	CALL MUSHIBATTLE_CHARASELECT(同室人数, 参加可能人数)
	;キャンセル
	SIF RESULT == -1
		RETURN -1
ENDIF

;セットアップ１
CALL MUSHI_BATTLE_SETUP_1
;見せ合い＆ムシ選出
CALL MUSHI_BATTLE_SELECTION
;セットアップ２
CALL MUSHI_BATTLE_SETUP_2
;TARGETを変更しておく
IF MUSHI_TEAM
	TARGET = MUSHI_BATTLER:3
ELSE
	TARGET = MUSHI_BATTLER:2
ENDIF
;試合開始！
CALL MUSHI_BATTLE
;結果発表
CALL MUSHI_BATTLE_RESULT

;ソース獲得
DOWNBASE:MASTER:기력 += 400
DOWNBASE:MASTER:체력 += 200
TFLAG:194 = TCVAR:MASTER:벌레배틀참가여부 % 10
FOR LOCAL, 2, MUSHI_CHARANUM + 1
	CHARA = MUSHI_BATTLER:LOCAL
	DOWNBASE:CHARA:기력 += 400
	DOWNBASE:CHARA:체력 += 200
	順位 = TCVAR:CHARA:벌레배틀참가여부 % 10
	SELECTCASE TFLAG:193
	;タイマン
	CASE 1
		IF 順位 == 1
			SOURCE:CHARA:환락 = 2000 + ABL:CHARA:친밀 * 50
			SOURCE:CHARA:정복 = 400 + 120 * ABL:CHARA:새드끼
		ELSE
			SOURCE:CHARA:환락 = 1000 + ABL:CHARA:친밀 * 100
			SOURCE:CHARA:수동 = 400 + 120 * ABL:CHARA:순종
			TFLAG:신뢰도관리 = 1
			;初勝利
			IF !CFLAG:CHARA:벌레배틀직접승리
				CFLAG:CHARA:벌레배틀직접승리 = 1
				TFLAG:호감도보너스 = 100
				TFLAG:신뢰도관리  += 1
			ENDIF
		ENDIF
	;バトルロイヤル
	CASE 2
		IF 順位 == 1
			SOURCE:CHARA:환락 = 2500 + ABL:CHARA:친밀 * 60
			SOURCE:CHARA:정복 = 600 + 150 * ABL:CHARA:새드끼
		ELSEIF 順位 == 2
			SOURCE:CHARA:환락 = 1500 + ABL:CHARA:친밀 * 80
			SOURCE:CHARA:수동 = 200 + 80 * ABL:CHARA:순종
		ELSEIF 順位 == 3
			SOURCE:CHARA:환락 = 1200 + ABL:CHARA:친밀 * 90
			SOURCE:CHARA:수동 = 300 + 100 * ABL:CHARA:순종
		ELSE
			SOURCE:CHARA:환락 = 900 + ABL:CHARA:친밀 * 100
			SOURCE:CHARA:수동 = 400 + 120 * ABL:CHARA:순종
		ENDIF
	;チーム戦
	CASE 3
		IF 順位 == 1
			SOURCE:CHARA:환락 = 1600 + ABL:CHARA:친밀 * 50
			SOURCE:CHARA:정복 = 200 + 80 * ABL:CHARA:새드끼
		ELSE
			SOURCE:CHARA:환락 = 1200 + ABL:CHARA:친밀 * 80
			SOURCE:CHARA:수동 = 200 + 80 * ABL:CHARA:순종
		ENDIF
		IF CHARA == TARGET
			TIMES SOURCE:CHARA:환락, 2.00
			TIMES SOURCE:CHARA:수동, 2.00
			TIMES SOURCE:CHARA:정복, 2.00
			TFLAG:신뢰도관리 = 1
		ENDIF
	ENDSELECT
NEXT

;今日はもう戦えない
FOR LOCAL, 1, 同室人数 + 1
	CHARA = TARGET:LOCAL
	SIF TCVAR:CHARA:벌레배틀참가여부 > 1
		TCVAR:CHARA:벌레배틀참가여부 = -1
NEXT

;시간경과
TIME += 60

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;벌레배틀
@COM_ABLE483
;時間停止中
SIF FLAG:시간정지
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(483)
	RETURN RESULT
;昆虫採集セットがない
SIF !ITEM:곤충채집세트
	RETURN 0
;昆虫採集できる場所である
SIF !MUSHITORI_SPOT(CFLAG:MASTER:현재위치)
	RETURN 0
;冬の間はおやすみ
SIF GET_MONTH() == "겨울의 달"
	RETURN 0
;虫を持ってない
SIF !MUSHI_CAGE:1
	RETURN 0
;対象がいない
SIF TARGET <= 0
	RETURN 0
;意識がない
SIF !SHIRAHU(TARGET)
	RETURN 0
;동침중
SIF CFLAG:MASTER:동침중
	RETURN 0
;うふふ中
SIF CFLAG:MASTER:우후후
	RETURN 0
;仕事中
SIF CFLAG:행동 == 5
	RETURN 0
;체력・気力不足
SIF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
	RETURN 0
;체력・気力不足
SIF BASE:TARGET:기력 < 500 || BASE:TARGET:체력 < 500
	RETURN 0
RETURN 1

;-------------------------------------------------
;벌레배틀：キャラクターセレクト関数
;ARG 同室人数
;TCVAR:벌레배틀참가여부 =-1,参加不可, =1,参加可能, =2～4,参加エントリー
;-------------------------------------------------
@MUSHIBATTLE_CHARASELECT(ARG, 参加可能人数)
#DIM CHARA
#DIM 参加可能人数
#DIM LINE
LINE = 7
PRINTFORML ＜＜참가자를 선택！！（최대%TOFULL(TOSTR(MUSHI_MAXBATTLE))%인）＞＞
SETCOLOR C_AQUA
PRINTPLAINFORM [  0] %CALLNAME:MASTER, 16, LEFT%★？Ｐ１
PRINTL 
RESETCOLOR
FOR LOCAL, 1, ARG + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF TCVAR:CHARA:벌레배틀참가여부 == -1
		CONTINUE
	IF TCVAR:CHARA:벌레배틀참가여부 > 1
		SETCOLOR C_AQUA
		PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 16, LEFT%
		CALL PRINT_MBP_RANK(CHARA)
		PRINTFORM Ｐ%TOFULL(TOSTR(TCVAR:CHARA:벌레배틀참가여부))%
		SIF MUSHI_TEAM && TCVAR:CHARA:벌레배틀참가여부 == 3
			PRINTFORM (동료)
		;＠試合前口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "구상색")
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "試合前")
		PRINTFORML %RESULTS%
		RESETCOLOR
	ELSE
		TCVAR:CHARA:벌레배틀참가여부 = 1
		PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 16, LEFT%
		CALL PRINT_MBP_RANK(CHARA)
		PRINTL 
	ENDIF
	LINE ++
NEXT
DRAWLINE
SIF MUSHI_TEAM
	SETCOLOR C_AQUA
SIF 参加可能人数 < 3
	SETCOLOR C_GRAY
PRINTL [901] 단체전(Ｐ１＆Ｐ３ VS Ｐ２＆Ｐ４）
RESETCOLOR
IF MUSHI_BATTLER:2
	PRINTL [900] 대전개시！
ELSE
	PRINTL 
ENDIF
PRINTL [999] 역시 그만둔다
INPUT

;그만둔다
IF RESULT == 999
	;参加可否をリセットする（-1以外）
	FOR LOCAL, 1, ARG + 1
		CHARA = TARGET:LOCAL
		SIF TCVAR:CHARA:벌레배틀참가여부 > 0
			TCVAR:CHARA:벌레배틀참가여부 = 0
	NEXT
	RETURN -1
;対戦開始
ELSEIF RESULT == 900
	IF MUSHI_TEAM && MUSHI_CHARANUM != 4
		PRINTW 참가자가 네 명 없으면 단체전은 할 수 없습니다！
		LINE ++
	ELSE
		;特殊補正(口上用？,数値は任意）
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			LOCAL:1 = 0
			MUSHI_POWER:LOCAL = LOCAL:1
		NEXT
		RETURN 1
	ENDIF
;チーム戦切り替え
ELSEIF RESULT == 901 && 参加可能人数 > 3
	MUSHI_TEAM = !MUSHI_TEAM
;参加キャンセル
ELSEIF INRANGE(RESULT, 1, CHARANUM) && TCVAR:RESULT:벌레배틀참가여부 > 1
	FOR LOCAL, TCVAR:RESULT:벌레배틀참가여부, MUSHI_MAXBATTLE + 1
		;参加者はキャンセルする
		IF MUSHI_BATTLER:LOCAL == RESULT
			MUSHI_BATTLER:LOCAL = 0
		;番号が後の人がいるなら登録番号を一つ下げる
		ELSEIF MUSHI_BATTLER:LOCAL
			TCVAR:(MUSHI_BATTLER:LOCAL):벌레배틀참가여부 --
		ENDIF
	NEXT
	;その後シフトする
	FOR LOCAL, TCVAR:RESULT:벌레배틀참가여부, MUSHI_MAXBATTLE + 1
		SIF !MUSHI_BATTLER:LOCAL
			SWAP MUSHI_BATTLER:LOCAL, MUSHI_BATTLER:(LOCAL + 1)
	NEXT
	;参加状態を戻す
	TCVAR:RESULT:벌레배틀참가여부 = 1
	MUSHI_CHARANUM --
;参加エントリー
ELSEIF INRANGE(RESULT, 1, CHARANUM) && TCVAR:RESULT:벌레배틀참가여부 == 1 && MUSHI_CHARANUM < MUSHI_MAXBATTLE
	FOR LOCAL, 2, MUSHI_MAXBATTLE + 1
		;空きに入れる
		IF !MUSHI_BATTLER:LOCAL
			MUSHI_BATTLER:LOCAL = RESULT
			TCVAR:RESULT:벌레배틀참가여부 = LOCAL
			BREAK
		ENDIF
	NEXT
	MUSHI_CHARANUM ++
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;벌레배틀：셋업 1: 보여주기 전
;-------------------------------------------------
@MUSHI_BATTLE_SETUP_1
#DIM CHARA
#DIM LPCOUNT
;1=MASTERはセット済み
FOR LPCOUNT, 2, MUSHI_MAXBATTLE + 1
	CHARA = MUSHI_BATTLER:LPCOUNT
	IF !CHARA
		BREAK
	ELSE
		CALL CHARA_MUSHICAGE_SETTING(CHARA, LPCOUNT)
	ENDIF
NEXT

;-------------------------------------------------
;벌레배틀：見せ合い
;-------------------------------------------------
@MUSHI_BATTLE_SELECTION
#DIM CHARA
#DIM LPCOUNT
VARSET MUSHI_SELECT
VARSET MUSHI_REST
PRINTFORML ◆대전 상대의 벌레 바구니◆
FOR LPCOUNT, -2, MUSHI_MAXCAGE + 2
	FOR LOCAL, 2, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:LOCAL
		IF LOCAL == 2
			IF LPCOUNT == -2
				PRINTFORM ┌
			ELSEIF LPCOUNT == 0
				PRINTFORM ├
			ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
				PRINTFORM └
			ELSE
				PRINTFORM │
			ENDIF
		ENDIF
		IF LPCOUNT == -2
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┐
			ELSE
				PRINTFORM %"─" * 20%┬
			ENDIF
		ELSEIF LPCOUNT == -1
			LOCALS = %CALLNAME:CHARA%
			SIF MUSHI_TEAM && LOCAL == 3
				LOCALS += "(동료)"
			PRINTFORM %LOCALS, 36, LEFT%
			CALL PRINT_MBP_RANK(CHARA)
			PRINTFORM │
		ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┘
			ELSE
				PRINTFORM %"─" * 20%┴
			ENDIF
		ELSEIF LPCOUNT == 0
			IF LOCAL == MUSHI_CHARANUM
				PRINTFORM %"─" * 20%┤
			ELSE
				PRINTFORM %"─" * 20%┼
			ENDIF
		ELSE
			CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL * 10 + LPCOUNT), MUSHI_CAGE:(LOCAL * 10 + LPCOUNT), 40)
			PRINTFORM │
		ENDIF
	NEXT
	PRINTL 
NEXT
;フィールドタイプ設定
MUSHI_FIELD:99 = 0
WHILE !MUSHI_FIELD:99
	SETBIT MUSHI_FIELD:99, RAND:8
	SIF !(MUSHITORI_SPOT(CFLAG:MASTER:현재위치) &  MUSHI_FIELD:99)
		MUSHI_FIELD:99 = 0
WEND
PRINTFORM ＜필드 타입：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ◆%CALLNAME:MASTER%의 벌레 바구니◆　배틀에 참가시킬 벌레를 1마리에서 3마리까지 선택하세요
DRAWLINE
;MASTERの選出に引き渡し
MUSHI_REST:1 = 0
MUSHI_MENTAL:1 = 1000
CALL MUSHI_BATTLE_SELECTION_MASTER

;-------------------------------------------------
;벌레배틀：MASTERのムシ選出
;-------------------------------------------------
@MUSHI_BATTLE_SELECTION_MASTER
#DIM LINE
#DIM LPCOUNT
LINE = 9
;虫カゴ表示
FOR LPCOUNT, 1, MUSHI_MAXCAGE + 1
	IF MUSHI_CAGE:LPCOUNT
		SIF MATCH(MUSHI_SELECT, MUSHI_CAGE:LPCOUNT)
			SETCOLOR C_AQUA
		PRINTFORM [{LPCOUNT, 2}]
		LOCAL:1 = 0
		;選出済み
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			IF MUSHI_SELECT:LOCAL == MUSHI_CAGE:LPCOUNT
				PRINTFORM 【%TOFULL(TOSTR(LOCAL))%】
				LOCAL:1 = 1
				BREAK
			ENDIF
		NEXT
		;未選出
		SIF !LOCAL:1
			PRINT 　　　
		PRINTFORM 
		CALL SHOW_MUSHI_DATA(MUSHI_CAGE:LPCOUNT, "一行")
		PRINTL 
		RESETCOLOR
	ELSE
		PRINTPLAINFORM [{LPCOUNT, 2}] 텅 비어 있음
		PRINTL 
	ENDIF
NEXT
DRAWLINE
IF !MUSHI_SELECT:1
	SETCOLOR C_GRAY
ELSE
	SETCOLOR C_CREAM
ENDIF
PRINTL [99] ＜＜READY to FIGHT！＞＞
RESETCOLOR
INPUT
;試合開始！
IF RESULT == 99 && MUSHI_SELECT:1
	;RESTを入れる
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SIF MUSHI_SELECT:LOCAL
			SETBIT MUSHI_REST:1, LOCAL
	NEXT
	RETURN
;ムシ選択
ELSEIF INRANGE(RESULT, 1, MUSHI_MAXCAGE) && MUSHI_CAGE:RESULT
	;参加キャンセル
	IF MATCH(MUSHI_SELECT, MUSHI_CAGE:RESULT)
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			;参加をキャンセルする
			IF MUSHI_SELECT:LOCAL == MUSHI_CAGE:RESULT
				MUSHI_SELECT:LOCAL = 0
				BREAK
			ENDIF
		NEXT
		;その後シフトする
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			SIF !MUSHI_SELECT:LOCAL
				SWAP MUSHI_SELECT:LOCAL, MUSHI_SELECT:(LOCAL + 1)
		NEXT
	;参加エントリー
	ELSE
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			;空きに入れる
			IF !MUSHI_SELECT:LOCAL
				MUSHI_SELECT:LOCAL = MUSHI_CAGE:RESULT
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;벌레배틀：セットアップ２：選出後
;-------------------------------------------------
@MUSHI_BATTLE_SETUP_2
#DIM CHARA
#DIM LPCOUNT
#DIM SETNUM
#DIM KIRIHUDA
VARSET MUSHI_STATUS
VARSET MUSHI_MENTAL
;MASTERの設定
FOR LPCOUNT, 1, MUSHI_MAXSELECT + 1
	MUSHI_SELECT:(10 + LPCOUNT) = MUSHI_SELECT:LPCOUNT
	CALL MUSHI_PALAM(MUSHI_SELECT:LPCOUNT, MUSHI_POWER:1)
	CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(10 + LPCOUNT), 10 + LPCOUNT)
NEXT
;戦闘初期設定
;MASTERのMENTALは1000固定
MUSHI_MENTAL:1 = 1000
MUSHI_STAND:1 = MUSHI_SELECT:11
MUSHI_STANDNUM:1 = 11
GETCOLOR
MUSHI_COLOR:1 = RESULT
;キャラの選出を決める
FOR LPCOUNT, 2, MUSHI_CHARANUM + 1
	CHARA = MUSHI_BATTLER:LPCOUNT
	SETNUM = LPCOUNT * 10
	KIRIHUDA = 0
	;切り札がいるなら切り札は確定(番号はランダム）
	RESULT = 0
	TRYCALLFORM MUSHI_KIRIHUDA_{CHARA}
	IF RESULT
		KIRIHUDA = 1
		LOCAL = RAND:MUSHI_MAXSELECT + 1
		MUSHI_SELECT:(SETNUM + LOCAL) = MUSHI_CAGE:(SETNUM + 1)
		MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(SETNUM + 1)
		CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
		CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
	ENDIF
	;虫カゴからランダムで空いてる番号に入れていく,ただし切り札がいるなら1番は除外
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		IF !MUSHI_SELECT:(SETNUM + LOCAL)
			WHILE !MUSHI_SELECT:(SETNUM + LOCAL)
				LOCAL:1 = SETNUM + RAND:(MUSHI_MAXCAGE - KIRIHUDA) + 1 + KIRIHUDA
				LOCAL:2 = MUSHI_CAGE:(LOCAL:1)
				IF !MATCH(MUSHI_SELECT, LOCAL:2)
					MUSHI_SELECT:(SETNUM + LOCAL) = LOCAL:2
					MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(LOCAL:1)
				ENDIF
			WEND
			CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
			CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
		ENDIF
	NEXT
	;戦闘初期設定
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SETBIT MUSHI_REST:LPCOUNT, LOCAL
	NEXT
	MUSHI_MENTAL:LPCOUNT = MBP(CHARA)
	MUSHI_STAND:LPCOUNT = MUSHI_SELECT:(SETNUM + 1)
	MUSHI_STANDNUM:LPCOUNT = SETNUM + 1
	;口上色の設定
	RESULT = 0
	GETCOLOR
	LOCAL = RESULT
	CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 0, CHARA, 0, 0, "구상색")
	GETCOLOR
	;色が変わってたら登録する
	IF LOCAL != RESULT
		MUSHI_COLOR:LPCOUNT = RESULT
	ELSE
		MUSHI_COLOR:LPCOUNT = LOCAL
	ENDIF
	RESETCOLOR
NEXT
[IF_DEBUG]
	;選出チェック
	FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:LPCOUNT
		SETNUM = LPCOUNT * 10
		DRAWLINE
		PRINTFORML %CALLNAME:CHARA%
		FOR LOCAL, 1, MUSHI_MAXSELECT + 1
			SIF MUSHI_SELECT:(SETNUM + LOCAL)
				CALL PRINT_MUSHINAME(MUSHI_NAME:(SETNUM + LOCAL), MUSHI_CAGE:(SETNUM + LOCAL), 40)
				PRINTFORML HP:{MUSHI_HP:(SETNUM + LOCAL), 5, LEFT} ATK:{MUSHI_ATK:(SETNUM + LOCAL), 4, LEFT} SPD:{MUSHI_SPD:(SETNUM + LOCAL), 4, LEFT}
		NEXT
	NEXT
	DRAWLINE
	WAIT
[ENDIF]

;-------------------------------------------------
;벌레배틀：結果発表関数
;-------------------------------------------------
@MUSHI_BATTLE_RESULT
#DIM LPCOUNT
#DIM CHARA
PRINTFORML ■시합 결과■
;チーム戦
IF MUSHI_TEAM
	TFLAG:193 = 3
	PRINTFORML ┌%"─" * 42%┐
	PRINT │
	CALL COLORMESSAGE(@"＜인생의 승리자！＞", 0xff7878)
	PRINTFORML %" " * 66%│
	FOR LOCAL, 1, MUSHI_CHARANUM / 2 + 1
		CHARA = MUSHI_BATTLER:(MUSHI_FINISH:LOCAL)
		TCVAR:CHARA:벌레배틀참가여부 = 11
		PRINTFORM │
		SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
			SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
		LOCALS = %CALLNAME:CHARA%
		;＠試合後一言口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 1, CHARA, MUSHI_FINISH:LOCAL, 0, "チーム戦：勝ち")
		LOCALS += RESULTS
		PRINTFORM %LOCALS, 84, LEFT%
		RESETCOLOR
		PRINTL │
		PRINT │　 ＆ 
		CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:LOCAL)), MUSHI_STAND:(MUSHI_FINISH:LOCAL), 78)
		PRINTL │
	NEXT
	PRINTFORML ├%"─" * 42%┤
	PRINT │
	CALL COLORMESSAGE(@"＜패자조！＞", 0x6464ff)
	PRINTFORML %" " * 72%│
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		IF !MATCH(MUSHI_FINISH, LOCAL)
			CHARA = MUSHI_BATTLER:LOCAL
			TCVAR:CHARA:벌레배틀참가여부 = 12
			PRINTFORM │
			SIF MUSHI_COLOR:LOCAL != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:LOCAL
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 2, CHARA, LOCAL, 0, "チーム戦：負け")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 84, LEFT%
			RESETCOLOR
			PRINTL │
			PRINT │　 ＆ 
			CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:LOCAL), MUSHI_STAND:LOCAL, 78)
			PRINTL │
		ENDIF
	NEXT
	PRINTFORML └%"─" * 42%┘
;タイマン
ELSEIF MUSHI_CHARANUM == 2
	TFLAG:193 = 1
	FOR LPCOUNT, -3, MUSHI_MAXSELECT + 2
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			CHARA = MUSHI_BATTLER:LOCAL
			IF LOCAL == 1
				IF LPCOUNT == -3
					PRINTFORM ┌
				ELSEIF LPCOUNT == 0
					PRINTFORM ├
				ELSEIF LPCOUNT == MUSHI_MAXSELECT + 1
					PRINTFORM └
				ELSE
					PRINTFORM │
				ENDIF
			ENDIF
			IF LPCOUNT == -3
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┐
				ELSE
					PRINTFORM %"─" * 20%┬
				ENDIF
			ELSEIF LPCOUNT == -2
				IF MUSHI_FINISH:1 == LOCAL
					LOCALS = %CALLNAME:CHARA%＜인생의 승리자！＞
					SETCOLOR 0xff7878
					TCVAR:CHARA:벌레배틀참가여부 = 11
				ELSE
					LOCALS = %CALLNAME:CHARA%＜패배자！＞
					SETCOLOR 0x6464ff
					TCVAR:CHARA:벌레배틀참가여부 = 12
				ENDIF
				PRINTFORM %LOCALS, 36, LEFT%
				RESETCOLOR
				CALL PRINT_MBP_RANK(CHARA)
				PRINTFORM │
			ELSEIF LPCOUNT == -1
				;＠試合後一言口上
				IF MUSHI_FINISH:1 == LOCAL
					CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 1, CHARA, LOCAL, 0, "タイマン：勝ち")
				ELSE
					CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 2, CHARA, LOCAL, 0, "タイマン：負け")
				ENDIF
				SIF MUSHI_COLOR:LOCAL != MUSHI_COLOR:1
					SETCOLOR MUSHI_COLOR:LOCAL
				PRINTFORM %RESULTS, 40, LEFT%
				RESETCOLOR
				PRINTFORM │
			ELSEIF LPCOUNT == MUSHI_MAXSELECT + 1
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┘
				ELSE
					PRINTFORM %"─" * 20%┴
				ENDIF
			ELSEIF LPCOUNT == 0
				IF LOCAL == MUSHI_CHARANUM
					PRINTFORM %"─" * 20%┤
				ELSE
					PRINTFORM %"─" * 20%┼
				ENDIF
			ELSE
				SIF !GETBIT(MUSHI_REST:LOCAL, LPCOUNT)
					SETCOLOR C_GRAY
				CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL * 10 + LPCOUNT), MUSHI_CAGE:(LOCAL * 10 + LPCOUNT), 40)
				RESETCOLOR
				PRINTFORM │
			ENDIF
		NEXT
		PRINTL 
	NEXT
	SIF MUSHI_FINISH:1 == 1
		CALL COLORMESSAGE(@"☆%CALLNAME:(MUSHI_BATTLER:(MUSHI_FINISH:2))%에 처음으로 맞대결에서 승리했다!", C_YELLOW, 1)
;バトルロイヤル
ELSE
	TFLAG:193 = 2
	PRINTFORML ┌%"─" * 44%┐
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		CHARA = MUSHI_BATTLER:(MUSHI_FINISH:LOCAL)
		PRINT │
		IF LOCAL == 1
			CALL COLORMESSAGE(@"☆우승☆", C_YELLOW)
			SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", LOCAL, CHARA, MUSHI_FINISH:LOCAL, 0, "バトルロイヤル：優勝")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 80, LEFT%
			RESETCOLOR
			PRINTL │
		ELSE
			PRINTFORM 　%TOFULL(TOSTR(LOCAL))%위　
			SIF MUSHI_COLOR:(MUSHI_FINISH:LOCAL) != MUSHI_COLOR:1
				SETCOLOR MUSHI_COLOR:(MUSHI_FINISH:LOCAL)
			LOCALS = %CALLNAME:CHARA%
			;＠試合後一言口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", LOCAL, CHARA, MUSHI_FINISH:LOCAL, 0, "バトルロイヤル：２位以下")
			LOCALS += RESULTS
			PRINTFORM %LOCALS, 80, LEFT%
			RESETCOLOR
			PRINTL │
		ENDIF
		TCVAR:CHARA:벌레배틀참가여부 = LOCAL + 10
		PRINT │　　　　 ＆ 
		CALL PRINT_MUSHINAME(MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:LOCAL)), MUSHI_STAND:(MUSHI_FINISH:LOCAL), 76)
		PRINTL │
		IF LOCAL == MUSHI_CHARANUM
			PRINTFORML └%"─" * 44%┘
		ELSE
			PRINTFORML ├%"─" * 44%┤
		ENDIF
	NEXT
ENDIF
WAIT

;-------------------------------------------------
;벌레배틀：캐릭터의 벌레 바구니를 설정하는 함수
;-------------------------------------------------
@CHARA_MUSHICAGE_SETTING(CHARA, BATTLE_NUM)
#DIM CHARA
#DIM BATTLE_NUM
#DIM MUSHI_ID
#DIM MUSHI_NUM
#DIM RARE_MIN
#DIM RARE_MAX
#DIM RANK_MIN
#DIM RANK_MAX
#DIM D_MUTA
#DIM D_FIELD
#DIM D_AREA
#DIMS D_TRIBE
#DIMS D_TYPE
#DIM MBP
RARE_MIN = 0
RARE_MAX = 0
RANK_MIN = 0
RANK_MAX = 0
D_MUTA = 0
D_FIELD = 0
D_AREA = 0
D_TRIBE = 
D_TYPE = 
MBP = MBP(CHARA)
;切り札チェック
RESULT = 0
TRYCALLFORM MUSHI_KIRIHUDA_{CHARA}
IF RESULT
	;비장의 카드가 있다면 1번으로 등록
	MUSHI_ID = RESULT:1 * 1000000 + RESULT:2 * 10000000 + RESULT
	CALL MUSHI_DATA(RESULT)
	MUSHI_ID += MUSHI_RANK * 10000 + MUSHI_RARE * 1000
	MUSHI_CAGE:(1 + BATTLE_NUM * 10) = MUSHI_ID
	MUSHI_NAME:(1 + BATTLE_NUM * 10) = %MUSHI_NAME%
	MUSHI_NUM = 2
ELSE
	MUSHI_NUM = 1
ENDIF
VARSET RESULT
VARSET RESULTS
;캐릭터 데이터를 통한 요청 접수
TRYCALLFORM MUSHI_CHARACAGE_{CHARA}
IF RESULT
	RARE_MIN = RESULT:1
	RARE_MAX = RESULT:2
	RANK_MIN = RESULT:3
	RANK_MAX = RESULT:4
	D_MUTA = RESULT:5
	D_FIELD = RESULT:6
	D_AREA = RESULT:7
	D_TRIBE = %RESULTS:1%
	D_TYPE = %RESULTS:2%
ENDIF
;리얼리티 설정
IF !RARE_MAX
	RARE_MIN = MBP_RANK(CHARA)
	IF MBP < 300
		RARE_MAX = 2
	ELSEIF MBP < 1000
		RARE_MAX = 3
	ELSEIF MBP < 1600
		RARE_MAX = 3
	ELSEIF MBP < 2200
		RARE_MAX = 4
	ELSEIF MBP < 3200
		RARE_MAX = 4
	ELSEIF MBP < 5000
		RARE_MAX = 5
	ELSEIF MBP < 6200
		RARE_MAX = 5
	ELSEIF MBP < 8000
		RARE_MAX = 6
	ELSEIF MBP < 12000
		RARE_MAX = 6
	ELSEIF MBP < 20000
		RARE_MAX = 7
	ELSEIF MBP < 30000
		RARE_MAX = 7
	ELSEIF MBP < 50000
		RARE_MAX = 8
	ELSEIF MBP < 150000
		RARE_MAX = 8
	ELSEIF MBP < 500000
		RARE_MAX = 9
	ELSEIF MBP < 1000000
		RARE_MAX = 9
	ELSE
		RARE_MAX = 9
	ENDIF
ENDIF
;ランク設定
IF !RANK_MAX
	RANK_MIN = LIMIT(MBP / 2000, 0 , 80)
	RANK_MAX = 100
ENDIF
;変異率設定
IF !D_MUTA
	D_MUTA = LIMIT(MBP / 10000, 1, 25)
ENDIF
;ムシを出力して虫カゴに登録していく
FOR LOCAL, MUSHI_NUM, MUSHI_MAXCAGE + 1
	CALL DRAW_MUSHI_DESIGN(CHARA, RARE_MIN, RARE_MAX, RANK_MIN, RANK_MAX, D_MUTA, D_FIELD, D_AREA, D_TRIBE, D_TYPE)
	MUSHI_CAGE:(LOCAL + BATTLE_NUM * 10) = RESULT
	MUSHI_NAME:(LOCAL + BATTLE_NUM * 10) = %MUSHI_NAME%
NEXT
;-------------------------------------------------
;벌레배틀：バトル用の戦闘力を出力する関数
;Mushi Battle Power
;-------------------------------------------------
@MBP(CHARA)
#FUNCTION
#DIM CHARA
;基本値1000
LOCAL = 1000
SELECTCASE TALENT:CHARA:채집Lv
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 2.00
CASE 2
	TIMES LOCAL, 4.00
CASE 3
	TIMES LOCAL, 8.00
CASE 4
	TIMES LOCAL, 12.00
CASE 5
	TIMES LOCAL, 18.00
CASEELSE
	TIMES LOCAL, 25.00
ENDSELECT
SELECTCASE ABL:CHARA:교양
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 1.20
CASE 2
	TIMES LOCAL, 1.40
CASE 3
	TIMES LOCAL, 1.60
CASE 4
	TIMES LOCAL, 1.80
CASE 5
	TIMES LOCAL, 2.00
CASEELSE
	TIMES LOCAL, 2.40
ENDSELECT
SELECTCASE ABL:CHARA:전투능력
CASE 0
	TIMES LOCAL, 1.00
CASE 1
	TIMES LOCAL, 1.10
CASE 2
	TIMES LOCAL, 1.20
CASE 3
	TIMES LOCAL, 1.30
CASE 4
	TIMES LOCAL, 1.50
CASE 5
	TIMES LOCAL, 1.70
CASEELSE
	TIMES LOCAL, 2.00
ENDSELECT
SELECTCASE TALENT:CHARA:악취내성
CASE -2
	TIMES LOCAL, 0.20
CASE -1
	TIMES LOCAL, 0.50
CASE 1
	TIMES LOCAL, 2.00
CASE 2
	TIMES LOCAL, 3.00
ENDSELECT
SIF TALENT:CHARA:조합지식
	TIMES LOCAL, 1.50
SIF TALENT:CHARA:금단의지식
	TIMES LOCAL, 2.50
SIF TALENT:CHARA:유치
	TIMES LOCAL, 15.00
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")) == "천인"
	TIMES LOCAL, 0.30
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")) == "선인"
	TIMES LOCAL, 1.50
;地上住みは除く
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")) == "월인" && !GROUPMATCH(CHARA, [[카구야]], [[에이린]])
	TIMES LOCAL, 0.05
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")) == "마계인"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "인간")) == "외계인"
	TIMES LOCAL, 0.80
ENDIF
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "오니"
	TIMES LOCAL, 4.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "흡혈귀"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "캇파"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "텐구"
	TIMES LOCAL, 1.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "요수"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "요조"
	TIMES LOCAL, 2.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "요충"
	TIMES LOCAL, 25.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "악마"
	TIMES LOCAL, 2.50
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "난쟁이"
	TIMES LOCAL, 0.05
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "야마와로"
	TIMES LOCAL, 4.00
ELSEIF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요괴")) == "요괴"
	TIMES LOCAL, 1.20
ENDIF
IF GET_TRIBENAME(CHARA, GETNUM(TALENT, "요정")) == "요정"
	TIMES LOCAL, 15.00
ENDIF
LOCALS = %GET_STR(, "캐릭터데이터", CHARA, "요리：좋아하는 맛")%
IF STRCOUNT(LOCALS, "그로테스크/") >= 5
	TIMES LOCAL, 4.00
ELSEIF STRCOUNT(LOCALS, "그로테스크/") >= 3
	TIMES LOCAL, 3.00
ELSEIF STRCOUNT(LOCALS, "그로테스크/") >= 1
	TIMES LOCAL, 2.00
ENDIF
LOCALS = %GET_STR(, "캐릭터데이터", CHARA, "요리：싫어하는 맛")%
IF STRCOUNT(LOCALS, "그로테스크/") >= 5
	TIMES LOCAL, 0.10
ELSEIF STRCOUNT(LOCALS, "그로테스크/") >= 3
	TIMES LOCAL, 0.20
ELSEIF STRCOUNT(LOCALS, "그로테스크/") == 1
	TIMES LOCAL, 0.50
ENDIF
RETURNF LOCAL

;-------------------------------------------------
;벌레배틀：ムシバトルパワーのランク,レアリティ最低値にもなる
;-------------------------------------------------
@MBP_RANK(CHARA)
#FUNCTION
#DIM CHARA
IF MBP(CHARA) < 300
	RETURNF 0
ELSEIF MBP(CHARA) < 1000
	RETURNF 0
ELSEIF MBP(CHARA) < 1600
	RETURNF 1
ELSEIF MBP(CHARA) < 2200
	RETURNF 1
ELSEIF MBP(CHARA) < 3200
	RETURNF 2
ELSEIF MBP(CHARA) < 5000
	RETURNF 2
ELSEIF MBP(CHARA) < 6200
	RETURNF 3
ELSEIF MBP(CHARA) < 8000
	RETURNF 3
ELSEIF MBP(CHARA) < 12000
	RETURNF 4
ELSEIF MBP(CHARA) < 20000
	RETURNF 4
ELSEIF MBP(CHARA) < 30000
	RETURNF 5
ELSEIF MBP(CHARA) < 50000
	RETURNF 5
ELSEIF MBP(CHARA) < 150000
	RETURNF 6
ELSEIF MBP(CHARA) < 500000
	RETURNF 6
ELSEIF MBP(CHARA) < 1000000
	RETURNF 7
ELSE
	RETURNF 8
ENDIF
;-------------------------------------------------
;벌레배틀：ムシバトルパワーのランクを★Ｘでプリントする関数
;-------------------------------------------------
@PRINT_MBP_RANK(CHARA)
#DIM CHARA
IF CHARA == MASTER
	PRINTFORM ★？
ELSE
	SETCOLOR RANKCOLOR(MBP_RANK(CHARA) + 1)
	PRINTFORM ★%TOFULL(TOSTR(MBP_RANK(CHARA)))%
	RESETCOLOR
ENDIF

;-------------------------------------------------
;벌레배틀：残りムシ数を○×でプリントする関数
;bit,0は使いません
;-------------------------------------------------
@PRINT_RESTMUSHI(ARG)
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	IF GETBIT(MUSHI_REST:ARG, LOCAL)
		PRINT ○
	ELSE
		PRINT ×
	ENDIF
NEXT

;-------------------------------------------------
;ムシバトル用のフィールドの描写っぽいなにか
;-------------------------------------------------
@PRINT_FIELD_EFFECT(ARG)
#DIM 色
IF GETBIT(ARG, 0)
	LOCALS = vvvv
	色 = 0x78ff78
ELSEIF GETBIT(ARG, 1)
	LOCALS = ＊＊
	色 = C_PINK
ELSEIF GETBIT(ARG, 2)
	LOCALS = 林林
	色 = 0x00c832
ELSEIF GETBIT(ARG, 3)
	LOCALS = ▒▒▒▒
	色 = 0xffc864
ELSEIF GETBIT(ARG, 4)
	LOCALS = ～～
	色 = 0x64c8ff
ELSEIF GETBIT(ARG, 5)
	LOCALS = ▲▼
	色 = 0xb49678
ELSEIF GETBIT(ARG, 6)
	LOCALS = ＃＃
	色 = 0xff7878
ELSEIF GETBIT(ARG, 7)
	LOCALS = _/_/
	色 = 0xc8dcff
ENDIF
CALL COLORMESSAGE(@"%LOCALS * 9% ", 色)
PRINT ＶＳ．
CALL COLORMESSAGE(@" %LOCALS * 9%", 色)

