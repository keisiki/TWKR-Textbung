;벌레 맞대기
;NPC와의 대면을 전제로 기존의 간소화를 증축하였으나,
;차라리 기존의 손을 집어넣어 NPC도 취급할 수 있는 제작으로 하는 것이 좋을까?
;좋아하는 캐릭터와 함께 NPC상대의 태그매치 같은 것을 할 수 있으면 좋겠다.

@COM497
#DIM 순위
VARSET MUSHI_BATTLER
VARSET MUSHI_TEAM
VARSET MUSHI_POWER
VARSET BATTLERNAME
MUSHI_BATTLER:1 = MASTER
BATTLERNAME:1 '= @"%CALLNAME:MASTER%"
;참가자는 일인
MUSHI_CHARANUM = 2

NPC_KIRIFUDA_ID = 0
NPC_KIRIFUDA_X = 0
NPC_KIRIFUDA_G = 0

PRINTFORML 대전 상대를 선택해주세요
CALL ASK_M("벌레 배틀러 1호",1,"벌레 배틀러 2호",1,"아마노가와 류세이",1)
SELECTCASE RESULT
	CASE 0
		BATTLERNAME:2 = 벌레 배틀러 1호
		NPC_LV = 1
	CASE 1
		BATTLERNAME:2 = 벌레 배틀러 2호
		NPC_LV = 3
	CASE 2
		BATTLERNAME:2 = 아마노가와 류세이
		NPC_LV = 6
		NPC_KIRIFUDA_ID = 322
		NPC_KIRIFUDA_X = 1
		NPC_KIRIFUDA_G = 1
ENDSELECT
;사양상 랭크 4이상은 강함에 폭이크다
SELECTCASE NPC_LV
	CASE 1
		NPC_MBP = 1600 + RAND:1600
	CASE 2
		NPC_MBP = 3200 + RAND:2800
	CASE 3
		NPC_MBP = 6200 + RAND:6000
	CASE 4
		NPC_MBP = 12000 + RAND:18000
	CASE 5
		NPC_MBP = 30000 + RAND:120000
	CASE 6
		NPC_MBP = 150000 + RAND:350000
	CASEELSE
		NPC_MBP = 500000 + RAND:500000
ENDSELECT


PRINTFORML %조사처리(BATTLERNAME:2,"와")% 대전합니다　
PRINTFORM %BATTLERNAME:2%의 벌레배틀 파워：
CALL COLOR_STAMP(NPC_LV + 1, 0, "★", RANKCOLOR(NPC_LV + 1), 1)
PRINTL 
RESETCOLOR
PRINTL [1] 대전개시！
PRINTL [0] 역시 그만둔다
INPUT
SIF !RESULT
	RETURN -1
;1은MASTER

;본래캐릭번호 들어가는곳
;MUSHI_BATTLER:2 = 999

;셋업1
CALL CHARA_MUSHICAGE_SETTING(999, 2, 1)

;서로보여＆벌레선출
CALL NPC_MUSHI_BATTLE_SELECTION
;셋업2
CALL NPC_MUSHI_BATTLE_SETUP_2

;시합개시！
CALL MUSHI_BATTLE
;결과발표
CALL MUSHI_BATTLE_RESULT(1)

;시간경과
TIME += 30

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;벌레배틀
@COM_ABLE497
;시간정지중
SIF FLAG:시간정지
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(497)
	RETURN RESULT
;곤충세트가 없다
SIF !ITEM:곤충채집세트
	RETURN 0
;;채집가능장소
SIF !MUSHITORI_SPOT(CFLAG:MASTER:현재위치)
	RETURN 0
;;겨울엔잔다
;SIF GET_MONTH() == "겨울의달"
;	RETURN 0
;벌레없다
SIF !MUSHI_CAGE:1
	RETURN 0
;동침중
SIF CFLAG:MASTER:동침중
	RETURN 0
;우후후중
SIF CFLAG:MASTER:우후후
	RETURN 0
;체력・기력부족
SIF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
	RETURN 0
RETURN 1


;-------------------------------------------------
;벌레배틀：見せ合い
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SELECTION

#DIM LPCOUNT
VARSET MUSHI_SELECT
VARSET MUSHI_REST
PRINTFORML ◆대전 상대의 벌레 바구니◆
FOR LPCOUNT, -2, MUSHI_MAXCAGE + 2
	IF LPCOUNT == -2
		PRINTFORM ┌
	ELSEIF LPCOUNT == 0
		PRINTFORM ├
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM └
	ELSE
		PRINTFORM │
	ENDIF
	IF LPCOUNT == -2
		PRINTFORM %"─" * 20%┐
	ELSEIF LPCOUNT == -1
		LOCALS = %BATTLERNAME:2%
		PRINTFORM %LOCALS, 36, LEFT%
		SETCOLOR RANKCOLOR(NPC_LV + 1)
		PRINTFORM ★%TOFULL(TOSTR(NPC_LV))%
		RESETCOLOR
		PRINTFORM │
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM %"─" * 20%┘
	ELSEIF LPCOUNT == 0
		PRINTFORM %"─" * 20%┤
	ELSE
		CALL PRINT_MUSHINAME(MUSHI_NAME:(20 + LPCOUNT), MUSHI_CAGE:(20 + LPCOUNT), 40)
		PRINTFORM │
	ENDIF
	PRINTL 
NEXT
;フィールドタイプ設定
MUSHI_FIELD:99 = 0
WHILE !MUSHI_FIELD:99
	SETBIT MUSHI_FIELD:99, RAND:8
	SIF !(MUSHITORI_SPOT(CFLAG:MASTER:현재위치) &  MUSHI_FIELD:99)
		MUSHI_FIELD:99 = 0
WEND
PRINTFORM ＜필드 타입：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ◆%CALLNAME:MASTER%의 벌레 바구니◆　배틀에 참가시킬 벌레를 1~3마리 선출해주세요
DRAWLINE
;MASTERの選出に引き渡し
MUSHI_REST:1 = 0
MUSHI_MENTAL:1 = 1000
CALL MUSHI_BATTLE_SELECTION_MASTER

;-------------------------------------------------
;벌레배틀：セットアップ２：選出後
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SETUP_2
#DIM LPCOUNT
#DIM SETNUM
#DIM KIRIHUDA
VARSET MUSHI_STATUS
VARSET MUSHI_MENTAL
;MASTERの設定
FOR LPCOUNT, 1, MUSHI_MAXSELECT + 1
	MUSHI_SELECT:(10 + LPCOUNT) = MUSHI_SELECT:LPCOUNT
	CALL MUSHI_PALAM(MUSHI_SELECT:LPCOUNT, MUSHI_POWER:1)
	CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(10 + LPCOUNT), 10 + LPCOUNT)
NEXT
;戦闘初期設定
;MASTERのMENTALは1000固定
MUSHI_MENTAL:1 = 1000
MUSHI_STAND:1 = MUSHI_SELECT:11
MUSHI_STANDNUM:1 = 11
GETCOLOR
MUSHI_COLOR:1 = RESULT
;キャラの選出を決める
FOR LPCOUNT, 2, MUSHI_CHARANUM + 1
	SETNUM = LPCOUNT * 10
	IF NPC_KIRIFUDA_ID
	;切り札がいるなら切り札は確定（番号はランダム）
		KIRIHUDA = 1
		LOCAL = RAND:MUSHI_MAXSELECT + 1
		MUSHI_SELECT:(SETNUM + LOCAL) = MUSHI_CAGE:(SETNUM + 1)
		MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(SETNUM + 1)
		CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
		CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
	ELSE
		KIRIHUDA = 0
	ENDIF
	;虫カゴからランダムで空いてる番号に入れていく,ただし切り札がいるなら1番は除外
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		IF !MUSHI_SELECT:(SETNUM + LOCAL)
			WHILE !MUSHI_SELECT:(SETNUM + LOCAL)
				LOCAL:1 = SETNUM + RAND:(MUSHI_MAXCAGE - KIRIHUDA) + 1 + KIRIHUDA
				LOCAL:2 = MUSHI_CAGE:(LOCAL:1)
				IF !MATCH(MUSHI_SELECT, LOCAL:2)
					MUSHI_SELECT:(SETNUM + LOCAL) = LOCAL:2
					MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(LOCAL:1)
				ENDIF
			WEND
			CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
			CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
		ENDIF
	NEXT
	;戦闘初期設定
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SETBIT MUSHI_REST:LPCOUNT, LOCAL
	NEXT
	MUSHI_MENTAL:LPCOUNT = NPC_MBP
	MUSHI_STAND:LPCOUNT = MUSHI_SELECT:(SETNUM + 1)
	MUSHI_STANDNUM:LPCOUNT = SETNUM + 1
	;口上色の設定
	;色が変わってたら登録する
	IF LOCAL != RESULT
		MUSHI_COLOR:LPCOUNT = RESULT
	ELSE
		MUSHI_COLOR:LPCOUNT = LOCAL
	ENDIF
	RESETCOLOR
NEXT