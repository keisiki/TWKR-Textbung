;-------------------------------------------------
;벌레배틀：전투의 통괄 함수
;-------------------------------------------------
@MUSHI_BATTLE
#DIM LPCOUNT
VARSET MUSHI_FINISH
;전원의 ETB 처리
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	CALL MUSHI_BATTLE_ETB(LOCAL, MUSHI_STANDNUM:LOCAL)
NEXT
MUSHI_ROUND = 0

; 이하 루프
WHILE !MUSHI_FINISH
	;최초 전장상태 표시
	IF !MUSHI_ROUND
		CALL MUSHI_BATTLE_FIELD
	ELSE
		CALL MUSHI_MSGSET(@"%" " * 30%＜라운드　%TOFULL(TOSTR(MUSHI_ROUND))%＞")
		MUSHI_MSGCL:RESULT = C_YELLOW
		CALL MUSHI_BATTLE_MSG
	ENDIF
	;행동순서 판정
	VARSET MUSHI_ORDER
	FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
		; 첫 번째로 높은 값을 추출해 간다.
		MUSHI_ORDER:0 = 0
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;すでに抽出してたら弾く
			SIF MATCH(MUSHI_ORDER, MUSHI_STANDNUM:LOCAL)
				CONTINUE
			IF MUSHI_ORDER:0 < MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
				MUSHI_ORDER:LPCOUNT = MUSHI_STANDNUM:LOCAL
				MUSHI_ORDER:0 = MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
			ENDIF
		NEXT
	NEXT
;	[IF_DEBUG]
;		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
;			LOCAL = MUSHI_ORDER:LPCOUNT / 10
;			DRAWLINE
;			PRINTFORML MUSHI_ORDER:{LPCOUNT}＝{MUSHI_ORDER:LPCOUNT}
;			PRINTFORML MUSHI_STANDNUM:{LOCAL} ＝ {MUSHI_STANDNUM:LOCAL}
;			PRINTFORML %CALLNAME:(MUSHI_BATTLER:LOCAL)%
;			PRINTFORML %MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_HP:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_HP:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_ATK:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_ATK:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SPD:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SKL:{(MUSHI_STANDNUM:LOCAL)} ＝ %MUSHI_SKL:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_STATUS:{LOCAL} ＝ %MUSHI_STATUS:LOCAL%
;		NEXT
;		WAIT
;	[ENDIF]
	IF MUSHI_ROUND
		;각 행동 처리
		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
			SIF !MUSHI_FINISH
				CALL MUSHI_BATTLE_COMBAT(MUSHI_ORDER:LPCOUNT / 10, MUSHI_ORDER:LPCOUNT)
		NEXT
		;턴 종료시 처리
		SIF !MUSHI_FINISH
			CALL MUSHI_BATTLE_ENDSTEP
	ELSE
		;개막 메시지
		CALL MUSHI_BATTLE_MSG
	ENDIF
	MUSHI_ROUND ++
WEND

;-------------------------------------------------
;벌레배틀：메인 전장 표시 함수(상반)
;LOCAL:1-2   플레이어 번호
;LOCAL : 3-4 캐릭터 번호
;MUSHI_ID: 5-6 전장에 나와있는 벌레의 ID
;MUSHI_FLAG : 7-8 전장에 나와있는 벌레 플래그 번호 (플레이어 번호 * 10 + 선출 번호)
;LOCAL : 99짝수 표시 여부
;-------------------------------------------------
@MUSHI_BATTLE_FIELD
#DIM LPCOUNT
#DIMS SHIFT
#DIM MUSHI_A_ID
#DIM MUSHI_A_FLAG
#DIM MUSHI_B_ID
#DIM MUSHI_B_FLAG
MUSHI_LINE = 3
PRINTFORM ◆ 라운드 %TOFULL(TOSTR(MUSHI_ROUND))%　＜ 필드 타입：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ┏%"━" * 40%┓
;2사람씩 표시해 나가다
FOR LOCAL, 0, (MUSHI_CHARANUM > 2) + 1
	;플래그가 뒤죽박죽이야~
	VARSET LOCALS
	LOCAL:1 = 1 + LOCAL * 2
	LOCAL:2 = 2 + LOCAL * 2
	LOCAL:3 = MUSHI_BATTLER:(LOCAL:1)
	LOCAL:4 = MUSHI_BATTLER:(LOCAL:2)
	LOCAL:99 = !LOCAL || !(MUSHI_CHARANUM % 2)
	;캐릭터 이름
	PRINT ┃
	SIF !MUSHI_REST:(LOCAL:2)
		LOCALS:2 += "Ｋ．Ｏ．"
	LOCALS:1 += CALLNAME:(LOCAL:3)
	LOCALS:2 += CALLNAME:(LOCAL:4)
	SIF MUSHI_TEAM && LOCAL:1 != 1
		LOCALS:1 += "(동료)"
	IF !MUSHI_REST:(LOCAL:1)
		LOCALS:1 += "Ｋ．Ｏ．"
		SETCOLOR C_RED
	ELSE
		SETCOLOR MUSHI_COLOR:(LOCAL:1)
	ENDIF
	PRINTFORM %LOCALS:1, 40, LEFT%
	RESETCOLOR
	IF LOCAL:99
		IF !MUSHI_REST:(LOCAL:2)
			SETCOLOR C_RED
		ELSE
			SETCOLOR MUSHI_COLOR:(LOCAL:2)
		ENDIF
		PRINTFORM %LOCALS:2, 40, RIGHT%
		RESETCOLOR
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	MUSHI_A_ID = MUSHI_STAND:(LOCAL:1)
	MUSHI_B_ID = MUSHI_STAND:(LOCAL:2)
	MUSHI_A_FLAG = MUSHI_STANDNUM:(LOCAL:1)
	MUSHI_B_FLAG = MUSHI_STANDNUM:(LOCAL:2)
	;ムシの名前
	PRINT ┃
	CALL PRINT_MUSHINAME(MUSHI_NAME:MUSHI_A_FLAG, MUSHI_A_ID, 40)
	IF LOCAL:99
		CALL PRINT_MUSHINAME(MUSHI_NAME:MUSHI_B_FLAG, MUSHI_B_ID, 40, 1)
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	;HP바의 잔여수
	PRINT ┃HP 
	CALL PRINT_COLORBAR(MUSHI_HP:MUSHI_A_FLAG, MUSHI_MAXHP:MUSHI_A_FLAG, 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("緑"), RESULT:1)
	PRINT  
	CALL PRINT_RESTMUSHI(LOCAL:1)
	IF LOCAL:99
		PRINT 　　　　　　　　　　
		CALL  PRINT_RESTMUSHI(LOCAL:2)
		PRINT  
		CALL PRINT_COLORBAR(MUSHI_HP:MUSHI_B_FLAG, MUSHI_MAXHP:MUSHI_B_FLAG, 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("緑"), RESULT:1)
		PRINT  HP
	ELSE
		PRINTFORM 　　　　　%" "* 40%
	ENDIF
	PRINTL ┃
	;속성과 상태변화
	PRINT ┃
	;전용 인수를 사용하여 RESULT:1을 출력하다
	CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:MUSHI_A_FLAG, MUSHI_TYPE:MUSHI_B_FLAG)
	PRINTFORM %MUSHI_STATUS:(LOCAL:1), 40 - RESULT, LEFT%
	IF LOCAL:99
		PRINTFORM %MUSHI_STATUS:(LOCAL:2), 40 - RESULT:1, RIGHT%
		CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:MUSHI_B_FLAG)
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	IF !LOCAL
		PRINT ┃
		CALL PRINT_FIELD_EFFECT(MUSHI_FIELD:99)
		PRINTL ┃
;		PRINTFORML ┃%" " * 37%ＶＳ．%" " * 37%┃
		MUSHI_LINE += 5
	ELSE
		MUSHI_LINE += 4
	ENDIF
NEXT
PRINTFORML ┣%"━" * 40%┫
;<메시지 표시에 계속>

;-------------------------------------------------
;벌레배틀：메시지를 설정하는 함수
;-------------------------------------------------
@MUSHI_MSGSET(ARGS)
FOR LOCAL, 0, 14
	IF MUSHI_MSG:LOCAL == ""
		MUSHI_MSG:LOCAL = %ARGS%
		RESULT = LOCAL
		BREAK
	ENDIF
NEXT
;채색한 번호를 돌려주다
RETURN RESULT

;-------------------------------------------------
;벌레배틀：메시지 표시 함수(하반)
;-------------------------------------------------
@MUSHI_BATTLE_MSG
MUSHI_LINE += 1
FOR LOCAL, 0, 14
	PRINTFORM ┃
	SIF MUSHI_MSGCL:LOCAL
		SETCOLOR MUSHI_MSGCL:LOCAL
	PRINTFORM %MUSHI_MSG:LOCAL, 80, LEFT%
	RESETCOLOR
	PRINTL ┃
	MUSHI_LINE ++
NEXT
PRINTFORML ┗%"━" * 40%┛
WAIT
;이력을 남길까? 말까?
;CLEARLINE MUSHI_LINE
VARSET MUSHI_MSG
VARSET MUSHI_MSGCL
SIF !MUSHI_FINISH
	CALL MUSHI_BATTLE_FIELD

;-------------------------------------------------
;벌레배틀：전투계산함수
;ARG    턴 플레이어 번호
;NUM    턴 플레이어의 전쟁터에 나와 있는 벌레 플래그 번호
;-------------------------------------------------
@MUSHI_BATTLE_COMBAT(ARG, NUM)
#DIM NUM
#DIM 공격대상
#DIM 대상NUM
#DIM 데미지보정
#DIM 고정데미지
#DIMS 공격스킬, 2
#DIM 회피율
#DIM 피해량
;행동 불능 판정
IF MUSHI_HP:NUM <= 0
	RETURN
;敗北している
ELSEIF !MUSHI_REST:ARG
	RETURN
ELSEIF STRCOUNT(MUSHI_STATUS:ARG, "<수면>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:NUM%》는 잠들어 있다…zZZ")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;대상 판정
공격대상 = 0
대상NUM = 0
공격스킬 = 
공격스킬:1 = 
LOCAL = 0
WHILE !대상NUM
	공격대상 = RAND:MUSHI_CHARANUM + 1
	;자신은 안 돼
	IF 공격대상 == ARG
		공격대상 = 0
	;우리 편은 안 돼
	ELSEIF MUSHI_TEAM && (공격대상 % 2) == (ARG % 2)
		공격대상 = 0
	;KO하면 안 돼
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:공격대상) <= 0
		공격대상 = 0
	;패퇴하면 안 돼
	ELSEIF !MUSHI_REST:공격대상
		공격대상 = 0
	;[의태]의 효과
	ELSEIF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:공격대상), "의태") && !RAND:2
		공격대상 = 0
	ENDIF
	SIF 공격대상
		대상NUM = MUSHI_STANDNUM:공격대상
	LOCAL ++
	;어느 정도 하고 정해지지 않으면 실패
	IF LOCAL > 100
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 공격은 대상이 없었다…")
		CALL MUSHI_BATTLE_MSG
		RETURN
	ENDIF
WEND

;공격시 판정
데미지보정= 100
고정데미지 = 0
;＠공격시대사
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 남은벌레수(ARG), MUSHI_BATTLER:ARG, ARG, 0, "攻撃時")
;단독공격스킬
;맹독 공격의 독공격을 방지
IF STRCOUNT(MUSHI_SKL:NUM, "독공격") && !STRCOUNT(MUSHI_SKL:NUM, "맹독공격") && 스킬판정(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<독>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『독공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 독공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "맹독공격") && 스킬판정(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<맹독>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『맹독공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 맹독공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "마비공격") && 스킬판정(30, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<마비>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『마비공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 마비공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "수면가루") && 스킬판정(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<수면>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『수면가루』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 수면가루
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "음파공격") && 스킬판정(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<혼란>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『음파공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 음파공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "점액공격") && 스킬판정(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<슬로우>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『점액공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 점액공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "악취공격") && 스킬판정(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:공격대상, "<악취>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『악취공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 악취공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "저주공격") && 스킬판정(30, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『저주공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 0.80
	공격스킬 = 저주공격
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "흡수공격") && 스킬판정(100, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『흡수공격』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES 데미지보정, 1.00
	공격스킬 = 흡수공격
ENDIF
;복합공격스킬
IF STRCOUNT(MUSHI_SKL:NUM, "강공격") && 스킬판정(40, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『강공격』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:대상NUM, "빛의갑옷")
		CALL MUSHI_MSGSET(@"하지만 【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 『빛의갑옷』으로 통상적인 데미지만 받았다！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"피해량 1.3배！")
		TIMES 데미지보정, 1.30
		공격스킬:1 += "강공격"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "크리티컬공격") && 스킬판정(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『크리티컬공격』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:대상NUM, "빛의갑옷")
		CALL MUSHI_MSGSET(@"하지만 【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 『빛의갑옷』으로 통상적인 데미지만 받았다！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"피해량 2배！！")
		TIMES 데미지보정, 2.00
		공격스킬:1 += "크리티컬공격"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "페이탈공격") && 스킬판정(10, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『페이탈공격』！")
	MUSHI_MSGCL:RESULT = C_RED
	IF STRCOUNT(MUSHI_SKL:대상NUM, "빛의갑옷")
		CALL MUSHI_MSGSET(@"하지만 【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 『빛의갑옷』으로 통상적인 데미지만 받았다！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"치명적인 데미지를 준다!")
		IF MUSHI_HP:대상NUM < MUSHI_MAXHP:대상NUM * 50 / 100
			고정데미지 = MUSHI_HP:대상NUM
		ELSE
			고정데미지 = MUSHI_HP:대상NUM - 1
		ENDIF
		공격스킬:1 += "페이탈공격"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "레이저공격") && 스킬판정(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『레이저공격』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"이 공격은 피할 수 없어!!")
	공격스킬:1 += "레이저공격"
ENDIF
;통상공격
SIF 공격스킬 == 공격스킬:1
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『통상공격』！")
;추가 데미지 업
IF STRCOUNT(MUSHI_SKL:NUM, "선수필승") && MUSHI_ORDER:1 == ARG
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『선수필승』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"최속 행동으로 데미지 상승!")
	TIMES 데미지보정, 1.20
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "구축") && 남은벌레수(공격대상) == 1
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『구축』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】의 벌레를 철저히 섬멸하라！")
	TIMES 데미지보정, 1.50
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "도미네이트") && MUSHI_HP:NUM == MUSHI_MAXHP:NUM
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『도미네이트』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"HP가 가득이면 반드시 크리티컬!!")
	TIMES 데미지보정, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "수서특효") && STRCOUNT(MUSHI_TRIBE:대상NUM, "수서")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『수서특효』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[수서]의 적에게 큰 데미지!!")
	TIMES 데미지보정, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "비행특효") && STRCOUNT(MUSHI_TRIBE:대상NUM, "비행")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『비행특효』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[비행]의 적에게 큰 데미지!!")
	TIMES 데미지보정, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "연체특효") && STRCOUNT(MUSHI_TRIBE:대상NUM, "연체")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『연체특효』발동！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[연체]의 적에게 큰 데미지!!")
	TIMES 데미지보정, 2.00
ENDIF

;회피 판정
IF MUSHI_SPD:NUM * 500 / 100 < MUSHI_SPD:대상NUM
	회피율 = 70
ELSEIF MUSHI_SPD:NUM * 400 / 100 < MUSHI_SPD:대상NUM
	회피율 = 60
ELSEIF MUSHI_SPD:NUM * 300 / 100 < MUSHI_SPD:대상NUM
	회피율 = 50
ELSEIF MUSHI_SPD:NUM * 250 / 100 < MUSHI_SPD:대상NUM
	회피율 = 40
ELSEIF MUSHI_SPD:NUM * 200 / 100 < MUSHI_SPD:대상NUM
	회피율 = 30
ELSEIF MUSHI_SPD:NUM * 150 / 100 < MUSHI_SPD:대상NUM
	회피율 = 20
ELSEIF MUSHI_SPD:NUM * 100 / 100 < MUSHI_SPD:대상NUM
	회피율 = 10
ELSE
	회피율 = 0
ENDIF
SIF STRCOUNT(MUSHI_SKL:대상NUM, "극소")
	회피율 += 30
SIF STRCOUNT(MUSHI_SKL:대상NUM, "고속회피")
	회피율 += 30
SIF STRCOUNT(MUSHI_STATUS:공격대상, "수면|마비|슬로우")
	회피율 = 0
SIF STRCOUNT(공격스킬:1, "레이저공격")
	회피율 = 0
;슬쩍그로테스크 공격은 필중
SIF STRCOUNT(MUSHI_SKL:NUM, "그로테스크공격")
	회피율 = 0
;최대 80%
회피율 = MIN(회피율, 80)
IF RAND:100 < 회피율
	IF STRCOUNT(MUSHI_SKL:대상NUM, "고속회피")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『고속회피』발동！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"민첩한 움직임으로 공격을 회피했다!!")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSEIF STRCOUNT(MUSHI_SKL:대상NUM, "극소")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『극소』발동！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"몸이 작아서 공격이 맞지 않아!!")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 공격을 회피했다！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ENDIF
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;방어시 판정
IF STRCOUNT(MUSHI_STATUS:공격대상, "<아지랑이>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》는 환영이었다！")
	MUSHI_MSGCL:RESULT = C_CREAM
	MUSHI_STATUS:공격대상'= REPLACE(MUSHI_STATUS:공격대상, "<아지랑이>", "")
	데미지보정= 0
ELSEIF STRCOUNT(MUSHI_SKL:대상NUM, "카운터") && 스킬판정(10, 공격대상, 대상NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『카운터』발동！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"데미지를 그대로 돌려준다!!")
	공격대상 = ARG
	대상NUM = NUM
ELSEIF STRCOUNT(MUSHI_SKL:대상NUM, "저스트가드") && 스킬판정(30, 공격대상, 대상NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의『저스트가드』발동！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"받는 데미지를 반으로 줄인다!")
	TIMES 데미지보정, 0.50
ENDIF
IF STRCOUNT(MUSHI_STATUS:공격대상, "<슬로우>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:대상NUM%》은 <슬로우>상태라 받는 데미지가 증가했다!")
	TIMES 데미지보정, 1.50
ENDIF

;데미지 판정
;비활성화되면 종료
IF !데미지보정
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF
[SKIPSTART]
	;리얼리티 차이 보정 ※보류
	SELECTCASE MUSHI_RARE:NUM - MUSHI_RARE:대상NUM
	CASE 1
		TIMES 데미지보정, 1.05
	CASE 2
		TIMES 데미지보정, 1.10
	CASE 3
		TIMES 데미지보정, 1.15
	CASE 4
		TIMES 데미지보정, 1.20
	CASE IS >= 5
		TIMES 데미지보정, 1.30
	ENDSELECT
[SKIPEND]
;상성 보정
VARSET LOCAL
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "赤") * STRCOUNT(MUSHI_TYPE:대상NUM, "緑")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "緑") * STRCOUNT(MUSHI_TYPE:대상NUM, "青")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "青") * STRCOUNT(MUSHI_TYPE:대상NUM, "赤")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "白") * STRCOUNT(MUSHI_TYPE:대상NUM, "黒")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "黒") * STRCOUNT(MUSHI_TYPE:대상NUM, "白")
WHILE LOCAL:1 < LOCAL
	TIMES 데미지보정, 1.50
	LOCAL:1 ++
WEND
SIF LOCAL:1
	CALL MUSHI_MSGSET(@"속성 궁합 최고다!")

;데미지결정
피해량 = MUSHI_ATK:NUM * 데미지보정/ 100
;난수 보정
피해량 = 피해량 * (80 + RAND:40) / 100

SIF 고정데미지
	피해량 = 고정데미지

CALL MUSHI_DAMAGE(대상NUM, 피해량)
CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》가 {피해량} 의 데미지！")
MUSHI_MSGCL:RESULT = C_ORANGE

;[흡수공격]의 효과
IF 공격스킬 == "흡수공격"
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》는 {피해량} 회복했다！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	MUSHI_HP:NUM = MIN(MUSHI_HP:NUM + 피해량, MUSHI_MAXHP:NUM)
ENDIF
;[역습]의 효과
IF INRANGE(MUSHI_HP:대상NUM, 1, MUSHI_MAXHP:대상NUM / 2) && STRCOUNT(MUSHI_SKL:대상NUM, "역습") && !STRCOUNT(MUSHI_STATUS:공격대상, "<역습>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『역습』발동！")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_MSGSET(@"공격을 받아 <역습> 상태가 되었다!!")
	MUSHI_STATUS:공격대상 += "<역습>"
	CALL MUSHI_PALAM(MUSHI_STAND:공격대상, MUSHI_POWER:공격대상, MUSHI_HP:대상NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:공격대상, 대상NUM)
ENDIF

;◆◆여기서 한번 메시지 표시◆◆
CALL MUSHI_BATTLE_MSG

VARSET LOCAL
;KO판정
IF MUSHI_HP:대상NUM <= 0
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ELSEIF 공격스킬 != "" && STRCOUNT(MUSHI_SKL:대상NUM, "태고의힘")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『태고의힘』발동！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 %공격스킬%의 효과를 받지 않아!")
	CALL MUSHI_BATTLE_MSG
ELSE
	;데미지 후 처리
	SELECTCASE 공격스킬
	CASE "독공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <독>에 중독됐다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<독>"
	CASE "맹독공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <맹독>에 중독됐다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<맹독>"
	CASE "마비공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <마비>에 걸렸다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<마비>"
	CASE "수면가루"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <수면>에 빠졌다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<수면>"
	CASE "음파공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <혼란>스럽다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<혼란>"
	CASE "점액공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <슬로우>상태가 되었다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<슬로우>"
	CASE "악취공격"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 <악취>상태가 되었다!")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:공격대상 += "<악취>"
	CASE "저주공격"
		;현재 HP의 1/2(절상) 피해를 입히는
		LOCAL = MUSHI_HP:대상NUM / 2 + 1
		CALL MUSHI_DAMAGE(대상NUM, LOCAL)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》은 추가적으로 {LOCAL} 의 데미지")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
	ENDSELECT
	IF 공격스킬 != ""
		;상태 변화를 고려하여 상태 재설정
		CALL MUSHI_PALAM(MUSHI_STAND:공격대상, MUSHI_POWER:공격대상, MUSHI_HP:대상NUM)
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:공격대상, 대상NUM)
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "나비효과") && 스킬판정(5, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『나비효과』발동！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"나비의 날갯짓이 회오리바람을 일으킨다!!")
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) > 1
			LOCAL:1 = MUSHI_HP:(MUSHI_STANDNUM:LOCAL) / 2
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, LOCAL:1)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》에 {LOCAL:1}의 데미지!")
			MUSHI_MSGCL:RESULT = C_ORANGE
		ENDIF
	NEXT
	;치명상은 받지 않지만(MSG만으로 좋다) 일단
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "소음공격") && MUSHI_CHARANUM > 2
	LOCAL:1 = 0
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		;당하고 있다
		SIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= 0
			CONTINUE
		;패배했어
		SIF !MUSHI_REST:LOCAL
			CONTINUE
		;자신
		SIF LOCAL == ARG
			CONTINUE
		;공격대상은 제외
		SIF LOCAL == 공격대상
			CONTINUE
		;동료
		SIF MUSHI_TEAM && (LOCAL % 2) == (ARG % 2)
			CONTINUE
		;한 번이라도 피해를 입히면 표시하겠다
		IF !LOCAL:1
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『소음공격』발동！")
			MUSHI_MSGCL:RESULT = C_L_GREEN
			CALL MUSHI_MSGSET(@"다른 모든 적에게 피해를 입힌다!")
			LOCAL:1 = 1
		ENDIF
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, 피해량)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》에 {피해량} 의 데미지!")
		MUSHI_MSGCL:RESULT = C_ORANGE
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:대상NUM, "발광")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:공격대상)%】《%MUSHI_NAME:대상NUM%》의 『발광』발동！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》은 <혼란>스럽다!")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	MUSHI_STATUS:ARG += "<혼란>"
	CALL MUSHI_BATTLE_MSG
ENDIF
IF STRCOUNT(MUSHI_STATUS:ARG, "<혼란>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》은 <혼란>상태에 있다!")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_DAMAGE(NUM, 피해량 / 2)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》에 {피해량 / 2} 의 데미지!")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_BATTLE_CHECK_KO
	SIF MUSHI_FINISH
		RETURN
ENDIF
VARSET LOCAL
IF STRCOUNT(MUSHI_SKL:NUM, "그로테스크공격")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『그로테스크공격』발동！")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_MSGSET(@"소름끼치는 거동으로 모두에게 정신적 데미지!!")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_BATTLE_MSG
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		SIF MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			MUSHI_MENTAL:LOCAL = MAX(MUSHI_MENTAL:LOCAL - 피해량, 0)
		IF !MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】은 눈앞의 참상을 견딜 수 없어 기브업 했다!")
			MUSHI_MSGCL:RESULT = C_RED
			LOCAL:1 = 1
		ENDIF
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_DO
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF

;-------------------------------------------------
;벌레배틀：기술발동판정함수
;ARG 플레이어 번호
;NUM 플래그 번호
;-------------------------------------------------
@스킬판정(確率, ARG, NUM)
#FUNCTION
#DIM 確率
#DIM NUM
SIF STRCOUNT(MUSHI_STATUS:ARG, "<得意地形>")
	TIMES 確率, 1.5
SIF STRCOUNT(MUSHI_SKL:NUM, "맹공")
	TIMES 確率, 1.5
SIF GET_MUSHIDATA(MUSHI_SELECT:NUM, "Ｘ型")
	TIMES 確率, 1.5
SIF STRCOUNT(MUSHI_STATUS:ARG, "<마비>")
	確率 = 0

RETURNF RAND:100 < 確率

;-------------------------------------------------
;벌레배틀：데미지 처리함수
;-------------------------------------------------
@MUSHI_DAMAGE(ARG, ARG:1)
MUSHI_HP:ARG = MAX(MUSHI_HP:ARG - ARG:1, 0)

;-------------------------------------------------
;벌레배틀：K.O.의 판정 함수
;ARG 공격 플레이어 번호
;NUM 공격 플레이어의 전장에 노출된 벌레 플래그 번호
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_KO(ARG, NUM)
#DIM NUM
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	;이미 당하고 있는
	IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) < 0
		CONTINUE
	;당했다(HP=0)
	ELSEIF !MUSHI_HP:(MUSHI_STANDNUM:LOCAL)
		IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "근성") && 스킬판정(15, LOCAL, MUSHI_STANDNUM:LOCAL)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》의 『근성』발동!!")
			MUSHI_MSGCL:RESULT = C_YELLOW
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = 1
		ELSE
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》는 쓰러졌다!!")
			MUSHI_MSGCL:RESULT = C_RED
			;쓰러지면 HP=-1로 해둔다.
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = -1
			CLEARBIT MUSHI_REST:LOCAL, MUSHI_STANDNUM:LOCAL % 10
			;@쓰러졌을 때 말투
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 남은벌레수(LOCAL), MUSHI_BATTLER:LOCAL, LOCAL, 0, "倒れた時")
			;[고양]의 효과
			IF STRCOUNT(MUSHI_SKL:NUM, "고양") && !STRCOUNT(MUSHI_STATUS:ARG, "<고양>")
				CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》의 『고양』발동！")
				MUSHI_MSGCL:RESULT = C_L_GREEN
				CALL MUSHI_MSGSET(@"적을 쓰러뜨리고 <고양> 상태가 되었다!!")
				MUSHI_STATUS:ARG += "<고양>"
				CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, MUSHI_HP:NUM)
				CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)
			ENDIF
		ENDIF
	;[역습]의 효과
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 2 && STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "역습") && !STRCOUNT(MUSHI_STATUS:LOCAL, "<역습>")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》의 『역습』발동！")
		MUSHI_MSGCL:RESULT = C_ORANGE
		CALL MUSHI_MSGSET(@"공격을 받아 <역습>상태로 나왔다!!")
		MUSHI_STATUS:LOCAL += "<역습>"
		CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:(MUSHI_STANDNUM:LOCAL))
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:LOCAL, MUSHI_STANDNUM:LOCAL)
	ENDIF
NEXT
CALL MUSHI_BATTLE_CHECK_DO
;-------------------------------------------------
;벌레배틀：드롭아웃 판정과 그에 이은 게임셋 판정 함수
; LOCAL: 2 드롭아웃된 플레이어의 순위
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_DO
#DIM LPCOUNT
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;MENTAL=-1은 제외
	IF (!MUSHI_REST:LPCOUNT || !MUSHI_MENTAL:LPCOUNT) && MUSHI_MENTAL:LPCOUNT >= 0
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】패퇴!!")
		MUSHI_MSGCL:RESULT = C_RED
		;＠패배시구상
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 남은벌레수(LPCOUNT), MUSHI_BATTLER:LPCOUNT, LPCOUNT, 0, "敗退時")
		;패퇴하면 MENTAL=-1로 해둘게
		MUSHI_MENTAL:LPCOUNT = -1
		MUSHI_REST:LPCOUNT = 0
		;순위 설정, 뒤에서 체크해 나가다
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			LOCAL:1 = MUSHI_CHARANUM + 1 - LOCAL
			IF !MUSHI_FINISH:(LOCAL:1)
				MUSHI_FINISH:(LOCAL:1) = LPCOUNT
				LOCAL:2 = LOCAL:1
				BREAK
			ENDIF
		NEXT
	ENDIF
NEXT
;◆◆◆여기서 KO및 패퇴 메세지 표시 ◆◆
CALL MUSHI_BATTLE_MSG
;무승부 시의 판정
IF LOCAL:2 == 1
	SETBIT MUSHI_REST:(MUSHI_FINISH:1), 3
	CALL MUSHI_MSGSET(@"놀랍게도、【%CALLNAME:(MUSHI_BATTLER:(MUSHI_FINISH:1))%】《%MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:1))%》는 최후의 힘을 다해 일어섰다!")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_BATTLE_MSG
	LOCAL:2 = 2
ENDIF
;팀전
IF MUSHI_TEAM
	;홀수냐 짝수냐가 전멸하면 게임세트
	IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7) || (!MUSHI_REST:2 && !MUSHI_REST:4 && !MUSHI_REST:6 && !MUSHI_REST:8)
		VARSET MUSHI_FINISH
		;귀찮아서 감당하기 힘들어.
		IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7)
			MUSHI_FINISH:1 = 2
			MUSHI_FINISH:2 = 4
			MUSHI_FINISH:3 = 6
			MUSHI_FINISH:4 = 8
		ELSE
			MUSHI_FINISH:1 = 1
			MUSHI_FINISH:2 = 3
			MUSHI_FINISH:3 = 5
			MUSHI_FINISH:4 = 7
		ENDIF
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (24 - STRLENS(CALLNAME:(MUSHI_BATTLER:LOCAL)))%☆☆☆승자　팀【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆게임셋 메시지 표시◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
;個人戦
ELSE
	;2등이 정해지면 게임 세트
	IF LOCAL:2 == 2
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (30 - STRLENS(CALLNAME:(MUSHI_BATTLER:LOCAL)))%☆☆☆승자　【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:1 = LOCAL
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆게임셋 메시지 표시◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF

;-------------------------------------------------
;벌레배틀：턴 종료시 처리
;-------------------------------------------------
@MUSHI_BATTLE_ENDSTEP
#DIM LPCOUNT
#DIM 데미지
;[유사] 효과(전원분)
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "유사") && NAME_FIELD_TYPE(MUSHI_FIELD:99) == "[모래밭]"
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;당하고 있다
			SIF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
				CONTINUE
			;자신
			SIF LOCAL == LPCOUNT
				CONTINUE
			;동료
			SIF MUSHI_TEAM && (LOCAL % 2) == (LPCOUNT % 2)
				CONTINUE
			;한 번이라도 피해를 입히면 표시하겠다
			IF !LOCAL:1
				CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》의 『유사』발동！")
				MUSHI_MSGCL:RESULT = C_CREAM
				CALL MUSHI_MSGSET(@"모든 적을 유사로 끌어들인다!")
				LOCAL:1 = 1
			ENDIF
			데미지 = MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 4
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, 데미지)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》에 {데미지}의 피해！")
			MUSHI_MSGCL:RESULT = C_ORANGE
		NEXT
		IF LOCAL:1
			CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
			SIF MUSHI_FINISH
				RETURN
		ENDIF
	ENDIF
NEXT

;독데미지처리,맹독우선
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<맹독>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		데미지 = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 2
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, 데미지)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》은 맹독에 {데미지}의 데미지！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ELSEIF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<독>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		데미지 = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, 데미지)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》은 독에 {데미지}의 데미지！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ENDIF
NEXT
;데미지가 발생하면 KO판정
IF LOCAL:1
	CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
	SIF MUSHI_FINISH
		RETURN
ENDIF

;수면이 일어나는 처리
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;確率1/2
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<수면>") && !RAND:2 && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》은 수면에서 깼다!")
		MUSHI_MSGCL:RESULT = C_YELLOW
		MUSHI_STATUS:LPCOUNT'= REPLACE(MUSHI_STATUS:LPCOUNT, "<수면>", "")
		LOCAL:1 = 1
	ENDIF
NEXT
;기상이 발생하면 메시지 표시
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;재생 처리
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "재생") && INRANGE(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT), 1, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) - 1)
		LOCAL = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) = MIN(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) + LOCAL, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT))
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》의 『재생』발동！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》은 {LOCAL} 회복했다! ")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		LOCAL:1 = 1
	ENDIF
NEXT
;재생이 발동되면 메세지 표시
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;교체
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;패퇴끝
	SIF !MUSHI_REST:LPCOUNT
		CONTINUE
	IF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
		;당신은 수동
		IF MUSHI_BATTLER:LPCOUNT == MASTER
			CLEARLINE MUSHI_LINE
			CALL MUSHI_BATTLE_CHANGE(LPCOUNT)
		ELSE
			;캐릭터는 위에서부터 순서대로 내놓는다
			FOR LOCAL, 1, MUSHI_MAXSELECT + 1
				IF GETBIT(MUSHI_REST:LPCOUNT, LOCAL)
					CALL MUSHI_BATTLE_ETB(LPCOUNT, LPCOUNT * 10 + LOCAL)
					BREAK
				ENDIF
			NEXT
		ENDIF
		LOCAL:1 = 1
	ENDIF
NEXT
;교체가 발생하면 메시지 표시
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;-------------------------------------------------
;마스터 수동 교체 함수
;전장 표시와 메시지 표시 사이에 INPUT가 들어가므로 사전에 전장 표시를 꺼두고,
;메시지 표시(ETB) 앞에 전장 표시하기
;-------------------------------------------------
@MUSHI_BATTLE_CHANGE(ARG)
#DIM 선택한벌레
PRINTFORML ◆다음에 낼 벌레를 골라라!
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF !GETBIT(MUSHI_REST:ARG, LOCAL)
		SETCOLOR C_GRAY
	PRINTFORM [{LOCAL, 2}] 
	CALL SHOW_MUSHI_DATA(MUSHI_SELECT:(ARG * 10 + LOCAL), "一行")
	PRINTL 
	RESETCOLOR
NEXT
INPUT
IF INRANGE(RESULT, 1, MUSHI_MAXSELECT) && GETBIT(MUSHI_REST:ARG, RESULT)
	선택한벌레 = RESULT
	CALL MUSHI_BATTLE_FIELD
	CALL MUSHI_BATTLE_ETB(ARG, ARG * 10 + 선택한벌레)
	MUSHI_LINE += 2 + MUSHI_MAXSELECT
	RETURN
ENDIF
CLEARLINE 2 + MUSHI_MAXSELECT
RESTART

;-------------------------------------------------
;벌레배틀：전장에 나갔을 때의 스킬 처리, 능력치 계산 함수
;ARG 플레이어 번호
;NUM 플래그 번호
;-------------------------------------------------
@MUSHI_BATTLE_ETB(ARG, NUM)
#DIM NUM
;STAND를 설정
MUSHI_STAND:ARG = MUSHI_SELECT:NUM
MUSHI_STANDNUM:ARG = NUM
MUSHI_STATUS:ARG = 
MUSHI_NAME = %MUSHI_NAME:NUM%
CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】은(는)《%MUSHI_NAME%》를 전장에 내보냈다！")
;＠ETB대사
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 남은벌레수(ARG), MUSHI_BATTLER:ARG, ARG, 0, "戦場に出した")

;필드 판정
IF STRCOUNT(MUSHI_SKL:NUM, "정화")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》의 [정화]발동！")
	MUSHI_MSGCL:RESULT = C_AQUA
	CALL MUSHI_BATTLE_필드변화(7)
ELSEIF MUSHI_FIELD:NUM & MUSHI_FIELD:99
	MUSHI_STATUS:ARG += "<得意地形>"
ENDIF
;得意季節
IF STRCOUNT(MUSHI_SKL:NUM, "봄벌레") && GET_MONTH() == "봄의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "여름벌레") && GET_MONTH() == "여름의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "가을벌레") && GET_MONTH() == "가을의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ENDIF
;ETBスキル
IF 남은벌레수(ARG) == 1
	IF STRCOUNT(MUSHI_SKL:NUM, "대장")
		MUSHI_STATUS:ARG += "<대장>"
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》의 [대장]발동！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》는 최후의 한 마리로서 의욕이 넘친다!")
		MUSHI_MSGCL:RESULT = C_YELLOW
	ELSE
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》는 마지막 한 마리다!")
		MUSHI_MSGCL:RESULT = C_ORANGE
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "아지랑이")
	MUSHI_STATUS:ARG += "<아지랑이>"
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》의 [아지랑이]발동！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》은 아지랑이에 휩싸였다!")
	MUSHI_MSGCL:RESULT = C_CREAM
ENDIF
;능력치 재설정, 전투HP 가득
CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, 0, MUSHI_STATUS:ARG)
CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)

;-------------------------------------------------
;벌레배틀：필드유형이바뀌었을때처리함수
;특기 지형이 바뀌고 상태도 바뀌므로 재설정함
;-------------------------------------------------
@MUSHI_BATTLE_필드변화(ARG)
#DIM NUM
MUSHI_FIELD:99 = 0
SETBIT MUSHI_FIELD:99, ARG
CALL MUSHI_MSGSET(@"필드가＜%NAME_FIELD_TYPE(MUSHI_FIELD:99)%＞변화했다!")
MUSHI_MSGCL:RESULT = C_L_GREEN
;특기지형 재설정 및 그에 따른 능력치 재설정
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	NUM = MUSHI_STANDNUM:LOCAL
	IF MUSHI_FIELD:NUM & MUSHI_FIELD:99
		MUSHI_STATUS:LOCAL += "<得意地形>"
	ELSEIF STRCOUNT(MUSHI_STATUS:LOCAL, "<得意地形>")
		MUSHI_STATUS:LOCAL '= REPLACE(MUSHI_STATUS:LOCAL, "<得意地形>", "")
	ENDIF
	CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:LOCAL, NUM)
NEXT
;-------------------------------------------------
;벌레배틀：플레이어 번호 ARG의남은벌레수를 돌려주는 함수
;-------------------------------------------------
@남은벌레수(ARG)
#FUNCTION
LOCAL:1 = 0
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF GETBIT(MUSHI_REST:ARG, LOCAL)
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1
