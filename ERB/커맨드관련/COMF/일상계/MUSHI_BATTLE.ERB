;-------------------------------------------------
;벌레배틀：戦闘の統括関数
;-------------------------------------------------
@MUSHI_BATTLE
#DIM LPCOUNT
VARSET MUSHI_FINISH
;全員のETB処理
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	CALL MUSHI_BATTLE_ETB(LOCAL, MUSHI_STANDNUM:LOCAL)
NEXT
MUSHI_ROUND = 0

;以下ループ
WHILE !MUSHI_FINISH
	;最初に戦場状態を表示
	IF !MUSHI_ROUND
		CALL MUSHI_BATTLE_FIELD
	ELSE
		CALL MUSHI_MSGSET(@"%" " * 30%＜ラウンド　%TOFULL(TOSTR(MUSHI_ROUND))%＞")
		MUSHI_MSGCL:RESULT = C_YELLOW
		CALL MUSHI_BATTLE_MSG
	ENDIF
	;行動順判定
	VARSET MUSHI_ORDER
	FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
		;一番高いものを抽出していく
		MUSHI_ORDER:0 = 0
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;すでに抽出してたら弾く
			SIF MATCH(MUSHI_ORDER, MUSHI_STANDNUM:LOCAL)
				CONTINUE
			IF MUSHI_ORDER:0 < MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
				MUSHI_ORDER:LPCOUNT = MUSHI_STANDNUM:LOCAL
				MUSHI_ORDER:0 = MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
			ENDIF
		NEXT
	NEXT
;	[IF_DEBUG]
;		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
;			LOCAL = MUSHI_ORDER:LPCOUNT / 10
;			DRAWLINE
;			PRINTFORML MUSHI_ORDER:{LPCOUNT}＝{MUSHI_ORDER:LPCOUNT}
;			PRINTFORML MUSHI_STANDNUM:{LOCAL} ＝ {MUSHI_STANDNUM:LOCAL}
;			PRINTFORML %CALLNAME:(MUSHI_BATTLER:LOCAL)%
;			PRINTFORML %MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_HP:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_HP:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_ATK:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_ATK:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SPD:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SKL:{(MUSHI_STANDNUM:LOCAL)} ＝ %MUSHI_SKL:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_STATUS:{LOCAL} ＝ %MUSHI_STATUS:LOCAL%
;		NEXT
;		WAIT
;	[ENDIF]
	IF MUSHI_ROUND
		;各行動処理
		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
			SIF !MUSHI_FINISH
				CALL MUSHI_BATTLE_COMBAT(MUSHI_ORDER:LPCOUNT / 10, MUSHI_ORDER:LPCOUNT)
		NEXT
		;ターン終了時処理
		SIF !MUSHI_FINISH
			CALL MUSHI_BATTLE_ENDSTEP
	ELSE
		;開幕メッセージ
		CALL MUSHI_BATTLE_MSG
	ENDIF
	MUSHI_ROUND ++
WEND

;-------------------------------------------------
;벌레배틀：メインの戦場表示関数(上半分）
;LOCAL:1-2   プレイヤー番号
;LOCAL:3-4   キャラクター番号
;LOCAL:5-6   戦場に出ているムシのID
;LOCAL:7-8   戦場に出ているムシのフラグ番号(プレイヤー番号 * 10 + 選出番号）
;LOCAL:99    偶数人目の表示の有無
;-------------------------------------------------
@MUSHI_BATTLE_FIELD
#DIM LPCOUNT
#DIMS SHIFT
MUSHI_LINE = 3
PRINTFORM ◆ラウンド%TOFULL(TOSTR(MUSHI_ROUND))%　＜フィールドタイプ：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ┏%"━" * 40%┓
;2人ずつ表示していく
FOR LOCAL, 0, (MUSHI_CHARANUM > 2) + 1
	;フラグがごっちゃごちゃだ～
	VARSET LOCALS
	LOCAL:1 = 1 + LOCAL * 2
	LOCAL:2 = 2 + LOCAL * 2
	LOCAL:3 = MUSHI_BATTLER:(LOCAL:1)
	LOCAL:4 = MUSHI_BATTLER:(LOCAL:2)
	LOCAL:99 = !LOCAL || !(MUSHI_CHARANUM % 2)
	;キャラの名前
	PRINT ┃
	SIF !MUSHI_REST:(LOCAL:2)
		LOCALS:2 += "Ｋ．Ｏ．"
	LOCALS:1 += CALLNAME:(LOCAL:3)
	LOCALS:2 += CALLNAME:(LOCAL:4)
	SIF MUSHI_TEAM && LOCAL:1 != 1
		LOCALS:1 += "(仲間)"
	IF !MUSHI_REST:(LOCAL:1)
		LOCALS:1 += "Ｋ．Ｏ．"
		SETCOLOR C_RED
	ELSE
		SETCOLOR MUSHI_COLOR:(LOCAL:1)
	ENDIF
	PRINTFORM %LOCALS:1, 40, LEFT%
	RESETCOLOR
	IF LOCAL:99
		IF !MUSHI_REST:(LOCAL:2)
			SETCOLOR C_RED
		ELSE
			SETCOLOR MUSHI_COLOR:(LOCAL:2)
		ENDIF
		PRINTFORM %LOCALS:2, 40, RIGHT%
		RESETCOLOR
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	LOCAL:5 = MUSHI_STAND:(LOCAL:1)
	LOCAL:6 = MUSHI_STAND:(LOCAL:2)
	LOCAL:7 = MUSHI_STANDNUM:(LOCAL:1)
	LOCAL:8 = MUSHI_STANDNUM:(LOCAL:2)
	;ムシの名前
	PRINT ┃
	CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL:7), LOCAL:5, 40)
	IF LOCAL:99
		CALL PRINT_MUSHINAME(MUSHI_NAME:(LOCAL:8), LOCAL:6, 40, 1)
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	;HPバーと残り手持ち数
	PRINT ┃HP 
	CALL PRINT_COLORBAR(MUSHI_HP:(LOCAL:7), MUSHI_MAXHP:(LOCAL:7), 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("초록빛"), RESULT:1)
	PRINT  
	CALL PRINT_RESTMUSHI(LOCAL:1)
	IF LOCAL:99
		PRINT 　　　　　　　　　　
		CALL  PRINT_RESTMUSHI(LOCAL:2)
		PRINT  
		CALL PRINT_COLORBAR(MUSHI_HP:(LOCAL:8), MUSHI_MAXHP:(LOCAL:8), 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("초록빛"), RESULT:1)
		PRINT  HP
	ELSE
		PRINTFORM 　　　　　%" "* 40%
	ENDIF
	PRINTL ┃
	;属性と状態変化
	PRINT ┃
	;専用引数を使ってRESULT:1を出力する
	CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:(LOCAL:7), MUSHI_TYPE:(LOCAL:8))
	PRINTFORM %MUSHI_STATUS:(LOCAL:1), 40 - RESULT, LEFT%
	IF LOCAL:99
		PRINTFORM %MUSHI_STATUS:(LOCAL:2), 40 - RESULT:1, RIGHT%
		CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:(LOCAL:8))
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	IF !LOCAL
		PRINT ┃
		CALL PRINT_FIELD_EFFECT(MUSHI_FIELD:99)
		PRINTL ┃
;		PRINTFORML ┃%" " * 37%ＶＳ．%" " * 37%┃
		MUSHI_LINE += 5
	ELSE
		MUSHI_LINE += 4
	ENDIF
NEXT
PRINTFORML ┣%"━" * 40%┫
;＜メッセージ表示に続く＞

;-------------------------------------------------
;벌레배틀：メッセージを設定する関数
;-------------------------------------------------
@MUSHI_MSGSET(ARGS)
FOR LOCAL, 0, 14
	IF MUSHI_MSG:LOCAL == ""
		MUSHI_MSG:LOCAL = %ARGS%
		RESULT = LOCAL
		BREAK
	ENDIF
NEXT
;色を着ける用に入れた番号を返す
RETURN RESULT

;-------------------------------------------------
;벌레배틀：メッセージ表示関数(下半分）
;-------------------------------------------------
@MUSHI_BATTLE_MSG
MUSHI_LINE += 1
FOR LOCAL, 0, 14
	PRINTFORM ┃
	SIF MUSHI_MSGCL:LOCAL
		SETCOLOR MUSHI_MSGCL:LOCAL
	PRINTFORM %MUSHI_MSG:LOCAL, 80, LEFT%
	RESETCOLOR
	PRINTL ┃
	MUSHI_LINE ++
NEXT
PRINTFORML ┗%"━" * 40%┛
WAIT
;履歴を残すか？どうするか？
;CLEARLINE MUSHI_LINE
VARSET MUSHI_MSG
VARSET MUSHI_MSGCL
SIF !MUSHI_FINISH
	CALL MUSHI_BATTLE_FIELD

;-------------------------------------------------
;벌레배틀：戦闘計算関数
;ARG    ターンプレイヤー番号
;NUM    ターンプレイヤーの戦場に出しているムシのフラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_COMBAT(ARG, NUM)
#DIM NUM
#DIM 攻撃対象
#DIM 対象NUM
#DIM ダメージ補正
#DIM 固定ダメージ
#DIMS 攻撃スキル, 2
#DIM 回避率
#DIM 与えるダメージ
;行動不能判定
IF MUSHI_HP:NUM <= 0
	RETURN
;敗北している
ELSEIF !MUSHI_REST:ARG
	RETURN
ELSEIF STRCOUNT(MUSHI_STATUS:ARG, "<眠り>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:NUM%》は眠っている……zZZ")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;対象判定
攻撃対象 = 0
対象NUM = 0
攻撃スキル = 
攻撃スキル:1 = 
LOCAL = 0
WHILE !対象NUM
	攻撃対象 = RAND:MUSHI_CHARANUM + 1
	;自分はダメ
	IF 攻撃対象 == ARG
		攻撃対象 = 0
	;味方はダメ
	ELSEIF MUSHI_TEAM && (攻撃対象 % 2) == (ARG % 2)
		攻撃対象 = 0
	;KOしてたらダメ
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:攻撃対象) <= 0
		攻撃対象 = 0
	;敗退してたらダメ
	ELSEIF !MUSHI_REST:攻撃対象
		攻撃対象 = 0
	;[擬態]の効果
	ELSEIF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:攻撃対象), "擬態") && !RAND:2
		攻撃対象 = 0
	ENDIF
	SIF 攻撃対象
		対象NUM = MUSHI_STANDNUM:攻撃対象
	LOCAL ++
	;ある程度やって決まらなければ失敗
	IF LOCAL > 100
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の攻撃は対象がいなかった…")
		CALL MUSHI_BATTLE_MSG
		RETURN
	ENDIF
WEND

;攻撃時判定
ダメージ補正 = 100
固定ダメージ = 0
;＠攻撃時口上
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(ARG), MUSHI_BATTLER:ARG, ARG, 0, "攻撃時")
;単独の攻撃スキル
;猛毒攻撃の毒攻撃を防止
IF STRCOUNT(MUSHI_SKL:NUM, "毒攻撃") && !STRCOUNT(MUSHI_SKL:NUM, "猛毒攻撃") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<독>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『毒攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 毒攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "猛毒攻撃") && スキル判定(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<猛毒>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『猛毒攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 猛毒攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "マヒ攻撃") && スキル判定(30, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<マヒ>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『マヒ攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = マヒ攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "眠り粉") && スキル判定(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<眠り>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『眠り粉』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 眠り粉
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "音波攻撃") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<混乱>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『音波攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 音波攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "粘液攻撃") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<スロウ>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『粘液攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 粘液攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "悪臭攻撃") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<悪臭>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『悪臭攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 悪臭攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "呪い攻撃") && スキル判定(30, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『呪い攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 0.80
	攻撃スキル = 呪い攻撃
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "吸収攻撃") && スキル判定(100, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『吸収攻撃』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ補正, 1.00
	攻撃スキル = 吸収攻撃
ENDIF
;複合攻撃スキル
IF STRCOUNT(MUSHI_SKL:NUM, "強攻撃") && スキル判定(40, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『強攻撃』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:対象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"しかし【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は『光の装甲』で通常のダメージしか受けない！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"与えるダメージ1.3倍！")
		TIMES ダメージ補正, 1.30
		攻撃スキル:1 += "強攻撃"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "クリティカル攻撃") && スキル判定(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『クリティカル攻撃』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:対象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"しかし【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は『光の装甲』で通常のダメージしか受けない！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"与えるダメージ2倍！！")
		TIMES ダメージ補正, 2.00
		攻撃スキル:1 += "クリティカル攻撃"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "フェイタル攻撃") && スキル判定(10, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『フェイタル攻撃』！")
	MUSHI_MSGCL:RESULT = C_RED
	IF STRCOUNT(MUSHI_SKL:対象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"しかし【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は『光の装甲』で通常のダメージしか受けない！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"致命的なダメージを与える！！")
		IF MUSHI_HP:対象NUM < MUSHI_MAXHP:対象NUM * 50 / 100
			固定ダメージ = MUSHI_HP:対象NUM
		ELSE
			固定ダメージ = MUSHI_HP:対象NUM - 1
		ENDIF
		攻撃スキル:1 += "フェイタル攻撃"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "レーザー攻撃") && スキル判定(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『レーザー攻撃』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"この攻撃は避けられない！！")
	攻撃スキル:1 += "レーザー攻撃"
ENDIF
;通常攻撃
SIF 攻撃スキル == 攻撃スキル:1
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『通常攻撃』！")
;追加ダメージアップ
IF STRCOUNT(MUSHI_SKL:NUM, "先手必勝") && MUSHI_ORDER:1 == ARG
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『先手必勝』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"最速行動でダメージ上昇！")
	TIMES ダメージ補正, 1.20
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "駆逐") && 残りムシ数(攻撃対象) == 1
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『駆逐』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】のムシを完膚無きまで殲滅する！")
	TIMES ダメージ補正, 1.50
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "ドミネイト") && MUSHI_HP:NUM == MUSHI_MAXHP:NUM
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『ドミネイト』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"HPが満タンだと必ずクリティカル！！")
	TIMES ダメージ補正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "水棲特効") && STRCOUNT(MUSHI_TRIBE:対象NUM, "습기선호")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『水棲特効』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[습기선호]の敵に大ダメージ！！")
	TIMES ダメージ補正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "飛行特効") && STRCOUNT(MUSHI_TRIBE:対象NUM, "飛行")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『飛行特効』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[飛行]の敵に大ダメージ！！")
	TIMES ダメージ補正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "軟体特効") && STRCOUNT(MUSHI_TRIBE:対象NUM, "軟体")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『軟体特効』発動！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"[軟体]の敵に大ダメージ！！")
	TIMES ダメージ補正, 2.00
ENDIF

;回避判定
IF MUSHI_SPD:NUM * 500 / 100 < MUSHI_SPD:対象NUM
	回避率 = 70
ELSEIF MUSHI_SPD:NUM * 400 / 100 < MUSHI_SPD:対象NUM
	回避率 = 60
ELSEIF MUSHI_SPD:NUM * 300 / 100 < MUSHI_SPD:対象NUM
	回避率 = 50
ELSEIF MUSHI_SPD:NUM * 250 / 100 < MUSHI_SPD:対象NUM
	回避率 = 40
ELSEIF MUSHI_SPD:NUM * 200 / 100 < MUSHI_SPD:対象NUM
	回避率 = 30
ELSEIF MUSHI_SPD:NUM * 150 / 100 < MUSHI_SPD:対象NUM
	回避率 = 20
ELSEIF MUSHI_SPD:NUM * 100 / 100 < MUSHI_SPD:対象NUM
	回避率 = 10
ELSE
	回避率 = 0
ENDIF
SIF STRCOUNT(MUSHI_SKL:対象NUM, "極小")
	回避率 += 30
SIF STRCOUNT(MUSHI_SKL:対象NUM, "高速回避")
	回避率 += 30
SIF STRCOUNT(MUSHI_STATUS:攻撃対象, "眠り|マヒ|スロウ")
	回避率 = 0
SIF STRCOUNT(攻撃スキル:1, "レーザー攻撃")
	回避率 = 0
;コッソリグロテスク攻撃は必中
SIF STRCOUNT(MUSHI_SKL:NUM, "グロテスク攻撃")
	回避率 = 0
;最大80%
回避率 = MIN(回避率, 80)
IF RAND:100 < 回避率
	IF STRCOUNT(MUSHI_SKL:対象NUM, "高速回避")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『高速回避』発動！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"俊敏な動きで攻撃を回避した！！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSEIF STRCOUNT(MUSHI_SKL:対象NUM, "極小")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『極小』発動！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"身体が小さくて攻撃が当たらない！！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は攻撃を回避した！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ENDIF
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;防御時判定
IF STRCOUNT(MUSHI_STATUS:攻撃対象, "<陽炎>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は幻影だった！")
	MUSHI_MSGCL:RESULT = C_CREAM
	MUSHI_STATUS:攻撃対象'= REPLACE(MUSHI_STATUS:攻撃対象, "<陽炎>", "")
	ダメージ補正 = 0
ELSEIF STRCOUNT(MUSHI_SKL:対象NUM, "カウンター") && スキル判定(10, 攻撃対象, 対象NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『カウンター』発動！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"ダメージをそのまま返す！！")
	攻撃対象 = ARG
	対象NUM = NUM
ELSEIF STRCOUNT(MUSHI_SKL:対象NUM, "ジャストガード") && スキル判定(30, 攻撃対象, 対象NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『ジャストガード』発動！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"受けるダメージを半分にする！")
	TIMES ダメージ補正, 0.50
ENDIF
IF STRCOUNT(MUSHI_STATUS:攻撃対象, "<スロウ>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:対象NUM%》は<スロウ>状態で受けるダメージが増えた！")
	TIMES ダメージ補正, 1.50
ENDIF

;ダメージ判定
;無効化されたら終了
IF !ダメージ補正
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF
[SKIPSTART]
	;レアリティ差補正※保留
	SELECTCASE MUSHI_RARE:NUM - MUSHI_RARE:対象NUM
	CASE 1
		TIMES ダメージ補正, 1.05
	CASE 2
		TIMES ダメージ補正, 1.10
	CASE 3
		TIMES ダメージ補正, 1.15
	CASE 4
		TIMES ダメージ補正, 1.20
	CASE IS >= 5
		TIMES ダメージ補正, 1.30
	ENDSELECT
[SKIPEND]
;相性補正
VARSET LOCAL
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "빨강빛") * STRCOUNT(MUSHI_TYPE:対象NUM, "초록빛")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "초록빛") * STRCOUNT(MUSHI_TYPE:対象NUM, "푸른빛")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "푸른빛") * STRCOUNT(MUSHI_TYPE:対象NUM, "빨강빛")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "흰빛") * STRCOUNT(MUSHI_TYPE:対象NUM, "흑빛")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "흑빛") * STRCOUNT(MUSHI_TYPE:対象NUM, "흰빛")
WHILE LOCAL:1 < LOCAL
	TIMES ダメージ補正, 1.50
	LOCAL:1 ++
WEND
SIF LOCAL:1
	CALL MUSHI_MSGSET(@"属性相性バツグンだ！")

;ダメージ決定
与えるダメージ = MUSHI_ATK:NUM * ダメージ補正 / 100
;乱数補正
与えるダメージ = 与えるダメージ * (80 + RAND:40) / 100

SIF 固定ダメージ
	与えるダメージ = 固定ダメージ

CALL MUSHI_DAMAGE(対象NUM, 与えるダメージ)
CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》に {与えるダメージ} のダメージ！")
MUSHI_MSGCL:RESULT = C_ORANGE

;[吸収攻撃]の効果
IF 攻撃スキル == "吸収攻撃"
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》は {与えるダメージ} 回復した！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	MUSHI_HP:NUM = MIN(MUSHI_HP:NUM + 与えるダメージ, MUSHI_MAXHP:NUM)
ENDIF
;[逆襲]の効果
IF INRANGE(MUSHI_HP:対象NUM, 1, MUSHI_MAXHP:対象NUM / 2) && STRCOUNT(MUSHI_SKL:対象NUM, "逆襲") && !STRCOUNT(MUSHI_STATUS:攻撃対象, "<逆襲>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『逆襲』発動！")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_MSGSET(@"攻撃を受けて<逆襲>状態になった！！")
	MUSHI_STATUS:攻撃対象 += "<逆襲>"
	CALL MUSHI_PALAM(MUSHI_STAND:攻撃対象, MUSHI_POWER:攻撃対象, MUSHI_HP:対象NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:攻撃対象, 対象NUM)
ENDIF

;◆◆ここで一度メッセージ表示◆◆
CALL MUSHI_BATTLE_MSG

VARSET LOCAL
;KO判定
IF MUSHI_HP:対象NUM <= 0
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ELSEIF 攻撃スキル != "" && STRCOUNT(MUSHI_SKL:対象NUM, "太古の力")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『太古の力』発動！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は%攻撃スキル%の効果を受けない！")
	CALL MUSHI_BATTLE_MSG
ELSE
	;ダメージ後処理
	SELECTCASE 攻撃スキル
	CASE "毒攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<독>を受けた！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<독>"
	CASE "猛毒攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<猛毒>を受けた！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<猛毒>"
	CASE "マヒ攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<マヒ>した！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<マヒ>"
	CASE "眠り粉"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<眠り>に落ちた！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<眠り>"
	CASE "音波攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<混乱>した！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<混乱>"
	CASE "粘液攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<スロウ>状態になった！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<スロウ>"
	CASE "悪臭攻撃"
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は<悪臭>状態になった！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻撃対象 += "<悪臭>"
	CASE "呪い攻撃"
		;現在HPの1/2(切り上げ)ダメージを与える
		LOCAL = MUSHI_HP:対象NUM / 2 + 1
		CALL MUSHI_DAMAGE(対象NUM, LOCAL)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》は呪いでさらに {LOCAL} のダメージ！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
	ENDSELECT
	IF 攻撃スキル != ""
		;状態変化を考慮してステータス再設定
		CALL MUSHI_PALAM(MUSHI_STAND:攻撃対象, MUSHI_POWER:攻撃対象, MUSHI_HP:対象NUM)
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:攻撃対象, 対象NUM)
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "バタフライエフェクト") && スキル判定(5, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『バタフライエフェクト』発動！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"蝶の羽ばたきが竜巻を引き起こす！！")
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) > 1
			LOCAL:1 = MUSHI_HP:(MUSHI_STANDNUM:LOCAL) / 2
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, LOCAL:1)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》に {LOCAL:1} のダメージ！")
			MUSHI_MSGCL:RESULT = C_ORANGE
		ENDIF
	NEXT
	;致命傷はうけない(MSGだけでいい）けど一応
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "騒音攻撃") && MUSHI_CHARANUM > 2
	LOCAL:1 = 0
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		;やられてる
		SIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= 0
			CONTINUE
		;敗北してる
		SIF !MUSHI_REST:LOCAL
			CONTINUE
		;自身
		SIF LOCAL == ARG
			CONTINUE
		;攻撃対象は除外
		SIF LOCAL == 攻撃対象
			CONTINUE
		;仲間
		SIF MUSHI_TEAM && (LOCAL % 2) == (ARG % 2)
			CONTINUE
		;一度でもダメージを与えるなら表示する
		IF !LOCAL:1
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『騒音攻撃』発動！")
			MUSHI_MSGCL:RESULT = C_L_GREEN
			CALL MUSHI_MSGSET(@"他の全ての敵にダメージを与える！")
			LOCAL:1 = 1
		ENDIF
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, 与えるダメージ)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》に {与えるダメージ} のダメージ！")
		MUSHI_MSGCL:RESULT = C_ORANGE
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:対象NUM, "発光")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:攻撃対象)%】《%MUSHI_NAME:対象NUM%》の『発光』発動！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》は<混乱>した！")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	MUSHI_STATUS:ARG += "<混乱>"
	CALL MUSHI_BATTLE_MSG
ENDIF
IF STRCOUNT(MUSHI_STATUS:ARG, "<混乱>")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》は<混乱>している！")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_DAMAGE(NUM, 与えるダメージ / 2)
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》に {与えるダメージ / 2} のダメージ！")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_BATTLE_CHECK_KO
	SIF MUSHI_FINISH
		RETURN
ENDIF
VARSET LOCAL
IF STRCOUNT(MUSHI_SKL:NUM, "グロテスク攻撃")
	CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『グロテスク攻撃』発動！")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_MSGSET(@"身の毛のよだつ挙動で全員に精神的ダメージ！！")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_BATTLE_MSG
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		SIF MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			MUSHI_MENTAL:LOCAL = MAX(MUSHI_MENTAL:LOCAL - 与えるダメージ, 0)
		IF !MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】は目の前の惨状に耐えられなくなりギブアップした！")
			MUSHI_MSGCL:RESULT = C_RED
			LOCAL:1 = 1
		ENDIF
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_DO
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF

;-------------------------------------------------
;벌레배틀：スキル発動判定関数
;ARG  プレイヤー番号
;NUM  フラグ番号
;-------------------------------------------------
@スキル判定(確率, ARG, NUM)
#FUNCTION
#DIM 確率
#DIM NUM
SIF STRCOUNT(MUSHI_STATUS:ARG, "<得意地形>")
	TIMES 確率, 1.5
SIF STRCOUNT(MUSHI_SKL:NUM, "猛攻")
	TIMES 確率, 1.5
SIF GET_MUSHIDATA(MUSHI_SELECT:NUM, "Ｘ型")
	TIMES 確率, 1.5
SIF STRCOUNT(MUSHI_STATUS:ARG, "<マヒ>")
	確率 = 0

RETURNF RAND:100 < 確率

;-------------------------------------------------
;벌레배틀：ダメージ処理関数
;-------------------------------------------------
@MUSHI_DAMAGE(ARG, ARG:1)
MUSHI_HP:ARG = MAX(MUSHI_HP:ARG - ARG:1, 0)

;-------------------------------------------------
;벌레배틀：Ｋ．Ｏ．の判定関数
;ARG    攻撃プレイヤー番号
;NUM    攻撃プレイヤーの戦場に出しているムシのフラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_KO(ARG, NUM)
#DIM NUM
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	;すでにやられてる
	IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) < 0
		CONTINUE
	;やられた(HP=0）
	ELSEIF !MUSHI_HP:(MUSHI_STANDNUM:LOCAL)
		IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "ふんばり") && スキル判定(15, LOCAL, MUSHI_STANDNUM:LOCAL)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》の『ふんばり』発動！！")
			MUSHI_MSGCL:RESULT = C_YELLOW
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = 1
		ELSE
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》は倒れた！！")
			MUSHI_MSGCL:RESULT = C_RED
			;倒れたらHP=-1にしておく
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = -1
			CLEARBIT MUSHI_REST:LOCAL, MUSHI_STANDNUM:LOCAL % 10
			;＠倒れた時口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(LOCAL), MUSHI_BATTLER:LOCAL, LOCAL, 0, "倒れた時")
			;[高揚]の効果
			IF STRCOUNT(MUSHI_SKL:NUM, "高揚") && !STRCOUNT(MUSHI_STATUS:ARG, "<高揚>")
				CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】《%MUSHI_NAME:NUM%》の『高揚』発動！")
				MUSHI_MSGCL:RESULT = C_L_GREEN
				CALL MUSHI_MSGSET(@"敵を倒して<高揚>状態になった！！")
				MUSHI_STATUS:ARG += "<高揚>"
				CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, MUSHI_HP:NUM)
				CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)
			ENDIF
		ENDIF
	;[逆襲]の効果
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 2 && STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "逆襲") && !STRCOUNT(MUSHI_STATUS:LOCAL, "<逆襲>")
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》の『逆襲』発動！")
		MUSHI_MSGCL:RESULT = C_ORANGE
		CALL MUSHI_MSGSET(@"攻撃を受けて<逆襲>状態になった！！")
		MUSHI_STATUS:LOCAL += "<逆襲>"
		CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:(MUSHI_STANDNUM:LOCAL))
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:LOCAL, MUSHI_STANDNUM:LOCAL)
	ENDIF
NEXT
CALL MUSHI_BATTLE_CHECK_DO
;-------------------------------------------------
;벌레배틀：ドロップアウト判定とそれに続くゲームセット判定の関数
;LOCAL:2     ドロップアウトしたプレイヤーの順位
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_DO
#DIM LPCOUNT
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;MENTAL=-1は除外
	IF (!MUSHI_REST:LPCOUNT || !MUSHI_MENTAL:LPCOUNT) && MUSHI_MENTAL:LPCOUNT >= 0
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】敗退！！")
		MUSHI_MSGCL:RESULT = C_RED
		;＠敗退時口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(LPCOUNT), MUSHI_BATTLER:LPCOUNT, LPCOUNT, 0, "敗退時")
		;敗退したらMENTAL=-1にしておく
		MUSHI_MENTAL:LPCOUNT = -1
		MUSHI_REST:LPCOUNT = 0
		;順位の設定,後ろからチェックしていく
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			LOCAL:1 = MUSHI_CHARANUM + 1 - LOCAL
			IF !MUSHI_FINISH:(LOCAL:1)
				MUSHI_FINISH:(LOCAL:1) = LPCOUNT
				LOCAL:2 = LOCAL:1
				BREAK
			ENDIF
		NEXT
	ENDIF
NEXT
;◆◆◆ここでKOおよび敗退メッセージ表示◆◆
CALL MUSHI_BATTLE_MSG
;引き分け時の判定
IF LOCAL:2 == 1
	SETBIT MUSHI_REST:(MUSHI_FINISH:1), 3
	CALL MUSHI_MSGSET(@"なんと、【%CALLNAME:(MUSHI_BATTLER:(MUSHI_FINISH:1))%】《%MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:1))%》は最後の力を振り絞って立ち上がった！！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_BATTLE_MSG
	LOCAL:2 = 2
ENDIF
;チーム戦
IF MUSHI_TEAM
	;奇数か偶数どっちかが全滅したらゲームセット
	IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7) || (!MUSHI_REST:2 && !MUSHI_REST:4 && !MUSHI_REST:6 && !MUSHI_REST:8)
		VARSET MUSHI_FINISH
		;めんどいので手捏ね
		IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7)
			MUSHI_FINISH:1 = 2
			MUSHI_FINISH:2 = 4
			MUSHI_FINISH:3 = 6
			MUSHI_FINISH:4 = 8
		ELSE
			MUSHI_FINISH:1 = 1
			MUSHI_FINISH:2 = 3
			MUSHI_FINISH:3 = 5
			MUSHI_FINISH:4 = 7
		ENDIF
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (24 - STRLENS(CALLNAME:(MUSHI_BATTLER:LOCAL)))%☆☆☆勝者　チーム【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆ゲームセットメッセージ表示◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
;個人戦
ELSE
	;2位が決まったらゲームセット
	IF LOCAL:2 == 2
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (30 - STRLENS(CALLNAME:(MUSHI_BATTLER:LOCAL)))%☆☆☆勝者　【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:1 = LOCAL
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆ゲームセットメッセージ表示◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF

;-------------------------------------------------
;벌레배틀：ターン終了時処理
;-------------------------------------------------
@MUSHI_BATTLE_ENDSTEP
#DIM LPCOUNT
#DIM ダメージ
;[流砂]の効果(全員分）
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "流砂") && NAME_FIELD_TYPE(MUSHI_FIELD:99) == "[砂地]"
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;やられてる
			SIF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
				CONTINUE
			;自身
			SIF LOCAL == LPCOUNT
				CONTINUE
			;仲間
			SIF MUSHI_TEAM && (LOCAL % 2) == (LPCOUNT % 2)
				CONTINUE
			;一度でもダメージを与えるなら表示する
			IF !LOCAL:1
				CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》の『流砂』発動！")
				MUSHI_MSGCL:RESULT = C_CREAM
				CALL MUSHI_MSGSET(@"全ての敵を流砂に引きずり込む！")
				LOCAL:1 = 1
			ENDIF
			ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 4
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, ダメージ)
			CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LOCAL)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》に {ダメージ} のダメージ！")
			MUSHI_MSGCL:RESULT = C_ORANGE
		NEXT
		IF LOCAL:1
			CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
			SIF MUSHI_FINISH
				RETURN
		ENDIF
	ENDIF
NEXT

;毒ダメージ処理,猛毒優先
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<猛毒>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 2
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, ダメージ)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》は猛毒で {ダメージ} のダメージ！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ELSEIF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<독>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, ダメージ)
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》は毒で {ダメージ} のダメージ！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ENDIF
NEXT
;ダメージが発生したらKO判定
IF LOCAL:1
	CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
	SIF MUSHI_FINISH
		RETURN
ENDIF

;眠りが起きる処理
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;確率1/2
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<眠り>") && !RAND:2 && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》は眠りから覚めた！")
		MUSHI_MSGCL:RESULT = C_YELLOW
		MUSHI_STATUS:LPCOUNT'= REPLACE(MUSHI_STATUS:LPCOUNT, "<眠り>", "")
		LOCAL:1 = 1
	ENDIF
NEXT
;起床が発生したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;再生処理
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "再生") && INRANGE(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT), 1, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) - 1)
		LOCAL = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) = MIN(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) + LOCAL, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT))
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》の『再生』発動！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:LPCOUNT)%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》は {LOCAL} 回復した！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		LOCAL:1 = 1
	ENDIF
NEXT
;再生が発動したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;入れ替え
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;敗退済み
	SIF !MUSHI_REST:LPCOUNT
		CONTINUE
	IF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
		;あなたは手動
		IF MUSHI_BATTLER:LPCOUNT == MASTER
			CLEARLINE MUSHI_LINE
			CALL MUSHI_BATTLE_CHANGE(LPCOUNT)
		ELSE
			;キャラは上から順に出していく
			FOR LOCAL, 1, MUSHI_MAXSELECT + 1
				IF GETBIT(MUSHI_REST:LPCOUNT, LOCAL)
					CALL MUSHI_BATTLE_ETB(LPCOUNT, LPCOUNT * 10 + LOCAL)
					BREAK
				ENDIF
			NEXT
		ENDIF
		LOCAL:1 = 1
	ENDIF
NEXT
;入れ替えが発生したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;-------------------------------------------------
;벌레배틀：MASTERの手動入れ替え関数
;戦場表示とメッセージ表示の間にINPUTが入るので事前に戦場表示を消しておき、
;メッセージ表示(ETB）の前に戦場表示をする
;-------------------------------------------------
@MUSHI_BATTLE_CHANGE(ARG)
#DIM 選んだムシ
PRINTFORML ◆次に出すムシを選べ！
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF !GETBIT(MUSHI_REST:ARG, LOCAL)
		SETCOLOR C_GRAY
	PRINTFORM [{LOCAL, 2}] 
	CALL SHOW_MUSHI_DATA(MUSHI_SELECT:(ARG * 10 + LOCAL), "一行")
	PRINTL 
	RESETCOLOR
NEXT
INPUT
IF INRANGE(RESULT, 1, MUSHI_MAXSELECT) && GETBIT(MUSHI_REST:ARG, RESULT)
	選んだムシ = RESULT
	CALL MUSHI_BATTLE_FIELD
	CALL MUSHI_BATTLE_ETB(ARG, ARG * 10 + 選んだムシ)
	MUSHI_LINE += 2 + MUSHI_MAXSELECT
	RETURN
ENDIF
CLEARLINE 2 + MUSHI_MAXSELECT
RESTART

;-------------------------------------------------
;벌레배틀：戦場に出た時のスキル処理・能力値計算関数
;ARG  プレイヤー番号
;NUM  フラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_ETB(ARG, NUM)
#DIM NUM
;STANDを設定
MUSHI_STAND:ARG = MUSHI_SELECT:NUM
MUSHI_STANDNUM:ARG = NUM
MUSHI_STATUS:ARG = 
MUSHI_NAME = %MUSHI_NAME:NUM%
CALL MUSHI_MSGSET(@"【%CALLNAME:(MUSHI_BATTLER:ARG)%】は《%MUSHI_NAME%》を戦場に出した！")
;＠ETB口上
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(ARG), MUSHI_BATTLER:ARG, ARG, 0, "戦場に出した")

;フィールド判定
IF STRCOUNT(MUSHI_SKL:NUM, "浄化")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[浄化]発動！")
	MUSHI_MSGCL:RESULT = C_AQUA
	CALL MUSHI_BATTLE_フィールド変化(7)
ELSEIF MUSHI_FIELD:NUM & MUSHI_FIELD:99
	MUSHI_STATUS:ARG += "<得意地形>"
ENDIF
;得意季節
IF STRCOUNT(MUSHI_SKL:NUM, "春の虫") && GET_MONTH() == "봄의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "夏の虫") && GET_MONTH() == "여름의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "秋の虫") && GET_MONTH() == "가을의 달"
	MUSHI_STATUS:ARG += "<得意季節>"
ENDIF
;ETBスキル
IF 残りムシ数(ARG) == 1
	IF STRCOUNT(MUSHI_SKL:NUM, "大将")
		MUSHI_STATUS:ARG += "<大将>"
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[大将]発動！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》は最後の一匹としてやる気に溢れている！")
		MUSHI_MSGCL:RESULT = C_YELLOW
	ELSE
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》は最後の一匹だ！")
		MUSHI_MSGCL:RESULT = C_ORANGE
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "陽炎")
	MUSHI_STATUS:ARG += "<陽炎>"
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[陽炎]発動！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》は陽炎に包まれている！")
	MUSHI_MSGCL:RESULT = C_CREAM
ENDIF
;能力値再設定,戦闘HPは満タン
CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, 0, MUSHI_STATUS:ARG)
CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)

;-------------------------------------------------
;벌레배틀：フィールドタイプが変わった時の処理関数
;得意地形が変わってステータスも変わるので再設定する
;-------------------------------------------------
@MUSHI_BATTLE_フィールド変化(ARG)
#DIM NUM
MUSHI_FIELD:99 = 0
SETBIT MUSHI_FIELD:99, ARG
CALL MUSHI_MSGSET(@"フィールドが＜%NAME_FIELD_TYPE(MUSHI_FIELD:99)%＞変化した！")
MUSHI_MSGCL:RESULT = C_L_GREEN
;得意地形の再設定とそれによる能力値の再設定
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	NUM = MUSHI_STANDNUM:LOCAL
	IF MUSHI_FIELD:NUM & MUSHI_FIELD:99
		MUSHI_STATUS:LOCAL += "<得意地形>"
	ELSEIF STRCOUNT(MUSHI_STATUS:LOCAL, "<得意地形>")
		MUSHI_STATUS:LOCAL '= REPLACE(MUSHI_STATUS:LOCAL, "<得意地形>", "")
	ENDIF
	CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:NUM, NUM)
NEXT
;-------------------------------------------------
;벌레배틀：プレイヤー番号ARGの残りムシ数を返す関数
;-------------------------------------------------
@残りムシ数(ARG)
#FUNCTION
LOCAL:1 = 0
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF GETBIT(MUSHI_REST:ARG, LOCAL)
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1
