;-------------------------------------------------
;ラジオをいじる
;日常系コマンド、レベル1
;-------------------------------------------------
@COM496
#DIMS 大雨情報
#DIMS 大雪情報
#DIMS 暴風情報
#DIMS 雷情報
#DIMS 低温情報
#DIMS 雪崩情報
#DIMS 低気圧位置
#DIMS 線状降水帯位置
#DIMS 積乱雲位置
#DIMS 台風勢力
#DIMS 早期注意情報

低気圧位置 = %GET_LOCATION_LOW()%
線状降水帯位置 = %GET_LOCATION_LINER_RAINBAND()%
積乱雲位置 = %GET_LOCATION_CUMULUS()%

大雨情報 = %GET_WEATHER_WARNING_HEAVY_RAIN()%
大雪情報 = %GET_WEATHER_WARNING_HEAVY_SNOWFALL()%
暴風情報 = %GET_WEATHER_WARNING_STORM()%
雷情報 = %GET_WEATHER_WARNING_LIGHTNING()%
低温情報 = %GET_WEATHER_WARNING_COLD()%
雪崩情報 = %GET_WEATHER_WARNING_AVALANCHE()%
早期注意情報 = %GET_WEATHER_ADVISORY_probability_subsequent_WARNING()%

SELECTCASE IS_TYPHOON:1
	CASE 5
		台風勢力 = 猛烈な
	CASE 4
		台風勢力 = 非常に強い
	CASE 3
		台風勢力 = 強い
	CASEELSE
		台風勢力 = 
ENDSELECT

[IF_DEBUG]
SELECTCASE TIME:0
	CASE 345 TO 359, 375 TO 389, 405 TO 419, 435 TO 449, 465 TO 479, 525 TO 539, 585 TO 599, 705 TO 719, 735 TO 749, 825 TO 839, 885 TO 899, 945 TO 959, 1005 TO 1019, 1065 TO 1079, 1125 TO 1139, 1185 TO 1199, 1245 TO 1259, 1305 TO 1319, 1365 TO 1379
	;天気予報(15分間)(05:45 06:15 06:45 07:15 07:45 08:45 09:45 11:45 12:15 13:45 14:45 15:45 16:45 17:45 18:45 19:45 20:45 21:45 22:45)
		DEBUGPRINTFORML 現在の放送：天気予報
	CASE 300 TO 329, 360 TO 374, 420 TO 434, 540 TO 554, 600 TO 614, 660 TO 674, 720 TO 734, 780 TO 794, 840 TO 854, 900 TO 914, 1020 TO 1034, 1140 TO 1184, 1200 TO 1214, 1260 TO 1274, 1380 TO 1394, 0 TO 14, 60 TO 74, 120 TO 134, 180 TO 194, 240 TO 254
	;ニュース(原則15分間)(05:00~05:29 06:00 07:00 09:00 10:00 11:00 12:00 13:00 14:00 15:00 17:00 19:00~19:44 20:00 21:00 23:00 24:00 01:00 02:00 03:00 04:00)
		DEBUGPRINTFORML 現在の放送：ニュース
	CASE 960 TO 989
	;気象通報(16:00~16:29)
		DEBUGPRINTFORML 現在の放送：気象通報
	CASEELSE
		DEBUGPRINTFORML 現在の放送：不明
ENDSELECT

DEBUGPRINTFORML %大雨情報%
DEBUGPRINTFORML %大雪情報%
DEBUGPRINTFORML %暴風情報%
DEBUGPRINTFORML %雷情報%
DEBUGPRINTFORML %低温情報%
DEBUGPRINTFORML %雪崩情報%
DEBUGPRINTFORML %早期注意情報%

[ENDIF]

PRINTW ラジオのつまみをいじってみた
PRINTL

PRINTL ┏━━━━━━┓
PRINT ┃電源［
IF 大雨情報 == "大雨特別警報"
	PRINT □□
ELSEIF 大雨情報 == "大雨警報(土砂災害警戒情報)" || 大雪情報 == "大雪特別警報" || 暴風情報 == "暴風雪特別警報" || 暴風情報 == "暴風特別警報"
	IF CONTINUOUS_RAINFALL >= 10
		SETCOLOR C_P_PURPLE
		PRINT ■
		RESETCOLOR
		PRINT □
	ELSE
		SETCOLOR C_P_PURPLE
		PRINT ■■
		RESETCOLOR
	ENDIF
ELSEIF 大雨情報 == "大雨警報" || 大雪情報 == "大雪警報" || 暴風情報 == "暴風雪警報" || 暴風情報 == "暴風警報" || 雷情報 == "雷注意報(竜巻注意情報)" || 雪崩情報 == "なだれ注意報"
	SETCOLOR C_RED
	PRINT ■■
	RESETCOLOR
ELSEIF 大雨情報 == "大雨注意報" || 大雪情報 == "大雪注意報" || 暴風情報 == "風雪注意報" || 暴風情報 == "強風注意報" || 雷情報 == "雷注意報"
	SELECTCASE 早期注意情報
		CASE "警報級の可能性［高］"
			SETCOLOR C_YELLOW
			PRINT ■■
			RESETCOLOR
		CASE "警報級の可能性［中］"
			SETCOLOR C_YELLOW
			PRINT ■
			RESETCOLOR
			PRINT ■
		CASEELSE
			SETCOLOR C_YELLOW
			PRINT ■
			SETCOLOR C_GREEN
			PRINT ■
			RESETCOLOR
	ENDSELECT
ELSE
	SELECTCASE 早期注意情報
		CASE "警報級の可能性［高］"
			PRINT ■■
		CASE "警報級の可能性［中］"
			SETCOLOR C_GREEN
			PRINT ■
			RESETCOLOR
			PRINT ■
		CASEELSE
			SETCOLOR C_GREEN
			PRINT ■■
			RESETCOLOR
	ENDSELECT
ENDIF
PRINTL ］┃
PRINTL ┗━━━━━━┛
;(天候パッチ)雷発生時はノイズが混じる
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1
	PRINT ……
	FONTBOLD
	PRINT ガリガリッ！
	FONTREGULAR
	PRINT ………
	FONTBOLD
	PRINT ジジジ
	FONTREGULAR
	PRINTL ……
ELSEIF GET_FRONTAL_THUNDERSTORM() == 1 || GET_HEAT_THUNDERSTORM() == 1
	PRINT ……
	FONTBOLD
	PRINT ガリッ
	FONTREGULAR
	PRINTL ………　……
ELSE
	PRINTL ……　………　……
ENDIF

SELECTCASE TIME:0
	CASE 345 TO 359, 375 TO 389, 405 TO 419, 435 TO 449, 465 TO 479, 525 TO 539, 585 TO 599, 705 TO 719, 735 TO 749, 825 TO 839, 885 TO 899, 945 TO 959, 1005 TO 1019, 1065 TO 1079, 1125 TO 1139, 1185 TO 1199, 1245 TO 1259, 1305 TO 1319, 1365 TO 1379
	;天気予報(15分間)(05:45 06:15 06:45 07:15 07:45 08:45 09:45 11:45 12:15 13:45 14:45 15:45 16:45 17:45 18:45 19:45 20:45 21:45 22:45)
		SELECTCASE 低気圧位置
			CASE "台風遠方"
				PRINTFORML …の南海上にある%台風勢力%台風…号は……
			CASE "台風勢力圏"
				PRINTFORML …沖にある%台風勢力%台風……中心気圧は…hPa…
			CASE "台風接近強風域", "台風接近暴風域"
				PRINTFORM …%台風勢力%台風…号は
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%に…数十年に一度の強さで…に接近…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%に…勢力を保ったまま…に接近…
				ELSE
					PRINTFORML ……{TIME_CLOSEST_TYPHOON:1}日%GET_NAME_WEATHER_TIMEZOME(TIME_CLOSEST_TYPHOON:2)%に…に接近…
				ENDIF
			CASE "台風最接近"
				PRINTFORM …%台風勢力%台風…号は
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……数十年に一度の強さで…最接近し…厳重な警戒を…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……最接近し…厳重な警戒を…
				ELSE
					PRINTFORML ……最接近し…十分な警戒を…
				ENDIF
			CASE "台風通過暴風域", "台風通過強風域"
				PRINTFORM …%台風勢力%台風…号は
				IF 暴風情報 == "暴風特別警報"
					PRINTFORML ……記録的な強さのまま…通過し…厳重な警戒を…
				ELSEIF IS_TYPHOON:1 >= 3
					PRINTFORML ……勢力を保ったまま…通過し…
				ELSE
					PRINTFORML ……を通過し…
				ENDIF
			CASE "寒冷渦遠方", "寒冷渦接近"
				SELECTCASE DAY:2
					CASE 1, 3, 4
						PRINTFORML …寒冷渦が日本海側に進み……
					CASE 2
						PRINTFORML …上空に強い寒気が流れ込み……
				ENDSELECT
			CASE "二つ玉低気圧遠方", "日本海低気圧遠方"
				PRINTFORML …日本海側を…低気圧が……
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML …日本海側を急速に発達した低気圧が……
				PRINTFORML …山では天気の急変に……
			CASE "日本海低気圧接近", "日本海低気圧通過中"
				PRINTFORML …日本海側を発達した低気圧が……
			CASE "南岸低気圧遠方"
				PRINTFORML …東シナ海を…低気圧が……
			CASE "南岸低気圧接近"
				PRINTFORML …太平洋側を発達した低気圧が……
			CASE "温暖前線遠方"
				IF TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:0 == DAY:2 && TIME_ENTER_TERRITORY_EXTRATROPICAL_LOW:1 <= DAY:3 + 1
					PRINTFORML …明日以降に前線が……
				ELSE
					PRINTFORML …週の中ごろに低気圧が……
				ENDIF
			CASE "温暖前線接近"
				PRINTFORML …温暖前線の接近により西から……
			CASE "暖域"
				PRINTFORML …寒冷前線通過の際には……
			CASE "寒冷前線通過"
				PRINTFORML …ところによりにわか雨が……
			CASEELSE
				SELECTCASE DAY:2
					CASE 1,3
						IF IS_RAINY_SEASON1:0 == 1 || IS_RAINY_SEASON2 == 1
							PRINTFORML …はぐずついた空模様……
						ELSE
							PRINTFORML …は穏やかな空模様……
						ENDIF
					CASE 2
						IF POWER_WARM_AIR == 1
							PRINTFORML …太平洋高気圧の張り出しが弱く……
						ELSE
							PRINTFORML …全国的に夏空が……
						ENDIF
					CASE 4
						PRINTFORML …西高東低…冬型の気圧配置に……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……これまでに経験したことのないような大雨が……
					CASE 1
						PRINTFORML …に数十年に一度の降水量が……
					CASE 2
						PRINTFORML ……地域に…大雨特別警報を……
					CASE 3
						PRINTFORML ……既に安全な避難が……命を守るための行動を……
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …過去数年間で最も土砂災害の危険性が……
					CASE 1
						PRINTFORML …象台は…地域に土砂災害の危険性が……
				ENDSELECT
			CASE "大雨警報"
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML …などの移動が困難な方は……
					CASE 1
						PRINTFORML …明るいうちに…避難を……
					CASE 2
						PRINTFORML …象台は…地域に大雨・洪水警報を……
				ENDSELECT
			CASE "大雨注意報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …お住いの地域のハザードマップを……
					CASE 1
						PRINTFORML …象台は…地域に大雨注意報を……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雪情報
			CASE "大雪特別警報"
				SELECTCASE RAND:3
					CASE 0
						PRINTFORML …豪雪以来の大雪となって……
					CASE 1
						PRINTFORML …数十年に一度の大雪が……
					CASE 2
						PRINTFORML …地域に大雪特別警報を……
				ENDSELECT
			CASE "大雪警報"
				IF CONTINUOUS_SNOWFALL >= 20
					PRINTFORML ……象台は…地域に顕著な大雪に関する……
				ELSE
					PRINTFORML ……地域には大雪警報が……
				ENDIF
			CASE "大雪注意報"
				PRINTFORML ……地域には大雪注意報が……
		ENDSELECT
		
		SELECTCASE 暴風情報
			CASE "暴風雪特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地域に…暴風雪特別警報を……
					CASE 1
						PRINTFORML …数十年に一度の猛吹雪が……
					CASE 2
						PRINTFORML …これまでにない猛吹雪が…不要不急の外出は……
					CASE 3
						PRINTFORML …猛烈な吹雪が…いつ発生してもおかしく……
				ENDSELECT
			CASE "暴風特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地域に…暴風特別警報を……
					CASE 1
						PRINTFORML …これまでに経験のない猛烈な風が……
					CASE 2
						PRINTFORML …湾台風以来の…記録的な……
					CASE 3
						PRINTFORML …猛烈な風が…身の安全を……
				ENDSELECT
			CASE "暴風雪警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …地域には暴風雪警報が…外出は控えてくだ……
					CASE 1
						PRINTFORML …数年に一度の猛吹雪が……
				ENDSELECT
			CASE "暴風警報"
				PRINTFORML …地域には暴風警報が…早めの避難を……
			CASE "風雪注意報"
				PRINTFORML ……地域には風雪注意報が……
			CASE "強風注意報"
				PRINTFORML ……地域には強風注意報が……
		ENDSELECT
		
		SELECTCASE 雷情報
			CASE "雷注意報(竜巻注意情報)"
					PRINTFORML …大気の状態が非常に不安定となって……
					PRINTFORML …地域には竜巻…のおそれが高まって……
			CASE "雷注意報"
				IF DAY:2 == 2 && (IS_COLD_LOW || 低気圧位置 == "台風勢力圏")
					PRINTFORML …大気の状態が非常に不安定となって……
				ELSE
					PRINTFORML …大気の状態が不安定に……
				ENDIF
		ENDSELECT
		
		SELECTCASE 雪崩情報
			CASE "なだれ注意報"
				PRINTFORML ……では雪崩が発生するおそれが……
		ENDSELECT
		
		SELECTCASE 低温情報
			CASE "低温注意報"
				PRINTFORML …地域は平年と比べて気温が低く……
		ENDSELECT
		
	CASE 300 TO 329, 360 TO 374, 420 TO 434, 540 TO 554, 600 TO 614, 660 TO 674, 720 TO 734, 780 TO 794, 840 TO 854, 900 TO 914, 1020 TO 1034, 1140 TO 1184, 1200 TO 1214, 1260 TO 1274, 1380 TO 1394, 0 TO 14, 60 TO 74, 120 TO 134, 180 TO 194, 240 TO 254
	;ニュース(原則15分間)(05:00~05:29 06:00 07:00 09:00 10:00 11:00 12:00 13:00 14:00 15:00 17:00 19:00~19:44 20:00 21:00 23:00 24:00 01:00 02:00 03:00 04:00)
		SELECTCASE 低気圧位置
			CASE "台風遠方"
				PRINTFORML …の南海上にある%台風勢力%台風…号は……
			CASE "台風勢力圏"
				PRINTFORML …沖にある%台風勢力%台風……中心気圧は…hPa…
			CASE "台風接近強風域", "台風接近暴風域", "台風最接近", "台風通過暴風域", "台風通過強風域"
				PRINTFORM …%台風勢力%台風…号は
				IF IS_TYPHOON:1 >= 3
					PRINTFORML ……勢力を保ったまま…に接近…
				ELSE
					PRINTFORML ……日には…に接近…
				ENDIF
			CASE "寒冷渦遠方", "寒冷渦接近"
				SELECTCASE DAY:2
					CASE 1, 3, 4
						PRINTFORML …寒冷渦が日本海側に進み……
					CASE 2
						PRINTFORML …上空に強い寒気が流れ込み……
				ENDSELECT
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML …山での遭難が相次いで……
		ENDSELECT
		
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……これまでに経験したことのないような大雨が……
					CASE 1
						PRINTFORML …に数十年に一度の降水量が……
					CASE 2
						PRINTFORML ……地域に…大雨特別警報を……
					CASE 3
						PRINTFORML ……の警戒レベルが5に……
					CASE 4
						PRINTFORML ……既に安全な避難が……命を守るための行動を……
				ENDSELECT
				
				SELECTCASE CONTINUOUS_RAINFALL
					CASE IS >= 40
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …政府は…激甚災害に指定……
							CASE 1
								PRINTFORML …各地で…寸断され……が救助……
						ENDSELECT
					CASE IS >= 20
						SELECTCASE RAND:3
							CASE 0
								PRINTFORML …土砂崩れにより…世帯…が孤立……
							CASE 1
								PRINTFORML …既に記録的な大雨になっている地域も……
							CASE 2
								PRINTFORML …川で氾濫危険水位を超え……
						ENDSELECT
					CASE IS >= 10
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …市に記録的短時間大雨情報を……
							CASE 1
								PRINTFORML …に数年に一度の大雨が……
						ENDSELECT
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……の警戒レベルが4に……
					CASE 1
						PRINTFORML …過去数年間で最も土砂災害の危険性が……
					CASE 2
						PRINTFORML …象台は…地域に土砂災害の危険性が……
					CASE 3
						PRINTFORML …市は……世帯…に避難指示を……
					CASE 4
						PRINTFORML …線は全線で運転を見合わせ……
				ENDSELECT
				
			CASE "大雨警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML …などの移動が困難な方は……
					CASE 1
						PRINTFORML …明るいうちに…避難を……
					CASE 2
						PRINTFORML …象台は…地域に大雨・洪水警報を……
					CASE 3
						PRINTFORML …市は……世帯…に高齢者等避難を……
					CASE 4
						PRINTFORML ……の警戒レベルが2に……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 大雪情報
			CASE "大雪特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML …豪雪以来の大雪となって……
					CASE 1
						PRINTFORML …数十年に一度の大雪が……
					CASE 2
						PRINTFORML …地域に大雪特別警報を……
					CASE 3
						PRINTFORML …記録的な大雪により各地で…孤立……
				ENDSELECT
			CASE "大雪警報"
				IF CONTINUOUS_SNOWFALL >= 20
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML ……象台は…地域に顕著な大雪に関する……
						CASE 1
							PRINTFORML …大雪により大規模な交通障害が……
					ENDSELECT
				ELSE
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML …象台は…地域に大雪警報を……
						CASE 1
							PRINTFORML …大雪により各地の交通機関に影響が……
					ENDSELECT
				ENDIF
		ENDSELECT
		
		SELECTCASE 暴風情報
			CASE "暴風雪特別警報"
				SELECTCASE RAND:6
					CASE 0
						PRINTFORML ……地域に…暴風雪特別警報を……
					CASE 1
						PRINTFORML …数十年に一度の猛吹雪が……
					CASE 2
						PRINTFORML …これまでにない猛吹雪が…不要不急の外出は……
					CASE 3
						PRINTFORML …猛烈な吹雪が…いつ発生してもおかしく……
					CASE 4
						PRINTFORML …各地で…が孤立…に救助を要請……
					CASE 5
						PRINTFORML …自動車道では…多くの車が立ち往生……
				ENDSELECT
			CASE "暴風特別警報"
				SELECTCASE RAND:4
					CASE 0
						PRINTFORML ……地域に…暴風特別警報を……
					CASE 1
						PRINTFORML …これまでに経験のない猛烈な風が……
					CASE 2
						PRINTFORML …湾台風以来の…記録的な……
					CASE 3
						PRINTFORML …猛烈な風が…身の安全を……
				ENDSELECT
			CASE "暴風雪警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …地域には暴風雪警報が…外出は控えてくだ……
					CASE 1
						PRINTFORML …数年に一度の猛吹雪が……
				ENDSELECT
			CASE "暴風警報"
				SELECTCASE RAND:2
					CASE 0
						PRINTFORML …象台は…地域に暴風警報を……
					CASE 1
						PRINTFORML …風が…からの避難は危険…早めの避難を……
				ENDSELECT
		ENDSELECT
		
		SELECTCASE 雷情報
			CASE "雷注意報(竜巻注意情報)"
				PRINTFORML …大気の状態が非常に不安定となって……
				PRINTFORML …地域には竜巻…のおそれが高まって……
			CASE "雷注意報"
				IF DAY:2 == 2 && (IS_COLD_LOW || 低気圧位置 == "台風勢力圏")
					PRINTFORML …大気の状態が非常に不安定となって……
				ENDIF
		ENDSELECT
		
		IF POSSIBLITY_AVALANCHE >= 1
			PRINTFORML …各地で雪崩による被害が……
		ENDIF
		
		SIF IS_RAINY_SEASON1:1 == 1
			PRINTFORML …象台は…西日本に梅雨明け宣言を……
		
		SELECTCASE IS_COLD_CROP_DAMAGE
			CASE IS >= 3
				PRINTFORML …記録的な冷害により……緊急で……
			CASE IS >= 1
				PRINTFORML …天候不順により作物に影響が……
		ENDSELECT
		
		SELECTCASE RAND:3
			CASE 0
				PRINTFORML …今日の円相場は……
			CASE 1
				PRINTFORML …続きまして各地のニュースを……
			CASE 2
				PRINTFORML …によりますと……
		ENDSELECT
		
	CASE 960 TO 989
	;気象通報(16:00~16:29)
		PRINTFORML …発表の…{DAY:3}日正午の気象通報です……
		SELECTCASE RAND:54
			CASE 0
				PRINTFORML ……引き続き…{DAY:3}日正午の天気をお伝え……
			CASE 1
				PRINTFORML …富士山…気温……
			CASEELSE
				{
				PRINTFORML …%TEXTR("/石垣島/那覇/名瀬/鹿児島/福江/厳原/足摺岬/室戸岬/松山/浜田/西郷/大阪/潮岬/
				八丈島/大島/御前崎/銚子/前橋/小名浜/輪島/相川/仙台/宮古/秋田/函館/浦河/根室/稚内/
				ポロナイスク/セベロクリリスク/ハバロフスク/ルドナヤプリスタ/ウラジオストク/
				ソウル/ウルルン島/プサン/モッポ/チェジュ島/台北/恒春/長春/北京/大連/青島/
				上海/武漢/アモイ/香港/バスコ/マニラ/父島/南鳥島/
				")%…風力……hPa……
				}
		ENDSELECT
		
		PRINTFORML …つづいて漁業気象です
		
		SELECTCASE 低気圧位置
			CASE "台風遠方", "台風勢力圏", "台風接近強風域", "台風接近暴風域", "台風最接近", "台風通過暴風域", "台風通過強風域"
				PRINTFORML ……には…hPaの台風…号があって…kmで進んで……
			CASE "寒冷渦遠方", "寒冷渦接近"
				PRINTFORML ……には…hPaの低気圧があって…ほとんど停滞……
			CASE "二つ玉低気圧遠方", "日本海低気圧遠方"
				PRINTFORML 黄海の…には…hPaの低気圧があって…kmで進んで……
			CASE "日本海低気圧接近", "日本海低気圧通過中"
				PRINTFORML 日本海の…には…hPaの低気圧があって…kmで進んで……
			CASE "二つ玉低気圧接近", "疑似好天", "二つ玉低気圧通過中"
				PRINTFORML 日本海の…には…hPaの低気圧があって…kmで進んで……
				PRINTFORML 本州の…には…hPaの低気圧があって…kmで進んで……
			CASE "南岸低気圧遠方"
				PRINTFORML 東シナ海の…には…hPaの低気圧があって…kmで進んで……
			CASE "南岸低気圧接近"
				PRINTFORML 本州の…には…hPaの低気圧があって…kmで進んで……
			CASE "温暖前線遠方"
				PRINTFORML 長江下流の…には…hPaの低気圧があって…kmで進んで……
			CASE "温暖前線接近"
				PRINTFORML 西日本の…には…hPaの低気圧があって…kmで進んで……
			CASE "暖域"
				PRINTFORML 北陸の…には…hPaの低気圧があって…kmで進んで……
			CASE "寒冷前線通過"
				PRINTFORML 三陸沖の…には…hPaの低気圧があって…kmで進んで……
			CASE "低気圧未発生"
				PRINTFORML …の…には…hPaの高気圧があって…ほとんど停滞……
		ENDSELECT
	CASEELSE
		IF GET_LOCATION_AREA_TYPHOON() == 1 && IS_TYPHOON:1 >= 5
			SELECTCASE RAND:5
				CASE 0
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …猛烈な台風…号が記録的な勢力のまま接近……
					ELSE
						PRINTFORML …猛烈な台風…号が接近……
					ENDIF
				CASE 1
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …台風…号により各地に甚大な被害が……
					ELSE
						PRINTFORML …台風…号により各地に被害が……
					ENDIF
				CASE 2
					PRINTFORML …線は全線で運転を見合わせ……
				CASE 3
					IF 暴風情報 == "暴風特別警報"
						PRINTFORML …湾台風以来の…身の安全を確保……
					ELSE
						PRINTFORML …できるだけ早いうちに避難を……
					ENDIF
				CASE 4
					PRINTFORML …空の便にも大きな影響が……
			ENDSELECT
		ENDIF
		SELECTCASE 大雨情報
			CASE "大雨特別警報"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……これまでに経験したことのないような大雨が……
					CASE 1
						PRINTFORML …に数十年に一度の降水量が……
					CASE 2
						PRINTFORML ……地域に…大雨特別警報を……
					CASE 3
						PRINTFORML ……の警戒レベルが5に……
					CASE 4
						PRINTFORML ……既に安全な避難が……命を守るための行動を……
				ENDSELECT
				
				SELECTCASE CONTINUOUS_RAINFALL
					CASE IS >= 40
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …政府は…激甚災害に指定……
							CASE 1
								PRINTFORML …各地で…寸断され……が救助……
						ENDSELECT
					CASE IS >= 20
						SELECTCASE RAND:3
							CASE 0
								PRINTFORML …土砂崩れにより…世帯…が孤立……
							CASE 1
								PRINTFORML …既に記録的な大雨になっている地域も……
							CASE 2
								PRINTFORML …川で氾濫危険水位を超え……
						ENDSELECT
					CASE IS >= 10
						SELECTCASE RAND:2
							CASE 0
								PRINTFORML …市に記録的短時間大雨情報を……
							CASE 1
								PRINTFORML …に数年に一度の大雨が……
						ENDSELECT
				ENDSELECT
			CASE "大雨警報(土砂災害警戒情報)"
				SELECTCASE RAND:5
					CASE 0
						PRINTFORML ……の警戒レベルが4に……
					CASE 1
						PRINTFORML …過去数年間で最も土砂災害の危険性が……
					CASE 2
						PRINTFORML …象台は…地域に土砂災害の危険性が……
					CASE 3
						PRINTFORML …市は……世帯…に避難指示を……
					CASE 4
						PRINTFORML …線は全線で運転を見合わせ……
				ENDSELECT
			CASEELSE
				IF 暴風情報 == "暴風雪特別警報"
					SELECTCASE RAND:6
						CASE 0
							PRINTFORML ……地域に…暴風雪特別警報を……
						CASE 1
							PRINTFORML …数十年に一度の猛吹雪が……
						CASE 2
							PRINTFORML …これまでにない猛吹雪が…不要不急の外出は……
						CASE 3
							PRINTFORML …猛烈な吹雪が…いつ発生してもおかしく……
						CASE 4
							PRINTFORML …各地で…が孤立…に救助を要請……
						CASE 5
							PRINTFORML …自動車道では…多くの車が立ち往生……
					ENDSELECT
				ELSEIF 大雪情報 == "大雪特別警報"
					SELECTCASE RAND:4
						CASE 0
							PRINTFORML …豪雪以来の大雪となって……
						CASE 1
							PRINTFORML …数十年に一度の大雪が……
						CASE 2
							PRINTFORML …地域に大雪特別警報を……
						CASE 3
							PRINTFORML …記録的な大雪により各地で…孤立……
					ENDSELECT
				ELSEIF 大雪情報 == "大雪警報" && CONTINUOUS_SNOWFALL >= 20
					SELECTCASE RAND:2
						CASE 0
							PRINTFORML ……象台は…地域に顕著な大雪に関する……
						CASE 1
							PRINTFORML …大雪により大規模な交通障害が……
					ENDSELECT
				ELSEIF 雷情報 == "雷注意報(竜巻注意情報)"
					SELECTCASE RAND:3
						CASE 0
							PRINTFORML …地域には竜巻…のおそれが高まって……
						CASE 1
							PRINTFORML …積乱雲が近づいて…安全な場所へ……
						CASE 2
							PRINTFORML …各地で突風や雹による被害が……
					ENDSELECT
				ELSEIF POSSIBLITY_AVALANCHE >= 1
					PRINTFORML …各地で雪崩による被害が……
				ELSE
					PRINTFORML … ……………… ……
				ENDIF
		ENDSELECT
ENDSELECT

TIME += 15

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE496
SIF !TFLAG:100
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
;ウフフ中は不可
SIF CFLAG:우후후
	RETURN 0
;睡眠イタズラ中は不可
SIF CFLAG:장난
	RETURN 0
;添い寝中
SIF CFLAG:동침중
	RETURN 0
;デート中
;SIF CHK_DATENOW(CFLAG:デート中) && CFLAG:現在位置 != OMANEKIBEYA()
;	RETURN 0

RETURN 1
