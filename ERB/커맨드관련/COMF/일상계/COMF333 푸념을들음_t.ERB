;-------------------------------------------------
;푸념을듣는다
;일상계 커맨드, 레벨 2
;-------------------------------------------------
@COM333
VARSET LOCAL

;고정으로 획득하는 소스
SOURCE:환락 = 400
IF RAND:3 == 0
	CALL 노말라이제이션(TARGET, 1)
ENDIF
LOCAL = 50 + ABL:MASTER:화술기능 * 5 + ABL:친밀 * 2

SIF BASE:술기운 > MAXBASE:술기운 / 2
	LOCAL -= 15
CALL SUC_FAIL(0, MIN(ABL:MASTER:화술기능 * 2 + ABL:친밀, 75))
SIF TFLAG:192 == -2
	RETURN 0
TFLAG:193 = TFLAG:193 + 2

SIF BASE:술기운 > MAXBASE:술기운 / 2
	TFLAG:193 += 3

SELECTCASE TFLAG:193
	CASE 1
		SOURCE:반감 = 100
	CASE 2
		SOURCE:환락 += ABL:친밀 * 30
		TALENT:기분 ++
		BASE:분노 /= 2
		CFLAG:언짢음 = 0
	CASE 3
		SOURCE:환락 += ABL:친밀 * 50
		SOURCE:사랑 = 200
		TALENT:기분 ++
		BASE:분노 /= 2
		CFLAG:언짢음 = 0
	CASE 4
		SOURCE:반감 = 1000
		SOURCE:환락 += ABL:친밀 * 30
	CASE 5
		SOURCE:환락 += ABL:친밀 * 50
	CASE 6
		SOURCE:환락 += ABL:친밀 * 75
		SOURCE:사랑 = 300
		TALENT:기분 = 1
		BASE:분노 /= 2
		CFLAG:언짢음 = 0
ENDSELECT

TIME += 20

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE333
;푸념을 (듣)묻는 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(333)
	RETURN RESULT
SIF FLAG:70
	RETURN 0
SIF ABL:친밀 < 3
	RETURN 0
SIF MARK:반발각인
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
SIF TALENT:기분 >= 0
	RETURN 0
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
RETURN 1
