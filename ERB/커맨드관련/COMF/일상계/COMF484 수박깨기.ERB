;スイカ割り
@COM484
#DIM CHARA
#DIM 동실인원
#DIM 참가가능인원

;雨天中止
IF GROUPMATCH(날씨, 4, 5, 6, 7, 8, 10, 11, 12, 13) || GROUPMATCH(FLAG:이상기상, 9, 10)
	IF OUTROOF(CFLAG:MASTER:현재위치)
		PRINTFORMW 날씨가 나쁘니 그만두자…
		RETURN -1
	ENDIF
;料理がある
ELSEIF DISH_NAME != ""
	PRINTFORMW 손에 들고 있는 요리를 어떻게든 해야한다…
	RETURN -1
ENDIF

;참가가능인원チェック
참가가능인원 = 0
동실인원 = GET_TARGETNUM()
FOR LOCAL, 1, 동실인원 + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:행동 == 5
		CONTINUE
	SIF BASE:CHARA:기력 < 500 || BASE:CHARA:체력 < 500
		CONTINUE
	참가가능인원 ++
NEXT
;TARGETは実行判定で可能なはずなのでエラーチェック
SIF 참가가능인원 == 0
	THROW
SUIKA_PLAYERNUM = 0
CALL SUIKA_CHARASELECT(동실인원, 참가가능인원)
;キャンセル
SIF RESULT == -1
	RETURN -1

;武器選択
CALL SUIKA_WEAPONSELECT

;開幕地の文
DRAWLINE
IF GROUPMATCH(SUIKA_WEAPON, "요술망치", "제어봉")
	PRINTFORML %마스터는% 안대를 착용하고 일사분란하게 제자리에서 회전하기 시작했다…
ELSE
	PRINTFORML %마스터는% 안대를 착용하고 바닥에 꽂아놓은 %조사처리(SUIKA_WEAPON,"를")% 중심으로 일사분란하게 회전하기 시작했다…
ENDIF
;セットアップ
CALL SUIKA_SETUP
WAIT
;TARGETが参加していない場合はTARGETを変更しておく
IF FLAG:데이트상대
	;参加しなくても最優先,そんなひどいことする？
	TARGET = FLAG:데이트상대
ELSEIF !MATCH(SUIKA_PLAYER, TARGET)
	;一人目をターゲットに変更
	TARGET = SUIKA_PLAYER:1
ENDIF

TARGET = SUIKA_PLAYER:1
;ゲーム開始
CALL SUIKA_SMASHING
TFLAG:193 = RESULT

;ソース獲得
DOWNBASE:MASTER:체력 = 200
DOWNBASE:MASTER:기력 = 300
FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
	CHARA = SUIKA_PLAYER:LOCAL
	DOWNBASE:CHARA:기력 += 100
	DOWNBASE:CHARA:체력 += 50
	SELECTCASE TFLAG:193
	CASE 0
		SOURCE:CHARA:환락 = 1500 + ABL:CHARA:친밀 * 100
		SOURCE:CHARA:수동 = 100 + 50 * ABL:CHARA:순종
		SOURCE:CHARA:정복 = 200 + 80 * ABL:CHARA:새드끼
	CASE 1, 2
		SOURCE:CHARA:환락 = 2500 + ABL:CHARA:친밀 * 100
		SOURCE:CHARA:수동 = 200 + 60 * ABL:CHARA:순종
		SOURCE:CHARA:정복 = 400 + 120 * ABL:CHARA:새드끼
	CASE -1
		IF SUIKA_LIAR:LOCAL || CHARA == [[세이자]]
			SOURCE:CHARA:환락 = 1500 + ABL:CHARA:친밀 * 100
			SOURCE:CHARA:정복 = 400 + 120 * ABL:CHARA:새드끼
		ELSE
			SOURCE:CHARA:환락 = 800 + ABL:CHARA:친밀 * 100
			SOURCE:CHARA:수동 = 100 + 50 * ABL:CHARA:순종
			SOURCE:CHARA:정복 = 50 + 20 * ABL:CHARA:새드끼
		ENDIF
	ENDSELECT
	IF TFLAG:193 >= 0
		SIF CHARA == TARGET
			TFLAG:신뢰도관리 = 1 + TFLAG:193
		IF TFLAG:193 == 2
			TIMES SOURCE:CHARA:환락, 1.20
			TIMES SOURCE:CHARA:수동, 1.20
			TIMES SOURCE:CHARA:정복, 1.20
		ENDIF
	ENDIF
	;口上用に役職情報を渡しておく
	SIF CHARA == TARGET && (SUIKA_LIAR:LOCAL || CHARA == [[세이자]])
		TFLAG:194 = 1
	;◆口上
	CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, SUIKA_LIAR:LOCAL, 0, "結果")
	IF RESULTS != ""
		PRINTL 
		PRINTFORML %CALLNAME:CHARA%
		PRINTFORMW 「%RESULTS%」
		RESETCOLOR
	ENDIF
	;なるべく動かない
	TCVAR:CHARA:가능한한움직이지않는다 = 1
NEXT

;料理生成
SIF TFLAG:193 >= 0
	CALL 요리생성("자른수박")
	
;結果記録
IF TFLAG:193 > 0
	SUIKA_GREAT ++
ELSEIF TFLAG:193 == 0
	SUIKA_SUCCESS ++
ENDIF

;時間経過
TIME += 60

;フラグをリセットしてあなたのフラグを立てる
VARSET SUIKA_PLAYER
CVARSET TCVAR, GETNUM(TCVAR, "수박깨기참가"), 0
TCVAR:MASTER:수박깨기참가 = 1

RETURN 1

;-------------------------------------------------
;実行可能判定
;-------------------------------------------------
;スイカ割り
@COM_ABLE484
;時間停止中
SIF FLAG:시간정지
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(484)
	RETURN RESULT
;スイカがない
SIF !ITEM:수박
	RETURN 0
;アイマスクがない
SIF !ITEM:눈가리개
	RETURN 0
;夏じゃない
SIF GET_MONTH() != "여름의 달"
	RETURN 0
;とりあえず訓練できる場所
SIF !전투훈련가능(CFLAG:MASTER:현재위치)
	RETURN 0
;一日一回まで
SIF TCVAR:MASTER:수박깨기참가
	RETURN 0
;対象がいない
SIF TARGET <= 0
	RETURN 0
;意識がない
SIF !SHIRAHU(TARGET)
	RETURN 0
;添い寝中
SIF CFLAG:MASTER:동침중
	RETURN 0
;うふふ中
SIF CFLAG:MASTER:우후후
	RETURN 0
;仕事中
SIF CFLAG:행동 == 5
	RETURN 0
;体力・気力不足
SIF BASE:MASTER:기력 < 500 || BASE:MASTER:체력 < 500
	RETURN 0
;体力・気力不足
SIF BASE:TARGET:기력 < 500 || BASE:TARGET:체력 < 500
	RETURN 0
RETURN 1

;-------------------------------------------------
;スイカ割り：キャラクターセレクト関数
;ARG 동실인원
;-------------------------------------------------
@SUIKA_CHARASELECT(ARG, 참가가능인원)
#DIM CHARA
#DIM 참가가능인원
#DIM LINE
#DIM 参加要求
VARSET LOCAL
LINE = 7
DRAWLINE
;全員参加指示
SIF 参加要求
	SUIKA_PLAYERNUM = MIN(참가가능인원, SUIKA_MAXPLAYER)
PRINTFORML ●참가자를 골라주세요（%TOFULL(TOSTR(SUIKA_PLAYERNUM))%／%TOFULL(TOSTR(SUIKA_MAXPLAYER))%）거짓말쟁이의 인원수：{SUIKA_LIARNUM()}
FOR LOCAL, 1, ARG + 1
	CHARA = TARGET:LOCAL
	SIF !SHIRAHU(CHARA)
		CONTINUE
	SIF CFLAG:CHARA:행동 == 5
		CONTINUE
	SIF BASE:CHARA:기력 < 500 || BASE:CHARA:체력 < 500
		CONTINUE
	;全員参加指示
	IF 参加要求
		TCVAR:CHARA:수박깨기참가 = LOCAL:1 + 1
		SUIKA_PLAYER:(LOCAL:1 + 1) = CHARA
	ENDIF
	SIF TCVAR:CHARA:수박깨기참가
		SETCOLOR C_AQUA
	PRINTFORM [{CHARA, 3}] %CALLNAME:CHARA, 18, LEFT%
	LOCAL:1 ++
	IF LOCAL:1 % 3 == 0
		PRINTL 
		LINE ++
	ENDIF
	RESETCOLOR
	SIF 参加要求 && LOCAL:1 == SUIKA_MAXPLAYER
		参加要求 = 0
NEXT
参加要求 = 0
IF LOCAL:1 % 3
	PRINTL 
	LINE ++
ENDIF
IF 참가가능인원 > SUIKA_MAXPLAYER
	PRINTL [902] 상위 8명 참가
ELSE
	PRINTL [901] 전원참가
ENDIF
DRAWLINE
PRINTL [900] 결정　　　　　　　[903] 규칙설명
PRINTL [999] 역시 그만둔다
INPUT

;やめる
IF RESULT == 999
	;参加フラグをリセットする
	CVARSET TCVAR, GETNUM(TCVAR, "수박깨기참가"), 0
	VARSET SUIKA_PLAYER
	RETURN -1
;決定
ELSEIF RESULT == 900
	IF INRANGE(SUIKA_PLAYERNUM, 1, SUIKA_MAXPLAYER)
		RETURN 1
	ELSE
		PRINTFORMW 참가자가 1~%TOFULL(TOSTR(SUIKA_MAXPLAYER))%명이 되도록 해주세요!
		LINE ++
	ENDIF
;全員参加,上から８人を参加
ELSEIF RESULT == 901 || RESULT == 902
	;一旦リセット
	CVARSET TCVAR, GETNUM(TCVAR, "수박깨기참가"), 0
	SUIKA_PLAYERNUM = 0
	VARSET SUIKA_PLAYER
	参加要求 = 1
;ルール説明
ELSEIF RESULT == 903
	CALL SUIKA_TUTORIAL
	RESTART
;キャラ選択
ELSEIF INRANGE(RESULT, 1, CHARANUM) && MATCH(TARGET, RESULT)
	;参加エントリー
	IF !TCVAR:RESULT:수박깨기참가
		;上限
		IF SUIKA_PLAYERNUM >= SUIKA_MAXPLAYER
			PRINTW 더 이상 참가할 수 없습니다!
			LINE ++
		ELSE
			FOR LOCAL, 1, SUIKA_MAXPLAYER + 1
				;空きに入れる
				IF !SUIKA_PLAYER:LOCAL
					SUIKA_PLAYER:LOCAL = RESULT
					TCVAR:RESULT:수박깨기참가 = LOCAL
					BREAK
				ENDIF
			NEXT
			SUIKA_PLAYERNUM ++
		ENDIF
	;参加キャンセル
	ELSE
		FOR LOCAL, TCVAR:RESULT:수박깨기참가, SUIKA_MAXPLAYER + 1
			;参加者はキャンセルする
			IF SUIKA_PLAYER:LOCAL == RESULT
				SUIKA_PLAYER:LOCAL = 0
			;番号が後の人がいるなら登録番号を一つ下げる
			ELSEIF SUIKA_PLAYER:LOCAL
				TCVAR:(SUIKA_PLAYER:LOCAL):수박깨기참가 --
			ENDIF
		NEXT
		;その後シフトする
		FOR LOCAL, TCVAR:RESULT:수박깨기참가, SUIKA_MAXPLAYER + 1
			SIF !SUIKA_PLAYER:LOCAL
				SWAP SUIKA_PLAYER:LOCAL, SUIKA_PLAYER:(LOCAL + 1)
		NEXT
		;参加状態を戻す
		TCVAR:RESULT:수박깨기참가 = 0
		SUIKA_PLAYERNUM --
	ENDIF
ENDIF
CLEARLINE LINE
RESTART

;-------------------------------------------------
;スイカ割り：チュートリアル
;-------------------------------------------------
@SUIKA_TUTORIAL
#DIM MAXPAGE
#DIM PAGE
MAXPAGE = 4
SIF !PAGE
	PAGE = 1
DRAWLINE
PRINTFORML ●룰 설명({PAGE}/{MAXPAGE})
SELECTCASE PAGE
CASE 1
	PRINTFORML 당신은 눈을 가린 상태에서 무기를 들고 환상소녀들의 목소리에 의지해
	PRINTFORML 필드 어딘가에 있는 수박을 두드려 깨는 게임입니다.
	PRINTL 
	PRINTFORML 다만 참가자 중에는 (인원에 따른) 거짓말쟁이가 섞여 있습니다.
	PRINTFORML 거짓말쟁이가 된 캐릭터는 당신에게 거짓 힌트를 줍니다.
CASE 2
	PRINTFORML 공격기회는 기본적으로 1회 뿐이므로 누가 거짓말쟁이인지,
	PRINTFORML 어떤 힌트가 진실인지 살펴보면서 신중하게 이동합시다.
	PRINTL 
	PRINTFORML 필드 밖으로 이동하려고 하면 참가한 소녀에게 부딪히게 됩니다.
	PRINTFORML 이 경우 이동할 수 없습니다.
CASE 3
	PRINTFORML 수박의 위치를 완전히 파악하고 공격하면 대성공.
	PRINTFORML 아깝게 빗나가더라도 일단 성공입니다.
	PRINTL 
	PRINTFORML 아깝게 빗나간 상태에서 캐릭터에 따라서는 "거기다!" 등의 지시를 합니다.
	PRINTFORML 대성공을 노린다면 더 심문하는 것이 좋을 것입니다.
CASE 4
	PRINTFORML 인원이 (3명 이상이고) 홀수일 때는 거짓말쟁이의 비율이 많아져 난이도가 높아집니다.
	PRINTFORML 어렵다고 느껴진다면 짝수로 플레이하는 것이 좋습니다.
	PRINTL 
	PRINTFORML 멋지게 수박을 쪼개면 <잘린수박>이 완성됩니다.
	PRINTFORML 꼭 참가자 모두 함께 먹읍시다.
ENDSELECT
PRINTL 
PRINTFORML \@ PAGE < MAXPAGE ? [1] 다음 # \@
PRINTFORML \@ PAGE > 1 ? [2] 이전 # \@
PRINTFORML [0] 돌아간다
INPUT
IF RESULT == 1 && PAGE < MAXPAGE
	PAGE ++
ELSEIF RESULT == 2 && PAGE > 1
	PAGE --
ELSE
	PAGE = 0
	RETURN
ENDIF
CLEARLINE 12
RESTART

;-------------------------------------------------
;スイカ割り：武器選択関数
;-------------------------------------------------
@SUIKA_WEAPONSELECT
DRAWLINE
PRINTFORML ●사용할 무기를 고르세요
PRINTFORML [1] - 마이빗자루       	%마스터가% 항상 가지고 다니는 빗자루. 추가로 1회의 공격기회가 주어진다.
PRINTFORML [2] - 불제봉         	약한 유도력을 지닌 신기한 제구. 시작할 때 반드시 수박이 있는 곳을 향한다
PRINTFORML [3] - 목도         	절단력이 있는 심플한 무기. 깨끗하게 자를 수 있어 획득 수량이 증가한다. ※상급자용
PRINTFORML [4] - 요술망치      	소원을 담은 망치. 닿기만 해도 깨끗하게 잘린다.
PRINTFORML [5] - 제어봉        	핵에너지를 흡수하는 묵직한 막대기. 시작할 때 수박을 끌어당긴다.
INPUT
IF INRANGE(RESULT, 1, 5)
	SELECTCASE RESULT
	CASE 1
		SUIKA_WEAPON = 마이빗자루
	CASE 2
		SUIKA_WEAPON = 불제봉
	CASE 3
		SUIKA_WEAPON = 목도
	CASE 4
		SUIKA_WEAPON = 요술망치
	CASE 5
		SUIKA_WEAPON = 제어봉
	ENDSELECT
ELSE
	CLEARLINE 8
	RESTART
ENDIF

;-------------------------------------------------
;スイカ割り：ウソつきの人数
;-------------------------------------------------
@SUIKA_LIARNUM
#FUNCTION
RETURNF MAX((SUIKA_PLAYERNUM - 1) / 2, 0)

;-------------------------------------------------
;スイカ割り：二点間の距離
;ARG:1,座標１ ARG:2,座標２ ARG:3,0=合計距離,1=X軸距離,2=Y軸距離
;-------------------------------------------------
@SUIKA_DISTANCE(ARG:1, ARG:2, ARG:3)
#FUNCTION
LOCAL:1 = ARG:1 / 10
LOCAL:2 = ARG:2 / 10
LOCAL:3 = ARG:1 % 10
LOCAL:4 = ARG:2 % 10
LOCAL:5 = ABS(LOCAL:1 - LOCAL:2)
LOCAL:6 = ABS(LOCAL:3 - LOCAL:4)
IF ARG:3 == 1
	RETURNF LOCAL:5
ELSEIF ARG:3 == 2
	RETURNF LOCAL:6
ELSE
	RETURNF LOCAL:5 + LOCAL:6
ENDIF

;-------------------------------------------------
;スイカ割り：打撃する位置（正面位置）
;-------------------------------------------------
@SUIKA_AIMING
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P - 1
CASE 4
	RETURNF SUIKA_MASTER_P - 10
CASE 6
	RETURNF SUIKA_MASTER_P + 10
CASE 8
	RETURNF SUIKA_MASTER_P + 1
ENDSELECT

;-------------------------------------------------
;スイカ割り：背面位置
;-------------------------------------------------
@SUIKA_BACK
#FUNCTION
SELECTCASE SUIKA_DIRECTION 
CASE 2
	RETURNF SUIKA_MASTER_P + 1
CASE 4
	RETURNF SUIKA_MASTER_P + 10
CASE 6
	RETURNF SUIKA_MASTER_P - 10
CASE 8
	RETURNF SUIKA_MASTER_P - 1
ENDSELECT

;-------------------------------------------------
;スイカ割り：位置が場外に出ている判定
;ARG,座標
;-------------------------------------------------
@SUIKA_OVER(ARG)
#FUNCTION
SIF ARG >= 100 || ARG % 10 == 0 || ARG / 10 == 0
	RETURNF 1

;-------------------------------------------------
;スイカ割り：スイカのある方向（客観方向）
;ARG,0=軸指定なし,1=X軸,2=Y軸
;-------------------------------------------------
@SUIKA_RADAR(ARG)
#FUNCTION
IF !ARG
	LOCAL = RAND:2 + 1
ELSE
	LOCAL = ARG
ENDIF

IF LOCAL == 1
	IF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 > 0
		RETURNF 4
	ELSEIF SUIKA_MASTER_P / 10 - SUIKA_SUIKA_P / 10 < 0
		RETURNF 6
	ELSE
		RETURNF 5
	ENDIF
ELSEIF LOCAL == 2
	IF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 > 0
		RETURNF 2
	ELSEIF SUIKA_MASTER_P % 10 - SUIKA_SUIKA_P % 10 < 0
		RETURNF 8
	ELSE
		RETURNF 5
	ENDIF
ENDIF

;-------------------------------------------------
;スイカ割り：セットアップ関数
;-------------------------------------------------
@SUIKA_SETUP
#DIM 最大距離
;挑戦回数＋１
SUIKA_CHALLENGE ++
;得点リセット
SUIKA_SCORE = 0
;スイカを失う
CALL LOST_ITEM("수박", 1, 1)
;お手つき許容回数
IF SUIKA_WEAPON == "마이빗자루"
	SUIKA_OTETSUKI = 1
ELSE
	SUIKA_OTETSUKI = 0
ENDIF
;スイカの位置
SUIKA_SUIKA_P = (RAND:9 + 1) * 10 + RAND:9 + 1
;あなたの位置,スイカの位置と距離が6以上12以内,（제어봉なら距離8以内）
SUIKA_MASTER_P = 0
IF SUIKA_WEAPON == "제어봉"
	PRINTFORML 【제어봉의 효과】즈즈즈…… 제어봉의 효과로 수박이 끌어당겨졌다!
	最大距離 = 8
ELSE
	最大距離 = 16
ENDIF
WHILE !SUIKA_MASTER_P || !INRANGE(SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P), 6, 最大距離)
	SUIKA_MASTER_P = (RAND:9 + 1) * 10 + RAND:9 + 1
WEND
;あなたの向き
SUIKA_DIRECTION = 5
IF SUIKA_WEAPON == "불제봉"
	WHILE SUIKA_DIRECTION % 2 != 0
		SUIKA_DIRECTION = SUIKA_RADAR()
	WEND
	PRINTFORML 【불제봉의 효과】불제봉에 이끌려 %CALLNAME:MASTER%의 몸은 수박이 있는 곳을 향했다!
ELSE
	SUIKA_DIRECTION = RAND:4 * 2 + 2
ENDIF
;ウソつき役の設定
VARSET SUIKA_LIAR
SIF !SUIKA_LIARNUM()
	RETURN
CALL FISHER_YATES_SHAFFLE(SUIKA_PLAYERNUM)
FOR LOCAL:1, 0, SUIKA_LIARNUM()
	LOCAL = SHAFFLE_ARRAY:(LOCAL:1) + 1
	SUIKA_LIAR:LOCAL = 1
NEXT
[IF_DEBUG]
	DRAWLINE
	PRINTFORML SUIKA_SUIKA_P:{SUIKA_SUIKA_P}
	PRINTFORML SUIKA_MASTER_P:{SUIKA_MASTER_P}
	PRINTFORML SUIKA_DISTANCE:{SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 1)}, {SUIKA_DISTANCE(SUIKA_SUIKA_P, SUIKA_MASTER_P, 2)}
	PRINTFORML SUIKA_DIRECTION:{SUIKA_DIRECTION}
	PRINTFORML SUIKA_AIMING:{SUIKA_AIMING()}
	FOR LOCAL, 1, SUIKA_PLAYERNUM + 1
		PRINTFORML SUIKA_PLAYER:{LOCAL} %CALLNAME:(SUIKA_PLAYER:LOCAL)% \@ SUIKA_LIAR:LOCAL ? ● # ○ \@
	NEXT
	PRINTFORML SUIKA_WEAPON:%SUIKA_WEAPON%
[ENDIF]

;-------------------------------------------------
;スイカ割り：ゲーム部分関数
;-------------------------------------------------
@SUIKA_SMASHING
#DIM CHARA
#DIM LINENUM
#DIM LINENUM_PLUS
CALL FISHER_YATES_SHAFFLE(8)

LINENUM_PLUS = 0
DRAWLINE
FOR LINENUM, 1, 9
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 30, LEFT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 2) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 1)
	ENDIF
	SELECTCASE LINENUM
	CASE 2
		PRINTFORM 　　　　　[8] 일보전진　　　　　
	CASE 4
		PRINTFORM 　[4] 좌회전　　[6] 우회전　
	CASE 6
		PRINTFORM 　　　＜＜[5] 공격!＞＞　　　　
	CASE 8
		PRINTFORM 　　　　　[2] 일보후퇴　　　　
	CASEELSE
		PRINTFORM %" " * 32%
	ENDSELECT
	IF LINENUM % 2 == 1
		LOCAL = SHAFFLE_ARRAY:(LINENUM) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		PRINTPLAINFORM % CHARA ? CALLNAME:CHARA # "" , 30, RIGHT%
	ELSE
		LOCAL = SHAFFLE_ARRAY:(LINENUM - 1) + 1
		CHARA = SUIKA_PLAYER:LOCAL
		CALL SUIKA_MESSAGE(CHARA, LOCAL, 2)
	ENDIF
	PRINTL 
NEXT
INPUT
DRAWLINE
SELECTCASE RESULT
CASE 5
	PRINTL 
	PRINTFORMW %마스터는% 정면을 향해 혼신의 힘을 다하며 %조사처리(SUIKA_WEAPON,"를")% 내려쳤다!
	PRINTL 
	LINENUM_PLUS += 3
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
		IF SUIKA_WEAPON == "목도"
			CALL COLORMESSAGE("SLAAAASH!!", C_ORANGE, 2, 3)
			RETURN 2
		ELSE
			CALL COLORMESSAGE("SMAAAASH!!", C_V_PINK, 2, 3)
			RETURN 1
		ENDIF
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		IF SUIKA_WEAPON == "요술망치"
			CALL COLORMESSAGE("LUCKY STRIKE!", C_YELLOW, 2, 3)
			RETURN 1
		ELSE
			CALL COLORMESSAGE("HIT!", C_YELLOW, 2, 2)
			RETURN 0
		ENDIF
	ELSE
		CALL COLORMESSAGE("빗나감!", C_CREAM, 2, 2)
		IF SUIKA_OTETSUKI
			PRINTL 
			PRINTFORMW …그러나 마이빗자루의 규칙으로 인해 한번 더 기회를 얻었다!
			SUIKA_OTETSUKI --
			LINENUM_PLUS += 4
		ELSE
			RETURN -1
		ENDIF
	ENDIF
CASE 4
	PRINTFORML …………
	PRINTFORMW %마스터는% 왼쪽으로 돌았다
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 6
	CASE 4
		SUIKA_DIRECTION = 2
	CASE 6
		SUIKA_DIRECTION = 8
	CASE 8
		SUIKA_DIRECTION = 4
	ENDSELECT
	LINENUM_PLUS += 2
CASE 6
	PRINTFORML …………
	PRINTFORMW %마스터는% 오른쪽으로 돌았다
	SELECTCASE SUIKA_DIRECTION
	CASE 2
		SUIKA_DIRECTION = 4
	CASE 4
		SUIKA_DIRECTION = 8
	CASE 6
		SUIKA_DIRECTION = 2
	CASE 8
		SUIKA_DIRECTION = 6
	ENDSELECT
	LINENUM_PLUS += 2
CASE 2
	PRINTFORML …………
	IF SUIKA_OVER(SUIKA_BACK())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW %마스터는% 한발자국 물러났다 
		SUIKA_MASTER_P = SUIKA_BACK()
		LINENUM_PLUS += 2
	ENDIF
CASEELSE
	PRINTFORML …………
	IF SUIKA_OVER(SUIKA_AIMING())
		CALL SUIKA_COLLISION
		LINENUM_PLUS += RESULT
	ELSE
		PRINTFORMW %마스터는% 한발자국 나아갔다
		SUIKA_MASTER_P = SUIKA_AIMING()
		LINENUM_PLUS += 2
	ENDIF
ENDSELECT
CLEARLINE 11 + LINENUM_PLUS
RESTART

;-------------------------------------------------
;スイカ割り：衝突メッセージ
;-------------------------------------------------
@SUIKA_COLLISION
#DIM LINE
LINE = 4
LOCAL = SUIKA_PLAYER:(RAND:SUIKA_PLAYERNUM + 1)
PRINTFORMW 꽈당!
PRINTL 
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, LOCAL, 0, 0, "衝突")
IF RESULTS != ""
	PRINTFORMW 「%RESULTS%」
	PRINTL 
	LINE += 2
ENDIF
RESETCOLOR
;◆口上設定（※未実装）
IF LOCAL == [[스이카]]
	PRINTFORMW 그것은 수박(スイカ,수이카)가 아니라 스이카다!
ELSE
	PRINTFORMW %마스터는% %CALLNAME:LOCAL%와 부딪쳐버렸다……
ENDIF
RETURN LINE

;-------------------------------------------------
;スイカ割り：台詞関数１,どんな台詞を言うかの判定
;ARG,プレイヤー番号
;ARG:1,表示位置 1=LEFT,2=RIGHT
;-------------------------------------------------
@SUIKA_MESSAGE(CHARA, ARG, ARG:1)
#DIM CHARA
#DIM 客観方向
#DIMS 主観方向
#DIM ウソつき
#DIMS 正直な指示
#DIMS 指示
;非表示
IF !CHARA
	IF ARG:1 == 1
		PRINTPLAINFORM %"" , 30, LEFT%
	ELSE
		PRINTPLAINFORM %"" , 30, RIGHT%
	ENDIF
	RETURN
ENDIF

;完全一致
IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 0
	正直な指示 = 여기다!
;1マスずれ（2分の1）
ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1 && !RAND:2
	正直な指示 = 여기다!
;重なっている（1マスずれの漏れ）
ELSEIF SUIKA_DISTANCE(SUIKA_MASTER_P, SUIKA_SUIKA_P) == 0
	正直な指示 = 약간뒤
;それ以外
ELSE
	;近さ判定
	;LOCAL:1,距離について言う判定
	;LOCAL:2,「少し」を付けて言う判定
	IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 1
		LOCAL:1 = !RAND:2
		LOCAL:2 = 1
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 2
		LOCAL:1 = !RAND:2
		LOCAL:2 = !RAND:2
	ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) == 3
		LOCAL:1 = !RAND:3
		LOCAL:2 = !RAND:4
	ELSE
		LOCAL:1 = !RAND:4
		LOCAL:2 = 0
	ENDIF
	;距離のヒント
	IF LOCAL:1
		IF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) <= 3
			正直な指示 = 가깝다
		ELSEIF SUIKA_DISTANCE(SUIKA_AIMING(), SUIKA_SUIKA_P) >= 8
			正直な指示 = 약간멀다
		ELSE
			正直な指示 = 멀다
		ENDIF
	;方向のヒント
	ELSE
		客観方向 = 5
		WHILE 客観方向 % 2 != 0
			客観方向 = SUIKA_RADAR()
		WEND
		IF 客観方向 == SUIKA_DIRECTION
			主観方向 = 앞
		ELSE
			SELECTCASE SUIKA_DIRECTION
			CASE 2
				SELECTCASE 客観方向
				CASE 4
					主観方向 = 오른쪽
				CASE 6
					主観方向 = 왼쪽
				CASE 8
					主観方向 = 뒤
				ENDSELECT
			CASE 4
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 왼쪽
				CASE 6
					主観方向 = 뒤
				CASE 8
					主観方向 = 오른쪽
				ENDSELECT
			CASE 6
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 오른쪽
				CASE 4
					主観方向 = 뒤
				CASE 8
					主観方向 = 왼쪽
				ENDSELECT
			CASE 8
				SELECTCASE 客観方向
				CASE 2
					主観方向 = 뒤
				CASE 4
					主観方向 = 왼쪽
				CASE 6
					主観方向 = 오른쪽
				ENDSELECT
			ENDSELECT
		ENDIF
		SIF LOCAL:2
			主観方向 = 약간 %主観方向%
		正直な指示 = %主観方向%
	ENDIF
ENDIF
;正邪は役職を無視して嘘をつく
IF SUIKA_LIAR:ARG || CHARA == [[세이자]]
	ウソつき = 1
ELSE
	ウソつき = 0
ENDIF
IF ウソつき
	SELECTCASE 正直な指示
	CASE "여기다!"
		指示 =%TEXTR("약간뒤/약간앞/약간오른쪽/약간왼쪽")%
	CASE "가깝다"
		指示 = 멀다
	CASE "멀다"
		指示 = 가깝다
	CASE "약간멀다"
		指示 = 멀다
	CASE "앞"
		指示 = 뒤
	CASE "약간앞"
		指示 = %TEXTR("약간뒤/여기다!")%
	CASE "뒤"
		指示 = 앞
	CASE "약간뒤"
		指示 = %TEXTR("약간앞/여기다!")%
	CASE "왼쪽"
		指示 = 왼쪽
	CASE "약간오른쪽"
		指示 = %TEXTR("약간왼쪽/여기다!")%
	CASE "오른쪽"
		指示 = 오른쪽
	CASE "약간왼쪽"
		指示 = %TEXTR("약간오른쪽/여기다!")%
	ENDSELECT
ELSE
	指示 = %正直な指示%
ENDIF
;◆口上
CALL KOJO_MESSAGE_SEND("SUIKA", 0, CHARA, 0, 0, 指示)
IF RESULTS != ""
	IF ARG:1 == 1
		PRINTPLAINFORM %"「" + RESULTS + "」", 30, LEFT%
	ELSE
		PRINTPLAINFORM %"「" + RESULTS + "」", 30, RIGHT%
	ENDIF
ELSE
	IF ARG:1 == 1
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 30, LEFT%
	ELSE
		PRINTPLAINFORM %SUIKA_MESSAGE_2(CHARA, ARG, 指示), 30, RIGHT%
	ENDIF
ENDIF

;-------------------------------------------------
;スイカ割り：台詞関数２,実際に喋る台詞
;ARG,プレイヤー番号
;-------------------------------------------------
@SUIKA_MESSAGE_2(CHARA, ARG, 指示)
#FUNCTIONS
#DIM CHARA
#DIMS 指示
;汎用台詞
SELECTCASE 指示
CASE "여기다!"
	SELECTCASE CFLAG:CHARA:어조
	;～です系
	CASE 1
		RESULTS = %TEXTR("거기에요!/지금이예요!")%
	;～だよ系
	CASE 2
		RESULTS = %TEXTR("거기!/지금!")%
	;～だぞ系
	CASE 3
		RESULTS = %TEXTR("여기다!/지금이다!")%
	;～のじゃ系
	CASE 4
		RESULTS = %TEXTR("거기야!/지금이야!")%
	;無口系
	CASE 5
		RESULTS = %TEXTR("거기…!/지금…!")%
	;～のよ系
	CASEELSE
		RESULTS = %TEXTR("거기요!/지금이요!")%
	ENDSELECT
CASE "가깝다"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("조금 가까워요/가까워지고 있어요/조금 더요")%
	CASE 2
		RESULTS = %TEXTR("가까워!가까워!/가까워지고 있어!/조금만 더!")%
	CASE 3
		RESULTS = %TEXTR("약간 가까워/가까워지고 있어/얼마 안남았어")%
	CASE 4
		RESULTS = %TEXTR("가깝다네/가까워지고있단다/앞으로 조금이야")%
	CASE 5
		RESULTS = %TEXTR("……조금 가까워/……가까워지고 있어/……조금 더")%
	CASEELSE
		RESULTS = %TEXTR("조금 가까워요/가까워지고 있어요/조금만 더요")%
	ENDSELECT
CASE "멀다"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("아직 멀었어요/아직 멀어요")%
	CASE 2
		RESULTS = %TEXTR("아직 멀었어/아직이야~")%
	CASE 3
		RESULTS = %TEXTR("아직 멀구나/아직 멀었어")%
	CASE 4
		RESULTS = %TEXTR("아직 멀다네/아직 멀었단다")%
	CASE 5
		RESULTS = %TEXTR("……멀어/……어직 멀어")%
	CASEELSE
		RESULTS = %TEXTR("아직 멀어요/아직 멀었어요")%
	ENDSELECT
CASE "약간멀다"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("아직 멀었습니다/상당히 머네요")%
	CASE 2
		RESULTS = %TEXTR("완전 멀었어/아직이야 계속 멀어")%
	CASE 3
		RESULTS = %TEXTR("약간 멀구나/꽤 떨어져있어")%
	CASE 4
		RESULTS = %TEXTR("아직 멀단다/아직 멀었단다")%
	CASE 5
		RESULTS = %TEXTR("……약간 멀어/……멀리 떨어져있어")%
	CASEELSE
		RESULTS = %TEXTR("약간 멀어요/꽤 떨어져 있네요")%
	ENDSELECT
CASE "前"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("앞에 있습니다/그대로입니다/방향은 맞습니다")%
	CASE 2
		RESULTS = %TEXTR("앞에 있어/그대로, 그대로/그쪽")%
	CASE 3
		RESULTS = %TEXTR("앞쪽이다/그대로 앞/정면을 향하고 있어")%
	CASE 4
		RESULTS = %TEXTR("앞이란다/그대로 좋구나/자, 걸으렴")%
	CASE 5
		RESULTS = %TEXTR("……앞/……그대로/……그방향")%
	CASEELSE
		RESULTS = %TEXTR("앞에 있어요/그대로 걸어서/그쪽 방향이에요")%
	ENDSELECT
CASE "약간앞"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("조금 앞이에요/약간 앞이예요/한걸음 앞인가요…")%
	CASE 2
		RESULTS = %TEXTR("조금만 앞!/조-금 앞/앞으로 한발자국!")%
	CASE 3
		RESULTS = %TEXTR("조금 앞이다/약간 앞이다/한걸음 앞인가")%
	CASE 4
		RESULTS = %TEXTR("조금 앞이란다/약간 앞이란다/한걸음 앞이란다")%
	CASE 5
		RESULTS = %TEXTR("……조금 앞/……약간 앞/……앞으로 한걸음")%
	CASEELSE
		RESULTS = %TEXTR("조금만 앞으로요/약간만 앞으로요/앞으로 한걸음일까요")%
	ENDSELECT
CASE "뒤"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("뒤입니다/반대를 향하고 있습니다/그쪽에는 없습니다")%
	CASE 2
		RESULTS = %TEXTR("뒤쪽이야!/반대로, 반대로!/그쪽이 아니야-!")%
	CASE 3
		RESULTS = %TEXTR("뒤쪽이야/반대쪽이야/그쪽에는 없어")%
	CASE 4
		RESULTS = %TEXTR("뒤란다/반대로 향하고있단다/그쪽에는 없단다")%
	CASE 5
		RESULTS = %TEXTR("……뒤/……반대쪽/……그쪽이 아니야")%
	CASEELSE
		RESULTS = %TEXTR("뒤예요/반대방향이예요/그쪽이 아니예요")%
	ENDSELECT
CASE "약간뒤"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("바로 뒤입니다/약간 뒤입니다/한걸음 뒤입니다…")%
	CASE 2
		RESULTS = %TEXTR("바로 뒤야!/약간 뒤야!/한걸음 뒤야!")%
	CASE 3
		RESULTS = %TEXTR("바로 뒤야/약간 뒤야/한걸음 뒤야")%
	CASE 4
		RESULTS = %TEXTR("바로 뒤란다/약간 뒤란다/한걸음 뒤란다")%
	CASE 5
		RESULTS = %TEXTR("……바로 뒤/……약간 뒤/……한걸음 뒤")%
	CASEELSE
		RESULTS = %TEXTR("바로 뒤에요/약간 뒤예요/한걸음 뒤예요")%
	ENDSELECT
CASE "右"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("오른쪽이요/오른쪽입니다/우측입니다")%
	CASE 2
		RESULTS = %TEXTR("오른쪽이야/오른쪽/더 오른쪽")%
	CASE 3
		RESULTS = %TEXTR("오른쪽/오른쪽이네/우측")%
	CASE 4
		RESULTS = %TEXTR("오른쪽이란다/오른쪽이라네/우측이란다")%
	CASE 5
		RESULTS = %TEXTR("오른쪽/……오른쪽/……오른쪽이요")%
	CASEELSE
		RESULTS = %TEXTR("오른쪽이예요/아직 오른쪽이예요/오른쪽이려나")%
	ENDSELECT
CASE "약간오른쪽"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("조금 오른쪽이요/약간 오른쪽일까요")%
	CASE 2
		RESULTS = %TEXTR("조금만 오른쪽/아직 오른쪽이랄까")%
	CASE 3
		RESULTS = %TEXTR("약간 오른쪽이다/아직도 오른쪽이구나")%
	CASE 4
		RESULTS = %TEXTR("약간 오른쪽이란다/조금 오른쪽이란다")%
	CASE 5
		RESULTS = %TEXTR("……약간 오른쪽/……조금 오른쪽")%
	CASEELSE
		RESULTS = %TEXTR("조금 오른쪽이예요/약간 오른쪽이려나")%
	ENDSELECT
CASE "左"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("왼쪽이요/왼쪽입니다/좌측입니다")%
	CASE 2
		RESULTS = %TEXTR("왼쪽이야/왼쪽/더 왼쪽")%
	CASE 3
		RESULTS = %TEXTR("왼쪽/왼쪽이네/좌측")%
	CASE 4
		RESULTS = %TEXTR("왼쪽이란다/왼쪽이라네/좌측이란다")%
	CASE 5
		RESULTS = %TEXTR("왼쪽/……왼쪽/……왼쪽이요")%
	CASEELSE
		RESULTS = %TEXTR("왼쪽이예요/아직 왼쪽이예요/왼쪽이려나")%
	ENDSELECT
CASE "약간왼쪽"
	SELECTCASE CFLAG:CHARA:어조
	CASE 1
		RESULTS = %TEXTR("조금 왼쪽이요/약간 왼쪽일까요")%
	CASE 2
		RESULTS = %TEXTR("조금만 왼쪽/아직 왼쪽이랄까")%
	CASE 3
		RESULTS = %TEXTR("약간 왼쪽이다/아직도 왼쪽이구나")%
	CASE 4
		RESULTS = %TEXTR("약간 왼쪽이란다/조금 왼쪽이란다")%
	CASE 5
		RESULTS = %TEXTR("……약간 왼쪽/……조금 왼쪽")%
	CASEELSE
		RESULTS = %TEXTR("조금 왼쪽이예요/약간 왼쪽이려나")%
	ENDSELECT
ENDSELECT
RETURNF "「" + RESULTS + "」"


