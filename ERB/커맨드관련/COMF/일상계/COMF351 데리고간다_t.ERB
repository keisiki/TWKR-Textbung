;-------------------------------------------------
;데리고나간다
;일상계 커맨드, 레벨 2
;-------------------------------------------------
@COM351
#DIM NUM_TARGET
#DIM N_TO_M
NUM_TARGET = GET_TARGETNUM()
N_TO_M = 0
;데리고나간다
VARSET LOCAL
SIF !NUM_TARGET
	RETURN -1
FOR LOCAL, 1, NUM_TARGET + 1
	IF COM_351_CONDITION(TARGET:LOCAL) >= 2
		LOCAL:97 += 1
	ENDIF
NEXT
IF NUM_TARGET == 1 || LOCAL:97 == 1
	IF COM_351_CONDITION(TARGET) < 1
		RETURN -1
	ELSEIF COM_351_CONDITION(TARGET) < 2
		DRAWLINE
		PRINTFORMW %타겟은% 지금 바쁩니다.
		RETURN -1
	ENDIF
	IF CFLAG:TARGET:동행
		CFLAG:TARGET:동행 = 0
		CFLAG:MASTER:동행 = 0
		PRINTFORMW 동행 해제합니다.
		IF CFLAG:TARGET:감금됨
			CFLAG:현재위치 = 1445
			PRINTFORMW 감금실로 보냈습니다
		ENDIF
	ELSE
		CFLAG:TARGET:동행 = 60
		;우＋외＋우산 or상대가 우산때는 한우산을씀
		SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:현재위치) && (ITEM:36 || EQUIP:TARGET:액세서리== 6)
			TEQUIP:TARGET:201 = 1
		PRINTFORMW %타겟을% 데리고 나갑니다
		CFLAG:MASTER:동행 = 60
	ENDIF
	TIME += 1
	SIF CFLAG:TARGET:동행
		CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
	RETURN 1
ENDIF



DO
	PRINTL 누구를 권합니까?
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF COM_351_CONDITION(TARGET:LOCAL) <= 0
			CONTINUE
		IF CFLAG:(TARGET:LOCAL):동행
			SETCOLOR 255, 200, 100
		ELSEIF CFLAG:(TARGET:LOCAL):동행준비
			SETCOLOR C_AQUA
		ENDIF
		PRINTFORML [{LOCAL}] - %CALLNAME:(TARGET:LOCAL)%
		RESETCOLOR
	NEXT
	PRINTL [97] - 데리고 나간다
	PRINTL [98] - 전원 데리고 나간다
	PRINTL [99] - 동행 해제
	PRINTL [100] - 그만둔다
	INPUT
	SELECTCASE RESULT
	CASE 100
		FOR LOCAL, 1, NUM_TARGET + 1
			CFLAG:(TARGET:LOCAL):동행준비 = 0
		NEXT
		RETURN -1
	CASE 99
		FOR LOCAL, 1, NUM_TARGET + 1
			CFLAG:(TARGET:LOCAL):동행 = 0
			CFLAG:(TARGET:LOCAL):동행준비 = 0
			;한우산을씀도 없어진다
			TEQUIP:(TARGET:LOCAL):한우산을씀 = 0
		NEXT
		PRINTFORMW 동행 해제합니다
		CFLAG:MASTER:동행 = 0
		RETURN 1
	CASE 98
		FOR LOCAL, 1, NUM_TARGET + 1
			SIF COM_351_CONDITION(TARGET:LOCAL) == 2
				N_TO_M = 1
		NEXT
		IF N_TO_M > 0
			PRINTFORMW 동시에 데리고 나갈 수 없는 캐릭터가 있습니다
			N_TO_M = 0
			CONTINUE
		ENDIF	
		FOR LOCAL, 1, NUM_TARGET + 1
			IF COM_351_CONDITION(TARGET:LOCAL) >= 3
				CFLAG:(TARGET:LOCAL):동행 = 60
				LOCAL:98 += 1
			ENDIF
		NEXT
		PRINTFORMW {LOCAL:98}인 정리해 데리고 나갑니다
		CFLAG:MASTER:동행 = 60
		TIME += LOCAL:98
		SIF CFLAG:TARGET:동행
			CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
		RETURN 1
	CASE 97
		FOR LOCAL, 1, NUM_TARGET + 1
			IF (COM_351_CONDITION(TARGET:LOCAL) >= 2) && (CFLAG:(TARGET:LOCAL):동행준비 || CFLAG:(TARGET:LOCAL):동행)
				LOCAL:98 += 1
				SIF COM_351_CONDITION(TARGET:LOCAL) == 2
					N_TO_M = 1
			ENDIF
		NEXT
		SIF LOCAL:98 == 0
			CONTINUE
		IF LOCAL:98 == 1
			TIME += 1
			LOCAL = 0
			LOCAL:98 = 0
			FOR LOCAL, 1, NUM_TARGET + 1
				IF (COM_351_CONDITION(TARGET:LOCAL) >= 2) && (CFLAG:(TARGET:LOCAL):동행준비 || CFLAG:(TARGET:LOCAL):동행)
					PRINTFORMW %조사처리(CALLNAME:(TARGET:LOCAL),"를")% 데리고 나갑니다
					CFLAG:(TARGET:LOCAL):동행 = 60
					CFLAG:(TARGET:LOCAL):동행준비 = 0
					;雨＋外＋傘or相手が傘の時は相合い傘
					SIF TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:현재위치) && (ITEM:36 || EQUIP:(TARGET:LOCAL):액세서리 == 6)
						TEQUIP:(TARGET:LOCAL):한우산을씀 = 1
				ENDIF
			NEXT
		ELSE
			IF N_TO_M > 0
				PRINTFORMW 동시에 데리고 나갈 수 없는 캐릭터가 있습니다
				N_TO_M = 0
				CONTINUE
			ENDIF
			PRINTFORMW {LOCAL:98}인 모아서 데리고 나갑니다
			TIME += LOCAL:98
			LOCAL = 0
			LOCAL:98 = 0
			FOR LOCAL, 1, NUM_TARGET + 1
				IF (COM_351_CONDITION(TARGET:LOCAL) >= 3) && (CFLAG:(TARGET:LOCAL):동행준비 || CFLAG:(TARGET:LOCAL):동행)
					CFLAG:(TARGET:LOCAL):동행 = 60
					CFLAG:(TARGET:LOCAL):동행준비 = 0
				ENDIF
			NEXT
		ENDIF
		CFLAG:MASTER:동행 = 60
		SIF CFLAG:TARGET:동행
			CALL KOJO_MESSAGE_SEND("DIRECT",351,TARGET,-1,-1)
		RETURN 1
	CASEELSE
		IF !INRANGE(RESULT, 1, NUM_TARGET)
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 1
			CLEARLINE 1
			CONTINUE
		ELSEIF COM_351_CONDITION(TARGET:RESULT) < 2
			PRINTFORMW %조사처리(CALLNAME:(TARGET:RESULT),"는")% 지금 바쁩니다.
			CLEARLINE 2
			CONTINUE
		ENDIF
		CFLAG:(TARGET:RESULT):동행준비 ^= 1
	ENDSELECT
LOOP 1



@EQUIP_COM351(ARG)
;소스는 조심스럽게 적당. 애정 너무 들어갔으므로 자중
IF FLAG:70 == 1

ELSE
	SOURCE:환락 += ABL:친밀 * 5
	SOURCE:사랑 += ABL:친밀 * 5
	SOURCE:반감 += 100 - MIN(ABL:친밀 * 10,100)
	SIF ABL:친밀 > 9 && TIME_PROGRESS(TIME:1) > 10
	EXP:ARG:애정경험 += RAND:2
ENDIF
RETURN 1

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE351
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
;데리고 나가는 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(351)
	RETURN RESULT
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후
	RETURN 0
;데이트중
SIF CHK_DATENOW(CFLAG:MASTER:데이트중)
	RETURN 0
SIF IN_HOME(CFLAG:현재위치) == 0
	RETURN 0
SIF COM_351_CONDITION(TARGET) == 0
	RETURN 0

;;TARGET의 친해져 강도 1
;FOR LOCAL,0,CHARANUM
;	;TARGET:1~를 찾는다
;	SIF !TARGET:LOCAL
;		CONTINUE
;	;동침중
;	SIF CFLAG:(TARGET:LOCAL):동침중
;		CONTINUE
;	;업무중은 제외
;	SIF CFLAG:(TARGET:LOCAL):행동 == 5 && CFLAG:(TARGET:LOCAL):350 != 49 && CFLAG:(TARGET:LOCAL):350 != 51
;		CONTINUE
;	;가능한 캐릭터가 있으면 1을 돌려준다
;	SIF COM_351_CONDITION(TARGET:LOCAL) > 0
;		RETURN 1
;NEXT
RETURN 1



;반환값
; 3 정리해 데리고 나갈 수 있다
; 2 개별 선택으로 데리고 나갈 수 있다
; 1 데리고 나갈 수 없다(바쁜 와중 메시지)
; 0 데리고 나갈 수 없다
@COM_351_CONDITION(CHARA)
#FUNCTION
#DIM CHARA
SIF CHARA == MASTER
	RETURNF 0
SIF FLAG:개인실입장 == CHARA
	RETURNF 0
SIF CFLAG:CHARA:태도 > 0 && CFLAG:CHARA:수면 == 0 && CFLAG:CHARA:행동 != 5
	RETURNF 3

;현재 동행중의 캐릭터는 허가(동행 조건을 채우지 않게 되었을 경우에 수동으로 동행 해제할 수 있도록(듯이) 한다)
SIF CFLAG:CHARA:동행
	RETURNF 2
SIF CFLAG:CHARA:패배자 || TALENT:CHARA:당신의노예
	RETURNF 2
SIF CFLAG:CHARA:태도 < 1 && TCVAR:CHARA:만취 == 0 && !CAN_IRAI_CHARA_GO_TOGATHER(CHARA)
	RETURNF 0
SIF CFLAG:CHARA:수면
	RETURNF 0
SIF CFLAG:CHARA:행동 == 5 && !TCVAR:CHARA:만취
	RETURNF 1

RETURNF 2



