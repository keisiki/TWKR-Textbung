;-------------------------------------------------
;키스
;커뮤니케이션 계 커맨드, 레벨 1
;-------------------------------------------------
@COM20

CALL KISS_CANCEL
SIF RESULT == -1
	RETURN -1
;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER

;봉사 쾌락 플래그
TCVAR:PLAYER:108 = 1
TCVAR:108 = 1

CALL 몸사용하기("입",1)
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------

;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:PLAYER:성행동 =(ABL:PLAYER:혀 * 3 + 20)/20
	SOURCE:쾌M = ABL:PLAYER:혀 * 3 + 20
	;정지 중에는 TARGET의 기술이 관련되는 처리는 하지 않는 것이 좋은 것 같아
	;SOURCE:PLAYER:쾌M = ABL:혀 + 10
ELSE

	DOWNBASE:체력 += 0
	DOWNBASE:기력 += 50
	SOURCE:PLAYER:성행동 =(ABL:PLAYER:혀 * 3 + 20)/20
	
	SOURCE:쾌M = ABL:PLAYER:혀 * 3 + 20
	SOURCE:PLAYER:쾌M = ABL:혀 * 3 + 10
	SOURCE:사랑 = 100 + GET_REVISION(EXP:애정경험, 200,100)
	SOURCE:성행동 = 50 + GET_REVISION(EXP:애정경험, 50,100)
	SOURCE:굴종 = 10
	SOURCE:일탈 = 10
	SOURCE:반감 = 10

	;위 쪽으로 계산한 더러운 데이터
	SOURCE:불결 = Y*20 + 10

	;호감 도를 본다
	IF CFLAG:2 <= 500
		SOURCE:사랑 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:사랑 += 300 + (CFLAG:2 - 500) / 6
	ELSE
		SOURCE:사랑 += 1000 + (CFLAG:2 - 5000) / 8
	ENDIF
	SIF SOURCE:사랑 < 0
		SOURCE:사랑 = 0

;-------------------------------------------------
;특수소스처리
;-------------------------------------------------
	CALL KISS_TALENTEFFECT
ENDIF
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 입⇔조교자의 입의 더러움이 이동
STAIN:구 |= STAIN:PLAYER:구
STAIN:PLAYER:구 |= STAIN:구

;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
CALL KISS_EXP
SIF TALENT:연모 && !(FLAG:70 == 1 || CFLAG:장난)
	EXP:애정경험 += 1 + RAND:2
TCVAR:FK상실 = 1

RETURN 1

@CAN_COM20(ARG)
SKIPDISP ARG
PRINTL 키스한다
;-------------------------------------------------
;실행할 수 있을까의 판정
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:장난

ELSE

	A = 0
	S = 0

	;모든 명령에 공통의 요소를 고려
	;(순종이 높다고 명령에 따르기 쉬운 등)
	CALL COM_ORDER

	;ABL:욕망
	IF ABL:욕망
		SIF S
			PRINT  + 
		A += ABL:욕망 * 1
		PRINT 욕망
		PRINTV 'LV, ABL:욕망, '(, ABL:욕망 * 1, ')
		S = 1
	ENDIF
	;ABL:봉사정신
	IF ABL:봉사정신
		SIF S
			PRINT  + 
		A += ABL:봉사정신 * 4
		PRINT 봉사정신
		PRINTV 'LV, ABL:봉사정신, '(, ABL:봉사정신 * 4, ')
		S = 1
	ENDIF

	IF MARK:쾌락각인
		SIF S
			PRINT  + 
		A += MARK:쾌락각인 * 2
		PRINT 쾌락각인
		PRINTV 'LV, MARK:쾌락각인, '(, MARK:쾌락각인 * 2, ')
		S = 1
	ENDIF

	;PALAM:욕정
	IF GETPALAMLV(PALAM:욕정, 5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:욕정, 5) * 1
		PRINT 욕정
		PRINTV 'LV, GETPALAMLV(PALAM:욕정, 5), '(, GETPALAMLV(PALAM:욕정, 5) * 1, ')
		S = 1
	ENDIF

	;수줍음
	IF TALENT:수치심 > 0
		PRINT  - 
		A -= 1
		PRINT 수줍음
		PRINTV '(, 1, ')
		S = 1
	ENDIF
	;악취 둔감
	IF TALENT:악취내성 > 0
		SIF S
			PRINT  + 
		A += 1
		PRINT 악취 둔감
		PRINTV '(, 1, ')
		S = 1
	ENDIF
	;악취 민감
	IF TALENT:악취내성 < 0
		PRINT  - 
		A -= 1
		PRINT 악취 민감
		PRINTV '(, 1, ')
		S = 1
	ENDIF
	;헌신적
	IF TALENT:헌신적
		SIF S
			PRINT  + 
		A += 6
		PRINT 헌신적
		PRINTV '(, 6, ')
		S = 1
	ENDIF
	;쾌감에 부정(음란, 창부로 무효)
	IF TALENT:쾌감응답 < 0 && !TALENT:음란
		PRINT  - 
		A -= 1
		PRINT 쾌감에 부정
		PRINTV '(, 1, ')
		S = 1
	ENDIF
	;연모
	IF TALENT:연모 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 연모
		PRINTV '(, 5, ')
		S = 5
	ENDIF
	;친애
	IF TALENT:연모 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 10
		PRINT 친애
		PRINTV '(, 10, ')
		S = 10
	ENDIF
	;창녀
	IF TALENT:창녀||TALENT:렌탈창녀
		SIF S
			PRINT  + 
		A += 30
		PRINT 창녀
		PRINTV '(, 30, ')
		S = 30
	ENDIF
	Y = 0
	;애액의 더러움
	SIF STAIN:PLAYER:구 & 1
		Y += 1
	;정액의 더러움
	SIF STAIN:PLAYER:구 & 4
		Y += 3
	;애널의 더러움
	SIF STAIN:PLAYER:구 & 8
		Y += 7
	SIF STAIN:PLAYER:구 & 16
		Y += 15
	;키스에서는 더러움이 그만큼 영향을 주지 않는다
	Y /= 2
	Y = Y * (2 - TALENT:악취내성) / 2

	;서로 더러워져
	IF Y
		PRINT  - 
		A -= Y
		PRINT 서로 더러워져
		;악취 둔감
		IF TALENT:악취내성 > 0
			PRINT , 악취 둔감
		ELSEIF TALENT:악취내성 < 0
			PRINT , 악취 민감
		ENDIF
		PRINTV '(, Y, ')
		S = 1
	ENDIF


	;합계를 표시(15이상으로 실행)
	PRINT  = 
	PRINTV A

	V = 15
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 실행치
	PRINTV V

	WAIT
	SKIPDISP 0
	;실행할 수 없다
	SIF A < V
		RETURN 0
ENDIF
RETURN 1


;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE20
;키스 하는 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(20)
	RETURN RESULT
;볼개그 사용중은 불가
SIF TEQUIP:볼개그
	RETURN 0


RETURN 1

