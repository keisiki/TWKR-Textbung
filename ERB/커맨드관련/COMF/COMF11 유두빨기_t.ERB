;-------------------------------------------------
;유두빨기
;애무계 커맨드, 레벨 1
;-------------------------------------------------
@COM11
;착탈의처리
TEQUIP:유두빨기 = !TEQUIP:유두빨기

;-------------------------------------------------
;COM별 FLAG의 설정
;-------------------------------------------------
;행위자
TCVAR:116 = PLAYER
;봉사 쾌락
TCVAR:PLAYER:108 = 1
CALL 몸사용하기("입",1)
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
;소비 체력, 기력
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:쾌B += ABL:PLAYER:혀 * 20 + 20

ELSE
	DOWNBASE:체력 += 5
	DOWNBASE:기력 += 50

	SOURCE:쾌B = 20
	SOURCE:사랑 = 30
	SOURCE:노출 = 20
	SOURCE:불결 = 30
	SOURCE:반감 = 100
	;;SOURCE:PLAYER:성행동=ABL:PLAYER:혀
	SOURCE:쾌B += ABL:PLAYER:혀 * 20
	SOURCE:사랑 += ABL:PLAYER:혀 * 5

	SIF ABL:친밀 >= 7
		SOURCE:사랑 += (ABL:친밀 * 20)

	;조교자의 TALENT:혀놀림을 본다
	SIF TALENT:PLAYER:혀놀림
		TIMES SOURCE:쾌B , 1.50

	;조교자의 TALENT:고양이혀를 본다
	SIF TALENT:PLAYER:고양이혀
		TIMES SOURCE:쾌B , 0.75
	;-------------------------------------------------
	;특수소스처리
	;-------------------------------------------------
	;억압, 저항 소유에는, 억울소스 추가
	SIF TALENT:자기애 < 0 || TALENT:저항
		SOURCE:울굴 += 100
ENDIF
;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 B⇔조교자의 입의 더러움이 이동
STAIN:5 |= STAIN:PLAYER:구
STAIN:PLAYER:구 |= STAIN:5
;-------------------------------------------------
;경험의 처리
;-------------------------------------------------
;동성애 레즈비언이라면 반환값 1 동성애자라면 반환값 2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET, PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET, PLAYER)
		EXP:(50 + HETEROSEX(TARGET, PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET, PLAYER)) += 5
	ENDIF
ENDIF
;조교자가 고양이혀 소유의 경우는, 조교자에게 가학쾌락경험을 추가
SIF TALENT:PLAYER:고양이혀
	EXP:PLAYER:가학쾌락경험 += 1
EXP:PLAYER:펠라티오경험 ++
RETURN 1
;-------------------------------------------------
;장착 계속의 처리
;-------------------------------------------------
@EQUIP_COM11(ARG)

SELECTCASE SELECTCOM
	CASE 0 TO 2,4,8,20,47 TO 49,60 TO 66,68,70,71,81 TO 85,91 TO 94,105,120 TO 126,140,410
		PRINTL ＜유두빨기 해제＞
		SIF TEQUIP:유두빨기
			TEQUIP:유두빨기 = 0
	CASEELSE
;유두빨기 안
PRINTL ＜유두빨기 안＞
;-------------------------------------------------
;소스 추가 처리
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:쾌B += (ABL:PLAYER:혀 + 1) * 10
ELSE
	DOWNBASE:ARG:체력 += 5
	DOWNBASE:ARG:기력 += 20
	DOWNBASE:PLAYER:체력 += 5
	DOWNBASE:PLAYER:기력 += 10
	SOURCE:쾌B += (ABL:PLAYER:혀 + 1) * 10
	SOURCE:노출 = 20
	SOURCE:불결 = 30
	SOURCE:반감 = 50
	SOURCE:사랑 += ABL:PLAYER:혀 * 5

	SIF ABL:친밀 >= 5
		SOURCE:사랑 += (ABL:친밀 * 3)
ENDIF

;-------------------------------------------------
;더러움의 처리
;-------------------------------------------------
;노예의 B⇔조교자의 입의 더러움이 이동
STAIN:5 |= STAIN:PLAYER:구
STAIN:PLAYER:구 |= STAIN:5
;조교자가 고양이혀 소유의 경우는, 조교자에게 가학쾌락경험을 추가
SIF TALENT:PLAYER:고양이혀
	EXP:PLAYER:가학쾌락경험 += 1
EXP:PLAYER:펠라티오경험 ++
;EXP:유두 ++
ENDSELECT
RETURN 1

@MESSAGE_EQUIP_COM11

IF TFLAG:50 == 19
	;수유플레이때는 표시하지 않는다
ELSE
	PRINT 혀와 입술로 
	SIF CFLAG:PLAYER:지금바쁨 == 1
		PRINTFORM %CALLNAME:TARGET%의 
	IF EX:B절정 * 10000 + PALAM:쾌B + PALAM:욕정 >= 10000 && ABL:B감각 <= 5
		PRINTFORM %TEXTR("유욕에 대한 기대에 치를 떨며/젖 괴롭히기를 이제나저제나 기다리며")% 
	ENDIF
	PRINTFORM %NIPPLE_DESCRIPTION(TARGET,1)%
	IF RAND:2
		;욕정의 PALAMLV4 이상
		IF PALAM:욕정 >= PALAMLV:4 && RAND:3
			PRINT 의 올강올강한
		ELSEIF EX:B절정 * 10000 + PALAM:쾌B + PALAM:욕정 >= 10000 && RAND:2
			PRINT 의 젤리같은 
		ELSE
			PRINT 의 부드러운 
		ENDIF
		PRINT 감촉을 맛보면서, 
	ELSE
		PRINTFORM %조사만처리(NIPPLE_DESCRIPTION(TARGET,1),"를")% 집요하게 빨면서, 
	ENDIF
ENDIF

;-------------------------------------------------
;실행 판정
;-------------------------------------------------
@COM_ABLE11
;유두빨기 실행 판정
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(11)
	RETURN RESULT
;상반신착의
SIF (TEQUIP:1 & 2) && TEQUIP:3 == 0
	RETURN 0
;착유기 사용중은 불가
SIF TEQUIP:17
	RETURN 0
;유두캡 사용중은 불가
SIF TEQUIP:16
	RETURN 0
;유방 전극 장착중은 안 돼
SIF TEQUIP:24
	RETURN 0
SIF TEQUIP:체위 == 5 || TEQUIP:체위 == 2 || TEQUIP:체위 == 3
	RETURN 0
;당신의 입사용중
SIF MASTER_POSE(0,4) && MASTER_POSE(0,4) != TARGET
	RETURN 0
IF IN_ROOM_NUMBER(CFLAG:PLAYER:현재위치) > 2
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TEQUIP:LOCAL:유두빨기 && LOCAL != TARGET
			LOCAL:1 ++
	NEXT
	SIF LOCAL:1
	RETURN 0
ENDIF

RETURN 1
