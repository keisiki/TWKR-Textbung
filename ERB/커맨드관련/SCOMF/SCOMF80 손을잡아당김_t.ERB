;-------------------------------------------------
;손을 잡아 당긴다
;-------------------------------------------------
@SCOM80
;手を引く
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:110
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 109
			LOCAL:1 = 109
		IF LOCAL >= LOCAL:1
			;失敗
			TFLAG:193 = -1
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;強制成功or大成功
	CASE 1
		TFLAG:193 = 0
	;強制失敗
	CASE -1
		TFLAG:193 = -1
	;コマンド終了
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

;선제 발생시는 실패하지 않는다
SIF TFLAG:102 == 3
	TFLAG:193 = MAX(TFLAG:193, 0)
DOWNBASE:기력 += 50
DOWNBASE:MASTER:기력 += 50

;고정으로 획득하는 소스
SOURCE:환락 = 150
SOURCE:공순 = 100
SOURCE:반감 = 100
SOURCE:수동 = 100

;ABL:친밀을 본다
IF ABL:친밀 <= 3
	SOURCE:환락 += 150 + (ABL:친밀 * 50)
ELSEIF ABL:친밀 <= 5
	SOURCE:환락 += 500 + (ABL:친밀 * 50)
ELSEIF ABL:친밀 <= 8
	SOURCE:환락 += 800 + (ABL:친밀 * 50)
ELSEIF ABL:친밀 <= 11
	SOURCE:환락 += 1000 + (ABL:친밀 * 90)
ELSE
	SOURCE:환락 += 1800 + (ABL:친밀 * 40)
ENDIF

;ABL:순종을 본다
IF ABL:순종 <= 3
	SOURCE:공순 += 100 + (ABL:순종 * 50)
	SOURCE:수동 += 170 + (ABL:순종 * 40)
ELSEIF ABL:순종 <= 5
	SOURCE:공순 += 300 + (ABL:순종 * 50)
	SOURCE:수동 += 350 + (ABL:순종 * 30)
ELSEIF ABL:순종 <= 8
	SOURCE:공순 += 500 + (ABL:순종 * 50)
	SOURCE:수동 += 600 + (ABL:순종 * 50)
ELSEIF ABL:순종 <= 11
	SOURCE:공순 += 800 + (ABL:순종 * 90)
	SOURCE:수동 += 700 + (ABL:순종 * 50)
ELSE
	SOURCE:공순 += 1500 + (ABL:순종 * 40)
	SOURCE:수동 += 1000 + (ABL:순종 * 20)
ENDIF

;호감 도를 본다
IF CFLAG:2 <= 500
	SOURCE:사랑 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:사랑 += 200 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:사랑 += 1000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:환락 < 0
	SOURCE:환락 = 0


IF TFLAG:193 == -1
	TIMES SOURCE:환락 , 0.10
	TIMES SOURCE:공순 , 0.50
	TIMES SOURCE:수동 , 1.50
	TIMES SOURCE:욕정 , 0.50
	TIMES SOURCE:반감 , 1.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:환락 , 1.00
	TIMES SOURCE:공순 , 1.00
	TIMES SOURCE:수동 , 1.00
	TIMES SOURCE:욕정 , 1.00
ELSE
	TIMES SOURCE:환락 , 2.00
	TIMES SOURCE:공순 , 1.50
	TIMES SOURCE:수동 , 2.00
	TIMES SOURCE:욕정 , 2.00
	TIMES SOURCE:반감 , 0.75
ENDIF
TIME += 10
;데이트도중을 가속
SIF TFLAG:193 >= 0
	TFLAG:데이트도중 = MAX(TFLAG:데이트도중 - 10, 1)
RETURN 1

@CAN_SCOM80(ARG)
SKIPDISP ARG
PRINTL 손을 잡아 당긴다
WAIT
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;선택 가능 판정
;-------------------------------------------------
;손을 잡아 당긴다
@COM_ABLE580
SIF !TFLAG:100
	RETURN 0
;일괄관리
SIF GLOBAL_COMABLE(580)
	RETURN RESULT
;데이트중이 아니면 안 돼
SIF !CFLAG:MASTER:데이트중 || !CFLAG:TARGET:데이트중
	RETURN 0
;홀로 여행은 안 돼
SIF !TARGET
	RETURN 0
;데이트 도중 이외
SIF !TFLAG:데이트도중
	RETURN 0
;수면중
SIF CFLAG:수면
	RETURN 0
RETURN 1
