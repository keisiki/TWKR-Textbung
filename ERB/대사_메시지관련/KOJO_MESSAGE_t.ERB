;====================================================================
;KOJO 표시 종합 관리
;@KOJO_MESSAGE_SEND
;ARGS   => 말종류 ("ENCOUNTER" "SP_EVENT" "EVENT" "COMMAND" "COUNTER" "PALAM" "MARK" "DIRECT" "SUCCESS" "ENDING")
;       => 추가분："ONABARE" "PERMISSION" "LOST_VIRGIN_STOP" "CHILD" "DANMAKU" "IRAI" "GIFT" "DAILY" "DIARY" "MUSHI_BATTLE" "GRAVITY" "SUIKA" "ODEKAKE" "SEX_FRIEND" "BEFORETRAIN"
;ARG:1 => 말타이밍(이벤트 번호, PALAM계 타이밍 번호, COMMAND 번호등 )
;ARG:2 => 발언자(말주) 넘버
;ARG:3 => 그외말전용 인수 buffer
;ARG:4 => 그외말전용 인수 buffer2
;ARGS:1 => 캐릭터라인 ver. 말타이밍(ARG:1은 무시한다)
;ARGS:2 => 캐릭터라인 ver. 그외말전용 인수(ARGS:1이 있을 때 마셔)
;ARG:5 => 그외말전용 인수 buffer3
;ARG:6 => 그외말전용 인수 buffer4
;RESULTS에 의한 범용화에 관해서는 사양을 읽을 수 없기 때문에 우선 진부 이식
;====================================================================
@KOJO_MESSAGE_SEND(ARGS, ARG:1, ARG:2, ARG:3, ARG:4, ARGS:1, ARGS:2, ARG:5, ARG:6)
IF ARG:2 == MASTER && ARGS == "MUSHI_BATTLE"
	RESULTS = 
	RETURN -1
ELSEIF ARG:2 == MASTER && !GROUPMATCH(ARGS, "COMMAND", "COM")
	RETURN -1
ENDIF
RESULTS =
VARSET LOCAL
SELECTCASE ARGS
;------------------------------------------------------------
;First 인프레션계 KOJO 출력 처리(일반적으로 말하는 엑스트라 구문)
;------------------------------------------------------------
	CASE "ENCOUNTER"
		;TARGET에 발언자 설정(현재의TARGET는 LOCAL에 퇴피)
		LOCAL = TARGET
		TARGET = ARG:2
		;말존재 판정 and RESULTS에 문자열 대입
		CALL 미움받기(ARG:2)
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		;말존재하지 않으면 종료
		IF !RESULT
			TARGET = LOCAL
			RETURN 0
		ENDIF
		;말표시 설정이라면 표시
		IF FLAG:구상텍스트설정 > 0
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM M_KOJO%RESULTS%_ENCOUNTER_K{NO:TARGET}
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;SP 이벤트 계 KOJO 출력 처리
;------------------------------------------------------------
	CASE "SP_EVENT"
		;TARGET에 발언자 설정(현재의TARGET는 LOCAL에 퇴피)
		LOCAL = TARGET
		TARGET = ARG:2
		;말미표시라면 공통 말에
		IF FLAG:구상텍스트설정 <= 0
			;공통 말출력
			CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
			TARGET = LOCAL
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;TARGET의 발언
		;@M_KOJO_KX에서 RESULTS를 정의하면(자) 말이 입지 않고 기동한다
		;말존재 판정 and RESULTS에 문자열 대입
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		;말이 존재하지 않으면 공통 말에
		IF !RESULT
			;말표시 설정이라면 공통 표시
			IF FLAG:구상텍스트설정 > 0
				;공통 말출력
				CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
				TARGET = LOCAL
				RETURN 0
			ENDIF
		ENDIF
		;말표시
		IF FLAG:구상텍스트설정 > 0
			;TARGET 말출력
			;M_KOJO_KX로 RESULTS를 정의하면(자) 변환가능
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCCALLFORM M_KOJO%RESULTS%_SPEVENT_K{NO:TARGET}_{ARG:1}(ARG:3,ARG:4)
			CATCH
				RESETCOLOR
				IF FLAG:구상텍스트설정 > 0
					;공통 말출력
					CALLFORM SPEVENT_MESSAGE_{ARG:1}(ARG:3,ARG:4)
					TARGET = LOCAL
					RETURN 0
				ENDIF
			ENDCATCH
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;이벤트 계 KOJO 출력 처리
;------------------------------------------------------------
	CASE "EVENT"
		;TARGET에 발언자 설정(현재의TARGET는 LOCAL에 퇴피)
		LOCAL = TARGET
		TARGET = ARG:2
		;말미표시라면 공통 말에
		IF FLAG:구상텍스트설정 <= 0
			;공통 말출력
			TRYCALLFORM M_KOJO_EVENT_{ARG:1}(ARG:3, ARG:4)
			TARGET = LOCAL
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;TARGET의 발언
		;말존재 판정 and RESULTS에 문자열 대입
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		;말이 존재하지 않으면 공통 말에
		IF !RESULT || (FLAG:70 && !CFLAG:(NO:TARGET):시간정지구상있음) || (FLAG:역극 && !CFLAG:(NO:TARGET):역극구상있음)
			;말표시 설정이라면 공통 표시
			IF FLAG:구상텍스트설정 > 0
				;공통 말출력
				TRYCALLFORM M_KOJO_EVENT_{ARG:1}(ARG:3,ARG:4)
				TARGET = LOCAL
				RETURN 0
			ENDIF
		ENDIF
		;말표시
		IF FLAG:구상텍스트설정 > 0
			;TARGET 말출력
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			IF ARGS:1 != ""
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_%ARGS:1%(ARGS:2, ARG:3, ARG:4)
			ELSE
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_{ARG:1}(ARG:3, ARG:4)
			ENDIF
		ENDIF
		RESETCOLOR
		TARGET = LOCAL
;------------------------------------------------------------
;커맨드 계 KOJO 출력 처리
;------------------------------------------------------------
	CASE "COMMAND"
		;텍스트 설정
		IF FLAG:구상텍스트설정 <= 0
			;정경 텍스트 출력
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
			CALL DISPLAY_COM_IMAGE(SELECTCOM)
			RETURN 0
		ENDIF
		;볼개그 때
		IF TEQUIP:(ARG:2):20 && SELECTCOM != 107
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
			CALL DISPLAY_COM_IMAGE(SELECTCOM)
		ELSEIF (FLAG:70 && !CFLAG:(ARG:2):시간정지구상있음) || (CFLAG:(ARG:2):장난 >= 1 && !CFLAG:(ARG:2):면간구상있음) || (FLAG:역극 && !CFLAG:(ARG:2):역극구상있음)
			SIF FLAG:6 > 0
				CALL TRAIN_MESSAGE
			CALL DISPLAY_COM_IMAGE(SELECTCOM)
		ELSE
			RESULT = 0
			RESULTS =
			;;말존재 판정 and RESULTS에 문자열 대입
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			IF !RESULT
				SIF FLAG:6 > 0
					CALL TRAIN_MESSAGE
				SIF CFLAG:(NO:TARGET):오토신음
					CALL AUTO_AEGI(NO:TARGET)
				CALL DISPLAY_COM_IMAGE(SELECTCOM)
				RETURN 0
			ENDIF
			;SIF FLAG:구상색
				;TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
			IF TFLAG:50
				LOCAL = TFLAG:50
				LOCALS = SCOM
			ELSE
				LOCAL = SELECTCOM
				LOCALS = COM
			ENDIF
			TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_%LOCALS%_K{NO:TARGET}_{LOCAL}
			CATCH
				;묻지 않은 곳은 범용적인 처리가 가능
				RESULT = 0
				TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_COM_K{NO:TARGET}_00
				IF !RESULT
					IF FLAG:6 > 0
						CALL TRAIN_MESSAGE
						SIF CFLAG:(NO:TARGET):오토신음
							CALL AUTO_AEGI(NO:TARGET)
						RESETCOLOR
						RETURN 0
					ENDIF
				ENDIF
			ENDCATCH
		ENDIF
		RESETCOLOR
;------------------------------------------------------------
;카운터계 KOJO 출력 처리
;------------------------------------------------------------
	CASE "COUNTER"
		LOCAL = TARGET
		TARGET = ARG:2
		IF FLAG:구상텍스트설정 <= 0
			IF FLAG:6 > 0
				CALL EVENT_COUNTER_MESSAGE
				TARGET = LOCAL
			ENDIF
			RETURN 0
		ENDIF
		
		;볼개그
		IF TEQUIP:(ARG:2):20 && FLAG:6 > 0
			CALL EVENT_COUNTER_MESSAGE
			TARGET = LOCAL
		ELSE
			RESULT = 0
			RESULTS =	
			;말존재 판정 and RESULTS에 문자열 대입
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			IF !RESULT || (FLAG:역극 && !CFLAG:(ARG:2):역극구상있음)
				SIF FLAG:6 > 0
					CALL EVENT_COUNTER_MESSAGE
				TARGET = LOCAL
				RESETCOLOR
				RETURN 0
			ENDIF
			TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_COUNTER_K{NO:TARGET}_{TCVAR:20}
			CATCH
				IF FLAG:6 > 0
					CALL EVENT_COUNTER_MESSAGE
					TARGET = LOCAL
					RESETCOLOR
					RETURN 0
				ENDIF
			ENDCATCH
			TARGET = LOCAL
		ENDIF
		RESETCOLOR
;------------------------------------------------------------
;파라미터계 KOJO 출력 처리
;------------------------------------------------------------
	CASE "PALAM"
		SELECTCASE ARG:1
		;==== 사정시 말 ============
			CASE 1
				VARSET LOCAL
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_A
					RETURN 0
				ENDIF
				FOR LOCAL, GET_TARGETNUM(), -1 ,-1
					SIF TARGET:LOCAL < 1
						CONTINUE
					;TARGET, TARGET:1,…의 순서에 출력
					SIF LOCAL > 0 && TARGET:LOCAL == TARGET
						CONTINUE
					;말출력용의 플래그가 없다고 날린다
					SIF !TCVAR:(TARGET:LOCAL):50
						CONTINUE
					LOCAL:1 = TARGET
					TARGET = TARGET:LOCAL
					RESULT = 0
					RESULTS =
					;말존재 판정 and RESULTS에 문자열 대입
					IF CFLAG:TARGET:구상셀렉터
						TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
					ELSE
						TRYCALLFORM M_KOJO_K{NO:TARGET}
					ENDIF
					IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):시간정지구상있음) || (FLAG:역극 && !CFLAG:(NO:TARGET):역극구상있음)
						CALL PALAM_MESSAGE_A
						TARGET = LOCAL:1
						RETURN 0
					ENDIF
					;사정 말출력 특수 효과
					CALL KOJO_MESSAGE_PALAMCNG_A2(LOCAL)
					RESETCOLOR
					SIF !RESULT
						LOCAL:2 ++
					TARGET = LOCAL:1
					;콘돔 말은 대상 한사람만
					SIF GETBIT(TFLAG:1, 0)
						BREAK
				NEXT
		;==== 절정시 말 ============
			CASE 2
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_B
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;말존재 판정 and RESULTS에 문자열 대입
				IF CFLAG:TARGET:구상셀렉터
					TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
				ELSE
					TRYCALLFORM M_KOJO_K{NO:TARGET}
				ENDIF
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):시간정지구상있음) || (FLAG:역극 && !CFLAG:(NO:TARGET):역극구상있음)
					CALL PALAM_MESSAGE_B
					RETURN 0
				ENDIF
				IF CFLAG:(NO:TARGET):장난
					TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_B2_K{NO:TARGET}
					CATCH
						CALL PALAM_MESSAGE_B
					ENDCATCH
				ELSE
					TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_B_K{NO:TARGET}
					CATCH
						CALL PALAM_MESSAGE_B
					ENDCATCH
				ENDIF
				RESETCOLOR
		;==== 조교 뒷맛상 ============
			CASE 3
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:6 > 0
						CALL PALAM_MESSAGE_C
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;말존재 판정 and RESULTS에 문자열 대입
				IF CFLAG:TARGET:구상셀렉터
					TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
				ELSE
					TRYCALLFORM M_KOJO_K{NO:TARGET}
				ENDIF
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):시간정지구상있음) || (FLAG:역극 && !CFLAG:(NO:TARGET):역극구상있음)
					CALL PALAM_MESSAGE_C
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_C_K{NO:TARGET}
				RESETCOLOR
		;==== 파라미터 변동 말 ====
			CASE 4
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:정경텍스트설정 > 0
						CALL PALAM_MESSAGE_D
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				IF CFLAG:TARGET:구상셀렉터
					TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
				ELSE
					TRYCALLFORM M_KOJO_K{NO:TARGET}
				ENDIF
				IF (!RESULT &&  FLAG:6 > 0) || (FLAG:70 && !CFLAG:(NO:TARGET):시간정지구상있음) || (FLAG:역극 && !CFLAG:(NO:TARGET):역극구상있음)
					CALL PALAM_MESSAGE_D
					RETURN 0
				ENDIF
				SIF FLAG:구상색
					TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
				TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_D_K{NO:TARGET}
				RESETCOLOR
		;==== 촉수의 사정 ============
			CASE 5
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:정경텍스트설정 > 0
						CALL PALAM_MESSAGE_E
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;말존재 판정 and RESULTS에 문자열 대입
				IF CFLAG:TARGET:구상셀렉터
					TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
				ELSE
					TRYCALLFORM M_KOJO_K{NO:TARGET}
				ENDIF
				IF !RESULT && FLAG:정경텍스트설정 > 0
					CALL PALAM_MESSAGE_E
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_E_K{NO:TARGET}
		;==== 초조하게 해 ============
			CASE 6
				IF FLAG:구상텍스트설정 <= 0
					SIF FLAG:정경텍스트설정 > 0
						CALL PALAM_MESSAGE_F
					RETURN 0
				ENDIF
				RESULT = 0
				RESULTS =
				;말존재 판정 and RESULTS에 문자열 대입
				IF CFLAG:TARGET:구상셀렉터
					TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
				ELSE
					TRYCALLFORM M_KOJO_K{NO:TARGET}
				ENDIF
				IF !RESULT && FLAG:정경텍스트설정 > 0
					CALL PALAM_MESSAGE_F
					RETURN 0
				ENDIF
				TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_F_K{NO:TARGET}
		ENDSELECT
;	;각인계 KOJO 출력 처리
	CASE "MARK"
		IF FLAG:구상텍스트설정 <= 0
			SIF FLAG:정경텍스트설정 > 0
				CALL MARK_MESSAGE
			RETURN 0
		ENDIF
		RESULT = 0
		RESULTS =
		;말존재 판정 and RESULTS에 문자열 대입
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		IF !RESULT && FLAG:정경텍스트설정 > 0
			CALL MARK_MESSAGE
			RETURN 0
		ENDIF
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_MARKCNG_K{NO:TARGET}
		CATCH
			CALL MARK_MESSAGE
		ENDCATCH
		RESETCOLOR
	;커맨드 계 직접 호출
	CASE "DIRECT"
		SELECTCASE ARG:1
			CASE 351
				;볼개그
				IF TEQUIP:(ARG:2):20 && SELECTCOM != 107 && FLAG:정경텍스트설정 > 0
					RETURN 1
				ELSE
					RESULT = 0
					RESULTS =
					;말존재 판정 and RESULTS에 문자열 대입
					IF CFLAG:TARGET:구상셀렉터
						TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
					ELSE
						TRYCALLFORM M_KOJO_K{NO:TARGET}
					ENDIF
					IF FLAG:구상텍스트설정 > 0
						IF FLAG:구상색
							TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
						ELSE
							RESETCOLOR
						ENDIF
					ELSE
						RESETCOLOR
						RETURN 1
					ENDIF
					TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_COM_K{NO:TARGET}_351
					RESETCOLOR
				ENDIF
		ENDSELECT

	; 구상 내에서 소스를 추가하는 처리, TARGET고정
	CASE "EXTRASOURCE"
		RESULTS = 
		;구상표시
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			IF TFLAG:50
				LOCAL = TFLAG:50
				LOCALS = SCOM
			ELSE
				LOCAL = SELECTCOM
				LOCALS = COM
			ENDIF
			TRYCCALLFORM M_KOJO%RESULTS%_EXTRASOURCE_%LOCALS%_K{NO:TARGET}_{LOCAL}
			CATCH
				TRYCALLFORM M_KOJO%RESULTS%_EXTRASOURCE_%LOCALS%_K{NO:TARGET}_GENERAL
			ENDCATCH
		ENDIF
	;기본 플래그 설정
	CASE "FLAGSETTING"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		RESULTS:1 = 
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			IF RESULT
				CFLAG:TARGET:면간구상있음 = 0
				CFLAG:TARGET:시간정지구상있음 = 0
				CFLAG:TARGET:역극구상있음 = 0
				CFLAG:TARGET:파과캔슬구상있음 = 0
				CFLAG:TARGET:구상내껴안기판정_첫회 = 0
				CFLAG:TARGET:구상내껴안기판정_통상 = 0
				CFLAG:TARGET:덮침금지 = 0
				CFLAG:TARGET:내방불능 = 0
				TRYCCALLFORM M_KOJO%RESULTS%_FLAGSETTING_K{NO:TARGET}
				CATCH
					PRINTFORMW ※경고：(%CALLNAME:TARGET%\@ RESULTS:1 != "" ? _ # \@%RESULTS:1%구상) 기본 플래그 설정을 찾을 수 없습니다
				ENDCATCH
			ENDIF
		ELSE
			RESULT = 0
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;오나바레
	CASE "ONABARE"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		;구상표시
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET 구상 출력
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			SELECTCASE ARG:1
			CASE 1, 2
				TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_ONABARE_{ARG:1}(ARG:3)
				CATCH
					;무심코RETURN 1 해버려도 커버할 수 있도록 RESULT = 2로 설정
					RESULT = 2
				ENDCATCH
			CASE 3
				TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_ONABARE_{ARG:1}(ARG:3, ARG:4, ARG:5, ARG:6)
				CATCH
					RESULT = 2
				ENDCATCH
			CASE 4
				TRYCALLFORM M_KOJO_EVENT_K{NO:TARGET}_26_1(ARGS:1)
			ENDSELECT
			RESETCOLOR
		ELSE
			RESULT = 2
		ENDIF
		TARGET = LOCAL
		RETURN RESULT

	;커맨드 성공 여부 말제어
	CASE "SUCCESS"
		RESULT = 0
		RESULTS =
		;말존재 판정 and RESULTS에 문자열 대입
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		IF !RESULT
			TFLAG:192 = 0
			RETURN 0
		ENDIF
		IF TFLAG:50
			LOCAL = TFLAG:50
			LOCALS = SCOM
		ELSE
			LOCAL = SELECTCOM
			LOCALS = COM
		ENDIF
		TRYCCALLFORM M_KOJO%RESULTS%_SUCCESS_%LOCALS%_K{NO:TARGET}_{LOCAL}
		CATCH
			TRYCCALLFORM M_KOJO%RESULTS%_SUCCESS_K{NO:TARGET}_GENERAL
			CATCH
				TFLAG:192 = 0
			ENDCATCH
		ENDCATCH
	;넘어트리기 성공 여부 구강 제어, 파과 취소 구강 제어
	CASE "PERMISSION", "LOST_VIRGIN_STOP"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;구상 표시
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET 구상 출력
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			SELECTCASE ARGS
			CASE "PERMISSION"
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_PERMISSION_{ARG:1}
			CASE "LOST_VIRGIN_STOP"
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_LOST_VIRGIN_STOP
			ENDSELECT
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
		RETURN RESULT

	;육아
	CASE "CHILD"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		;구상 표시
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			SELECTCASE ARGS:1
			CASE "DAILY_구상편지"
				TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_CHILD_DAILY_구상편지(ARG:3)
				CATCH
					;うっかりRETURN 1しちゃってもカバーできるようにRESULT = 2に設定
					RESULT = 2
				ENDCATCH
			CASE "DAILY_공통편지"
				TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_CHILD_DAILY_공통편지(ARG:3, ARGS:2)
				CATCH
					RESULT = 2
				ENDCATCH
			CASEELSE
				TRYCCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_CHILD_%ARGS:1%
				CATCH
					RESULT = 2
				ENDCATCH
			ENDSELECT
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;弾幕勝負
	CASE "DANMAKU"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;인카운트 모브코용 구상표시
		IF ARG:3 == 1 && FLAG:구상텍스트설정 > 0
			CALL TALK_MOBGIRL(ARGS:1, ARG:1)
			RESETCOLOR
		;口上表示
		ELSEIF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM M_KOJO%RESULTS%_MESSAGE_COM_K{NO:TARGET}_DANMAKU(ARGS:1, ARG:1)
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
	;依頼
	CASE "IRAI"
		LOCAL = TARGET
		TARGET = ARG:1
		RESULT = 0
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			RESULT = 0
			TRYCALLFORM M_KOJO%RESULTS%_IRAI_K{NO:TARGET}(ARGS:1, ARGS:2, ARG:2)
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;贈り物
	CASE "GIFT"
		LOCAL = TARGET
		TARGET = ARG:2
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_GIFT(ARG:1, ARG:3, ARG:4, ARGS:1, ARGS:2)
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
	;デイリーイベント,DIARYと綴り間違えなように
	CASE "DAILY"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS =
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			IF RESULT
				SIF FLAG:구상색
					TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
				TRYCCALLFORM M_KOJO%RESULTS%_DAILY_EVENT_K{NO:TARGET}_{ARG:1}(ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARGS:1, ARGS:2)
				CATCH
					TRYCALLFORM DAILY_MESSAGE_{ARG:1}(ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARGS:1, ARGS:2)
				ENDCATCH
				RESETCOLOR
			ELSE
				TRYCALLFORM DAILY_MESSAGE_{ARG:1}(ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARGS:1, ARGS:2)
			ENDIF
		ELSE
			TRYCALLFORM DAILY_MESSAGE_{ARG:1}(ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARGS:1, ARGS:2)
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;日記
	CASE "DIARY"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;日記の存在判定
			RESULT = 0
			TRYCALLFORM DIARY%RESULTS%_K{NO:TARGET}_EXIST
			IF RESULT
				SIF FLAG:구상색
					TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
				SELECTCASE ARGS:1
				CASE "EXIST"
					RESULT = 1
				;存在判定を通っているので強制呼び出しで関数抜けチェックを兼ねる
				CASE "BEFORE_CHECK", "AFTER_CHECK"
					CALLFORM DIARY%RESULTS%_%ARGS:1%_K{NO:TARGET}
				CASE "TEXT"
					CALLFORM DIARY%RESULTS%_TEXT_K{NO:TARGET}(ARG:3, ARGS:2, ARG:4)
				ENDSELECT
				RESETCOLOR
			ENDIF
		ELSE
			RESULT = 0
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;ムシバトル
	CASE "MUSHI_BATTLE"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		RESULTS:1 = 
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;RESULTSを使うので退避してリセットしておく
			RESULTS:1 = %RESULTS%
			RESULTS = 
			MUSHI_NAME = %MUSHI_NAME:(MUSHI_STANDNUM:(ARG:3))%
			IF ARGS:1 == "구상색"
				TRYCALLFORM M_KOJO%RESULTS:1%_COLOR_K{NO:TARGET}
			ELSEIF GROUPMATCH(ARGS:1, "전장에 내보냈다", "공격시", "쓰러졌을시", "패퇴시")
				TRYCALLFORM M_KOJO%RESULTS:1%_MESSAGE_COM_K{NO:TARGET}_MUSHI_BATTLE(ARGS:1, ARG:1)
				IF RESULTS != ""
					CALL MUSHI_MSGSET(@"%RESULTS%")
					SIF MUSHI_COLOR:(ARG:3) != MUSHI_COLOR:1
						MUSHI_MSGCL:(RESULT) = MUSHI_COLOR:(ARG:3)
				ENDIF
			ELSE
				TRYCALLFORM M_KOJO%RESULTS:1%_MESSAGE_COM_K{NO:TARGET}_MUSHI_BATTLE(ARGS:1, ARG:1)
			ENDIF
		ENDIF
		TARGET = LOCAL
	;引力
	CASE "GRAVITY"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_GRAVITY(ARG:1)
		ENDIF
		TARGET = LOCAL
	;スイカ割り
	CASE "SUIKA"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		RESULTS = 
		RESULTS:1 = 
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;RESULTSを使うので退避してリセットしておく
			RESULTS:1 = %RESULTS%
			RESULTS = 
			SIF GROUPMATCH(ARGS:1, "衝突", "結果")
				TRYCALLFORM M_KOJO%RESULTS:1%_COLOR_K{NO:TARGET}
			TRYCALLFORM M_KOJO%RESULTS:1%_MESSAGE_COM_K{NO:TARGET}_SUIKA(ARGS:1, ARG:3)
		ENDIF
		TARGET = LOCAL
	;お出かけイベント
	CASE "ODEKAKE"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM RUN_INTO%RESULTS%_EVENT_K{NO:TARGET}(ARG:1)
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
	;セフレ契約
	CASE "SEX_FRIEND"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM KOJO%RESULTS%_SF_CONTRACT_EVENT_K{NO:TARGET}(ARGS:1)
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
	;毎朝
	CASE "BEFORETRAIN"
		LOCAL = TARGET
		TARGET = ARG:2
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			TRYCALLFORM K{NO:TARGET}_BEFORETRAIN%RESULTS%
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
	;風チラ
	CASE "PANCHIRA", "REV_PANCHIRA"
		LOCAL = TARGET
		TARGET = ARG:2
		RESULT = 0
		;口上表示
		IF FLAG:구상텍스트설정 > 0
			IF CFLAG:TARGET:구상셀렉터
				TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
			ELSE
				TRYCALLFORM M_KOJO_K{NO:TARGET}
			ENDIF
			;TARGET口上出力
			SIF FLAG:구상색
				TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{NO:TARGET}
			SELECTCASE ARGS
			CASE "PANCHIRA"
				PRINTL 
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_PANCHIRA
			CASE "REV_PANCHIRA"
				PRINTL 
				TRYCALLFORM M_KOJO%RESULTS%_EVENT_K{NO:TARGET}_REV_PANCHIRA
			ENDSELECT
			RESETCOLOR
		ENDIF
		TARGET = LOCAL
		RETURN RESULT
		
	;언젠가 오는 마지막을 위해
	CASE "ENDING"
		IF CFLAG:TARGET:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		SIF FLAG:구상색
			TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
		TRYCALLFORM M_KOJO_MESSAGE_ENDING_K{NO:TARGET}
		RESETCOLOR
		;custom shop button appear condition
	CASE "SHOP_BUTOON_CONDITION"
		RESULTS =
		;TARGET退让
		OriginTarget = TARGET
		TARGET = ARG:2
		IF CFLAG:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		TRYCCALLFORM KOJO%RESULTS%_CUSTOM_BUTTON_CONDITION_K{NO:TARGET}_{ARG:1}
			TARGET = OriginTarget
			RETURN RESULT
		CATCH
			TARGET = OriginTarget
			RETURN 0
		ENDCATCH

	;execute custon shop button 
	CASE "SHOP_BUTOON"
		RESULTS =
		;TARGET退让
		OriginTarget = TARGET
		TARGET = ARG:2
		IF CFLAG:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		TRYCALLFORM KOJO%RESULTS%_CUSTOM_BUTTON_K{NO:TARGET}_{ARG:1}
		TARGET = OriginTarget

	;kojo update
	;copy from ckr
	CASE "KOJO_VERSION"
		OriginTarget = TARGET
		TARGET = ARG:2
		IF CFLAG:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		;get the version of this kojo
		;if there is no such function, remain to 0
		TRYCCALLFORM KOJO%RESULTS%_VERSION_K{NO:TARGET}
			IF CFLAG:kojo_version < RESULT
				TRYCCALLFORM KOJO%RESULTS%_VERSION_UPDATE_K{NO:TARGET}
					CFLAG:kojo_version = RESULT
				CATCH
				ENDCATCH
			ENDIF
		CATCH
			CFLAG:kojo_version = 0
		ENDCATCH
		TARGET = OriginTarget
;------------------------------------------------------------
;kojo custom button
;------------------------------------------------------------
	CASE "COM"
		SIF TARGET < 0 || TARGET >= CHARANUM
			RETURN 0
		IF CFLAG:구상셀렉터
			TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:구상셀렉터}
		ELSE
			TRYCALLFORM M_KOJO_K{NO:TARGET}
		ENDIF
		SELECTCASE ARG:1
		;==== COM info ===========
			CASE 0
				TRYCALLFORM KOJO%RESULTS%_COM_NAME_K{NO:TARGET}_{ARG:3}
		;==== COM_ABLE ===========
			CASE 1
				TRYCJUMPFORM KOJO%RESULTS%_COM_ABLE_K{NO:TARGET}_{ARG:3}
				CATCH
					RETURN 0
				ENDCATCH
		;==== COM body ===========
			CASE 2
				TRYCJUMPFORM KOJO%RESULTS%_COM_K{NO:TARGET}_{ARG:3}
				CATCH
					THROW [KOJO COM ERROR] can't find "KOJO%RESULTS%_COM_K{NO:TARGET}_{ARG:3}"
				ENDCATCH
		;==== CAN_COM ============
			CASE 3
				TRYCJUMPFORM KOJO%RESULTS%_CAN_COM_K{NO:TARGET}_{ARG:3}(ARG:4)
				CATCH
					RETURN -1
				ENDCATCH
		ENDSELECT
ENDSELECT

;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG_A2
;사정 말출력의 특수 효과
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_A2(ARG)
;ARG W펠라등으로 TARGET라면 ARG=0, 두 명째라면 ARG=1
IF CFLAG:TARGET:구상셀렉터
	TRYCALLFORM M_KOJO_K{NO:TARGET}_{CFLAG:TARGET:구상셀렉터}
ELSE
	TRYCALLFORM M_KOJO_K{NO:TARGET}
ENDIF
;-------------------------------------------------
;사정 대상이 비타겟 또한 비복수 캐릭터 SCOMF 사정
;-------------------------------------------------
IF MASTER_POSE(0, SET_P) != TARGET && !ARG
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_00(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_00(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;콘돔
;-------------------------------------------------
IF GETBIT(TCVAR:50, 0)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_0(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_0(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;授乳プレイ
;-------------------------------------------------
IF GETBIT(TCVAR:50,26)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_26(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;이라마치오
;-------------------------------------------------
IF GETBIT(TCVAR:50,23)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_23(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_23(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;식스나인
;-------------------------------------------------
IF GETBIT(TCVAR:50,22)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_22(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;파이즈리 펠라
;-------------------------------------------------
IF GETBIT(TCVAR:50,21)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_21(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;수음 펠라
;-------------------------------------------------
IF GETBIT(TCVAR:50,20)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_20(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;애널 봉사
;-------------------------------------------------
IF GETBIT(TCVAR:50,9)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_9(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;거품춤
;-------------------------------------------------
IF GETBIT(TCVAR:50,8)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_8(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;풋잡
;-------------------------------------------------
IF GETBIT(TCVAR:50,7)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_7(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_7(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;가랑이
;-------------------------------------------------
IF GETBIT(TCVAR:50,6)
	TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_6(ARG)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF
;-------------------------------------------------
;파이즈리
;-------------------------------------------------
IF GETBIT(TCVAR:50,5)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_5(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_5(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;펠라치오
;-------------------------------------------------
IF GETBIT(TCVAR:50,4)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_4(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_4(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;수음
;-------------------------------------------------
IF GETBIT(TCVAR:50,3)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_3(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_3(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;애널
;-------------------------------------------------
IF GETBIT(TCVAR:50,2)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_2(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_2(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
;-------------------------------------------------
;질내
;-------------------------------------------------
IF GETBIT(TCVAR:50,1)
	IF CFLAG:(NO:TARGET):장난 
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A2_K{NO:TARGET}_1(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO%RESULTS%_MESSAGE_PALAMCNG_A_K{NO:TARGET}_1(ARG)
			RETURN RESULT
		CATCH
		ENDCATCH
	ENDIF
ENDIF
CALL PALAM_MESSAGE_A
RESETCOLOR
RETURN 0

@SILENCE_TSTOP(ARG)
#FUNCTION
;시간정지중에는 말하지 않는 캐릭터를 지정
SIF GROUPMATCH(ARG,39,41,54,86,87,88)
	RETURNF 1

;show custom talent
@showKojoCustomTalent(chara)
	#DIM chara
	#DIM index
	#DIM DYNAMIC length = 1
	#DIM DYNAMIC titlePrint
	FOR index, 0, MAX_CUSTOM_TALENT
		IF custom_talent:chara:index
			;初个
			IF !titlePrint
				PRINT   구상  ：
				titlePrint = 1
			ENDIF
			IF length > 60
				length = 1
				PRINTL
				PRINT       
			ENDIF
			;打印
			SIF custom_talent_color:chara:index
				SETCOLOR custom_talent_color:chara:index
			PRINTFORM [%custom_talent_name:chara:index%]
			RESETCOLOR
			length += 2
			length += STRLENS(custom_talent_name:chara:index)
		ENDIF
	NEXT
	PRINTL

;create the list of who have custon shop button kojo
@createKojoShopButtonTargetList
	#DIM chara
	#DIM index
	VARSET ShopButtonTargetList, 0
	ShopButtonTargetNum = 0
	FOR chara, 0, CHARANUM
		;judge the chara if have custom shop button kojo
		FOR index, 0, 10
			CALL KOJO_MESSAGE_SEND("SHOP_BUTOON_CONDITION", index, chara)
			IF RESULT == 1
				ShopButtonTargetList:ShopButtonTargetNum = chara
				ShopButtonTargetNum ++
				BREAK
			ENDIF
		NEXT
	NEXT
	;don't have
	SIF ShopButtonTargetNum == 0
		RETURN
	ShopButtonTargetId = FINDELEMENT(ShopButtonTargetList, ShopButtonTarget)
	;defaul the first chara
	IF ShopButtonTargetId == -1 || ShopButtonTargetId >= ShopButtonTargetNum
		ShopButtonTargetId = 0
		ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
	ENDIF

;show the ui of kojo button
@showKojoShopButton
	#DIM index
	#DIMS DYNAMIC buttonList, 10
	#DIM DYNAMIC buttonIndex
	#DIM DYNAMIC printNum
	;no
	SIF ShopButtonTargetNum == 0
		RETURN
	PRINTL
	SETCOLOR 103,228,126
	PRINT 【구상메뉴】
	IF ShopButtonTargetId <= 0
		SETCOLOR 0x747474
		PRINTPLAIN 【＜】
		RESETCOLOR
	ELSE
		SETCOLOR 103,228,126
		PRINTBUTTON "【＜】", 798
	ENDIF
	SETCOLOR 103,228,126
	PRINTBUTTON @"【%CALLNAME:ShopButtonTarget%】", 797
	RESETCOLOR
	IF ShopButtonTargetId + 1 >= ShopButtonTargetNum
		SETCOLOR 0x747474
		PRINTPLAIN 【＞】
	ELSE
		SETCOLOR 103,228,126
		PRINTBUTTON "【＞】", 799
	ENDIF
	RESETCOLOR
	;统计可用的button
	FOR index, 0, 10
		CALL KOJO_MESSAGE_SEND("SHOP_BUTOON_CONDITION", index, ShopButtonTarget)
		IF RESULT
			SIF printNum % 3 == 0
				PRINTL
			PRINTFORMLC [{800 + index}] %custom_button_name:ShopButtonTarget:index%
			printNum ++
		ENDIF
	NEXT
	PRINTL

;use custon kojo button
@useKojoShopButton, comId
	#DIM comId
	;PRINTFORML comid {comId} ShopButtonTarget{ShopButtonTarget}
	CALL KOJO_MESSAGE_SEND("SHOP_BUTOON_CONDITION", comId, ShopButtonTarget)
	SIF RESULT
		CALL KOJO_MESSAGE_SEND("SHOP_BUTOON", comId, ShopButtonTarget)
	WAIT
;check the chara who have the custon button
@choiceButtonTarget
	#DIM line
	#DIM DYNAMIC page
	#DIM DYNAMIC maxPage
	#DIM index
	maxPage = ShopButtonTargetNum / 20
	SIF ShopButtonTargetNum % 20
		maxPage ++
	line = LINECOUNT
	WHILE 1
		DRAWLINE
		;need to translate
		;PRINTFORML the chara whose kojo have custom shop button＜page.{page + 1}/{maxPage}＞
		PRINTFORML 커스텀 SHOP버튼이 존재하는 캐릭터 구상 ＜page.{page + 1}/{maxPage}＞
		DRAWLINE
		FOR index, page * 20, MIN(page * 20 + 20, ShopButtonTargetNum)
			CALL CHARA_BUTTON(index, ShopButtonTargetList:index)
		NEXT
		DRAWLINE
		IF page > 0
			PRINTLC [4444] 다음캐릭터
		ELSE
			PRINTLC  
		ENDIF
		PRINTLC [5555] 돌아가기
		IF page + 1 < maxPage
			PRINTLC [6666] 이전캐릭터
		ELSE
			PRINTLC  
		ENDIF
		INPUT
		SELECTCASE RESULT
			CASE page * 20 TO MIN(page * 20 + 20, ShopButtonTargetNum)
				ShopButtonTargetId = RESULT
				ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
				RETURN
			CASE 4444
				SIF page > 0
					page --
			CASE 6666
				SIF page + 1 < maxPage
					page ++
			CASE 5555
				RETURN
		ENDSELECT
		CLEARLINE LINECOUNT - line
	WEND
