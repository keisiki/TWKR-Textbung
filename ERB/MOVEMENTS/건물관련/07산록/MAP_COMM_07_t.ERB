;--------------------------------------------------
;지점명 변경 함수
;--------------------------------------------------
@MAP_PLACENAME_7(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P734오두막
			IF CFLAG:MASTER:초기위치 == ARG
				RETURNF CALLNAME:MASTER + "의 오두막"
			ELSE
				RETURNF "빈 오두막"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このマップでは未実装
			IF FLAG:더부살이캐릭터
				RETURNF CALLNAME:(FLAG:더부살이캐릭터) + "의 방"
			ELSE
				RETURNF "빈 방"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "길의 저편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "산록의 밖"
ENDIF

;--------------------------------------------------
;장소 ARG가 어느 지역에 있는지
;표시하는 맵의 번호와 연동 (TFLAG:맵변환)
;ARG     장소ID
;--------------------------------------------------
@MAP_AREASELECT_7(ARG)
#FUNCTION
SELECTCASE ARG
	;秋姉妹の家
	CASE P715아키자매의집・밭 TO P719아키자매의집・손님방
		RETURNF 2
	;선인의저택・１階～２階
	CASE P720선인저택・부엌 TO P725선인저택・큰수리방, P729선인저택・현관, P730선인저택・마당
		RETURNF 3
	;선인의저택・２階～３階
	CASE P726선인저택・거실, P727선인저택・카센방, P728선인저택・빈방
		RETURNF 4
	;妖怪の山・山麓
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;내부구조가 있는 장소의 내부 판정
;		TFLAG:맵변환	내부감지(察知)가 필요한 맵 번호
;				P_ID  	표시버튼의 장소ID
;				Q_ID  	판정하는 내부 장소ID, 반드시 P_ID와 일치하는 수치도 포함
;--------------------------------------------------
@GETMAP_BUILDING_7(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:맵변환 == 1
	SELECTCASE P_ID
		;秋姉妹の家
		CASE P716아키자매의집・봉당
			SELECTCASE Q_ID
			CASE P716아키자매의집・봉당 TO P719아키자매의집・손님방
				RETURNF 1
			ENDSELECT
		;선인의저택
		CASE P729선인저택・현관
			SELECTCASE Q_ID
			CASE P720선인저택・부엌 TO P729선인저택・현관
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;이동제한설정
;ARG    장소ID
;CHARA  이동하는 캐릭터ID
;RETURNF 1=이동 가능, 0=이동 불가능
;TFLAG:이동불능메시지   ↓の@MAP_CANNOT_WORD용 설정
;--------------------------------------------------
@MAP_CAN_MOVE_7(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;華扇ハウス
	CASE P720선인저택・부엌 TO P730선인저택・마당
		;本人は除外
		SIF CHARA == [[카센]]
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:CHARA:현재위치, P720선인저택・부엌, P730선인저택・마당)
			RETURNF 1
		;恋慕か住み込み
		IF CHARA == MASTER && (INRANGE(CFLAG:MASTER:초기위치, P720선인저택・부엌, P730선인저택・마당) || TALENT:[[카센]]:연모)
			RETURNF 1
		;デート相手が天子か自分が天子
		ELSEIF FLAG:데이트상대 == [[텐시]] || CHARA == [[텐시]]
			RETURNF 1
		;教養が足りないとたどり着けない
		ELSEIF ABL:CHARA:교양 < 3
			TFLAG:이동불능메시지 = 1
			RETURNF 0
			;戦闘力3以上必要
		ELSEIF ABL:CHARA:전투능력 < 3 && !FLAG:시간정지
			TFLAG:이동불능메시지 = 2
			RETURNF 0
		ENDIF
	CASE P733세이자은신처
		;あなたは恋慕があれば入れてもらえる
		IF TALENT:[[세이자]]:연모 && CHARA == MASTER
			RETURNF 1
		;本人は入れる
		ELSEIF CHARA == [[세이자]]
			RETURNF 1
		ELSE
			TFLAG:이동불능메시지 = 3
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;이동불능시 표시되는 메시지
;--------------------------------------------------
@MAP_CANNOT_WORD_7(ARG)
SELECTCASE TFLAG:이동불능메시지
	CASE 1
		PRINTFORMW 조금 전부터 저택과의 거리가 전혀 줄어들지 않는다……
		PRINTFORMW 같은 곳을 빙빙 돌고 있는 것 같다
		CLEARLINE 3
		TIME += 30
	CASE 2
		PRINTFORMW 뜰에 웅크리고 있던 무서운 호랑이가, %CALLNAME:MASTER%의 모습을 알아차리고 으르렁거렸다
		PRINTFORMW 다가가지 않는 편이 좋을 것 같다
		CLEARLINE 3
		TIME += 15
	CASE 3
		PRINTFORMW 어디에 있는지 모른다…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;거점 전용의 정경묘사
;WAIT는 걸리지 않도록
;-----------------------------------------
@MAP_UNIQUEMESSAGE_7
SELECTCASE CFLAG:MASTER:현재위치
	CASE P703현무의늪
		IF DAY:2 == 2 && TALENT:MASTER:형상 == 4 && GROUPMATCH(TIME:2, 5, 6, 7)
			PRINTFORML 고간의 불빛에 매료된 반딧불이들이, %CALLNAME:MASTER%의 주위를 맴돌고 있다
			PRINTFORML 환상적인 광경이다……
		ENDIF
ENDSELECT

;-----------------------------------------
;거점의 장소ID를 외출맵상의 장소ID로 치환하는 함수
;ARG      대상의 장소ID (거점맵)
;RETURNF  교체 장소ID (외출맵)
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_7(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P703현무의늪, P704캇파의아지트, P735물놀이터
		RETURNF 현무의늪
	CASE P708큰두꺼비의연못
		RETURNF 큰두꺼비의연못
	CASE P733세이자은신처
		RETURNF 세이자은신처앞
	CASE P720선인저택・부엌 TO P730선인저택・마당
		RETURNF 선인의저택
	CASE P712간헐천지하센터
		RETURNF 간헐천지하센터
	CASE P738깊은숲속 TO P740야마와로의아지트
		RETURNF 깊은숲속
	CASEELSE
		RETURNF 산록의수해
ENDSELECT
RETURNF 0

;--------------------------------------------------
;맵의 소개문
;--------------------------------------------------
@MAP소개_7
;とりあえず5行
PRINTFORML 수많은 채집 포인트와 두 곳의 낚시 포인트도 갖추고 있는, 산림업자가 몹시 탐낼 거점.
PRINTFORML 간헐천 지하 센터에서는 작열 지옥터에 직통할 수 있는 것 외에, 산정으로 가는 단락로도 있다.
PRINTFORML 세이자의 집이나 카센의 집은 특정 조건을 충족해야 들어갈 수 있다.
PRINTFORML 비천애에서는 도박장이 개장되어, 이름 없는 요괴들이 날마다 노름으로 흥청거리고 있다.
PRINTFORML 
PRINTFORML 
;PRINTFORML 제재소와 벌채 포인트를 모두 갖기 때문에 제재소의 영업시간 내라면 벌채 → 제재 → 벌채……
;PRINTFORML 를 반복하면 효율적으로 재료를 모아 갈 수 있다.
