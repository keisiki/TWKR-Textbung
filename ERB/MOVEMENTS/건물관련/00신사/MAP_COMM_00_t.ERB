;--------------------------------------------------
;地点名変更関数
;--------------------------------------------------
@MAP_PLACENAME_0(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P53창고
			IF CFLAG:MASTER:초기위치 == ARG
				RETURNF CALLNAME:MASTER + "의 창고"
			ELSE
				RETURNF "창고"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:더부살이캐릭터
				RETURNF CALLNAME:(FLAG:더부살이캐릭터) + "의 오두막"
			ELSE
				RETURNF "낡은 오두막"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "참배길 저편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "신사 밖"
ENDIF

;--------------------------------------------------
;場所ARGがどのエリアにあるか
;表示するマップの番号と連動（TFLAG:マップ切り替え）
;ARG     場所ID
;--------------------------------------------------
@MAP_AREASELECT_0(ARG)
#FUNCTION
SELECTCASE ARG
	;三妖精の家
	CASE P27삼요정의집・객실 TO P35삼요정의집・지하실
		RETURNF 3
	;몽환유적
	CASE P36몽환유적앞 TO P51몽환유적・창고
		RETURNF 2
	;神社周辺
	CASE P22참배길 TO P25수호신의숲
		RETURNF 4
	;博麗神社
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部構造がある場所の内部の判定
;TFLAG:マップ切り替え  内部察知が必要なマップ番号
;                P_ID  表示ボタンの場所ID
;                Q_ID  判定する内部の場所ID,必ずP_IDと一致した数値も含める
;--------------------------------------------------
@GETMAP_BUILDING_0(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:맵변환 == 4
	SELECTCASE P_ID
		;三妖精の家
		CASE P27삼요정의집・객실
			SELECTCASE Q_ID
			CASE P27삼요정의집・객실 TO P35삼요정의집・지하실
				RETURNF 1
			ENDSELECT
		;몽환유적
		CASE P36몽환유적앞
			SELECTCASE Q_ID
			CASE P36몽환유적앞 TO P51몽환유적・창고
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移動制限設定
;ARG    場所ID
;CHARA  移動するキャラID
;RETURNF 1=移動できる,0=移動できない
;TFLAG:이동불능메시지   ↓の@MAP_CANNOT_WORD用の設定
;--------------------------------------------------
@MAP_CAN_MOVE_0(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;虫かご
	CASE P54난쟁이벌레통
		;小人じゃないと入れない
		IF TALENT:CHARA:체형 > -5
			TFLAG:이동불능메시지 = 2
			RETURNF 0
		ENDIF
	;三妖精ハウス
	CASE P27삼요정의집・객실 TO P35삼요정의집・지하실
		;妖精なら入れる
		SIF TALENT:CHARA:요정
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:CHARA:현재위치, P27삼요정의집・객실, P35삼요정의집・지하실)
			RETURNF 1
		;あなたは恋慕か教養LV1以上で入れる
		IF CHARA == MASTER
			IF TALENT:[[서니]]:연모 || TALENT:[[루나]]:연모 || TALENT:[[스타]]:연모 || ABL:CHARA:교양 >= 1
				RETURNF 1
			ELSEIF INRANGE(CFLAG:MASTER:초기위치, P27삼요정의집・객실, P35삼요정의집・지하실)
				RETURNF 1
			ELSE
				TFLAG:이동불능메시지 = 1
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;몽환유적,あなた以外は教養LV3が必要
	CASE P37몽환유적・1층통로 TO P51몽환유적・창고
		;中からは動ける
		IF INRANGE(CFLAG:CHARA:현재위치, P37몽환유적・1층통로, P51몽환유적・창고)
			RETURNF 1
		ELSEIF ABL:CHARA:교양 <= 2 && CHARA != MASTER
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移動不能時に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_0(ARG)
SELECTCASE TFLAG:이동불능메시지
	CASE 1
		PRINTFORMW 미숙한 %마스터로%는 찾아낼 수가 없다…
	CASE 2
		PRINTFORMW 도저히 %마스터로%는 들어갈 수 없다
ENDSELECT
CLEARLINE 2
REUSELASTLINE

;-----------------------------------------
;拠点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_0
SELECTCASE CFLAG:MASTER:현재위치
	CASEELSE
ENDSELECT

;-----------------------------------------
;非自宅時に差し替える場所IDを返す関数
;ARG,対象ID
;RETURNF,差し替え済み対象ID
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_0(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P1토리이, P22참배길
		RETURNF 벚꽃길토리이
	CASE P2경내, P5개수대, P6헛간, P20본전뒷편, P21모리야신사분사
		RETURNF 신사경내
	;賽銭箱,本堂
	CASE P3새전함, P4본당
		RETURNF 신사본당
	;三妖精の家
	CASE P27삼요정의집・객실 TO P35삼요정의집・지하실
		RETURNF 요정의큰나무
	;몽환유적
	CASE P36몽환유적앞 TO P51몽환유적・창고
		RETURNF 몽환유적
	CASEELSE
		RETURNF 신사툇마루
ENDSELECT
RETURNF 0

;--------------------------------------------------
;マップの紹介文
;--------------------------------------------------
@MAP소개_0
;일단 5줄
PRINTFORML eratohoTW에 있어서 원조 맵. 여러 가지 설정을 담고 있기 때문에, 신사는 큰 세대를 이루고 있다.
PRINTFORML 신사 외에도 삼요정이 살고 있는 큰 나무와 유메미&치유리가 사는 몽환 유적이 있다.
PRINTFORML 기본적인 시설은 대충 갖추어져 있고, 업무차 방문하는 캐릭터도 많다.
PRINTFORML 또한, 삼요정의 집은 교양이 일정 이상 있어야 들어갈 수 있다.
PRINTFORML 또 모리야 세 명의 신 중 하나와 동행중이라면 분사에서 모리야신사로 워프할 수 있다.