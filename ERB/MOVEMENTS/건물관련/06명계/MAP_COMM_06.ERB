;--------------------------------------------------
;지점명 변경 함수
;--------------------------------------------------
@MAP_PLACENAME_6(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P618시비곡직청빈방, P625백옥루빈방, P634야쿠모가빈방
			IF CFLAG:MASTER:초기위치 == ARG
				RETURNF CALLNAME:MASTER + "의 방" ;私室
			ELSE
				RETURNF "빈 방"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このマップでは未実装
			IF FLAG:더부살이캐릭터
				RETURNF CALLNAME:(FLAG:더부살이캐릭터) + "의 방"
			ELSE
				RETURNF "빈 방"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "길의 저편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "명계의 밖"
ENDIF

;--------------------------------------------------
;장소 ARG가 어느 지역에 있는지
;표시하는 맵의 번호와 연동 (TFLAG:맵변환)
;ARG     장소ID
;--------------------------------------------------
@MAP_AREASELECT_6(ARG)
#FUNCTION
SELECTCASE ARG
	;是非曲直庁
	CASE P610시비곡직청홀 TO P618시비곡직청빈방
		RETURNF 2
	;백옥루
	CASE P620명계의문 TO P629사이교
		RETURNF 3
	;八雲邸
	CASE P630야쿠모가현관 TO P639야쿠모가거실
		RETURNF 4
	;축생계
	CASE P640이계의문 TO P655유마댁목욕탕
		RETURNF 5
	;피안
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;내부구조가 있는 장소의 내부 판정
;		TFLAG:맵변환	내부감지(察知)가 필요한 맵 번호
;				P_ID  	표시버튼의 장소ID
;				Q_ID  	판정하는 내부 장소ID, 반드시 P_ID와 일치하는 수치도 포함
;--------------------------------------------------
@GETMAP_BUILDING_6(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:맵변환 == 1
	SELECTCASE P_ID
		;是非曲直庁
		CASE P610시비곡직청홀
			SELECTCASE Q_ID
			CASE P610시비곡직청홀 TO P618시비곡직청빈방
				RETURNF 1
			ENDSELECT
		;冥界
		CASE P620명계의문
			SELECTCASE Q_ID
			CASE P620명계의문 TO P629사이교
				RETURNF 1
			ENDSELECT
		;八雲邸
		CASE P630야쿠모가현관
			SELECTCASE Q_ID
			CASE P630야쿠모가현관 TO P639야쿠모가거실
				RETURNF 1
			ENDSELECT
		;축생계
		CASE P640이계의문
			SELECTCASE Q_ID
			CASE P640이계의문 TO P655유마댁목욕탕
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;이동제한설정
;ARG    장소ID
;CHARA  이동하는 캐릭터ID
;RETURNF 1=이동 가능, 0=이동 불가능
;TFLAG:이동불능메시지   ↓の@MAP_CANNOT_WORD용 설정
;--------------------------------------------------
@MAP_CAN_MOVE_6(ARG, CHARA)
#FUNCTION
#DIM CHARA
#DIM 家主
SELECTCASE ARG
	;各組長の部屋
	CASE P646야치에댁목욕탕 TO P655유마댁목욕탕
		IF GROUPMATCH(ARG, P646야치에댁목욕탕, P647야치에댁거실, P648야치에댁침실)
			家主 = [[야치에]]
			LOCAL:1 = P646야치에댁목욕탕
			LOCAL:2 = P647야치에댁거실
			LOCAL:3 = P648야치에댁침실
		ELSEIF GROUPMATCH(ARG, P650사키댁거실, P651사키댁침실, P652사키댁목욕탕)
			家主 = [[사키]]
			LOCAL:1 = P650사키댁거실
			LOCAL:2 = P651사키댁침실
			LOCAL:3 = P652사키댁목욕탕
		ELSEIF GROUPMATCH(ARG, P653유마댁거실, P654유마댁침실, P655유마댁목욕탕)
			家主 = [[유마]]
			LOCAL:1 = P653유마댁거실
			LOCAL:2 = P654유마댁침실
			LOCAL:3 = P655유마댁목욕탕
		ENDIF
		;除外
		SIF CHARA == 家主
			RETURNF 1
		;中からは動ける
		SIF INRANGE(CFLAG:CHARA:현재위치, LOCAL:1, LOCAL:3)
			RETURNF 1
		;MAIN_MAP == 6
		SIF INRANGE(CFLAG:CHARA:초기위치, LOCAL:1, LOCAL:3)
			RETURNF 1
		;あなた
		IF CHARA == MASTER
			;同行か恋慕なら入れる（家主睡眠中も可
			IF CFLAG:家主:동행 || TALENT:家主:연모
				RETURNF 1
			;家主がいて起きてる
			ELSEIF INRANGE(CFLAG:家主:현재위치, LOCAL:1, LOCAL:3) && SHIRAHU(家主)
				RETURNF 1
			;家主以外の誰かがいる,この人物は寝てたりしても良い
			ELSEIF IN_ROOM_NUMBER(LOCAL:1) || IN_ROOM_NUMBER(LOCAL:2) || IN_ROOM_NUMBER(LOCAL:3)
				RETURNF 1
			ELSE
				TFLAG:이동불능메시지 = 999
				RETURNF 0
			ENDIF
		;組長
		ELSEIF GROUPMATCH(CHARA, [[야치에]], [[사키]], 143)
			;家主以外の誰かがいる,この人物は寝てたりしても良い
			IF IN_ROOM_NUMBER(LOCAL:1) || IN_ROOM_NUMBER(LOCAL:2) || IN_ROOM_NUMBER(LOCAL:3)
				RETURNF 1
			ELSE
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;誰かが住んでないと入れない
	CASE P618시비곡직청빈방, P625백옥루빈방, P634야쿠모가빈방
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:초기위치 != ARG
			TFLAG:이동불능메시지 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;이동불능시 표시되는 메시지
;--------------------------------------------------
@MAP_CANNOT_WORD_6(ARG)
RETURN
SELECTCASE TFLAG:이동불능메시지
	CASE 1
		PRINTFORMW ※ここにメッセージを入れる。行数+1のCLEARLINEをする
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;거점 전용의 정경묘사
;WAIT는 걸리지 않도록
;-----------------------------------------
@MAP_UNIQUEMESSAGE_6
SELECTCASE CFLAG:MASTER:현재위치
	CASE P604피안
		PRINTFORML 삼도천에서는 때때로 나룻배가 왕래하고 있다
	CASE P610시비곡직청홀
		PRINTFORML 시비곡직청의 홀이다
		PRINTFORML 만듦새는 훌륭하지만 노후화되어 있어, 어쩐지 돈이 없는 듯한 느낌이 든다
ENDSELECT

;-----------------------------------------
;거점의 장소ID를 외출맵상의 장소ID로 치환하는 함수
;ARG      대상의 장소ID (거점맵)
;RETURNF  교체 장소ID (외출맵)
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_6(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P601중유의길
		RETURNF 중유의길
	CASE P630야쿠모가현관 TO P639야쿠모가거실
		RETURNF 마요이가
	CASE P604피안, P610시비곡직청홀 TO P618시비곡직청빈방
		RETURNF 피안
	CASE P620명계의문, P621백옥루정원, P629사이교
		RETURNF 백옥루정원
	CASE P620명계의문 TO P628백옥루거실
		RETURNF 백옥루
	CASE P642영장원 TO P645마유미의방
		RETURNF 영장원
	CASE P640이계의문 TO P655유마댁목욕탕
		RETURNF 축생계
	CASE P605지옥, P606히사미집, P607잔무집
		RETURNF 지옥
	CASEELSE
		RETURNF 삼도천
ENDSELECT
RETURNF 0

;--------------------------------------------------
;맵의 소개문
;--------------------------------------------------
@MAP소개_6
;とりあえず5行
PRINTFORML 명계・피안등 죽은자의 세계에 더해 야쿠모 일가가 사는 마요이가를 포함한 맵。
PRINTFORML 원작에서는 방문도 힘든 장소뿐이어서, 다른 거점으로의 접근성은 그다지 좋지 않다。
PRINTFORML 하지만 era의 당신이라면 교양만 있으면 삼도천도 마음껏 건너, 문제없이 사는것도 가능。
PRINTFORML 도와줄 일이 적다는 것 말고는 생활수준도 다른 거점과 별반 다르지 않은 것 같다。
PRINTFORML 또한 삼도천에서는 돌쌓기 경연이 열려, 참가자들이 밤낮으로 각축을 벌이고 있다。
