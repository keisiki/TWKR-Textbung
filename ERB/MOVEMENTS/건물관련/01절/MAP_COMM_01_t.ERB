;--------------------------------------------------
;지점명 변경 함수
;--------------------------------------------------
@MAP_PLACENAME_1(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE SUMIKOMI_ROOM
			;※このマップでは未実装
			IF FLAG:더부살이캐릭터
				RETURNF CALLNAME:(FLAG:더부살이캐릭터) + "의 방"
			ELSE
				RETURNF "빈 방"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "참배길 저편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "길의 저편"
ENDIF

;--------------------------------------------------
;장소 ARG가 어느 지역에 있는지
;표시하는 맵의 번호와 연동 (TFLAG:맵변환)
;ARG     장소ID
;--------------------------------------------------
@MAP_AREASELECT_1(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P140지장길
		RETURNF 1
	;命蓮寺
	CASE P101산문 TO P122범종루, P135복도 TO P137마미조의거처
		RETURNF 1
	;神霊廟
	CASEELSE
		RETURNF 2
ENDSELECT

;--------------------------------------------------
;내부구조가 있는 장소의 내부 판정
;		TFLAG:맵변환	내부감지(察知)가 필요한 맵 번호
;				P_ID  	표시버튼의 장소ID
;				Q_ID  	판정하는 내부 장소ID, 반드시 P_ID와 일치하는 수치도 포함
;--------------------------------------------------
@GETMAP_BUILDING_1(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:맵변환 == 1
	SELECTCASE P_ID
		;神霊廟
		CASE P123영묘대문
			SELECTCASE Q_ID
			CASE P123영묘대문 TO P134곳간, P138종루, P139사선루
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:맵변환 == 2
	SELECTCASE P_ID
		;命蓮寺
		CASE P101산문
			SELECTCASE Q_ID
			CASE P101산문 TO P122범종루, P135복도 TO P137마미조의거처
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;이동제한설정
;ARG    장소ID
;CHARA  이동하는 캐릭터ID
;RETURNF 1=이동 가능, 0=이동 불가능
;TFLAG:이동불능메시지   ↓の@MAP_CANNOT_WORD용 설정
;--------------------------------------------------
@MAP_CAN_MOVE_1(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;誰かが住んでないと入れない
	CASE P119별채
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:초기위치 != ARG
			TFLAG:이동불능메시지 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;이동불능시 표시되는 메시지
;--------------------------------------------------
@MAP_CANNOT_WORD_1(ARG)
RETURN
SELECTCASE TFLAG:이동불능메시지
	CASE 1
		PRINTFORMW ※ここにメッセージを入れる。行数+1のCLEARLINEをする
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;거점 전용의 정경묘사
;WAIT는 걸리지 않도록
;-----------------------------------------
@MAP_UNIQUEMESSAGE_1
VARSET LOCAL
SELECTCASE CFLAG:MASTER:현재위치
	CASE P104강당
		PRINTFORML 큰 목탁이 놓여 있다
		RETURN 1
	CASE P129응접실
		PRINTFORML 목각 곰이 놓여 있다
		RETURN 1
	CASE P110툇마루
		PRINTFORML 처마 끝에 무가 매달려 있다
		RETURN 1
	CASE P119별채
		FOR LOCAL, 1, CHARANUM
			IF CFLAG:LOCAL:현재위치 == P112목욕탕
				LOCAL:1 = LOCAL
				LOCAL:2 ++
			ENDIF
		NEXT
		IF !LOCAL:2
			PRINTFORML 가만히 앉아서 목욕탕을 들여다볼 수 있는 비밀장치가 있다
		ELSE
			PRINTFORML 비밀장치를 통해 목욕중인 %CALLNAME:(LOCAL:1)%\@ LOCAL:2 > 1? 들# \@이 보인다
			PRINTFORML 절경일까나！
		ENDIF
		RETURN 1
	CASEELSE
ENDSELECT

;-----------------------------------------
;거점의 장소ID를 외출맵상의 장소ID로 치환하는 함수
;ARG      대상의 장소ID (거점맵)
;RETURNF  교체 장소ID (외출맵)
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_1(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P140지장길
		RETURNF 지장길
	CASE P101산문 TO P106뒷마당, P119별채, P120헛간, P122범종루, P137마미조의거처
		RETURNF 명련사경내
	CASE P121묘지
		RETURNF 묘지
	CASE P123영묘대문, P124영묘광장, P138종루, P139사선루
		RETURNF 신령묘광장
	CASE P125토지코방 TO P134곳간
		RETURNF 신령묘도장
	CASEELSE
		RETURNF 명련사방장
ENDSELECT
RETURNF 0

;--------------------------------------------------
;맵의 소개문
;--------------------------------------------------
@MAP소개_1
;とりあえず5行
PRINTFORML ※소개문 모집 중※
PRINTFORML 맵 이름이 명련사인데 신령묘도 포함되어 있어 꽤 큰 살림집이다.
PRINTFORML 
PRINTFORML 
PRINTFORML 