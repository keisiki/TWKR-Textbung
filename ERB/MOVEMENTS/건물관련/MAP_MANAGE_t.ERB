
;------------------------------------------
;	이사의 표시와 처리
;------------------------------------------
@SET_MAINHOME
#DIM CHK_TRAINING
#DIM CHK_GARDENING
#DIM 설정전MAIN_MAP
#DIM 설정전술벌레
#DIM 설정전초기위치
#DIM 설정전주인
#DIM ANIMATION_SEED

ANIMATION_SEED = 0

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
;
;
;멤버의 기상시간재설정
;
IF !이사
	IF 게임시작처리중 || 게임인계처리중
		MAIN_MAP = 0
		TRYCALLFORM ROOMSETTING_{MAIN_MAP}
	ENDIF
	설정전MAIN_MAP = MAIN_MAP
	설정전술벌레 = 거점_술벌레
	설정전초기위치 = Default초기위치
	설정전주인 = 거점_주인
	이사 = 1
ENDIF
IF FLAG:일시체재
	PRINTFORMW 묵고 있는 캐릭터가 있으므로 이사할 수 없습니다
	RETURN
ENDIF
DRAWLINE
;Color map
IF RECOLOREDMAPS && !GROUPMATCH(MAIN_MAP,12)
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "HIKKOSHI")
ELSE
	CALL DRAW_MAP("HIKKOSHI")
ENDIF
DRAWLINE
PRINTFORML ■%GET_MAPNAME(MAIN_MAP)%
CALL 건물데이터(MAIN_MAP)
PRINTFORM 채집가능 지점：
CALL ShowGatheringSpots(MAIN_MAP)
PRINTFORML 
FONTITALIC
CALLFORM MAP소개_{MAIN_MAP}
FONTREGULAR
PRINTL 
DRAWLINEFORM =
PRINT 자택을 설정할 수 있습니다. 어디로 설정하겠습니까?　
PRINTBUTTON @"[프리뷰 맵변환({TFLAG:맵변환})]", 1997
PRINTL
FOR LOCAL, 0, 15
	SIF LOCAL == 12
		CONTINUE
	SIF MAIN_MAP == LOCAL
		SETCOLOR C_AQUA
	SIF GET_EXISTMAP(LOCAL)
		PRINTFORM [{LOCAL + 2000}] %GET_MAPNAME(LOCAL), 16, LEFT%
	SIF GROUPMATCH(LOCAL, 4, 9, 10)
		PRINTL
	RESETCOLOR
NEXT
PRINT [1999] 결정
SIF !게임시작처리중 && !게임인계처리중
	PRINT 　[1998] 취소
PRINTL
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,99999,0,""
ELSE
	INPUT
ENDIF
;거점 전환
IF RESULT == 99999
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT >= 2000 && GET_EXISTMAP(RESULT - 2000) && RESULT != 2012
	MAIN_MAP = RESULT - 2000
	CALLFORM ROOMSETTING_{MAIN_MAP}
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
	TFLAG:맵변환 = 1
	RESTART
;프리뷰 전환
ELSEIF RESULT == 1997
	TFLAG:맵변환 += 1
	SIF TFLAG:맵변환 > MAP_PAGES
		TFLAG:맵변환 = 1
	RESTART
;キャンセル
ELSEIF RESULT == 1998 && !게임시작처리중 && !게임인계처리중
	이사 = 0
	MAIN_MAP = 설정전MAIN_MAP
	거점_술벌레 = 설정전술벌레
	Default초기위치 = 설정전초기위치
	거점_주인 = 설정전주인
	TFLAG:맵변환 = 0
	CALLFORM ROOMSETTING_{MAIN_MAP}
	RETURN
;決定
ELSEIF RESULT == 1999
	SIF 설정전MAIN_MAP == MAIN_MAP
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
	IF DAY >= 1
		IF MAIN_MAP == 3&&설정전MAIN_MAP != 3&& !게임시작처리중 && !게임인계처리중
			CALL 홍마거주신청
			IF RESULT == 0
				PRINTFORMW 신뢰도가 부족해 이사할 수 없습니다.
				RESTART
			ENDIF
		ELSEIF MAIN_MAP == 9&&설정전MAIN_MAP != 9&& !게임시작처리중 && !게임인계처리중
			CALL 지저거주신청
			IF RESULT == 0
				PRINTFORMW 신뢰도가 부족해 이사할 수 없습니다.
				RESTART
			ENDIF
		ENDIF
	ENDIF
ELSE
	RESTART
ENDIF
;결정 후
FLAG:운세뽑기계약 = 0
TFLAG:맵변환 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
CALL MAP_DEFAULTRESIDENT(MAIN_MAP)
;자물쇠를 채워 문을 잠금
VARSET LOCK

FOR LOCAL,1,CHARANUM
	CALL CHARA_SLEEP, LOCAL, 0
NEXT
CHK_TRAINING = 0
CHK_GARDENING = 0
FOR LOCAL,1,100
	SIF ROOMDATA:LOCAL & 장소_훈련
		CHK_TRAINING = 1
	SIF ROOMDATA:LOCAL & 장소_채소밭
		CHK_GARDENING = 1
NEXT
SIF !CHK_TRAINING
	CALL COLORMESSAGE(@"이 맵에서는 [전투훈련]이 불가합니다",C_YELLOW,2)
SIF !CHK_GARDENING
	CALL COLORMESSAGE(@"이 맵에서는 [가정텃밭]이 불가합니다",C_YELLOW,2)
SIF FLAG:역극 && CFLAG:(FLAG:역극):출금 && MAIN_MAP == GET_MAPID(CSVCFLAG(FLAG:역극, 311))
	CFLAG:MASTER:초기위치 = CSVCFLAG(FLAG:역극, 311)
;덫 포인트 교체
CALL TRAP_MOVING
FLAG:이삿날 = DAY

@HAS_DUPLICATE_KEY, ARG
#FUNCTION
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:311 == ARG
		SIF TALENT:LOCAL:연모
			RETURNF 1
	ENDIF
NEXT

@MAP_DEFAULTRESIDENT(ARG, 업데이트중)
#DIM 업데이트중
;ARG=맵 번호
SIF !업데이트중
	FLAG:더부살이캐릭터 = 0
MAIN_MAP = ARG
FOR LOCAL, 1, CHARANUM
	IF MAP_거주자(MAIN_MAP, LOCAL)
		CFLAG:LOCAL:초기위치 = CSVCFLAG(LOCAL, GETNUM(CFLAG, "초기위치"))
		CFLAG:LOCAL:신사거주 = CFLAG:LOCAL:초기위치
		SIF !업데이트중
			DEBUGPRINTFORML %CALLNAME:LOCAL%가 거점에 삽니다
	ELSE
		PRIVATEROOM:(CFLAG:LOCAL:초기위치 % 100) = 0
		CFLAG:LOCAL:초기위치 = SUKIMA()
		CFLAG:LOCAL:신사거주 = 0
	ENDIF
	CFLAG:LOCAL:자택위치 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "자택위치"))
	CFLAG:LOCAL:현재위치 = CFLAG:LOCAL:초기위치
	CFLAG:LOCAL:데이트중 = MAIN_MAP
NEXT
SIF !업데이트중
	PRINTFORMW %조사처리(GET_MAPNAME(ARG),"으로")% 설정했습니다
CALL ROOMSETTING
IF !업데이트중
	CFLAG:MASTER:초기위치 = Default초기위치
	CFLAG:MASTER:현재위치 = CFLAG:MASTER:초기위치
	이사 = 0
ENDIF

@ROOMSETTING
#DIM R_ID
#DIM 개인실후보
#DIM CHARA
개인실후보 = 0
SUMIKOMI_ROOM = 0
;拠点住人は初期位置をPRIVATEROOMに設定,施錠の都合で先に処理
VARSET PRIVATEROOM
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:신사거주 && !CFLAG:LOCAL:출금
		;PRIVATEROOM変数に、そこの家主の番号を保存
		PRIVATEROOM:(CFLAG:LOCAL:초기위치 % 100) = LOCAL 
	 	SIF CFLAG:LOCAL:집주인후보
			개인실후보 ++
	ENDIF
NEXT
TRYCCALLFORM ROOMSETTING_{MAIN_MAP}
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%의 방 정보를 설정했습니다
	;住み込みキャラ用のワンルーム
	IF FLAG:더부살이캐릭터 && SUMIKOMI_ROOM
		R_ID = SUMIKOMI_ROOM % 100
		ROOMDATA:R_ID |= 장소_원룸
	ENDIF
	FOR LOCAL, 1, 100
		;보완설정
		;MASTERの初期位置は最低限の機能を備える,飲食はできなくなる
		IF CFLAG:MASTER:초기위치 % 100 == LOCAL
			ROOMDATA:LOCAL |= 장소_이불
			ROOMDATA:LOCAL |= 장소_공부
			ROOMDATA:LOCAL |= 장소_Private
			ROOMDATA:LOCAL &= ~장소_음식
		ENDIF
		;ワンルーム
		IF ROOMDATA:LOCAL & 장소_원룸
			ROOMDATA:LOCAL |= 장소_이불
			ROOMDATA:LOCAL |= 장소_옥내
			ROOMDATA:LOCAL |= 장소_주방
			ROOMDATA:LOCAL |= 장소_공부
			ROOMDATA:LOCAL |= 장소_휴식
			ROOMDATA:LOCAL |= 장소_음식
			ROOMDATA:LOCAL |= 장소_Private
			ROOMDATA:LOCAL |= 장소_코타츠
			ROOMDATA:LOCAL &= ~장소_과소
		ENDIF
		;布団
		IF ROOMDATA:LOCAL & 장소_이불
			ROOMDATA:LOCAL |= 장소_휴식
		ENDIF
		;洋室
		IF ROOMDATA:LOCAL & 장소_서양식
			ROOMDATA:LOCAL |= 장소_옥내
		ENDIF
		;厨房
		IF ROOMDATA:LOCAL & 장소_주방
			ROOMDATA:LOCAL |= 장소_집어먹기
		ENDIF
		;施錠チェック,初期位置が変わった場合、施錠を解錠する
		R_ID = LOCAL + MAIN_MAP * 100
		SIF LOCK:R_ID && !PRIVATEROOM:LOCAL
			LOCK:R_ID = 0

	NEXT
CATCH
	PRINTFORMW 방 정보 설정에 실패했습니다
ENDCATCH
IF (게임시작처리중 || 게임인계처리중) || !Default초기위치 || 이사
	IF 개인실후보
		CALL 이사초기위치설정
	ELSE
		CALL DEFAULT이사
	ENDIF
ENDIF

@DEFAULT이사
TRYCCALLFORM DEFAULT_OWNER_{MAIN_MAP}
CATCH
	PRINTFORMW 방 정보의 설정에 실패했습니다.
ENDCATCH
PRINTFORMW %조사처리(NAME_FROM_PLACE(Default초기위치),"는")% %CALLNAME:MASTER%의 개인방이 되었다

@이사초기위치설정
#DIM CHARA
#DIM ANIMATION_SEED
ANIMATION_SEED = 0

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
DRAWLINE
IF RECOLOREDMAPS && !GROUPMATCH(MAIN_MAP,12)
	CALL DRAW_COLOREDMAP(ANIMATION_SEED)
ELSE
	CALL DRAW_MAP
ENDIF
DRAWLINE
PRINTFORML %PRINT_COLOR("이 맵에는", C_WHITE) + PRINT_COLOR(" 개인실후보", C_ORANGE) + PRINT_COLOR(" 가 다수 존재합니다。", C_WHITE)%
SIF DAY <= 1
	PRINTFORML 첫날 한정으로, 필요 신뢰도에 관계없이 방을 선택할 수 있습니다.
PRINTBUTTON @"[프리뷰맵변환({TFLAG:맵변환})]", 1997
PRINTL
FOR CHARA, 1, CHARANUM
	IF CFLAG:CHARA:신사거주 && CFLAG:CHARA:집주인후보
		SIF CFLAG:CHARA:더부살이필요신뢰도 > CFLAG:CHARA:신뢰도 && DAY > 1
			SETCOLOR C_L_GRAY
	PRINTBUTTON @"[{CFLAG:CHARA:집주인후보 % 100, 3}] - %NAME_FROM_PLACE(CFLAG:CHARA:집주인후보), 20, LEFT%　주인:%CALLNAME:CHARA, 15, LEFT%　필요 신뢰도:{CFLAG:CHARA:더부살이필요신뢰도}", CHARA
	PRINTL
	RESETCOLOR
	ENDIF
NEXT
PRINTFORML [  0] - ({Default초기위치 % 100, 2})%NAME_FROM_PLACE(Default초기위치)%（DEFAULT설정）
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	CURRENTREDRAW
	ORIREDRAW = RESULT
	REDRAW ORIREDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

IF RESULT == 0
	CALL DEFAULT이사
	ELSEIF RESULT == 1234
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT < CHARANUM && CFLAG:RESULT:신사거주 && CFLAG:RESULT:집주인후보
	CHARA = RESULT
	IF CHARA != 0 && CFLAG:RESULT:더부살이필요신뢰도 > CFLAG:RESULT:신뢰도 && DAY > 1
		PRINTFORMW %CALLNAME:RESULT%는 그만큼 %마스터를% 신뢰하고 있지 않다
		RESTART
	ELSE
		거점_주인 = CHARA
		Default초기위치 = CFLAG:CHARA:집주인후보
		ROOMDATA:(Default초기위치 % 100) |= 장소_이불
		ROOMDATA:(Default초기위치 % 100) |= 장소_Private
		PRINTFORMW %조사처리(NAME_FROM_PLACE(CFLAG:CHARA:집주인후보),"는")% %CALLNAME:MASTER%의 개인실이 되었다
	ENDIF
;프리뷰 전환
ELSEIF RESULT == 1997
	TFLAG:맵변환 += 1
	SIF TFLAG:맵변환 > MAP_PAGES
		TFLAG:맵변환 = 1
	RESTART
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;引っ越し下見時にROOMDATAを参照して施設の有無を表示する関数
;ARG	MAP_ID
;-------------------------------------------------
@건물데이터(ARG)
#DIM MAPDATA
VARSET MAPDATA
VARSET LOCAL
PRINT 시설：
;マップデータの参照
FOR LOCAL, 1, 100
	SIF (ROOMDATA:LOCAL & 장소_목욕탕) != 0
		MAPDATA |= 장소_목욕탕
	SIF (ROOMDATA:LOCAL & 장소_주방) != 0
		MAPDATA |= 장소_주방
	SIF (ROOMDATA:LOCAL & 장소_훈련) != 0
		MAPDATA |= 장소_훈련
	SIF (ROOMDATA:LOCAL & 장소_공부) != 0
		MAPDATA |= 장소_공부
	SIF (ROOMDATA:LOCAL & 장소_채소밭) != 0
		MAPDATA |= 장소_채소밭
	SIF (ROOMDATA:LOCAL & 장소_하늘의소리) != 0
		MAPDATA |= 장소_하늘의소리
NEXT
FONTBOLD
CALL COLORMESSAGE(@"【목욕탕】", (MAPDATA & 장소_목욕탕) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【주방】", (MAPDATA & 장소_주방) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【훈련】", (MAPDATA & 장소_훈련) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【공부】", (MAPDATA & 장소_공부) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【텃밭】", (MAPDATA & 장소_채소밭) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【하늘의소리】", (MAPDATA & 장소_하늘의소리) ? C_WHITE # C_GRAY, 0)
FONTREGULAR

;地点の参照
FOR LOCAL, MINROOM(), MAXROOM
	SIF FISHING_SPOT(LOCAL)
		LOCAL:1 = 1
	SIF IS_LIBRARY(LOCAL, 1)
		LOCAL:2 = 1
	SIF GATHERING_SPOT(LOCAL)
		LOCAL:3 = 1
	SIF PHARMACY_SPOT(LOCAL) == 2
		LOCAL:4 = 1
	SIF STALL_SPOT(LOCAL)
		LOCAL:5 = 1
	SIF PLACE_SENTO(LOCAL)
		LOCAL:6 = 1
	SIF OMIKUJI_SPOT(LOCAL)
		LOCAL:7 = 1
	SIF MORIYA_OMIKUJI(LOCAL)
		LOCAL:8 = 1
	SIF KAIDASHI_SHOP(LOCAL)
		LOCAL:9 = 1
	SIF FLOWER_SHOP(LOCAL)
		LOCAL:10 = 1
	SIF SAKE_SHOP(LOCAL)
		LOCAL:11 = 1
	SIF BOOK_RENTAL(LOCAL)
		LOCAL:12 = 1
	SIF YASAI_SHOP(LOCAL)
		LOCAL:13 = 1
	SIF CASINO_SPOT(LOCAL)
		LOCAL:14 = 1
	SIF HUNTING_SPOT(LOCAL)
		LOCAL:15 = 1
NEXT
FOR LOCAL, 1, 20
	IF LOCAL:LOCAL
		SELECTCASE LOCAL
		CASE 1
			PRINTFORM 【낚시】
		CASE 2
			PRINTFORM 【독서】
		CASE 3
			PRINTFORM 【채집】
		CASE 4
			PRINTFORM 【조제】
		CASE 5
			PRINTFORM 【노점】
		CASE 6
			PRINTFORM 【대중목욕탕】
		CASE 7
			PRINTFORM 【제비뽑기】
		CASE 8
			PRINTFORM 【모리야복권】
		CASE 9
			PRINTFORM 【식료품가게】
		CASE 10
			PRINTFORM 【꽃집】
		CASE 11
			PRINTFORM 【술집】
		CASE 12
			PRINTFORM 【스즈나안】
		CASE 13
			PRINTFORM 【야채가게】
		CASE 14
			PRINTFORM 【카지노】
		CASE 15
			PRINTFORM 【덫사냥】
		ENDSELECT
	ENDIF
NEXT
PRINTL
PRINT 거주자：
;キャラクター表示
FOR LOCAL, 1, CHARANUM
	IF MAP_거주자(MAIN_MAP, LOCAL)
		SIF LOCAL:21
			PRINTFORM 、
		RESULT = 0
		TRYCALLFORM M_KOJO_K{LOCAL}
		PRINTFORM \@ LOCAL:20 ? 、 # \@\@ RESULT ? * # \@%CALLNAME:LOCAL%
		LOCAL:21 ++
	ENDIF
NEXT
PRINTL 

;マップ内の採集可能地点を表示する関数
@ShowGatheringSpots(ARG)
#DIM cStart
#DIM cEnd
#DIM SpotID
#DIM Num
#DIM Wordcount
#DIM pCount

cStart = 1 + ARG * 100
cEnd = 99 + ARG * 100
Num = 0
pCount = 0
FOR LOCAL, cStart, cEND
	SpotID = GATHERING_SPOT(LOCAL, 1)
	IF SpotID
		pCount ++
		STRLENFORM %ForagePlaceName(SpotID)%
		Wordcount = RESULT:0
		Num += Wordcount
		IF Num > 110
			PRINTL 
			PRINTFORM 　　　　　　　
			Num = Wordcount
		ENDIF
		PRINTFORM %ForagePlaceName(SpotID)%
		SIF Num <= 106
			PRINTFORM 　　
	ENDIF
NEXT
SIF !pCount
	PRINT 없음
PRINTFORML 
