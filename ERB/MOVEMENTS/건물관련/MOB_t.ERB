;-------------------------------------------------
;모브가 있는 맵을 반환
;ARG=MAPID
;-------------------------------------------------

@GET_EXISTMOB(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 3,4,9, 10)
	RETURNF 1
;-------------------------------------------------
;외출시 모브 판정
;ARG=場所ID
;-------------------------------------------------
@DATE_HITOGOMI(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:현재위치
IF IN_HOME(ARG)
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "모브")
		RETURNF 1
ENDIF
RETURNF 0


;-------------------------------------------------
;MASTER가 재택안 또한 모브와 같은 위치에 있다면 MAIN_MAP를 돌려준다
@WITH_MOB()
#FUNCTION
SIF AT_HOME(MASTER) && ROOMDATA:(CFLAG:MASTER:현재위치 % 100) & 장소_모브
	RETURNF MAIN_MAP

;-------------------------------------------------
;모브가 주는 오염치 (청소치)
;ARG=MAPID
;-------------------------------------------------
@MOB_YOGORE(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 3
		RETURNF -3
	CASE 4
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 1
	CASE 9
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 2
	CASE 10
		;玉兎はプラスマイナスゼロ
ENDSELECT
;-------------------------------------------------
;모브의 기본명
;ARG=MAPID
;-------------------------------------------------
@MOB_NAME(ARG)
#FUNCTIONS
SIF !ARG
	ARG = MAIN_MAP
SELECTCASE ARG
CASE 3
	LOCALS = 홉고블린
CASE 4
	LOCALS = 이나바
CASE 9
	LOCALS = %ANIMALNAME()%
CASE 10
	LOCALS = 옥토끼
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;외출중의 모브명
;ARG=장소ID
;-------------------------------------------------
@DATE_HITOGOMI_SYUBETU(ARG)
#FUNCTIONS
VARSET LOCALS
SIF !ARG
	ARG = CFLAG:PLAYER:현재위치
SELECTCASE GET_MAPID(ARG)
	;モブの種類
	;月
	CASE 10
		IF ARG == 달의도시
			LOCALS += "월인"
		ELSE
			LOCALS += "옥토끼"
		ENDIF
	;地底（街道と温泉では鬼も出てくる）
	CASE 9
		IF ARG != 지령전 && !RAND:3
			LOCALS += "오니"
		ELSE
			LOCALS += "요괴"
		ENDIF
	;妖怪の山山頂（守矢神社境内のみ希に人間）
	CASE 8
		IF ARG == 모리야신사경내 && !RAND:5
			LOCALS += "인간"
		ELSE
			LOCALS += "요괴"
		ENDIF
	;妖怪の山山麓
	CASE 7
	;三途の川～冥界（死神、妖怪、希に人間）
	CASE 6
		IF !RAND:5
			LOCALS += "인간"
		ELSEIF !RAND:3
			LOCALS += "요괴"
		ELSE
			LOCALS += "사신"
		ENDIF
	;魔法の森
	CASE 5
	;迷いの竹林（妖怪ウサギ、希に人間(患者)）
	CASE 4
		IF RAND:5
			LOCALS += "요괴 토끼"
		ELSE
			LOCALS += "인간"
		ENDIF
	;紅魔館（お出かけ中のモブは全員少女なのでホフさんはお休み）
	CASE 3
		LOCALS += "요정 메이드"
	;人間の里（基本人間、希に妖怪）
	CASE 2
		IF RAND:5
			LOCALS += "인간"
		ELSE
			LOCALS += "요괴"
		ENDIF
	;命蓮寺（人間と妖怪）
	CASE 1
		IF !RAND:2
			LOCALS += "인간"
		ELSE
			LOCALS += "요괴"
		ENDIF
	;博麗神社
	CASE 0
ENDSELECT
LOCALS += " 소녀"
RETURNF LOCALS

;-------------------------------------------------
;まかないイベント処理
;-------------------------------------------------
@MAKANAI
#DIM 기력회복량
#DIM 체력회복량
VARSET LOCAL
IF DISH_NAME == "" && !TFLAG:새참 && TIME:2 == 3
	;식사할 수 없는 사람이 있는지를 체크
	FOR LOCAL,0,CHARANUM
		SIF TCVAR:LOCAL:공복시각 - TIME:1 > 0 && CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
			LOCAL:1 ++
	NEXT
	IF !LOCAL:1 && !RAND:5
		TFLAG:새참 = 1
		PRINTFORML %MOB_NAME()%에게서 새참을 권유받았다
		PRINTFORML 받겠습니까?
		CALL ASK_YN
		IF RESULT
			PRINTFORML %마스터는% 정중히 거절했다
		ELSE
			SELECTCASE RAND:5
				CASE 0,1
					PRINTFORML 대충 먹을 만하다
					체력회복량 = 100
					기력회복량 = 300
				CASE 2,3
					PRINTFORML 꽤 입맛을 돋운다
					체력회복량 = 150
					기력회복량 = 400
				CASE 4
					PRINTFORML 이건 맛있다!
					체력회복량 = 300
					기력회복량 = 500
			ENDSELECT
			PRINTFORMW %CALLNAME:MASTER%\@GET_TARGETNUM() ?  들은# %조사만처리(CALLNAME:MASTER,"는")%\@ 출출하던 배를 채웠다
			TIME += 15
			FOR LOCAL,0,CHARANUM
				IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치
					CALL RECOVER(LOCAL, 체력회복량, "체력")
					CALL RECOVER(LOCAL, 기력회복량, "기력")
					CALL 만복도상승(LOCAL,"경식")
				ENDIF
			NEXT
		ENDIF
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;홍마관 모브 설정（홉고블린）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;모브가 있는 장소
;-------------------------------------------------
@MOB_PLACE_3
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~장소_모브
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P302정원, P306뒤뜰, P307동쪽테라스, P308서쪽테라스)
				ROOMDATA:ROOM |= 장소_모브
		CASE 3
			SIF GROUPMATCH(LOCAL, P313주방, P314식당, P315응접실)
				ROOMDATA:ROOM |= 장소_모브
		CASE 4
			SIF GROUPMATCH(LOCAL, P309객실, P3192층복도)
				ROOMDATA:ROOM |= 장소_모브
		CASE 5
			SIF GROUPMATCH(LOCAL, P324큰발코니, P327작은발코니)
				ROOMDATA:ROOM |= 장소_모브
		CASE 6
			SIF GROUPMATCH(LOCAL, P306뒤뜰, P328지하복도, P329창고, P330고용인실)
				ROOMDATA:ROOM |= 장소_모브
	ENDSELECT
NEXT

;-------------------------------------------------
;모브 메시지
;-------------------------------------------------
@MOB_MESSAGE_3
IF FLAG:시간정지
	PRINTFORML 홉고블린이 정지해 있다…
ELSEIF CFLAG:MASTER:현재위치 == P329창고
	PRINTFORML 홉고블린이 고양이를 돌보고 있다…
ELSE
	PRINTFORML 홉고블린이 일하고 있다…
ENDIF

;-------------------------------------------------
;모브 이벤트
;-------------------------------------------------
@MOB_TURNENDEVENT_3
IF !FLAG:시간정지
	IF CFLAG:MASTER:우후후 && !TCVAR:패배강간
		PRINTFORML 홉고블린에게서 이곳은 공공의 공간이니 그런 행위는 지양해달라는 말을 들었다
		PRINTFORML %마스터는% 겸연쩍은 기분을 느꼈다
		BASE:TARGET:무드 = 0
		CALL ENDUFUFU_ALL
	ELSEIF CFLAG:MASTER:현재위치 == P313주방
		CALL MAKANAI
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;영원정 모브 설정（이나바）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;모브가 있는 장소
;-------------------------------------------------
@MOB_PLACE_4
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~장소_모브
	SELECTCASE TIME:2
		CASE 1 TO 7
			SIF GROUPMATCH(LOCAL, P407복도, P408중앙복도, P409툇마루, P421마당, P422뒷마당, P431토끼동굴)
				ROOMDATA:ROOM |= 장소_모브
	ENDSELECT
NEXT
;-------------------------------------------------
;모브 메시지
;-------------------------------------------------
@MOB_MESSAGE_4
IF FLAG:시간정지
	PRINTFORML 이나바들이 정지해 있다
ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
	PRINTFORML 이나바들이 잠들어 있다…
ELSEIF !DAY:3 % 10
	IF !TALENT:MASTER:성별 & 2
		PRINTFORML 발정중인 이나바들은 몹시 괴로워하고 있다……
	ELSE
		PRINTFORML 발정중인 이나바들에게서 강렬한 시선을 느낀다……
	ENDIF
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTFORML 이나바들이 배회하고 있다
		CASE 1
			PRINTFORML 이나바들이 놀고 있다
		CASE 2
			PRINTFORML 이나바들이 대화를 나누고 있다
		CASE 3 ;イナバとイナバがごっつんこしている
			PRINTFORML 이나바와 이나바가 서로 부딪치고 있다
		CASE 4
			PRINTFORML 이나바들이 겹겹히 쌓이고 있다
	ENDSELECT
ENDIF
;-------------------------------------------------
;모브 이벤트
;-------------------------------------------------
@MOB_TURNENDEVENT_4
IF !FLAG:시간정지
	IF CFLAG:MASTER:우후후
		PRINTFORML %CALLNAME:MASTER%들을 보고 주위 이나바들이 꺅꺅 떠들기 시작하고, 나아가 그 소리를 주워들은 구경꾼이 모이기 시작했다
		PRINTFORML 도저히 일을 벌일 분위기는 아니다……
		BASE:TARGET:무드 = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;지령전 모브 설정（ペット＆怨霊＆鬼）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;모브가 있는 장소
;-------------------------------------------------
@MOB_PLACE_9
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~장소_모브
	SIF GROUPMATCH(LOCAL, P904홀, P9051층복도, P913식당, P9142층복도, P915발코니, P923뒤뜰, P926안뜰)
		ROOMDATA:ROOM |= 장소_모브
	SIF GROUPMATCH(LOCAL, P931큰길, P932뒷길, P933술집, P938오니소굴)
		ROOMDATA:ROOM |= 장소_모브
	SIF GROUPMATCH(LOCAL, P928작열지옥터, P929지옥의인공태양, P930옛혈지지옥)
		ROOMDATA:ROOM |= 장소_모브
NEXT
;-------------------------------------------------
;모브 메시지
;-------------------------------------------------
@MOB_MESSAGE_9
IF INRANGE(CFLAG:MASTER:현재위치, P928작열지옥터, P930옛혈지지옥)
	IF FLAG:시간정지
		PRINTL 원령들이 정지해 있다
	ELSE
		IF CFLAG:우후후
			PRINTL 원령이 불편해 하고 있다…
		ELSEIF !RAND:2
			PRINTL 원령이 헤매고 있다
		ELSE
			PRINTL 원령이 감돌고 있다
		ENDIF
	ENDIF
ELSEIF INRANGE(CFLAG:MASTER:현재위치, P931큰길, P939뒷골목)
	IF FLAG:시간정지
		PRINTL 오니들이 정지해 있다
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTL 오니들이 잠들어 있다…
	ELSE
		SELECTCASE RAND:6
			CASE 0
				PRINTL 오니들이 술판을 벌이고 있다
			CASE 1
				PRINTL 오니들이 모여 있다
			CASE 2
				PRINTL 오니들이 다툼을 벌이고 있다
			CASE 3
				PRINTL 오니들이 서성거리고 있다
			CASE 4
				PRINTL 오니들이 떠들고 있다
			CASE 5
				PRINTL 오니들이 엎드려 누워 있다… 거추장스러워
		ENDSELECT
	ENDIF
ELSE
	IF FLAG:시간정지
		PRINTFORML %조사처리(ANIMALNAME(),"가")% 정지해 있다
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTFORML %조사처리(ANIMALNAME(),"가")% 잠들어 있다…
	ELSEIF CFLAG:MASTER:현재위치 == P923뒤뜰
		IF !RAND:2
			PRINTFORML %조사처리(ANIMALNAME(),"가")% 목욕을 하고 있다
		ELSE
			PRINTFORML %조사처리(ANIMALNAME(),"가")% 헤엄치고 있다
		ENDIF
	ELSE
		IF !RAND:2
			PRINTFORML %조사처리(ANIMALNAME(),"가")% 서성거리고 있다
		ELSE
			PRINTFORML %조사처리(ANIMALNAME(),"가")% 엎드려 누워 있다… 거추장스러워
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;펫의 이름
;-------------------------------------------------
@ANIMALNAME()
#FUNCTIONS
SELECTCASE TIME:1 % 25
	;소형 포유류
	CASE 0
		LOCALS = 개
	CASE 1
		LOCALS = 고양이
	CASE 2
		LOCALS = 코알라
	CASE 3
		LOCALS = 고릴라
	CASE 4
		LOCALS = 저지데빌
	CASE 5
		LOCALS = 아르마딜로
	CASE 6
		LOCALS = 새끼멧돼지
	CASE 7
		LOCALS = 카피바라
	;대형 포유류
	CASE 8
		LOCALS = 개미핥기
	CASE 9
		LOCALS = 말
	CASE 10
		LOCALS = 소
	CASE 11
		LOCALS = 양
	CASE 12
		LOCALS = 알파카
	CASE 13
		LOCALS = 바다표범
	;초대형 포유류
	CASE 14
		LOCALS = 코끼리
	CASE 15
		LOCALS = 기린
	;조류
	CASE 16
		LOCALS = 병아리
	CASE 17
		LOCALS = 펭귄
	CASE 18
		LOCALS = 넓적부리황새
	CASE 19
	;LOCALS = 좀비 페어리
		LOCALS = 공작
	CASE 20
		LOCALS = 타조
	;파충류
	CASE 21
		LOCALS = 악어
	CASE 22
		LOCALS = 코모도도마뱀
	CASE 23
		LOCALS = 버마왕뱀
	CASE 24
		LOCALS = 코끼리거북
ENDSELECT
RETURNF LOCALS

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;달의 도시의 모브 설정（玉兎）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
@MOB_PLACE_10
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~장소_모브
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P1003옥토거리, P1004광장, P1014복숭아숲, P1020와타츠키정・대문, P1023문지기초소, P1025옥토초소)
				ROOMDATA:ROOM |= 장소_모브
		CASE 3
			SIF GROUPMATCH(LOCAL, P1003옥토거리, P1004광장, P1020와타츠키정・대문, P1024옥토식당)
				ROOMDATA:ROOM |= 장소_모브
		CASE 4
			SIF GROUPMATCH(LOCAL, P1003옥토거리, P1004광장, P1014복숭아숲, P1020와타츠키정・대문, P1023문지기초소, P1025옥토초소)
				ROOMDATA:ROOM |= 장소_모브
		CASE 5
			SIF GROUPMATCH(LOCAL, P1003옥토거리, P1004광장, P1020와타츠키정・대문, P1024옥토식당)
				ROOMDATA:ROOM |= 장소_모브
		CASE 6
			SIF GROUPMATCH(LOCAL, P1003옥토거리, P1004광장, P1020와타츠키정・대문, P1023문지기초소, P1024옥토식당, P1025옥토초소)
				ROOMDATA:ROOM |= 장소_모브
		CASEELSE
			SIF GROUPMATCH(LOCAL, P1020와타츠키정・대문)
				ROOMDATA:ROOM |= 장소_모브
	ENDSELECT
NEXT
;-------------------------------------------------
;モブのメッセージ
;-------------------------------------------------
@MOB_MESSAGE_10
IF FLAG:시간정지
	IF GROUPMATCH(CFLAG:MASTER:현재위치, P1020와타츠키정・대문, P1023문지기초소)
		PRINTFORML 문지기들이 정지해 있다
	ELSE
		PRINTFORML 옥토끼들이 정지해 있다
	ENDIF
ELSE
	SELECTCASE CFLAG:MASTER:현재위치
	CASE P1003옥토거리, P1004광장
		PRINTFORML 옥토끼들로 북적거리고 있다
	CASE P1014복숭아숲
		PRINTFORML 옥토끼들이 훈련하고 있다
	CASE P1020와타츠키정・대문
		PRINTFORML 문지기가 망을 보고 있다
	CASE P1023문지기초소
		PRINTFORML 문지기들이 휴식하고 있다
	CASE P1024옥토식당
		PRINTFORML 옥토끼들이 식사를 하고 있다
	CASE P1025옥토초소
		PRINTFORML 옥토끼들이 휴식하고 있다
	ENDSELECT
ENDIF
;-------------------------------------------------
;모브 이벤트
;-------------------------------------------------
@MOB_TURNENDEVENT_10
IF !FLAG:시간정지
	IF CFLAG:MASTER:우후후
		IF GROUPMATCH(CFLAG:MASTER:현재위치, P1020와타츠키정・대문, P1023문지기초소)
			PRINTFORML 문지기가 무슨 일인가 하고 법석을 떨며 달려왔다
			PRINTFORML 도저히 일을 벌일 분위기는 아니다……
		ELSE
			PRINTFORML %CALLNAME:MASTER%들을 보고 주위의 옥토끼들이 꺅꺅 떠들기 시작하고, 나아가 그 소리를 주워들은 구경꾼이 모이기 시작했다
			PRINTFORML 도저히 일을 벌일 분위기는 아니다……
		ENDIF
		BASE:TARGET:무드 = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF