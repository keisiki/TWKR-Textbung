;--------------------------------------------------
;지점명 변경 함수
;--------------------------------------------------
@MAP_PLACENAME_3(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P304빈오두막
			IF CFLAG:MASTER:초기위치 == ARG
				RETURNF CALLNAME:MASTER + "의 오두막"
			ELSE
				RETURNF "빈 오두막"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:더부살이캐릭터
				RETURNF CALLNAME:(FLAG:더부살이캐릭터) + "의 방"
			ELSE
				RETURNF "빈 방"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "스키마 공간"
		CASE MAXROOM
			RETURNF "안개 저편"
		CASEELSE
			RETURNF "불명"
	ENDSELECT
ELSE
	RETURNF "호수 밖"
ENDIF

;--------------------------------------------------
;장소 ARG가 어느 지역에 있는지
;표시하는 맵의 번호와 연동 (TFLAG:맵변환)
;ARG     장소ID
;--------------------------------------------------
@MAP_AREASELECT_3(ARG)
#FUNCTION
SELECTCASE ARG
	;紅魔館・一階＆地下
	CASE P301정문 TO P3161층화장실, P328지하복도 TO P333대욕탕
		RETURNF 2
	;紅魔館・二階＆三階
	CASE P3192층복도 TO P327작은발코니
		RETURNF 3
	;폐양관
	CASE P334폐양관・입구 TO P342리리카방
		RETURNF 4
	;안개의호수
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;내부구조가 있는 장소의 내부 판정
;		TFLAG:맵변환	내부감지(察知)가 필요한 맵 번호
;				P_ID  	표시버튼의 장소ID
;				Q_ID  	판정하는 내부 장소ID, 반드시 P_ID와 일치하는 수치도 포함
;--------------------------------------------------
@GETMAP_BUILDING_3(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:맵변환 == 1
	SELECTCASE P_ID
		;紅魔館
		CASE P301정문
			SELECTCASE Q_ID
			CASE P301정문 TO P333대욕탕
				RETURNF 1
			ENDSELECT
		;폐양관
		CASE P334폐양관・입구
			SELECTCASE Q_ID
			CASE P334폐양관・입구 TO P342리리카방
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:맵변환 == 2
	SELECTCASE P_ID
		;紅魔館・二階＆三階
		CASE P3192층복도
			SELECTCASE Q_ID
			CASE P3192층복도 TO P327작은발코니
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:맵변환 == 3
	SELECTCASE P_ID
		;紅魔館・一階＆地下
		CASE P309객실
			SELECTCASE Q_ID
			CASE P309객실 TO P3161층화장실, P328지하복도 TO P333대욕탕
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;이동제한설정
;ARG    장소ID
;CHARA  이동하는 캐릭터ID
;RETURNF 1=이동 가능, 0=이동 불가능
;TFLAG:이동불능메시지   ↓の@MAP_CANNOT_WORD용 설정
;--------------------------------------------------
@MAP_CAN_MOVE_3(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;湖底
	CASE P348안개의호수・바닥
		;水棲でないと入れない
		IF !TALENT:CHARA:수서
			TFLAG:이동불능메시지 = 1
			RETURNF 0
		ENDIF
	;誰かが住んでないと入れない
	CASE P304빈오두막, P321빈방
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:초기위치 != ARG
			TFLAG:이동불능메시지 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;이동불능시 표시되는 메시지
;--------------------------------------------------
@MAP_CANNOT_WORD_3(ARG)
SELECTCASE TFLAG:이동불능메시지
	CASE 1
		PRINTFORMW 물에 빠져버린다…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;거점 전용의 정경묘사
;WAIT는 걸리지 않도록
;-----------------------------------------
@MAP_UNIQUEMESSAGE_3
IF WITH_MOB()
	CALL MOB_MESSAGE_3
	RETURN 1
ELSEIF !FLAG:시간정지
	IF CFLAG:MASTER:현재위치 == P329창고
		PRINTFORML 고양이가 있다
		RETURN 1
	ENDIF
ENDIF

;-----------------------------------------
;거점의 장소ID를 외출맵상의 장소ID로 치환하는 함수
;ARG      대상의 장소ID (거점맵)
;RETURNF  교체 장소ID (외출맵)
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P343호숫가, P346대요정의거처 TO P348안개의호수・바닥
		RETURNF 안개의호수
	CASE P344레티의움집, P345치르노의움집
		RETURNF 얼음집
	CASE P334폐양관・입구 TO P342리리카방
		RETURNF 폐양관
	CASE P301정문 TO P308서쪽테라스
		RETURNF 정문
	CASE P313주방, P314식당
		RETURNF 식당
	CASE P310도서관 TO P312소악마방
		RETURNF 대도서관
	CASE P328지하복도 TO P333대욕탕
		RETURNF 지하실
	CASEELSE
		RETURNF 객실
ENDSELECT
RETURNF 0

;--------------------------------------------------
;맵의 소개문
;--------------------------------------------------
@MAP소개_3
;とりあえず5行
PRINTFORML 홍마관・안개의 호수를 중심으로, 프리즘리버 세자매가 사는 폐양관까지를 포함한 지역.
PRINTFORML 가장 큰 특징은 여기에 밖에 존재하지 않는 카지노.
PRINTFORML 호수의 물은 술을 만드는 것에 도움이 되는 것 외에도 낚시도 할 수 있어, 꽤 편리성이 높은 거점.
PRINTFORML 흡혈귀의 저택답게 일부 주민은 밤이 돼야 활동을 시작하기 때문에, 필요에 따라 기상시간을 늦추는 것도 필요할 것이다.
PRINTFORML 거점내에서의 일 심부름처도 부족하지 않지만, 세세하게 청소가 행해져서인지 홍마관 안은 의외로 더러워지지 않는다.
