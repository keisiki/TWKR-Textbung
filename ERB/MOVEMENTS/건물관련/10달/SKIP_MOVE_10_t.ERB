@SKIP_MOVE_10(ARG,ARG:1)
#DIM 현재위치
#DIM CONST SPT괴안통로 = 1001
#DIM CONST SPT달의수도 = 1002
#DIM CONST SPT옥토가 = 1003
#DIM CONST SPT광장 = 1004
#DIM CONST SPT사구메집거실 = 1005
#DIM CONST SPT사구메집침실 = 1006
#DIM CONST SPT빈집 = 1007
#DIM CONST SPT링고집 = 1008
#DIM CONST SPT세이란집 = 1009
#DIM CONST SPT식사처 = 1010
#DIM CONST SPT감미처 = 1011
#DIM CONST SPT주점 = 1012
#DIM CONST SPT야채가게 = 1013
#DIM CONST SPT복숭아숲 = 1014
#DIM CONST SPT모래사장 = 1015
#DIM CONST SPT후미 = 1016

#DIM CONST SPT면월정대문 = 1020
#DIM CONST SPT면월정현관 = 1021
#DIM CONST SPT1층복도 = 1022
#DIM CONST SPT문지기초소 = 1023
#DIM CONST SPT옥토식당 = 1024
#DIM CONST SPT옥토초소 = 1025
#DIM CONST SPT식당 = 1026
#DIM CONST SPT주방 = 1027
#DIM CONST SPT식자재창고 = 1028
#DIM CONST SPT면월정탈의실 = 1029
#DIM CONST SPT면월정욕탕 = 1030
#DIM CONST SPT보물창고 = 1031
#DIM CONST SPT레이센방 = 1032
#DIM CONST SPT면월정빈방 = 1033
#DIM CONST SPT안뜰 = 1035
#DIM CONST SPT정자 = 1036

#DIM CONST SPT2층복도 = 1040
#DIM CONST SPT토요히메방 = 1041
#DIM CONST SPT요리히메방= 1042
#DIM CONST SPT서재 = 1043
#DIM CONST SPT응접실 = 1044

#DIM CONST SPT도레미집거실 = 1050
#DIM CONST SPT도레미집침실 = 1051
#DIM CONST SPT도레미집창고 = 1052
#DIM CONST SPT도레미집세면장 = 1053
#DIM CONST SPT도레미집욕탕 = 1054
#DIM CONST SPT도레미집화장실 = 1055

#DIM CONST SPT순호저택현관 = 1060
#DIM CONST SPT거실 = 1061
#DIM CONST SPT부엌 = 1062
#DIM CONST SPT순호저택빈방 = 1063
#DIM CONST SPT클라운피스방 = 1064
#DIM CONST SPT헛간 = 1065
#DIM CONST SPT순호저택탈의실 = 1066
#DIM CONST SPT순호저택욕탕 = 1067
#DIM CONST SPT순호저택안뜰 = 1068
#DIM CONST SPT옥상 = 1070
#DIM CONST SPT순호방 = 1071
#DIM CONST SPT헤카티아방 = 1072

#DIM CONST SPT꿈의저편 = 1073

;현재위치취득
현재위치 = CFLAG:MASTER:현재위치
SIF ARG:1
	현재위치 = ARG:1

;잠정 목적지로 직접 이동할 수 있는 경우
IF CAN_MOVE(현재위치, ARG) & 1
	;そこに移動
	RETURN ARG
ENDIF

;袋小路から行ける場所は固定なので、そこを目指す
SELECTCASE 현재위치
	CASE SPT사구메집침실
		RETURN SPT사구메집거실
	CASE SPT빈집
		RETURN SPT달의수도
	CASE SPT링고집, SPT세이란집
		RETURN SPT옥토가
	CASE SPT주점, SPT야채가게
		RETURN SPT광장
	CASE SPT문지기초소
		RETURN SPT면월정대문
	CASE SPT옥토초소
		RETURN SPT옥토식당
	CASE SPT순호저택욕탕
		RETURN SPT순호저택탈의실
	CASE SPT보물창고, SPT레이센방, SPT면월정빈방
		RETURN SPT1층복도
	CASE SPT정자
		RETURN SPT안뜰
	CASE SPT토요히메방, SPT요리히메방, SPT서재, SPT응접실
		RETURN SPT2층복도
	CASE SPT도레미집침실, SPT도레미집창고
		RETURN SPT도레미집거실
	CASE SPT도레미집욕탕, SPT도레미집화장실
		RETURN SPT도레미집세면장
	CASE SPT부엌, SPT순호저택빈방, SPT클라운피스방, SPT헛간, SPT순호저택안뜰
		RETURN SPT거실
	CASE SPT순호저택욕탕
		RETURN SPT순호저택탈의실
	CASE SPT순호방, SPT헤카티아방
		RETURN SPT옥상
ENDSELECT

;直接移動できない場合、1つ近い位置を暫定目的地に設定し直してループ
;地点追加時要フォロー（全ケース修正すること）
SELECTCASE ARG
	CASE SPT괴안통로
		SELECTCASE 현재위치
		;ドレミー宅からはドレミー宅 玄関を目指す
		CASE SPT도레미집침실 TO SPT도레미집화장실
			RETURN SPT도레미집거실
		;それ以外は달의수도を目指す
		CASEELSE
			RETURN SPT달의수도
		ENDSELECT
	CASE SPT달의수도
		SELECTCASE 현재위치
		;사구메집침실からは사구메집거실を目指す
		CASE SPT사구메집침실
			RETURN SPT사구메집거실
		;綿月亭からは綿月亭 門を目指す
		CASE SPT면월정현관 TO SPT응접실
			RETURN SPT면월정대문
		;ドレミー宅からは괴안통로を目指す
		CASE SPT도레미집거실 TO SPT도레미집화장실
			RETURN SPT괴안통로
		;それ以外は7옥토가を目指す
		CASEELSE
			RETURN SPT옥토가
		ENDSELECT
	CASE SPT옥토가
		SELECTCASE 현재위치
		;주점と야채가게は광장を目指す
		CASE SPT주점, SPT야채가게
			RETURN SPT광장
		;모래사장～순호저택からは복숭아숲を目指す
		CASE SPT모래사장, SPT후미, SPT순호저택현관 TO SPT헤카티아방
			RETURN SPT복숭아숲
		;それ以外は달의수도を目指す
		CASEELSE
			RETURN SPT달의수도
		ENDSELECT
	CASE SPT광장
		RETURN SPT옥토가
	CASE SPT사구메집거실, SPT빈집
		RETURN SPT달의수도
	CASE SPT사구메집침실
		RETURN SPT사구메집거실
	CASE SPT링고집, SPT세이란집
		RETURN SPT옥토가
	CASE SPT식사처, SPT감미처
		SELECTCASE 현재위치
		;주점と야채가게は광장を目指す
		CASE SPT주점, SPT야채가게
			RETURN SPT광장
		;それ以外は옥토가を目指す
		CASEELSE
			RETURN SPT옥토가
		ENDSELECT
	CASE SPT주점, SPT야채가게
		RETURN SPT광장
	CASE SPT복숭아숲
		SELECTCASE 현재위치
		;静かの海、순호저택からは모래사장を目指す
		CASE SPT후미, SPT순호저택현관 TO SPT헤카티아방
			RETURN SPT모래사장
		;それ以外は옥토가をを目指す
		CASEELSE
			RETURN SPT옥토가
		ENDSELECT
	CASE SPT모래사장
		SELECTCASE 현재위치
		;순호저택からは후미を目指す
		CASE SPT순호저택현관 TO SPT헤카티아방
			RETURN SPT후미
		CASEELSE
			RETURN SPT복숭아숲
		ENDSELECT
	CASE SPT후미
		SELECTCASE 현재위치
		;순호저택内部からは순호저택 玄関を目指す
		CASE SPT순호저택현관 TO SPT헤카티아방
			RETURN SPT순호저택현관
		;それ以外は모래사장を目指す
		CASEELSE
			RETURN SPT모래사장
		ENDSELECT

	CASE SPT면월정대문
		SELECTCASE 현재위치
		;옥토초소からは옥토식당を目指す
		CASE SPT옥토초소
			RETURN SPT옥토식당
		;それ以外の綿月亭内部からは綿月亭 玄関を目指す
		CASE SPT면월정현관 TO SPT응접실
			RETURN SPT면월정현관
		;それ以外は달의수도を目指す
		CASEELSE
			RETURN SPT달의수도
		ENDSELECT
	CASE SPT면월정현관
		SELECTCASE 현재위치
		;옥토초소からは옥토식당を目指す
		CASE SPT옥토초소
			RETURN SPT옥토식당
		;주방からは식자재창고を目指す
		CASE SPT주방
			RETURN SPT식자재창고
		;それ以外の綿月亭内部からは1층복도を目指す
		CASE SPT면월정현관 TO SPT응접실
			RETURN SPT1층복도
		;それ以外は綿月亭 門を目指す
		CASEELSE
			RETURN SPT면월정대문
		ENDSELECT
	CASE SPT1층복도
		SELECTCASE 현재위치
		;주방からは식당を目指す
		CASE SPT주방
			RETURN SPT식당
		;정자からは안뜰を目指す
		CASE SPT정자
			RETURN SPT안뜰
		;風呂からは脱衣所を目指す
		CASE SPT순호저택욕탕
			RETURN SPT순호저택탈의실
		;二階からは2층복도を目指す
		CASE SPT토요히메방 TO SPT응접실
			RETURN SPT2층복도
		;それ以外は면월정현관を目指す
		CASEELSE
			RETURN SPT면월정현관
		ENDSELECT
	CASE SPT문지기초소
		RETURN SPT면월정대문
	CASE SPT옥토식당
		SELECTCASE 현재위치
		;綿月亭内部からは綿月亭 玄関を目指す
		CASE SPT1층복도, SPT식당 TO SPT응접실
			RETURN SPT면월정현관
		;それ以外は면월정대문を目指す
		CASEELSE
			RETURN SPT면월정대문
		ENDSELECT
	CASE SPT옥토초소
		RETURN SPT옥토식당
	CASE SPT식당
		SELECTCASE 현재위치
		;식자재창고からは주방を目指す
		CASE SPT식자재창고
			RETURN SPT주방
		;それ以外は1층복도を目指す
		CASEELSE
			RETURN SPT1층복도
		ENDSELECT
	CASE SPT주방
		SELECTCASE 현재위치
		;綿月亭内部からは식당を目指す
		CASE SPT1층복도, SPT순호저택탈의실 TO SPT응접실
			RETURN SPT식당
		;それ以外は식자재창고を目指す
		CASEELSE
			RETURN SPT식자재창고
		ENDSELECT
	CASE SPT식자재창고
		SELECTCASE 현재위치
		;식당からは주방を目指す
		CASE SPT식당
			RETURN SPT주방
		;それ以外は면월정현관を目指す
		CASEELSE
			RETURN SPT면월정현관
		ENDSELECT
	CASE SPT순호저택탈의실, SPT보물창고, SPT레이센방, SPT면월정빈방, SPT안뜰, SPT2층복도
		RETURN SPT1층복도
	CASE SPT순호저택욕탕
		RETURN SPT순호저택탈의실
	CASE SPT정자
		RETURN SPT안뜰
	CASE SPT토요히메방, SPT요리히메방, SPT서재, SPT응접실
		RETURN SPT2층복도

	CASE SPT도레미집거실
		SELECTCASE 현재위치
		;風呂、トイレは洗面所を目指す
		CASE SPT도레미집욕탕, SPT도레미집화장실
			RETURN SPT도레미집세면장
		;それ以外は1괴안통로を目指す
		CASEELSE
			RETURN SPT괴안통로
		ENDSELECT
	CASE SPT도레미집침실, SPT도레미집창고, SPT도레미집세면장
		RETURN SPT도레미집거실
	CASE SPT도레미집욕탕, SPT도레미집화장실
		RETURN SPT도레미집세면장

	CASE SPT순호저택현관
		SELECTCASE 현재위치
		;순호저택内部からは순호저택 거실を目指す
		CASE SPT순호저택현관 TO SPT헤카티아방
			RETURN SPT거실
		;それ以外は후미を目指す
		CASEELSE
			RETURN SPT후미
		ENDSELECT
	CASE SPT거실
		SELECTCASE 현재위치
		;순호저택 風呂は순호저택 脱衣所を目指す
		CASE SPT순호저택욕탕
			RETURN SPT순호저택탈의실
		;순호저택二階からは옥상を目指す
		CASE SPT순호방 TO SPT헤카티아방
			RETURN SPT옥상
		;それ以外は순호저택 玄関を目指す
		CASEELSE
			RETURN SPT순호저택현관
		ENDSELECT
	CASE SPT부엌, SPT순호저택빈방, SPT클라운피스방, SPT헛간, SPT순호저택탈의실, SPT순호저택안뜰, SPT옥상
		RETURN SPT거실
	CASE SPT순호저택욕탕
		RETURN SPT순호저택탈의실
	CASE SPT순호방, SPT헤카티아방
		RETURN SPT옥상
	CASEELSE
		RETURN SPT괴안통로
ENDSELECT