@캐릭터이동처리(ARG)
#DIM 머물어비율
#DIM 이동처
#DIM 이동결정
#DIM 무차별이동
#DIM 강제이동
#DIM 원룸인원수카운트
VARSET LOCAL
;감금중이면 어림도 없어
IF CFLAG:ARG:감금됨
	IF CFLAG:ARG:현재위치 != 1445
		CFLAG:ARG:현재위치 = 1445
	ENDIF
	GOTO FINISH
ENDIF

;당신의 거점 물건에 있는 캐릭터만이 대상
SIF !AT_HOME(ARG)
	GOTO FINISH

SIF FLAG:시간정지 || CFLAG:ARG:쇠약 || CFLAG:ARG:초대 || CFLAG:ARG:수면
	GOTO FINISH

;일, 데일리, 동침, 우후후, 동행중은 이동 처리 리셋
SIF CFLAG:ARG:행동 == 5 || CFLAG:ARG:데일리 || CFLAG:ARG:동침중 || CFLAG:ARG:우후후 || CFLAG:ARG:동행
	GOTO FINISH

;루프 앞에 처리
CALLFORM CHARAMOVE_DATA_{ARG}(4, CFLAG:ARG:현재위치)
머물어비율 = RESULT
;머물기 어려운 장소
SIF CFLAG:ARG:현재위치 == GET_ENTRANCE(MAIN_MAP) || ROOMDATA:(CFLAG:ARG:현재위치 % 100) & 장소_머물지않는다
	머물어비율 += -5
;당신과 동실이라면 태도로 머문다
SIF CFLAG:ARG:현재위치 == CFLAG:MASTER:현재위치
	머물어비율 += CFLAG:ARG:태도 * 2
SIF CFLAG:ARG:현재위치 == MAXROOM
	머물어비율 = -5

무차별이동 = 0
강제이동 = 0
IF BATHROOM(CFLAG:ARG:현재위치)
	;목욕탕
	IF CFLAG:ARG:목욕탕
		GOTO FINISH
	;오른다
	ELSE
		무차별이동 = 1
	ENDIF
ENDIF
;화장실에 있어 10분 이상 경과하고 있다
SIF ROOMDATA:(CFLAG:ARG:현재위치 % 100) & 장소_화장실 && TIME_PROGRESS(TIME:1) >= 10
	무차별이동 = 1
;날씨 대미지가 아프다
SIF 날씨에의한영향(ARG, CFLAG:ARG:현재위치) <= -3
	무차별이동 = 1
;현재위치가 본래 들어가지 않는 장소(게다가 당신이 없다)
CALL 캐릭터이동율(ARG, CFLAG:ARG:현재위치, 머물어비율)
SIF RESULT <= 0 && CFLAG:ARG:현재위치 != CFLAG:MASTER:현재위치
	무차별이동 = 1
;가능한한움직이지않는다
SIF TCVAR:ARG:가능한한움직이지않는다 && !무차별이동
	GOTO FINISH

;차례 셔플
CALL FISHER_YATES_SHAFFLE(MAXROOM - MINROOM())
;셔플 했으므로 이동할 수 있을 것 같은 곳부터 순서대로 이동해 나간다
;PRINTFORML %CALLNAME:ARG%＠%NAME_FROM_PLACE(CFLAG:ARG:현재위치)%
이동결정 = 0
$KYOUSEI
FOR LOCAL, 0, MAXROOM - MINROOM()
	이동처 = SHAFFLE_ARRAY:LOCAL + MAIN_MAP * 100 + 1
;	PRINTFORML 이동처：%NAME_FROM_PLACE(이동처)%({이동처})
	;현재위치
	SIF 이동처 == CFLAG:ARG:현재위치
		CONTINUE
	;이동할 수 없다
	SIF !(CAN_MOVE(CFLAG:ARG:현재위치, 이동처) & 1)
		CONTINUE
	;이동할 수 없는 2, 원룸 제한등 , 이동에 제한이 있는 장소
	SIF !MAP_CAN_MOVE(이동처, ARG)
		CONTINUE
	;밖으로 나올 수  없다
	SIF 날씨에의한영향(ARG, 이동처) <= -3 && CFLAG:ARG:현재위치 != MAXROOM && 날씨에의한영향(ARG, CFLAG:ARG:현재위치) >= -2
		CONTINUE
	;이동처가 당신 방이며, 그 캐릭터의 방이 아니고, 거기서 당신이 우후후중의 경우는 들어가 이것 없다
	SIF 이동처 == CFLAG:MASTER:초기위치 && 이동처 != CFLAG:ARG:초기위치 && 이동처 == CFLAG:MASTER:현재위치 && CFLAG:MASTER:우후후
		CONTINUE
	;결계발동중
	SIF TEQUIP:MASTER:결계발동 && 이동처 == CFLAG:MASTER:현재위치
		CONTINUE
	SIF 이동처 == TCVAR:ARG:쫒겨난장소
		CONTINUE
	CALL 캐릭터이동율(ARG, 이동처, 머물어비율)
;	PRINTFORM ＞이동율：{RESULT * TIME_PROGRESS(TIME:1)}
	;이동 판정, 강제이동이라면 즉이동
	IF RESULT * TIME_PROGRESS(TIME:1) > RAND:1000
;		PRINTFORMW ＞이동!
		이동결정 = 이동처
		BREAK
	;무차별이동시는 이동율이 있으면 즉이동, 강제이동이 발동하고 있다면 어디라도 이동
	ELSEIF (RESULT && 무차별이동) || 강제이동
;		PRINTFORMW ＞이동!
		이동결정 = 이동처
		BREAK
	;이동율0%가 아니면 종료
	ELSEIF RESULT
;		PRINTFORMW ＞루프
		BREAK
	ENDIF
NEXT
;무차별이동시에 전방위 이동율 0이라면 강제이동
IF 무차별이동 && !이동결정 && !강제이동
	강제이동 = 1
	GOTO KYOUSEI
ENDIF
;이동하지 않았으면 종료
IF !이동결정
	$FINISH
	TCVAR:ARG:가능한한움직이지않는다 = 0
	RETURN
ENDIF

IF CAN_SEE(ARG) && !CFLAG:MASTER:수면
	IF CFLAG:ARG:현재위치 != 이동처 && AT_HOME(MASTER) == 1 && INRANGE(이동처, MINROOM(), MAXROOM)
		IF CFLAG:ARG:현재위치 == MAXROOM
			PRINTFORMW %ARG가% 【%NAME_FROM_PLACE(이동처)%】에 왔다
		ELSE
			PRINTFORM %ARG는% 【
			PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:현재위치)
			PRINT 】에서 【
			PRINTFORMS NAME_FROM_PLACE(이동처)
			PRINTFORMW 】%조사만처리(NAME_FROM_PLACE(이동처),"로")% 이동했다
			LOCAL:4 = 1
		ENDIF
	ENDIF
ENDIF
IF ROOMDATA:(CFLAG:ARG:현재위치 % 100) & 장소_원룸
	IF CFLAG:ARG:현재위치 == CFLAG:ARG:초기위치
		원룸인원수카운트 = 0
		LOCAL:2 = FINDCHARA(CFLAG:현재위치, CFLAG:ARG:현재위치, 1)
		WHILE INRANGE(LOCAL:2, 1, CHARANUM - 1)
			IF LOCAL:2 != ARG
				CFLAG:(LOCAL:2):현재위치 = 이동처
				원룸인원수카운트 ++
			ENDIF
			SIF LOCAL:2 == CHARANUM - 1
				BREAK
			LOCAL:2 = FINDCHARA(CFLAG:현재위치, CFLAG:ARG:현재위치, LOCAL:2 + 1)
		WEND
		IF LOCAL:4
			IF !TALENT:ARG:연모 && CFLAG:ARG:현재위치 == CFLAG:MASTER:현재위치
				PRINTFORM %CALLNAME:MASTER%\@ 원룸인원수카운트 ?  들 #  \@도 %CALLNAME:ARG%에 촉구받아 【
				PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:현재위치)
				PRINT 】에서 【
				PRINTFORMS NAME_FROM_PLACE(이동처)
				PRINTFORMW 】%조사만처리(NAME_FROM_PLACE(이동처),"로")% 이동했다
				CFLAG:MASTER:현재위치 = 이동처
			ELSEIF 원룸인원수카운트
				PRINTFORM %CALLNAME:ARG%에게 재촉받아 %CALLNAME:MASTER% 외에는 【
				PRINTFORMS NAME_FROM_PLACE(CFLAG:MASTER:현재위치)
				PRINT 】에서 【
				PRINTFORMS NAME_FROM_PLACE(이동처)
				PRINTFORMW 】%조사만처리(NAME_FROM_PLACE(이동처),"로")% 이동했다
			ELSE
				PRINTFORMW %ARG는% %CALLNAME:MASTER%에게 빈집을 지켜달라 부탁하고 이동했다
			ENDIF
		ENDIF
	ELSEIF CFLAG:ARG:현재위치 == CFLAG:MASTER:현재위치 && IN_ROOM_NUMBER(CFLAG:ARG:현재위치) == 2
		PRINTFORM %CALLNAME:MASTER%도 %CALLNAME:ARG%에게 촉구받아 【
		PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:현재위치)
		PRINT 】에서 【
		PRINTFORMS NAME_FROM_PLACE(이동처)
		PRINTFORMW 】%조사만처리(NAME_FROM_PLACE(이동처),"로")% 이동했다
		CFLAG:MASTER:현재위치 = 이동처
	ENDIF
ENDIF
;이동 처리
CFLAG:ARG:현재위치 = 이동처
TCVAR:ARG:가능한한움직이지않는다 = 1

;--------------------------------------------------------
;캐릭터(ARG)가 지점(ARG:1)에 이동하는 확률/분
;ARG:2=머물어비율(마이나스 보정)
;실수치는  10배가 된 것
;식 나카노세키수로 하고 싶었지만…
;--------------------------------------------------------
@캐릭터이동율(ARG, ARG:1, ARG:2)
#DIM 이동율
#DIM 이동처
#DIM 욕실들어가고싶다
이동처 = ARG:1
IF TALENT:ARG:한기선호
	욕실들어가고싶다 = 1500
;CHARAMOVE에 욕실이 싫으면 기술이 있던 캐릭터(등, 별, 정사)
ELSEIF GROUPMATCH(ARG, 24, 87, 97)
	욕실들어가고싶다 = 1500
ELSEIF TALENT:ARG:악취내성 == -2
	욕실들어가고싶다 = 600
ELSEIF TALENT:ARG:악취내성 == -1
	욕실들어가고싶다 = 800
ELSEIF TALENT:ARG:악취내성 == 0
	욕실들어가고싶다 = 1000
ELSEIF TALENT:ARG:악취내성 == 1
	욕실들어가고싶다 = 1500
ELSEIF TALENT:ARG:악취내성 == 2
	욕실들어가고싶다 = 2000
ENDIF
;기본 이동율, 1.5% / 분 = 45.0% / 30분
이동율 = 15 + CFLAG:ARG:이동율보정
;당신이 보이고 있으면(자) 태도로 업
SIF CAN_MOVE(CFLAG:ARG:현재위치, 이동처) == 3
	이동율 += CFLAG:ARG:태도 * 2
;청소부는 청소중 더러움에 영향을 받는다
SIF TALENT:ARG:청소부 && CFLAG:ARG:신사거주 && BETWEENTIME(720,1080)
	이동율 += YOGORE:이동처 / 300
;MOVEMENT_PRESET의 설정 인계(추가의 보정)
CALLFORM CHARAMOVE_DATA_{ARG}(4, 이동처)
이동율 = 이동율 + RESULT

;목욕을 하고 싶을 때는 접어들러 간다
IF ROOMDATA:(이동처 % 100) & 장소_목욕탕
	IF CFLAG:ARG:목욕탕 >= 욕실들어가고싶다
		이동율 = 이동율 * 10
	ELSE
		이동율 = 0
	ENDIF
ENDIF

;은둔 형 외톨이 대책으로서 날씨 대미지가 없는 상황으로 옥내에 있는 경우 옥외에 가고 싶어하도록(듯이)
SIF !ROOMDATA:(이동처 % 100) & 장소_옥내 && 날씨에의한영향(ARG, 이동처) >= 0 && ROOMDATA:(CFLAG:ARG:현재위치 % 100) & 장소_옥내
	이동율 += 30

;이동절도에 의해 가지 않는 장소를 설정
;MAP 거주자의 Private(사적인) 개소는 개별적으로 설정해 주세요
;1=창고나 골목길 뒤, 지하실등 불필요한 장소에는 이동하지 않는다
;-1=Private(사적인)장소에서도 그다지 신경쓰지 않고 이동한다
;-2=온갖 곳으로 이동한다
IF 이동처 != CFLAG:ARG:초기위치 && CFLAG:ARG:현재위치 != MAXROOM
	IF CFLAG:ARG:이동절도 >= 1 && !CFLAG:ARG:신사거주
		SIF ROOMDATA:(이동처 % 100) & 장소_과소
			이동율 = 0
	ENDIF
	IF CFLAG:ARG:이동절도 >= 0 && !CFLAG:ARG:신사거주
		SIF ROOMDATA:(이동처 % 100) & 장소_Private
			이동율 = 0
		SIF ROOMDATA:(이동처 % 100) & 장소_과소
			이동율 = 이동율 * 1 / 3
	ENDIF
	IF CFLAG:ARG:이동절도 >= -1
		SIF 자는아이가있다(이동처)
			이동율 = 이동율 * 1 / 3
		SIF ROOMDATA:(이동처 % 100) & 장소_Private && !CFLAG:ARG:신사거주
			이동율 = 이동율 * 2 / 3
		SIF ROOMDATA:(이동처 % 100) & 장소_화장실
			이동율 = 이동율 * 1 / 3
		SIF 날씨에의한영향(ARG, 이동처) <= -1
			이동율 = 이동율 * 1 / 3
	ENDIF
ENDIF
IF CFLAG:ARG:강제탈의 == 2 && ABL:ARG:노출벽 < 3 && (ROOMDATA:(이동처 % 100) & 장소_Private) && (ROOMDATA:(CFLAG:현재위치 % 100) & 장소_Private) == 0
	이동율 = 이동율 * 3
	ARG:2 = ARG:2 * 1 / 3
ENDIF
;머물어비율
이동율 = MAX(이동율 - ARG:2, 0)
RETURN 이동율

@자는아이가있다(ARG)
#FUNCTION
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:현재위치 == ARG && CFLAG:LOCAL:수면
		RETURNF 1
NEXT
