@EXIST_FreeAct12
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct12(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 경과시간
경과시간 = Activity_Duration:TARGET
SELECTCASE O_DATA
;自由行動に付き合うコマンドの名称
CASE "명칭"
	CALLF MAKE_STR(V_NAME, "함께 한가로이 쉬기")
CASE "CHAIN"
;他キャラに連鎖しない場合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "경과시간"
;自由行動に付き合うコマンドでの경과시간
;0だと30分か残り時間すべての小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@자유행동내용_12(CHARA, ARGS, 시간계수, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 현위치
#DIM 메시지표시
#DIM 시간계수
#DIM 경과시간
현위치 = CFLAG:CHARA:현재위치
메시지표시 = 현위치 == CFLAG:MASTER:현재위치 ? 1 # 0
SELECTCASE ARGS
	CASE "발생"
		;昼間に休憩できる場所または푹쉼장소にいる時
		SIF !INRANGE(TIME, 600, 960)
			RETURN 0
		 ;炬燵が発生するならそっちを優先する
		SIF 코타츠발생가능(CHARA, 현위치)
			RETURN 0
		SIF !휴식가능(현위치) && !푹쉼장소(현위치)
			RETURN 0
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %CALLNAME:CHAIN%에게 꾀여 한가로이 지내기 시작했다.# 한숨 돌려 쉬기 시작했다.\@
		RETURN 60 + RAND:30
	CASE "행동중"
		IF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %TEXTR("하품을 하고 있다/맥이 풀려 있다/멍하니 있다/한가롭다")%.
		ENDIF
	CASE "완료"
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 휴식을 마친 것 같다.
	CASE "중단"
	CASE "같이한다"
		;自由行動に付き合ったとき用メッセージ
		PRINTFORML %조교자는% %타겟과% 함께 %TEXTR("멍하니/느긋하게/한가롭게")% 시간을 보냈다…
	CASE "소스"
		;付き合うコマンド選択時の入手ソース、시간계수は付き合った時間の1/10
		CALL RECOVER_PERMIL(MASTER, 30, "체력", 1)
		CALL RECOVER_PERMIL(MASTER, 80, "기력", 1)
		CALL RECOVER_PERMIL(TARGET, 40, "체력", 1)
		CALL RECOVER_PERMIL(TARGET, 100, "기력", 1)
		SOURCE:환락 = (100 + ((ABL:친밀 + 호감도계수(TARGET)) * 10)) * 시간계수
		SOURCE:수동 = (50 + ((ABL:순종 + 호감도계수(TARGET)) * 5)) * 시간계수
ENDSELECT