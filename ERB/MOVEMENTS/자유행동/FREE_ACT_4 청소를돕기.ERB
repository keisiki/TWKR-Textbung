;掃除を手伝う
@EXIST_FreeAct4
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct4(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 경과시간
경과시간 = Activity_Duration:TARGET
SELECTCASE O_DATA
CASE "명칭"
	CALLF MAKE_STR(V_NAME, "청소를 돕기")
CASE "CHAIN"
;他キャラに連鎖する場合コメントアウト外すこと
	CALLF MAKE_INT(V_NAME, 1)
CASE "경과시간"
;自由行動に付き合うコマンドでの경과시간
;未入力だと最大30分
	;CALLF MAKE_INT(V_NAME, 경과시간)
ENDSELECT

@자유행동내용_4(CHARA, ARGS, 시간계수, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 현위치
#DIM 메시지표시
#DIM 시간계수
#DIM 경과시간
현위치 = CFLAG:CHARA:현재위치
메시지표시 = 현위치 == CFLAG:MASTER:현재위치 ? 1 # 0
SELECTCASE ARGS
	CASE "발생"
		;個室にいる時
		SIF !BEDROOM(현위치)
			RETURN 0
		SIF BASE:CHARA:기력 <= 500
			RETURN 0
		;악취내성（청소부）によって積極性が変わる
		IF TALENT:CHARA:악취내성 < 0 || TALENT:CHARA:청소부
			SIF YOGORE:(현위치) < 2000
				RETURN 0
		ELSEIF TALENT:CHARA:악취내성 == 0
			SIF YOGORE:(현위치) < 3500
				RETURN 0
		ELSEIF TALENT:CHARA:악취내성 > 0
			SIF YOGORE:(현위치) < 5000
				RETURN 0
		ENDIF
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% \@ CHAIN ? %조사처리(CALLNAME:CHAIN,"을")% 돕기 시작했다.# 더러움이 신경쓰여 청소를 시작했다.\@
		RETURN 30 + RAND:20
	CASE "행동중"
		;実際に掃除する
		YOGORE:(현위치) = MAX(YOGORE:(현위치) -250 * TIME_PROGRESS(TIME:1) / 5, 0)
		IF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% %TEXTR("쓰레기를 정리하고 있다/정리정돈을 하고 있다/먼지를 털어내고 있다/싹싹 방청소를 하고 있다")%.
		ENDIF
	CASE "완료"
		;청소경험が入る
		EXP:CHARA:청소경험 += 1
		SIF 메시지표시
			PRINTFORMW %조사처리(CALLNAME:CHARA,"는")% 청소를 끝낸 것 같다.
	CASE "중단"
		;중단されても청소경험は入る
		EXP:CHARA:청소경험 += 3
	CASE "같이한다"
		PRINTFORML %조교자는% %타겟이% %TEXTR("모아둔 쓰레기를 치우거나/털었던 장소를 걸레로 닦아내거나/닿지 않을 곳을 청소하거나")% 하며 도왔다
	CASE "소스"
		LOCAL = YOGORE:(CFLAG:MASTER:현재위치)
		YOGORE:(CFLAG:MASTER:현재위치) = MAX(YOGORE:(CFLAG:MASTER:현재위치) -100 * 경과시간 / 5, 0)
		EX:MASTER:오늘의청소회수 += LOCAL - YOGORE:(CFLAG:MASTER:현재위치)
		DOWNBASE:MASTER:체력 += 20 * 시간계수
		DOWNBASE:MASTER:기력 += 40 * 시간계수
		DOWNBASE:TARGET:체력 += 10 * 시간계수
		DOWNBASE:TARGET:기력 += 20 * 시간계수
		SOURCE:환락 = (100 + ((ABL:순종 + ABL:MASTER:청소기능) * 50)) * 시간계수
		TFLAG:98 = 2
		EXP:MASTER:청소경험 ++
		EXP:TARGET:청소경험 ++
ENDSELECT