
@TSC_10
#DIM CONST 참여인원 = 6

#DIM DYNAMIC 강함, 300
#DIM DYNAMIC 룰렛, 300
#DIM DYNAMIC 포인터
#DIM DYNAMIC 최약체
#DIM DYNAMIC 최강자
#DIM DYNAMIC 플레이어강함
PRINTFORML 탄막놀이 축제에 참가할까?
CALL ASK_YN
IF RESULT
	RETURN -1
ELSE
	PRINTFORML 매칭중...0％
	CALL 매칭상황표시(참여인원)
	TWAIT 200,0
	VARSET 룰렛, -1
	VARSET 참가자, -1
	참가자:0 = MASTER
	;우선 전투력 산정
	FOR LOCAL, 0, CHARANUM
		IF CFLAG:LOCAL:현재위치 == CFLAG:MASTER:현재위치 || LOCAL == MASTER
			강함:LOCAL = MAX(1,(ABL:LOCAL:전투능력 * 20) + (ABL:LOCAL:레벨 * 3) + (CFLAG:LOCAL:힘 + CFLAG:LOCAL:지성 + CFLAG:LOCAL:속도 + CFLAG:LOCAL:인내+ CFLAG:LOCAL:운) + 1)
			룰렛:LOCAL = LOCAL
			IF LOCAL == MASTER
				DEBUGPRINTFORML 마스터 캐치 {LOCAL},{강함:LOCAL},{룰렛:LOCAL}
			ENDIF
			IF LOCAL == 1
				DEBUGPRINTFORML 1번 캐치 {LOCAL},{강함:LOCAL},{룰렛:LOCAL}
			ENDIF
		ELSE
			강함:LOCAL = -1
			룰렛:LOCAL = -1
		ENDIF
	NEXT
	CLEARLINE 2
	PRINTFORML 매칭중...20％
	CALL 매칭상황표시(참여인원)
	TWAIT 200,0
	;강한순서대로 세팅된다
	[IF_DEBUG]
		PRINTFORML 매칭표-소트전
		FOR LOCAL,0,CHARANUM
			PRINTFORML {LOCAL} : {강함:LOCAL}-{룰렛:LOCAL}
		NEXT
	[ENDIF]
	ARRAYMSORT 강함, 룰렛
	[IF_DEBUG]
		DRAWLINE
		PRINTFORML 소트후
		FOR LOCAL,0,200
			PRINTFORML {LOCAL} : {강함:LOCAL}-{룰렛:LOCAL}
		NEXT
	[ENDIF]
	FINDELEMENT 강함, 0, 0, 300
	최약체 = RESULT + 1
	DEBUGPRINTFORML 최약체- {최약체}
	FINDLASTELEMENT 룰렛, 0, 0, 300
	CLEARLINE 1
	PRINTFORML 매칭중...50％
	TWAIT 150,0
	IF RESULT != -1
		포인터 = RESULT
		IF 최약체 - (참여인원/2) < 포인터
			;마스터가 상위 50% 미만
			DEBUGPRINTFORML 마스터가 최약체에 가까워- {최약체},{포인터}
			FOR LOCAL,0,6
				포인터--
				IF 최약체 - (참여인원/2) <= 포인터
					BREAK
				ENDIF
			NEXT
			DEBUGPRINTFORML 이렇게 고칠게- {최약체},{포인터}
		ENDIF
		CLEARLINE 2
		PRINTFORML 매칭중...70％
		CALL 매칭상황표시(참여인원)
		TWAIT 125,0
		FOR LOCAL,1,참여인원/2
			
			IF 강함:(포인터 + LOCAL) > 0 && 룰렛:(포인터 + LOCAL) != MASTER
				참가자:LOCAL = 룰렛:(포인터 + LOCAL)
				LOCAL:1 ++
			ENDIF
			IF LOCAL != 참여인원/2&&포인터 - LOCAL >= 0
				IF 강함:(포인터 - LOCAL) > 0&&룰렛:(포인터 - LOCAL) != MASTER
					참가자:(참여인원 - LOCAL) = 룰렛:(포인터 - LOCAL)
					LOCAL:1 ++
				ENDIF
			ENDIF
		NEXT
		CLEARLINE 2
		PRINTFORML 매칭중...100％
		CALL 매칭상황표시(참여인원)
		TWAIT 100,0
	ENDIF
ENDIF
CLEARLINE 2
PRINTFORML 매칭완료...100％
CALL 매칭상황표시(참여인원)
$탄막축제시작
PRINTFORML
탄막놀이축제진행중 = 1
FOR LOCAL,1,LOCAL:1
	TARGET = (참가자:LOCAL)
	PRINTFORML 제 {LOCAL}번째 참가자는 %CALLNAME:TARGET%!!!
	PRINTFORML
	CALL 뉴탄막전투(MASTER,TARGET,0)
	TIME += 20
	DOWNBASE:기력 += 50
	DOWNBASE:MASTER:기력 += 100
	DOWNBASE:체력 += 50
	DOWNBASE:MASTER:체력 += 100
	IF RESULT == 2
		PRINTFORML 
		PRINTFORMW %마스터는% 결국 쓰러지고 말았다...
		BREAK
	ENDIF
	PRINTL
NEXT
탄막놀이축제참가완료 = 1
탄막놀이축제진행중 = 0

@TSC_NAME10
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 탄막놀이 축제 참가
RESULTS = %커맨드이름%

@TSC_ABLE10

IF FLAG:개시일 - DAY != 0 || EnkaiTitle != "탄막놀이 축제"
    RETURN 0
ENDIF
SIF 탄막놀이축제참가완료
	RETURN 0
IF TIME < 720 ;|| TIME > 930
	RETURN 0
ENDIF
IF CFLAG:MASTER:현재위치 != 202 && CFLAG:MASTER:현재위치 != 210
	RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2


@매칭상황표시(ARG)
FOR LOCAL,0,ARG
	IF 참가자:LOCAL != -1
		PRINTFORM [%CALLNAME:(참가자:LOCAL)%]
	ENDIF
NEXT
PRINTL