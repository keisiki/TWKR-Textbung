@TSC_1
PRINTFORMW %마스터는% 산길을 따라 움직이며 홀로 있는 텐구들을 수색했다...
$사냥대상선택
PRINTW 저 아이는 어떨까?
DRAWLINE
CALL 텐구생성
PRINTFORML 대상 : %텐구이름%
PRINTFORML 체력 : {텐구능력치:0}
PRINTFORML 능력치 : 힘-{텐구능력치:1}/지성-{텐구능력치:2}/인내-{텐구능력치:3}/속도-{텐구능력치:4}/운-{텐구능력치:5}
DRAWLINE
PRINTFORML [0] 사냥 개시!
PRINTFORML [1] 다른 대상을 찾는다
PRINTFORML [9] 돌아가기
$LOOP
INPUT
SELECTCASE RESULT
    CASE 0
        ADDCHARA 134
        TARGET = CHARANUM -1
        CALLNAME:TARGET = %텐구이름%
        NAME:TARGET = %텐구이름%
        BASE:TARGET:체력 = 텐구능력치:0
        ABL:TARGET:전투능력 = RAND:4 + 9
        ABL:TARGET:레벨 = (텐구능력치:1 + 텐구능력치:2 + 텐구능력치:3 + 텐구능력치:4 + 텐구능력치:5)/5
        CFLAG:TARGET:힘 = 텐구능력치:1
        CFLAG:TARGET:지성 = 텐구능력치:2
        CFLAG:TARGET:인내 = 텐구능력치:3
        CFLAG:TARGET:속도 = 텐구능력치:4
        CFLAG:TARGET:운 = 텐구능력치:5
        CFLAG:TARGET:물리내성 = 텐구능력치:6
        CFLAG:TARGET:화염내성 = 텐구능력치:7
        CFLAG:TARGET:빙결내성 = 텐구능력치:8
        CFLAG:TARGET:전격내성 = 텐구능력치:9
        CFLAG:TARGET:질풍내성 = 텐구능력치:10
        CFLAG:TARGET:마력내성 = 텐구능력치:11
        CFLAG:TARGET:수격내성 = 텐구능력치:12
        CFLAG:TARGET:정신내성 = 텐구능력치:13
        TALENT:TARGET:정화 = 1
        TALENT:TARGET:전투스타일 = 2
        SIF RAND:5 == 0
            TALENT:TARGET:그레이즈능숙함 = 1
        ;-----------
        후처리인터셉트 = 1
        CALL 뉴탄막전투(MASTER,TARGET,1)
        DRAWLINE   
        SELECTCASE 후처리인터셉트승패여부
            CASE 0
                ;타겟 패배
                TIME += 60
                DOWNBASE:MASTER:체력 += 150
                DOWNBASE:MASTER:기력 += 300
                PRINTFORMW %마스터의% 공격에 넘어진 %타겟은% 바닥에서 목숨을 구걸하고 있다...
                PRINTFORML %타겟을%...
                PRINTFORML [0] 죽인다
                PRINTFORML [1] 범한다
                PRINTFORML [2] 살려준다
                INPUT
                IF RESULT == 0
                    PRINTFORMW 눈물을 흘리며 애원하는 %타겟을% 단번에 보내주었다...
                    CALL 텐구살인
                ELSEIF RESULT == 1
                    PRINTFORMW ......
                    PRINTFORMW ...
                    PRINTFORMW ..
                    PRINTFORMW 저항조차 하지 못하는 %타겟을% 무자비하게 범해주었다
                    IF TALENT:MASTER:성별 != 0
                        IF TALENT:MASTER:비동정 != 0
                            IF RAND:3 == 0
                                PRINTFORMW %타겟의% 순결했던 보지에선 붉은 피와 정액이 흘러내렸다
                            ELSE
                                PRINTFORMW %타겟의% 보지에선 정액이 뚝뚝 떨어지고 있었다...
                            ENDIF
                            LOCAL = RAND:7 + 1
                            PRINTFORMW {LOCAL}번이나 무자비하게 범해 하반신의 감각이 없어진 %타겟은% 그래도 목숨은 건졌다는 사실에 안도하고 있다...
                            EXP:MASTER:질내사정경험 += LOCAL
                            EXP:MASTER:사정경험 += LOCAL
                            EXP:MASTER:C절정경험 += LOCAL
                        ELSE
                            PRINTFORMW %타겟의% 몸을 수십차례나 핥고, 문질러 대며 여체탐구를 해나갔다
                            PRINTFORMW %타겟은% 무척이나 수치스러운지 고개를 푹 숙이며 끝나기 만을 초조히 기다렸다
                            PRINTFORMW 온 몸을 마구 능욕당한 %타겟은% 그래도 목숨은 건졌다는 사실에 안도하고 있다...
                        ENDIF
                    ELSE
                        PRINTFORMW %타겟의% 몸을 핥고, 문질러 범해댔다
                        PRINTFORMW 수치심과 공포심에 발버둥치는 %타겟을% 짓누르며 마음껏 성욕을 푼 뒤, 너덜너덜해진 %타겟을% 걷어차 밖으로 내다 버렸다
                        PRINTFORMW ...%타겟은% 감각이 없어진 하반신을 붙들고 치욕에 눈물을 삼켰지만 그래도 목숨은 건졌다는 사실에 안도하고 있다...
                    ENDIF
                    PRINTFORML [0] 죽인다
                    PRINTFORML [1] 살려준다
                    INPUT
                    IF RESULT == 0
                        PRINTFORMW 청천벽력같은 소리에 눈물을 흘리며 매달려오는 %타겟을% 단번에 보내주었다
                        CALL 텐구살인
                    ELSE
                        PRINTFORMW %타겟을% 적당한 수풀가에 묶어 두고 %마스터는% 떠나갔다...
                    ENDIF
                ELSE
                    PRINTFORMW %마스터는% 쓰러진 %타겟의% 머리를 한대 후려쳐 기절시킨 후, 적당한 길가에 방치해 두었다...
                ENDIF
                IF ABL:TARGET:레벨 > ABL:MASTER:레벨
                    CALL 경험치처리(MASTER,MAX(ABL:TARGET:레벨 - ABL:MASTER:레벨, 1)*10)
                ELSE
                    CALL 경험치처리(MASTER,(ABL:MASTER:레벨 / (ABL:MASTER:레벨 - ABL:TARGET:레벨 + 1)) *5)
                ENDIF
            CASE 1
                ;무승부
                PRINTFORMW 곧 행인에게 들킬수도 있으니 이만 물러나도록 한다
                TIME += 30
                
            CASE 2
                ;마스터 패배
                TIME += 60
                DOWNBASE:MASTER:체력 += 150
                DOWNBASE:MASTER:기력 += 300
                PRINTFORMW %타겟은% 쓰러진 %마스터를% 향해 마무리 일격을 날렸다...
                PRINTFORMW ...
                PRINTFORMW ..
                PRINTFORMW .
                IF RAND:5 == 0
                    PRINTFORMW 치명상을 입은채 방치된 %마스터는% 한참이 지나서야 겨우 눈을 떴다..
                    DOWNBASE:MASTER:체력 += 1500
                    DOWNBASE:MASTER:기력 += 3000
                    TIME += 360
                ELSE
                    PRINTFORMW 요행히 빗맞았지만 %타겟은% 눈치채지 못하고 떠나갔다...
                    PRINTFORMW 완전히 사라진걸 확인한 %마스터는% 지친 몸을 겨우 일으켰다...
                    DOWNBASE:MASTER:체력 += 300
                    DOWNBASE:MASTER:기력 += 600
                    TIME += 30
                ENDIF
        ENDSELECT
        DRAWLINE
        후처리인터셉트승패여부 = 0
        DELCHARA TARGET
        TARGET = 0
        DOWNBASE:MASTER:체력 += 150
        DOWNBASE:MASTER:기력 += 300
        RETURN 1
    CASE 1
        GOTO 사냥대상선택
    CASE 9
        RETURN -1
    CASEELSE
        GOTO LOOP
ENDSELECT

@TSC_NAME1
#DIMS DYNAMIC 커맨드이름
커맨드이름 = 텐구 사냥
RESULTS = %커맨드이름%

@TSC_ABLE1

IF !GROUPMATCH(CFLAG:현재위치,810,820,801,802,805,806)
    RETURN 0
ENDIF
;기력 0
SIF BASE:MASTER:기력 <= 0
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
;정지 중에는 불가
SIF FLAG:70
	RETURN 0
RETURN 2


@텐구생성
SELECTCASE RAND:2
    CASE 0
        ;백랑
        텐구이름 = 백랑 텐구
        ;체
        텐구능력치:0 = 1700 + (RAND:10 * 100)
        ;힘
        텐구능력치:1 = 70 + RAND:20
        ;지
        텐구능력치:2 = 65 + RAND:20
        ;인
        텐구능력치:3 = 80 + RAND:20
        ;속
        텐구능력치:4 = 110 + RAND:20
        ;운
        텐구능력치:5 = 45 + RAND:20
        LOCAL = (텐구능력치:1 + 텐구능력치:2 + 텐구능력치:3 + 텐구능력치:4 + 텐구능력치:5)/5
        ;물
        텐구능력치:6 = 6 * LOCAL
        ;화
        텐구능력치:7 = 5 * LOCAL
        ;빙
        텐구능력치:8 = 5 * LOCAL
        ;전
        텐구능력치:9 = 5 * LOCAL
        ;질
        텐구능력치:10 = 5 * LOCAL
        ;마
        텐구능력치:11 = 5 * LOCAL
        ;수
        텐구능력치:12 = 5 * LOCAL
        ;정
        텐구능력치:13 = -10 * LOCAL
    CASE 1
        ;카라스
        텐구이름 = 카라스 텐구
        ;체
        텐구능력치:0 = 1650 + (RAND:12 * 100)
        ;힘
        텐구능력치:1 = 80 + RAND:20
        ;지
        텐구능력치:2 = 75 + RAND:20
        ;인
        텐구능력치:3 = 65 + RAND:20
        ;속
        텐구능력치:4 = 140 + RAND:20
        ;운
        텐구능력치:5 = 70 + RAND:20
        LOCAL = (텐구능력치:1 + 텐구능력치:2 + 텐구능력치:3 + 텐구능력치:4 + 텐구능력치:5)/5
        ;물
        텐구능력치:6 = 5 * LOCAL
        ;화
        텐구능력치:7 = 5 * LOCAL
        ;빙
        텐구능력치:8 = 5 * LOCAL
        ;전
        텐구능력치:9 = 5 * LOCAL
        ;질
        텐구능력치:10 = 10 * LOCAL
        ;마
        텐구능력치:11 = 5 * LOCAL
        ;수
        텐구능력치:12 = 5 * LOCAL
        ;정
        텐구능력치:13 = -10 * LOCAL
ENDSELECT

@텐구살인
고로시수 ++
PRINTFORML 이 시신을 어떻게할까?
PRINTFORML [0] 해체한다
PRINTFORML [1] 길가에 전시한다
PRINTFORML [2] 묻어준다
INPUT
IF RESULT == 0
    PRINTFORML 길가에서 급히 베어내느라 고기가 조금 상했다...
    IF 텐구이름 == "카라스 텐구"
        ITEM:고기 += 30
        ITEM:간 ++
        ITEM:뼈 += 10
        ITEM:영혼조각++
        ITEM:머리카락 += 4
        ITEM:날개 += 2
        PRINTFORML 고기 30개, 간 1개, 뼈 10개, 영혼조각 1개, 머리카락 4개, 날개 2개를 얻었다!
    ELSE
        ITEM:고기 += 35
        ITEM:간 ++
        ITEM:뼈 += 10
        ITEM:영혼조각++
        ITEM:머리카락 += 4
        PRINTFORML 고기 35개, 간 1개, 뼈 10개, 영혼조각 1개, 머리카락 4개를 얻었다!
    ENDIF
ELSEIF RESULT == 1
    PRINTFORMW 근처의 굵은 나뭇가지로 만든 십자가에 %타겟을% 매달았다...
    CALL COLORMESSAGE("텐구들에게 공포심을 주었다...", C_YELLOW, 1)
    SIF RELATION:29:0 > 5
        RELATION:29:0 --
    SIF RELATION:65:0 > 5
        RELATION:65:0 --
    SIF RELATION:42:0 > 5
        RELATION:42:0 --
ELSE
    PRINTFORMW 근처 양지바른곳을 얇게 파고 그 안에 죽은 %타겟을% 묻어주었다
    PRINTFORMW 쓸쓸하지 않게, 앞에 나무토막으로 묘비도 세워주고, 공양도 해 주었다...
ENDIF